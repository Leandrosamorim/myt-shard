/////////////////////////////////////////////////////////////
////////////// Sistema de Chaveiro completo  ///////////////
/////////////    Jean Gabin - MyT Shard     ///////////////
//////////////////////////////////////////////////////////

// Neste script você encontrará:
// - Fechaduras para serem instaladas em portas, baús e caixas com fechadura
// - cadeados para aplicações diversas
// - Chaves que funcionam com o script de armadilha
// - sistema de cópia de chave integrado ao tinker
// - Skill Lockpick funcionando
// - Chave-mestra para GM (abre qualquer coisa sem o priv de GM)

// Id`s:
// - Cadeado = 0dcf (colorir) color=03cf
// - Fechadura = 012ab (colorir) color=03cf
// - Lockpick = 014fb - sfx 577
// - kit lockpick = 014fd
// - keyring = 01011 i_key_ring
// - iron key = 01010 i_key_iron // reforcada
// - copper key = 0100e  i_key_copper // normal
// - gold key = 01013 i_key_gold // complexa
// - rusty iron key = 01012 i_key_rusty // simples
// - magic key = 

// - cadeado = (chave é i_key_rusty) lockpicking 30 - acima: erro = 2 para 1 - timer 15 - skillmake = tinkering 40 - para instalar não requer skill
// - fechadura simples = (chave é i_key_copper)lockpicking 50 - acima: erro = 2 para 1 - timer 20 - skillmake = tinkering 50 - skillinstall = tinkering 35
// - fechadura normal = (chave é i_key_iron)lockpicking 70 - acima: erro = 2 para 1 - timer 30 - skillmake = tinkering 70 - skillinstall = tinkering 40
// - fechadura complexa = (chave é i_key_gold)lockpicking 90 - acima: erro = 4 para 1 - timer 40 - skillmake = tinkering 90 - skillinstall = tinkering 50
// - lockpicking 100 = erro 1 para 1 em todas e complexa 2 para 1 - timer 40

///////////////////////////////////////////////////////////////////////////
////////////////////  fechadura, chave e cadeado simples  /////////////////
///////////////////////////////////////////////////////////////////////////

///////////////////////////// fechadura

[ITEMDEF i_fechadura]  
ID=0E7A//012ab
NAME=Fechadura
RESOURCES=i_springs, i_gears,
TYPE=t_script
CATEGORY=MyT - Items by Professions
SUBSECTION=Chaveiro
DESCRIPTION=Fechadura


on=@create
color=03cf // AO CRIAR A FECHADURA COM O SISTEMA DE TINKERING PRECISA COLOCAR O MORE1 COM O TIPO DE FECHADURA
NAME=Fechadura simples // retirar isto quando unir ao sistema de tinker
more1=1  // retirar isto quando unir ao sistema de tinker

on=@dclick
if (<topobj.uid> != <src.uid>)
	src.sysmessage Coloque a fechadura na bolsa antes de instala-la.
return 1
else
   target Selecione a porta ou o container para instalar esta fechadura.
return 1

ON=@targon_char
   src.sysmessage Nao e possivel instalar uma fechadura neste local.
   target Selecione a porta ou o container para instalar esta fechadura.
return 1

ON=@targon_item
if (<src.targ.distance> > 1)
   src.sysmessage Chegue mais perto.
   target Selecione a porta ou o container para instalar esta fechadura.
return 1
elif ((<src.tinkering> < 30.0) && (<more1>==1)) || ((<src.tinkering> >= 30.0) && (<src.tinkering> < 40.0) && (<more1>==2)) || ((<src.tinkering> >= 40.0) && (<src.tinkering> < 50.0) && (<more1>==3))
   src.sysmessage Voce nao tem conhecimento suficiente para instalar esta fechadura.
return 1
elif (<src.targ.type>==t_armadilha_container) || (<src.targ.type>==t_armadilha_porta)
   src.targ.use
return 1
elif (<src.targ.type>==t_door_locked) || (<src.targ.type>==t_container_locked)
   src.sysmessage isto esta trancado, portanto ja deve ter uma fechadura ou um cadeado.
return 1
elif (<src.targ.type>==t_door) || (<src.targ.type>==t_container)
	if (<src.targ.baseid>==i_door_secret_stone_1) || (<src.targ.baseid>==i_door_secret_stone_2) || (<src.targ.baseid>==i_door_secret_stone_3) || (<src.targ.baseid>==i_door_secret_stone_4) || (<src.targ.baseid>==i_door_secret_stone_4_O) || (<src.targ.baseid>==i_door_secret_wood_1) || (<src.targ.baseid>==i_door_secret_wood_2)
	   src.sysmessage Nao se coloca tranca em porta secreta.
	return 1
	elif (<src.targ.baseid>==i_crate_small) || (<src.targ.baseid>==i_box_wood) || (<src.targ.baseid>==i_basket_bushel) || (<src.targ.baseid>==i_pouch) || (<src.targ.baseid>==i_basket_2) || (<src.targ.baseid>==i_crate_lg) || (<src.targ.baseid>==i_crate_md) || (<src.targ.baseid>==i_backpack) || (<src.targ.baseid>==i_bag) || (<src.targ.baseid>==i_barrel_open) || (<src.targ.baseid>==i_basket_round) || (<src.targ.baseid>==i_basket_square) || (<src.targ.baseid>==i_tub_empty) || (<src.targ.baseid>==i_keg_small) || (<src.targ.baseid>==i_basin) || (<src.targ.baseid>==i_crate_small_2) || (<src.targ.baseid>==i_box_wood)
	   src.sysmessage Nao e possivel instalar uma fechadura neste local. Tente um cadeado.
	return 1
	else
		if (strmatch(<src.targ.tag.fechadura>,simples)) || (strmatch(<src.targ.tag.fechadura>,normal)) || (strmatch(<src.targ.tag.fechadura>,complexa))
		   src.sysmessage Ja existe uma fechadura neste local.
		return 1
		elif (strmatch(<src.targ.tag.fechadura>,cadeado))
		   src.sysmessage Ja existe um cadeado neste local.
		return 1
		else
		   src.emote Iniciou a instalacao de uma fechadura
		   src.anim 020
		   sfx 1449
		   src.newitem=i_mry_install_fechadura
		   src.act.more1=<more1>		// tipo de fechadura a ser instalada: 1=simples, 2=normal, 3=complexa
		   src.act.morep=<src.p>		// marca o local do src. se ele se mover para o script
		   src.act.more2=0
		   src.act.link=<src.targ.uid>		// linka a memory a porta
		   src.act.timer=1
		   src.act.equip
		   src.update
		   //remove
		endif
	endif
else
   src.sysmessage Nao e possivel instalar uma fechadura neste local.
endif
return 1

//if (<src.targ.tag.dono>!=<src.uid>)


///////////////////////////// cadeado


[ITEMDEF i_cadeado]  
ID=0dcf
NAME=Cadeado
RESOURCES=i_springs, i_gears,
TYPE=t_script
CATEGORY=MyT - Items by Professions
SUBSECTION=Chaveiro
DESCRIPTION=Cadeado

on=@create
color=03cf
more2=0 // no 1 o cadeado está quebrado e inutilizável 

on=@dclick
if (<topobj.uid> != <src.uid>)
	src.sysmessage Coloque o cadeado na bolsa antes de usa-lo.
return 1
elif (<more2> != 1)
   target Selecione a porta ou o container para trancar com este cadeado.
return 1
else
   src.sysmessage Este cadeado foi arrombado e esta quebrado.
return 1

ON=@targon_char
   src.sysmessage Nao e possivel colocar um cadeado em alguem.
   target Selecione a porta ou o container para trancar com este cadeado.
return 1

ON=@targon_item
if (<src.targ.distance> > 1)
   src.sysmessage Chegue mais perto.
   target Selecione a porta ou o container para trancar com este cadeado.
return 1
elif (<src.targ.type>==t_trap)
   src.targ.use
return 1
elif (<src.targ.type>==t_door_locked) || (<src.targ.type>==t_container_locked)
   src.sysmessage isto esta trancado, portanto ja deve ter uma fechadura ou um cadeado.
return 1
elif (<src.targ.type>==t_door) || (<src.targ.type>==t_container)
	if (<src.targ.baseid>==i_door_secret_stone_1) || (<src.targ.baseid>==i_door_secret_stone_2) || (<src.targ.baseid>==i_door_secret_stone_3) || (<src.targ.baseid>==i_door_secret_stone_4) || (<src.targ.baseid>==i_door_secret_stone_4_O) || (<src.targ.baseid>==i_door_secret_wood_1) || (<src.targ.baseid>==i_door_secret_wood_2)
	   src.sysmessage Nao se coloca cadeado em porta secreta.
	return 1
	elif (<src.targ.baseid>==i_basket_bushel) || (<src.targ.baseid>==i_pouch) || (<src.targ.baseid>==i_basket_2) || (<src.targ.baseid>==i_backpack) || (<src.targ.baseid>==i_bag) || (<src.targ.baseid>==i_barrel_open) || (<src.targ.baseid>==i_basket_round) || (<src.targ.baseid>==i_basket_square) || (<src.targ.baseid>==i_tub_empty) || (<src.targ.baseid>==i_keg_small) || (<src.targ.baseid>==i_basin)
	   src.sysmessage Nao e possivel trancar este item com um cadeado.
	return 1
	else
		if (strmatch(<src.targ.tag.fechadura>,simples)) || (strmatch(<src.targ.tag.fechadura>,normal)) || (strmatch(<src.targ.tag.fechadura>,complexa))
		   src.sysmessage Ja existe uma fechadura neste local.
		return 1
		elif (strmatch(<src.targ.tag.fechadura>,cadeado))
		   src.sysmessage Ja existe um cadeado neste local.
		return 1
		else
		src.emote Trancando com cadeado
		//src.newitem=i_key_rusty
		//src.act.bounce
		src.targ.link=<link> // coloca no link da porta o uid da chave
		//src.act.more1=<src.targ.uid>   // coloca no more1 da chave o uid da porta
		link.more1=<src.targ.uid>   // coloca no more1 da chave o uid da porta
		src.targ.more1=<src.targ.uid>   // coloca no more1 da porta a uid da porta
		if (<src.targ.type>==t_door)
			src.targ.type=t_door_locked
		elif (<src.targ.type>==t_container)
			src.targ.type=t_container_locked
		endif
		src.targ.tag.fechadura=cadeado
		src.targ.update
		remove
		endif
	endif
else
   src.sysmessage Nao e possivel trancar isto com um cadeado.
endif
return 1

///////////////////////////////////////////////////////////////////
////////////////////////  chaves  ////////////////////////////////
/////////////////////////////////////////////////////////////////

[ITEMDEF 01013]
DEFNAME=i_key_rusty
NAME=Chave de cadeado
RESOURCES=1 i_INGOT_RUSTY
WEIGHT=1
TYPE=T_KEY
CATEGORY=MyT - Items by Professions
SUBSECTION=Chaveiro
DESCRIPTION=Chave de cadeado

on=@dclick
if (<topobj.uid> != <src.uid>)
	src.sysmessage Coloque a chave na bolsa antes de usa-la.
return 1
else
  target Selecione a porta ou container com o cadeado.
return 1
endif

on=@targon_item
if (<link.type>==t_ship) || (<link.type>==t_multi)
elif (<src.targ.distance> > 1)
   src.sysmessage Chegue mais perto.
   target Selecione a porta ou container com o cadeado.
return 1
elif (<more1>==0) && (<src.targ.baseid>==i_cadeado) && (<src.targ.link>==04FFFFFFF)  // apenas para testes. isto sairá com o sistema de tinker fazendo cõpia
	src.targ.link=<uid>
	src.sysmessage Agora esta chave pertence a este cadeado.
return 1
elif (strmatch(<src.targ.tag.fechadura>,cadeado))
	if (<more1>==<src.targ.more1>)
		src.emote Abrindo o cadeado
		sfx 1451
		src.targ.link=04fffffff	// tira o link da porta
		//src.targ.more1=0	// tira o more1 da porta
		src.newitem=i_cadeado
		src.act.bounce
		src.act.link=<uid>   // linka o novo cadeado a chave
		more1=<src.act.uid>  // desvincula a chave a porta e vincula ao cadeado
		if (<src.targ.type>==t_door_locked)
		  src.targ.type=t_door
		elif (<src.targ.type>==t_container_locked)
		  src.targ.type=t_container
		endif
		src.targ.tag.fechadura=
	else
		src.sysmessage Esta chave nao pertence a este cadeado
	endif
else
	src.sysmessage Isto nao tem um cadeado
endif
return 1

//////////////////////////////////////

[ITEMDEF 0100e]
DEFNAME=i_key_copper
NAME=Chave simples
RESOURCES=3 i_ingot_iron
WEIGHT=1
TYPE=T_KEY
CATEGORY=MyT - Items by Professions
SUBSECTION=Chaveiro
DESCRIPTION=Chave simples
SKILLMAKE=TINKERING 43.0,t_tinker_tools

on=@dclick
if (<topobj.uid> != <src.uid>)
	src.sysmessage Coloque a chave na bolsa antes de usa-la.
return 1
endif

on=@targon_item
if (<link.type>==t_ship) || (<link.type>==t_multi)
elif (<src.targ.distance> > 1)
   src.sysmessage Chegue mais perto.
   target 
return 1
elif !(strmatch(<src.targ.tag.fechadura>,simples))
	src.sysmessage Esta chave nao entra no buraco da fechadura
return 1
elif !(<more1> == <src.targ.more1>)
	src.sysmessage Esta chave nao pertence a esta fechadura
return 1
endif

//////////////////////////////////////

[ITEMDEF 01010]
DEFNAME=i_key_iron
NAME=Chave normal
RESOURCES=3 i_ingot_iron
WEIGHT=1
TYPE=T_KEY
CATEGORY=MyT - Items by Professions
SUBSECTION=Chaveiro
DESCRIPTION=Chave normal
SKILLMAKE=TINKERING 30.0,t_tinker_tools

on=@dclick
if (<topobj.uid> != <src.uid>)
	src.sysmessage Coloque a chave na bolsa antes de usa-la.
return 1
endif

on=@targon_item
if (<link.type>==t_ship) || (<link.type>==t_multi)
elif (<src.targ.distance> > 1)
   src.sysmessage Chegue mais perto.
   target 
return 1
elif !(strmatch(<src.targ.tag.fechadura>,normal))
	src.sysmessage Esta chave nao entra no buraco da fechadura
return 1
elif !(<more1> == <src.targ.more1>)
	src.sysmessage Esta chave nao pertence a esta fechadura
return 1
endif

//////////////////////////////////////


[ITEMDEF 0100f]
DEFNAME=i_key_gold
NAME=Chave complexa
RESOURCES=2 i_ingot_iron, 1 i_ingot_gold
WEIGHT=1
TYPE=T_KEY
CATEGORY=MyT - Items by Professions
SUBSECTION=Chaveiro
DESCRIPTION=Chave complexa
SKILLMAKE=TINKERING 60.0,t_tinker_tools

on=@dclick
if (<topobj.uid> != <src.uid>)
	src.sysmessage Coloque a chave na bolsa antes de usa-la.
return 1
endif

on=@targon_item
if (<link.type>==t_ship) || (<link.type>==t_multi)
elif (<src.targ.distance> > 1)
   src.sysmessage Chegue mais perto.
   target 
return 1
elif !(strmatch(<src.targ.tag.fechadura>,complexa))
	src.sysmessage Esta chave nao entra no buraco da fechadura
return 1
elif !(<more1> == <src.targ.more1>)
	src.sysmessage Esta chave nao pertence a esta fechadura
return 1
endif

//////////////////////////////////////

[ITEMDEF 01011]
DEFNAME=i_key_ring
NAME=Chaveiro
TYPE=T_KEYRING
TDATA2=1
WEIGHT=1
CATEGORY=MyT - Items by Professions
SUBSECTION=Chaveiro
DESCRIPTION=Chaveiro
RESOURCES=1 i_ingot_iron
SKILLMAKE=TINKERING 30.0,t_tinker_tools

ON=@DCLICK
	SRC.MESSAGE Arraste e solte as chaves no chaveiro para guarda-las.
return 1

//////////////////////////////////////

[ITEMDEF 01769]
DEFNAME=i_key_ring_1
NAME=Chaveiro com chaves
TYPE=T_KEYRING
TDATA2=1
RESOURCES=1 i_ingot_iron
WEIGHT=1
CATEGORY=MyT - Items by Professions
SUBSECTION=Chaveiro
DESCRIPTION=Chaveiro com chaves
FLIP=0
//DUPELIST=0176a,0176b

on=@dclick
if (<topobj.uid> != <src.uid>)
	src.sysmessage Coloque o chaveiro com chaves na bolsa antes de usa-lo.
return 1
endif

on=@targon_item
if (<link.type>==t_ship) || (<link.type>==t_multi)
elif (<src.targ.distance> > 1)
   src.sysmessage Chegue mais perto.
   target 
return 1
elif (strmatch(<src.targ.tag.fechadura>,cadeado))
  more2=0
  cont_uid
  if (0<FINDCONT(<more2>).UID>==<src.targ.link>)
	src.newitem=i_cadeado
	src.act.bounce
	src.act.link=<src.targ.link>   // linka o novo cadeado a chave
	src.targ.link.more1=<src.act.uid>  // desvincula a chave a porta e vincula ao cadeado
	src.targ.link=04fffffff	// tira o link da porta
	if (<src.targ.type>==t_door_locked)
	  src.targ.type=t_door
	elif (<src.targ.type>==t_container_locked)
	  src.targ.type=t_container
	endif
	src.targ.tag.fechadura=
  return 1
  endif
endif


[FUNCTION cont_uid]
if (<more2>==50)  // acaba com o loop eterno
return 1
elif (0<FINDCONT(<more2>).UID>==<src.targ.link>)
return 1
ENDIF
more2=<more2>+1
cont_uid
RETURN 1



[ITEMDEF 0176a]
//key ring
DUPEITEM=01769

[ITEMDEF 0176b]
//key ring
DUPEITEM=01769

//////////////////////////////////////

[ITEMDEF 01012]
DEFNAME=i_key_magic
NAME=Chave mestra
WEIGHT=1
TYPE=T_script
CATEGORY=MyT - Items by Professions
SUBSECTION=Chaveiro
DESCRIPTION=Chave mestra
SKILLMAKE=TINKERING 80.0, MAGERY 50.0,t_tinker_tools
RESOURCES=5 i_ingot_gold, 5 i_ingot_silver, 5 i_ingot_copper

ON=@Create
	ATTR=attr_magic|attr_newbie

on=@dclick
if (<topobj.uid> != <src.uid>)
	src.sysmessage Coloque a chave na bolsa antes de usa-la.
return 1
endif

on=@targon_item
if (<link.type>==t_ship) || (<link.type>==t_multi)
elif (<src.targ.distance> > 1)
   src.sysmessage Chegue mais perto.
   target 
return 1
elif (<src.targ.type> == t_door_lock) //BUG!! || (<src.targ.type> == t_container_lock)
	src.targ.type> == t_door
	src.sysmessage Voce destrancou a porta.
return 1
elif (<src.targ.type> == t_door)
	src.targ.type> == t_door_lock
	src.sysmessage Voce trancou a porta.
return 1
elif (<src.targ.type> == t_container_lock)
	src.targ.type> == t_container
	src.sysmessage Voce destrancou o container.
elif (<src.targ.type> == t_container)
	if (<src.targ.baseid>==i_basket_bushel) || (<src.targ.baseid>==i_pouch) || (<src.targ.baseid>==i_basket_2) || (<src.targ.baseid>==i_backpack) || (<src.targ.baseid>==i_bag) || (<src.targ.baseid>==i_barrel_open) || (<src.targ.baseid>==i_basket_round) || (<src.targ.baseid>==i_basket_square) || (<src.targ.baseid>==i_tub_empty) || (<src.targ.baseid>==i_keg_small) || (<src.targ.baseid>==i_basin)
		src.sysmessage Nao e possivel trancar isto.
	return 1
	else
		src.targ.type> == t_container_lock
		src.sysmessage Voce trancou o container.
	return 1
	endif
return 1
endif


////////////////////////////////////////////////////////////////
/////////////  Memory instalando fechadura  ////////////////////
////////////////////////////////////////////////////////////////

[ITEMDEF i_mry_install_fechadura]
id=i_memory
NAME=Instalando Fechadura
TYPE=T_eq_script
WEIGHT=0

ON=@Create
	ATTR=attr_newbie
	attr=(<attr> | 080 )
	more1=0  // determina o tipo de fechadura: 1=simples, 2=normal, 3=complexa
	more2=0  // counter


ON=@TIMER
if !(<cont.p.x> == <morex>) || !(<cont.p.y> == <morey>) //!(<cont.p> == <morep>)
	cont.sysmessage Voce interrompeu a instalacao
	cont.newitem=i_fechadura
	cont.act.more1=<more1>
	cont.act.p=<cont.p>
	cont.act.cont=<cont> // bug do bounce
	if (<more1>==1)
		cont.act.name=Fechadura simples
	elif (<more1>==2)
		cont.act.name=Fechadura normal  //tag.tipofechadura=normal
	elif (<more1>==3)
		cont.act.name=Fechadura complexa
	endif
	cont.act.update
	remove
return 1
elif (<more2>==20) && (<more1>==1) // se é normal
	cont.emote Terminou de instalar a fechadura
	cont.newitem=i_key_copper
	cont.act.cont=<cont> // bug do bounce
	cont.act.more1=<link>   // coloca no more1 da chave o uid da porta
	link.more1=<link.uid>   // coloca no more1 da porta a uid da porta
	link.tag.fechadura=simples
	ganho_skill_chaveiro
	remove
return 1
elif (<more2>==30) && (<more1>==2) // se é reforcada
	cont.emote Terminou de instalar a fechadura
	cont.newitem=i_key_iron
	cont.act.cont=<cont> // bug do bounce
	cont.act.more1=<link>   // coloca no more1 da chave o uid da porta
	link.more1=<link.uid>   // coloca no more1 da porta a uid da porta
	link.tag.fechadura=normal
	ganho_skill_chaveiro
	remove
return 1
elif (<more2>==40) && (<more1>==3) // se é complexa
	cont.emote Terminou de instalar a fechadura
	cont.newitem=i_key_gold
	cont.act.cont=<cont> // bug do bounce
	cont.act.more1=<link>   // coloca no more1 da chave o uid da porta
	link.more1=<link.uid>   // coloca no more1 da porta a uid da porta
	link.tag.fechadura=complexa
	ganho_skill_chaveiro
	remove
return 1
else
 	dorand 6
	  cont.emote Instalando uma fechadura
	  cont.anim 020
	  sfx 1449
	enddo
	timer=1
	more2=<more2>+1
return 1
endif
return 1

////////////////////////////////////////////////////////////

[FUNCTION ganho_skill_chaveiro]
if (<cont.tinkering>>40.0)
 	dorand 2
	cont.tinkering=<cont.tinkering>+(0.1)
	enddo
return 1
elif (<cont.tinkering>>50.0)
 	dorand 3
	cont.tinkering=<cont.tinkering>+(0.1)
	enddo
return 1
elif (<cont.tinkering>>60.0)
 	dorand 4
	cont.tinkering=<cont.tinkering>+(0.1)
	enddo
return 1
endif
if (<cont.lockpicking>>40.0)
 	dorand 2
	cont.lockpicking=<cont.lockpicking>+(0.1)
	enddo
return 1
elif (<cont.lockpicking>>50.0)
 	dorand 3
	cont.lockpicking=<cont.lockpicking>+(0.1)
	enddo
return 1
elif (<cont.lockpicking>>60.0)
 	dorand 4
	cont.lockpicking=<cont.lockpicking>+(0.1)
	enddo
return 1
endif
return 1

////////////////////////////////////////////////////////////
/////////////////////  Lockpick  //////////////////////////
//////////////////////////////////////////////////////////

[ITEMDEF 014fc]
//lockpick
DUPEITEM=014fb

[ITEMDEF 014fb]
DEFNAME=i_lockpick
NAME=Chave micha
TYPE=t_script//T_LOCKPICK
FLIP=1
RESOURCES=1 i_ingot_iron
WEIGHT=.5
CATEGORY=MyT - Items by Professions
SUBSECTION=Chaveiro
DESCRIPTION=Chave micha
DUPELIST=014fc
SKILLMAKE=TINKERING 48.5,t_tinker_tools

on=@dclick
if (<topobj.uid> != <src.uid>)
	src.sysmessage Coloque a micha na bolsa antes de usa-la.
return 1
else
   target Selecione a porta ou o container com fechadura ou cadeado.
return 1
endif

ON=@targon_char
   src.sysmessage Use isto em uma fechadura ou cadeado.
   target Selecione a porta ou o container com fechadura ou cadeado.
return 1

ON=@targon_item
if (<src.targ.distance> > 1)
   src.sysmessage Chegue mais perto.
   target Selecione a porta ou o container com fechadura ou cadeado.
return 1
elif (<src.targ.type>==t_trap)
   src.targ.use
return 1
elif (<src.targ.type>==t_door) || (<src.targ.type>==t_container)
   src.sysmessage isto nao esta trancado.
return 1
elif !(<src.targ.attr>&attr_stolen)
   src.sysmessage Esta fechadura eh absurdamente complexa para sua habilidade.
return 1
elif (<src.targ.type>==t_door_locked) || (<src.targ.type>==t_container_locked)
	if (strmatch(<src.targ.tag.fechadura>,complexa))
		src.sysmessage Nao e possivel tentar abrir esta fechadura com esta Chave micha. Use um kit de chave micha.
	return 1
	elif (<src.lockpicking> < 30.0) && (strmatch(<src.targ.tag.fechadura>,simples))
		src.sysmessage Voce olha para a fechadura e percebe que ela e muito complexa para suas habilidades.
   	return 1
	elif (<src.lockpicking> < 40.0) && (strmatch(<src.targ.tag.fechadura>,normal))
		src.sysmessage Voce olha para a fechadura e percebe que ela e muito complexa para suas habilidades.
   	return 1
	elif (strmatch(<src.targ.tag.fechadura>,simples)) || (strmatch(<src.targ.tag.fechadura>,normal)) || (strmatch(<src.targ.tag.fechadura>,cadeado))
		src.emote Comecou a tenta abrir a fechadura ou cadeado
		src.anim 020
		sfx 577
		src.newitem=i_mry_lockpick_fechadura
		//src.act.more1=<more1>		// tipo de fechadura a ser instalada: 1=simples, 2=normal, 3=complexa
		src.act.morep=<src.p>		// marca o local do src. se ele se mover para o script
		src.act.more2=0			// timer da memory
		src.act.link=<src.targ.uid>	// linka a memory a porta
		src.act.timer=1
		src.act.equip
		src.update
		//remove
	endif

else
   src.sysmessage Nao ha fechadura ou cadeado neste local.
endif
return 1

///////////////////////////////////////////////////////

[ITEMDEF 014fe]
//lockpicks
DUPEITEM=014fd

[ITEMDEF 014fd]
DEFNAME=i_lockpick_set
NAME=Kit de chaves micha
TYPE=t_script//T_LOCKPICK
FLIP=1
RESOURCES=3 i_LOCKPICK
WEIGHT=1
CATEGORY=MyT - Items by Professions
SUBSECTION=Chaveiro
DESCRIPTION=Kit de chaves micha
DUPELIST=014fe

on=@dclick
if (<topobj.uid> != <src.uid>)
	src.sysmessage Coloque a micha na bolsa antes de usa-la.
return 1
else
   target Selecione a porta ou o container com fechadura ou cadeado.
return 1
endif

ON=@targon_char
   src.sysmessage Use isto em uma fechadura ou cadeado.
   target Selecione a porta ou o container com fechadura ou cadeado.
return 1

ON=@targon_item
if (<src.targ.distance> > 1)
   src.sysmessage Chegue mais perto.
   target Selecione a porta ou o container com fechadura ou cadeado.
return 1
elif (<src.targ.type>==t_trap)
   src.targ.use
return 1
elif (<src.targ.type>==t_door) || (<src.targ.type>==t_container)
   src.sysmessage isto nao esta trancado.
return 1
elif !(<src.targ.attr>&attr_stolen)
   src.sysmessage Esta fechadura eh muito absurdamente complexa para sua habilidade.
return 1
elif (<src.targ.type>==t_door_locked) || (<src.targ.type>==t_container_locked)
   if (<src.lockpicking> < 50.0)
	src.sysmessage Voce olha para a fechadura e percebe que ela e muito complexa para suas habilidades.
   return 1
   else
	src.emote Comecou a tenta abrir a fechadura ou cadeado
	src.anim 020
	sfx 577
	src.newitem=i_mry_lockpick_fechadura
	//src.act.more1=<more1>		// tipo de fechadura a ser instalada: 1=simples, 2=normal, 3=complexa
	src.act.morep=<src.p>		// marca o local do src. se ele se mover para o script
	src.act.more2=0			// timer da memory
	src.act.link=<src.targ.uid>	// linka a memory a porta
	src.act.timer=1
	src.act.equip
	src.update
	//remove
   endif
else
   src.sysmessage Nao ha fechadura ou cadeado neste local.
endif
return 1

////////////////////////////////////////////////////////////////
/////////////  Memory arrombando fechadura  ////////////////////
////////////////////////////////////////////////////////////////

[ITEMDEF i_mry_lockpick_fechadura]
id=i_memory
NAME=Instalando Fechadura
TYPE=T_eq_script
WEIGHT=0

ON=@Create
	ATTR=attr_newbie
	attr=(<attr> | 080 )
	more1=0  	// vai fazer a operação que resultará na probabilidade de acerto
	more2=0  	// counter

ON=@TIMER
if !(<cont.p.x> == <morex>) || !(<cont.p.y> == <morey>)
	cont.sysmessage Voce interrompeu a tentativa de abrir a fechadura ou cadeado.
	remove
return 1
endif
 	dorand 6
	  cont.anim 020
	  sfx 577
	enddo
	timer=1
	more2=<more2>+1
	if (<more2> == 1)
		cont.sysmessage Voce introduz a micha na fechadura com cuidado.
	elif (<more2> == 10)
		cont.sysmessage Voce gira lentamente a micha procurando a fenda da tranca.
	elif (<more2> == 20)
		cont.sysmessage Voce encosta a micha na tranca e comeca a fazer pressao para tentar destravar.
	elif (<more2> == 30)
		cont.sysmessage Voce posiciona a micha na fenda oposta para forcar a trava a soltar.
	elif (<more2> == 40)
		cont.sysmessage Voce comeca a cutucar a fenda do fundo para soltar a trava principal
	elif (<more2> == 50)
		cont.sysmessage Voce tenta soltar a trava secundaria enquanto mantem a trava principal aberta.
	elif (<more2> == 60)
		cont.sysmessage Voce gira a micha para destravar a trava oposta enquanto mantem a trava principal e secundaria abertas.
	endif
	ganho_skill_lockpick
//	cont.say LOOP!  	// DEBUG
if (strmatch(<link.tag.fechadura>,cadeado)) && (<more2>>=10)
  dorand <eval (101 + (-((<cont.lockpicking>)/(10))))/(5) >
     begin
	cont.message *Conseguiu abrir o cadeado*
	cont.newitem=i_cadeado
	cont.act.name=Cadeado quebrado
	cont.act.cont=<cont> 	// bug do bounce
	cont.act.more2=1   	// quebra o cadeado, tornando-o inutilizável
	link.link=04fffffff	// tira o link da porta
	f_unlock <link>		// troca o type, testa se precisa retrancar, põe o timer pra trancar.
	ganho_skill_lockpick_fim
	link.tag.fechadura=	// apaga a tag da fechadura
	sfx 511
	remove
	return 1
     end
     begin
	cont.sysmessage A Micha escapou da fechadura. Tente novamente
	remove
	return 1
     end
  enddo
elif (strmatch(<link.tag.fechadura>,simples)) && (<more2>>=20)
  dorand <eval (131 + (-((<cont.lockpicking>)/(10))))/(4) >
     begin
	cont.message *Conseguiu abrir a fechadura*
	f_unlock <link.uid>
	endif
	sfx 511
	ganho_skill_lockpick_fim
	remove
     end
     begin
	cont.sysmessage A Micha escapou da fechadura. Tente novamente
	remove
	return 1
     end
  enddo
elif (strmatch(<link.tag.fechadura>,normal)) && (<more2>>=40)
  dorand <eval (141 + (-((<cont.lockpicking>)/(10))))/(3) >
     begin
	cont.message *Conseguiu abrir a fechadura*
	f_unlock <link.uid>
	endif
	sfx 511
	ganho_skill_lockpick_fim
	remove
     end
     begin
	cont.sysmessage A Micha escapou da fechadura. Tente novamente
	remove
	return 1
     end
  enddo
elif (strmatch(<link.tag.fechadura>,complexa)) && (<more2>>=60)
  dorand <eval (151 + (-((<cont.lockpicking>)/(10))))/(2) >
     begin
	cont.message *Conseguiu abrir a fechadura*
	f_unlock <link.uid>
	sfx 511
	ganho_skill_lockpick_fim
	remove
     end
     begin
	cont.sysmessage Uma das michas escapou da fechadura. Tente novamente
	remove
	return 1
     end

     begin
	cont.sysmessage Uma das michas escapou da sua mao e travou a porta. Tente novamente
	remove
	return 1
     end

  enddo
endif
return 1


////////////////////////////////////////////////////////////

[FUNCTION ganho_skill_lockpick]
if (<cont.lockpicking><40.0)
 	dorand 30
	cont.lockpicking=<cont.lockpicking>+(0.1)
	enddo
return 1
elif (<cont.lockpicking><60.0)
 	dorand 35
	cont.lockpicking=<cont.lockpicking>+(0.1)
	enddo
return 1
elif (<cont.lockpicking><80.0)
 	dorand 40
	cont.lockpicking=<cont.lockpicking>+(0.1)
	enddo
return 1
elif (<cont.lockpicking><100.0)
 	dorand 45
	cont.lockpicking=<cont.lockpicking>+(0.1)
	enddo
return 1
endif
return 1

//////////////////////////////////////////////////////////

[FUNCTION ganho_skill_lockpick_fim]
if (<cont.lockpicking><40.0)
 	dorand 4
	cont.lockpicking=<cont.lockpicking>+(4)
	enddo
return 1
elif (<cont.lockpicking><60.0)
 	dorand 6
	cont.lockpicking=<cont.lockpicking>+(2)
	enddo
return 1
elif (<cont.lockpicking><80.0)
 	dorand 8
	cont.lockpicking=<cont.lockpicking>+(1)
	enddo
return 1
elif (<cont.lockpicking><100.0)
 	dorand 10
	cont.lockpicking=<cont.lockpicking>+(0.5)
	enddo
return 1
endif
return 1

[FUNCTION f_unlock]
//Destranca a porta/container e retranca de for attr_owned
obj=<argn>
if (<obj.attr>&attr_owned)		//É público. Precisa retrancar.
 serv.newitem i_lockpick_reset
 new.tag.type=<obj.type>
 new.tag.fechadura=<obj.tag.fechadura>
 new.link=<obj.uid>
 new.timer=60*35		//35 minutos pra retrancar ou até um GM dar dclick na Worldgem Bit
 new.p=<obj.p>
 new.update
// new.emotered <obj.name> em <new.p>
endif
if (<obj.type>==t_door_locked)
 obj.type=t_door
elif (<obj.type>==t_container_locked)
 obj.type=t_container
endif
obj.update

[ITEMDEF i_lockpick_reset]
NAME=reset de tranca
ID=i_worldgem_bit
TYPE=t_script

on=@create
attr=092
color=015

on=@timer
if !(STRMATCH(<tag.fechadura>,))	//Tinha fechadura?
 link.tag.fechadura=<tag.fechadura>	//Repor fechadura
endif
link.type=<tag.type>
//link.emotered tranca
link.update
remove
return 1

on=@dclick
src.sysmessage <link.name> trancado(a).
timerd 1
return 1

[PLEVEL 2]
fechadura

[FUNCTION fechadura]
src.sysmessage Selecione porta/container:
targetf _fechadura

[FUNCTION _fechadura]
obj=<argo>
if (<obj.type>!=t_door) && (<obj.type>!=t_door_locked) && (<obj.type>!=t_container) && (<obj.type>!=t_container_locked)
 sysmessageyellow Apenas portas ou containeres.
 return 1
endif
cTAG.trancaUID=<obj.uid>
if (STRMATCH(<obj.tag.fechadura>,))	//Não tem tranca?
 cTAG.trancaTipo=Nenhuma
else
 cTAG.trancaTipo=<obj.tag.fechadura>
endif
menu m_gm_tranca

[MENU m_gm_tranca]
(MyT] Tranca de <UID.<cTAG.trancaUID>.name> [MyT)
on=0 Tipo de fechadura: <cTAG.trancaTipo>
if (STRMATCH(<cTAG.trancaTipo>,Nenhuma)
 cTAG.trancaTipo=cadeado
elif (STRMATCH(<cTAG.trancaTipo>,cadeado)
 cTAG.trancaTipo=simples
elif (STRMATCH(<cTAG.trancaTipo>,simples)
 cTAG.trancaTipo=normal
elif (STRMATCH(<cTAG.trancaTipo>,normal)
 cTAG.trancaTipo=complexa
elif (STRMATCH(<cTAG.trancaTipo>,complexa)
 cTAG.trancaTipo=Nenhuma
endif
UID.<cTAG.trancaUID>.tag.fechadura=<QVAL STRMATCH(<cTAG.trancaTipo>,Nenhuma)? :<cTAG.trancaTipo>>
sysmessagegreen A fechadura de <UID.<cTAG.trancaUID>.name> foi alterada para <cTAG.trancaTipo>.
menu m_gm_tranca

on=0 Auto-Rearmavel: <QVAL <UID.<cTAG.trancaUID>.attr>&attr_owned ? Sim : Nao>
IF (<UID.<cTAG.trancaUID>.attr>&attr_owned)
 UID.<cTAG.trancaUID>.attr=<UID.<cTAG.trancaUID>.attr>&~attr_owned
else
 UID.<cTAG.trancaUID>.attr=<UID.<cTAG.trancaUID>.attr>|attr_owned
endif
menu m_gm_tranca

on=0 Lockpick permitido: <QVAL <UID.<cTAG.trancaUID>.attr>&attr_stolen ? Sim : Nao>
IF (<UID.<cTAG.trancaUID>.attr>&attr_stolen)
 UID.<cTAG.trancaUID>.attr=<UID.<cTAG.trancaUID>.attr>&~attr_stolen
else
 UID.<cTAG.trancaUID>.attr=<UID.<cTAG.trancaUID>.attr>|attr_stolen
endif
menu m_gm_tranca

on=0 Copiar chave (codigo: <UID.<cTAG.trancaUID>.more1>)
LOCAL.key=<f_copykey <cTAG.trancaUID>>
if (<LOCAL.key>)
 sysmessageyellow Chave copiada com codigo <UID.<LOCAL.key>.more1>
else
 sysmessageyellow <UID.<cTAG.trancaUID>.name> nao tem uma tranca!
endif
menu m_gm_tranca

on=0 Gerar chave (CUIDADO! Troca o codigo)
LOCAL.key=<f_makekey <cTAG.trancaUID>>
if (<LOCAL.key>)
 sysmessageyellow Codigo trocado para <UID.<LOCAL.key>.more1>.
else
 sysmessageyellow <UID.<cTAG.trancaUID>.name> nao tem uma tranca!
endif
menu m_gm_tranca


[FUNCTION f_makekey]
//Faz uma chave para o container/porta de UID <argv[0]>. Também usado no sistema de aluguel e artesão.
//Retonra a UID da chave (para efeitos bounce, p= e etc...)
obj=<argv[0]>
LOCAL.tranca=<obj.tag.fechadura>
//Para se não tiver tranca.
if (STRMATCH(<LOCAL.tranca>,))
 return 0
endif
//cria a chave apropriada para a tranca
if (STRMATCH(<LOCAL.tranca>,cadeado)
 serv.newitem=i_key_rusty
elif (STRMATCH(<LOCAL.tranca>,simples)
 serv.newitem=i_key_copper
elif (STRMATCH(<LOCAL.tranca>,normal)
 serv.newitem=i_key_iron
elif (STRMATCH(<LOCAL.tranca>,complexa)
 serv.newitem=i_key_gold
endif
//Gera um código pra tranca.
LOCAL.code=<eval {01000 0ffff}>
new.more1=<LOCAL.code>
obj.more1=<LOCAL.code>
new.attr=<new.attr>|attr_newbie
new.bounce
return <new.uid>

[FUNCTION f_copykey]
//Copia uma chave a partir de um container/porta ou de outrao chave.
//Retonra a UID da chave (para efeitos bounce, p= e etc...)
obj=<argv[0]>
// É uma chave? duplique.
if (<obj.type>==t_key)
 serv.newitem <obj.baseid>
else
//É um container/porta. Crie uma chave.
 LOCAL.tranca=<obj.tag.fechadura>
 //Para se não tiver tranca.
 if (STRMATCH(<LOCAL.tranca>,))
  return 0
 endif
 //cria a chave apropriada para a tranca
 if (STRMATCH(<LOCAL.tranca>,cadeado)
  serv.newitem=i_key_rusty
 elif (STRMATCH(<LOCAL.tranca>,simples)
  serv.newitem=i_key_copper
 elif (STRMATCH(<LOCAL.tranca>,normal)
  serv.newitem=i_key_iron
 elif (STRMATCH(<LOCAL.tranca>,complexa)
  serv.newitem=i_key_gold
 endif
endif
//Copia o dodigo da tranca
new.more1=<obj.more1>
new.attr=<new.attr>|attr_newbie
new.bounce
return <new.uid>
[EOF]