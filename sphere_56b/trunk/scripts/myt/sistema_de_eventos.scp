//CREATE TABLE  `mytserver`.`accounts` (
//  `idAcct` int(10) unsigned NOT NULL auto_increment,
//  `username` varchar(45) NOT NULL,
//  PRIMARY KEY  (`idAcct`),
//  UNIQUE KEY `byUsername` (`username`)
//) ENGINE=MyISAM DEFAULT CHARSET=latin1;

//CREATE TABLE `mytserver`.`eventsLog` (
//  `idEvent` INTEGER UNSIGNED NOT NULL AUTO_INCREMENT,
//  `timestamp` TIMESTAMP NOT NULL,
//  `charUid` INTEGER UNSIGNED NOT NULL,
//  `charAcct` INTEGER UNSIGNED NOT NULL,
//  `eventType` ENUM('Kill PC','Kill NPC','Killed by PC','Killed by NPC','Login','Logout','GM Note','Jail') NOT NULL,
//  `char2Uid` INTEGER UNSIGNED NOT NULL,
//  `char2Acct` INTEGER UNSIGNED NOT NULL,
//  `x` SMALLINT UNSIGNED NOT NULL,
//  `y` SMALLINT UNSIGNED NOT NULL,
//  `z` TINYINT NOT NULL,
//  `message` VARCHAR(255) NOT NULL,
//  PRIMARY KEY (`idEvent`),
//  INDEX `byTs`(`timestamp`),
//  INDEX `byUid`(`charUid`),
//  INDEX `byAcct`(`charAcct`)
//)
//ENGINE = MyISAM;

//*****************************************************************************
// event_log( type )
//*****************************************************************************
//log de um evento generico realizado pelo SRC
[FUNCTION event_log]
DB.QUERY "INSERT INTO eventsLog SET timestamp=NOW(), eventType='<argv0>',charUid=<eval <src>>,charAcct=<eval <src.tag0.idAcct>>,x=<eval <src.p.x>>,y=<eval <src.p.y>>,z=<eval <src.p.z>>"

//*****************************************************************************
// event_login()
//*****************************************************************************
//cria conta na db se nao existe e loga um evento de login
[FUNCTION event_login]
if <DB.CONNECTED>
    DB.QUERY "INSERT IGNORE INTO accounts SET username='<src.account>'"
    DB.QUERY "SELECT idAcct FROM accounts WHERE username='<src.account>'"
    if (<DB.ROW.NUMROWS> > 0)
        SRC.TAG.idAcct=<DB.ROW.0.idAcct>
        serv.log [EVENT]Login: <src.account> (<SRC.TAG.idAcct>)
        event_log Login
    else
        serv.log [EVENT]Login: id not found (<src.account>)
    endif
endif

//*****************************************************************************
// event_logout()
//*****************************************************************************
//loga um evento de logout
[FUNCTION event_logout]
if <DB.CONNECTED>
    event_log Logout  
endif

//*****************************************************************************
// event_kill()
//*****************************************************************************
//loga um evento de kill
[FUNCTION event_kill]
if <DB.CONNECTED>
    
    //se quem matou eh player
    if <src.isplayer>
        //se quem morreu eh player
        if <src.act.isplayer>
            LOCAL.e=Kill PC
            LOCAL.ku=<src>
            LOCAL.ka=<src.tag0.idAcct>
            LOCAL.du=<src.act>
            LOCAL.da=<src.act.tag0.idAcct>
        else
            LOCAL.e=Kill NPC
            LOCAL.ku=<src>
            LOCAL.ka=<src.tag0.idAcct>
            LOCAL.du=0
            LOCAL.da=0
            LOCAL.m=<src.act.id>
        endif
    //se quem matou eh npc
    else        
        //se quem morreu eh player
        if <src.act.isplayer>
            LOCAL.e=Kill PC
            LOCAL.ku=0
            LOCAL.ka=0
            LOCAL.du=<src.act>
            LOCAL.da=<src.act.tag0.idAcct>
            LOCAL.m=<src.id>
        else
            //npc kill npc nao interessa
            return
        endif
    endif
    
    DB.QUERY "INSERT INTO eventsLog SET timestamp=NOW(), eventType='<LOCAL.e>',charUid=<eval <LOCAL.ku>>,charAcct=<eval <LOCAL.ka>>,x=<eval <src.p.x>>,y=<eval <src.p.y>>,z=<eval <src.p.z>>,char2Uid=<eval <LOCAL.du>>,char2Acct=<eval <LOCAL.da>>,message='<LOCAL.m>'"
endif

//*****************************************************************************
// event_note(uidToNote,Message)
//*****************************************************************************
//loga um evento de GM Note 
[FUNCTION event_note]
OBJ=<argv0>
if <DB.CONNECTED>
    DB.QUERY "INSERT INTO eventsLog SET timestamp=NOW(), eventType='GM Note',charUid=<eval <src>>,charAcct=<eval <src.tag0.idAcct>>,x=<eval <obj.p.x>>,y=<eval <obj.p.y>>,z=<eval <obj.p.z>>,char2Uid=<eval <obj>>,char2Acct=<obj.tag0.idAcct>,message='<DB.ESCAPEDATA <argv1>>'"
endif
