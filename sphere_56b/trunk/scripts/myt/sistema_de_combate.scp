//*****************************************************************************
//*****************************************************************************
//
// SISTEMA DE COMBATE por
//	Kell
//	Galthar
//	Silver
//	UsMarine (vinicius.arroyo at gmail.com)
//
//*****************************************************************************
//*****************************************************************************
//TODO:

//@HIT
//
//Verifica Parrying - (50%*skill)[%]*(1-agressividade_defensor)[%]
//  Danifica Escudo
//  Fim
//Tenta esquivar - ( 130% tactics + 200% dex + (-250 a 500)[agressividade] )*35%
//  Esquiva
//  Fim
//Tenta envenenar - (atacante é animal venenoso && poisoning > 30% && nao envenenado)
//  Envenena com 10% poisoning e duração de 20 a 30 ataques.
//Tenta envenenar - (armas envenenadas && ( 50% swords || 50% fencing || 76% poison ) )
//  Envenena com spell poison, 30% de skill.
//Tenta queimar - (armas com fogo)
//  Queima (fire collumn) - 3 a 6 de dano
//Ajusta dano
//  STR 
//    >= 95% +3
//    >= 90% +2
//    >= 85% +1
//  ANATOMY
//    >= 95% +3
//    >= 75% +2
//    >= 50% +1
//  ATACANTE ENVENENADO - 25% chance 
//    - (more2/15)
//  STAMINA
//    MAX   +1
//    <= 5  -2
//    <= 15 -1
//  PESO
//    >= 1500 -3
//    >= 1000 -2
//    >= 750  -1
//  AGRESSIVIDADE
//    9,10  +2
//    7,8   +1
//    4,5,6  0
//    2,3   -1
//    1     -5
//    0    -10
//  ARMADURA ALVO
//    - { AR/6 AR/3 }
//  QUALIDADE DA ARMA DE ATAQUE
//    >= 90%  0
//    >= 80% -1
//    >= 60% -2
//    >= 30% -3
//    else   -4 
//  SKILL DE ATAQUE (afeta dano original: argn1)
//    WRESTLING
//      < 30%  70% + 1
//      < 40%  80% + 1
//      < 50%  90% + 1
//      < 60% 100% + 1
//      < 70% 110% + 1
//      < 80% 120% + 1
//    FENCING
//      < 20%   60% + 1
//      < 30%   65% + 1
//      < 40%   80% + 1
//      < 50%   90% + 1
//      < 60%  100% + 1
//      < 70%  110% + 1
//      < 80%  120% + 1
//      < 90%  135% + 1
//      < 100% 150% + 1
//      = 100% 170% + 1
//    SWORDSMANSHIP
//      < 10%   30% + 1
//      < 20%   45% + 1
//      < 30%   60% + 1
//      < 40%   75% + 1
//      < 50%   90% + 1
//      < 60%  100% + 1
//      < 70%  110% + 1
//      < 80%  120% + 1
//      < 90%  130% + 1
//      < 100% 140% + 1
//      = 100% 155% + 1
//    MACEFIGHTING
//      < 10%   60% + 1
//      < 20%   60% + 1
//      < 30%   70% + 1
//      < 40%   80% + 1
//      < 50%   90% + 1
//      < 60%  100% + 1
//      < 70%  110% + 1
//      < 80%  120% + 1
//      < 90%  120% + 1
//      < 100% 130% + 1
//      = 100% 130% + 1
//    ARCHERY
//      < 10%   50% + 1
//      < 20%   70% + 1
//      < 30%   90% + 1
//      < 40%  100% + 1
//      < 50%  100% + 1
//      < 60%  100% + 1
//      < 70%  100% + 1
//      < 80%  110% + 1
//      < 90%  115% + 1
//      < 100% 120% + 1
//      = 100% 125% + 1
//
//Define local do ataque ( rand 0 a 10% tactics )
//  torax
//  torax
//  mao
//  braco
//  perna
//  perna
//  torax
//  torax
//  pescoco
//  cabeca
//  cabeca
//
//Danifica armadura protegendo local do ataque - 1 a 4
//Danifica armas utilizadas - 0 a 1
//Emite sons
//Cansa atacante: - 0 a 2 de stamina
//Desconcentra vitima: - 0 a 2 de mana
//Aplica dano


// POISON por magia: i_veneno.MORE2 = 50% magery repeticoes e 7% magery dano
// HEAL POISON por magia: i_veneno.MORE2 -= 33% magery
// POISON por pocao: pocao.MORE2 = (890,1000,1500,2000) (fraca,normal,concentrada,forte)
//   flecha envenenada: dano constante: i_poison_arrow
//   arma envenenada: 4,5% more2 e 1,5% na recarga (recargas de dano constante:2)
//   alvo envenenado: 5% more2 repeticoes e 0.71% more2 dano
//   chao envenenado: 2,5% more2 repeticoes e 0.36% more2 dano
// POISON NPC: i_veneno.MORE2 = ( 10% poisoning ) repeticoes e 1 a ( 10% poisoning * 14%) 

//TAGS usadas:
//tag.agr	nível de agressividade do combate (0 a 10)

//tag.fRP	pontos de rp
//tag.lastkillname nome da ultima vitima fatal
//tag.lastkillUID  uid da ultima vitima fatal
//tag.fpk	contador de vitimas fatais
//tag.lastagr2uid  uid do penultimo agressor
//tag.lastagr2name nome do penultimo agressor
//tag.lastagr1uid  uid do ultimo agressor
//tag.lastagr1name nome do ultimo agressor

//item.tag.name nome original do item antes de ser envenenado
//*****************************************************************************

[PLEVEL 1]
combate

//*****************************************************************************
//*****************************************************************************
// EVENTs
//*****************************************************************************
//*****************************************************************************
[EVENTS e_combat] 

//*************************************
// @HITTRY
//*************************************
ON=@HITTRY
//sai do modo batalha se alvo invul, morto, desmaiado, escondido, etc
if ( <src.flags>&(statf_invul|statf_dead|statf_invisible|statf_insubstantial|statf_stone|statf_hidden)) || (<src.restest 1 i_mry_desmaio>)
 flags=<flags>&~statf_war
 update
 return 1
endif

//*************************************
// @HITMISS - somente evita mensagem de miss
//*************************************
ON=@HITMISS
return 1

//*************************************
// @HIT
//*************************************
ON=@HIT
//MORE1H = maxItemHits
//MORE1L = itemHits

///////////////////////////////////////////////
////////////Tentar aparar com escudo///////////
///////////////////////////////////////////////

if (<src.findlayer.layer_hand2.type>==t_shield)
 LOCAL.escudo=<src.parrying>/2
 if ({1 1000} <= <LOCAL.escudo>) && (RAND(<src.tag0.agr>)==0)
   if (<NPC>==0) // se não for NPC...
   sysmessageyellow <src.name> apara seu golpe com o escudo!
  endif
  if (<src.NPC>==0) // Se o defensor não for NPC...
   src.sysmessageyellow Voce apara o golpe de <name> com o escudo!
  endif

  //danifica escudo
  IF (<action> == Skill_Wrestling)
   src.findlayer.layer_hand2.more1l=<src.findlayer.layer_hand2.more1l>-{1 2}
  ELIF (<action> == Skill_Swordsmanship) || (<action> == Skill_Fencing) 
   src.findlayer.layer_hand2.more1l=<src.findlayer.layer_hand2.more1l>-{1 2}
  ELIF (<action> == Skill_Macefighting)
   src.findlayer.layer_hand2.more1l=<src.findlayer.layer_hand2.more1l>-{2 4}
  ELIF (<action> == Skill_Archery)
   src.findlayer.layer_hand2.more1l=<src.findlayer.layer_hand2.more1l>-1
  endif

//  src.sysmessageorange <src.findlayer.layer_hand2.more1l> / <src.findlayer.layer_hand2.more1h>
  
  //caso escudo destruido
  if (0<src.findlayer.layer_hand2.more1l> > 08000) && !(<src.findlayer.layer_hand2.attr> & attr_newbie)
   if (<NPC>==0) // se não for NPC...
    sysmessageorange O escudo de <src.name> foi totalmente arruinado!
   endif
   if (<src.NPC>==0) // Se o defensor não for NPC...
    src.sysmessageorange Seu escudo foi totalmente arruinado!
   endif
   src.findlayer.layer_hand2.remove
  endif
  //som e animacao
  dorand 4
   sfx 0237
   sfx 0236
   sfx 023c
   sfx 023b
  enddo
  anim 19
  return 1
 endif
endif


///////////////////////////////////////////////
////////////Tentar esquivar o golpe////////////
///////////////////////////////////////////////
LOCAL.esquiva=<eval (((<src.tactics>/15)*2)+(<src.dex>*2))>

//&& (<src.npc>==0)
if (<src.tag0.agr>==0) 
 LOCAL.esquiva=<eval <LOCAL.esquiva>+500>
elif (<src.tag0.agr>==1)
 LOCAL.esquiva=<eval <LOCAL.esquiva>+400>
elif (<src.tag0.agr>==2)
 LOCAL.esquiva=<eval <LOCAL.esquiva>+300>
elif (<src.tag0.agr>==3)
 LOCAL.esquiva=<eval <LOCAL.esquiva>+200>
elif (<src.tag0.agr>==4)
 LOCAL.esquiva=<eval <LOCAL.esquiva>+100>
elif (<src.tag0.agr>==5)
// LOCAL.esquiva=<eval <LOCAL.esquiva>+000>
elif (<src.tag0.agr>==6)
 LOCAL.esquiva=<eval <LOCAL.esquiva>-50>
elif (<src.tag0.agr>==7)
 LOCAL.esquiva=<eval <LOCAL.esquiva>-100>
elif (<src.tag0.agr>==8)
 LOCAL.esquiva=<eval <LOCAL.esquiva>-150>
elif (<src.tag0.agr>==9)
 LOCAL.esquiva=<eval <LOCAL.esquiva>-200>
elif (<src.tag0.agr>==10)
 LOCAL.esquiva=<eval <LOCAL.esquiva>-250>
endif

//quando o atacado for um player ou npc
if (({1 2400} <= <eval <LOCAL.esquiva>>)
 if (<NPC>==0) && (<action> != Skill_Archery) // se não for NPC e nem desvia-se de flechas como o Neo...
  sysmessageyellow <src.name> esquiva-se de seu golpe!
 endif
 if (<src.NPC>==0) && (<action> != Skill_Archery) // Se o defensor não for NPC e nem desvia-se de flechas como o Neo......
  src.sysmessageyellow Voce consegue se esquivar do golpe!
 endif
 //som e animação
 dorand 5
  sfx 0239
  sfx 023a
  sfx 0233
  sfx 0238
  sfx 0232
 enddo 
 anim 18
 return 1
endif

///////////////////////////////////////////////
//Tentar usar Poison///////////////////////////
///////////////////////////////////////////////

// Se eu for um animal venenoso e a minha presa não está envenenada...
if (<poisoning> >= 300) && (<NPC>!=0) && (!<src.ispoisoned>)
 if ({1 1000} < <poisoning>) && (RAND(4)==0)
  src.veneno <eval <poisoning>/10>
  emotered morde profundamente
 endif
endif

//ARMAS ENVENENADAS
//MORE2=020
//MOREX=carga de veneno
//Se a arma na HAND1 estiver envenenada // && (<src.npc>!=12)
if (<argo.more2>==20)
 if (<swordsmanship> <= {1 2000}) || (<fencing> <= {1 2000}) || (<poisoning> <= {1 1300})
  if (<src.restest 1 i_veneno>==0)
   src.spelleffect s_poison, 300
  endif
  argo.morex=<argo.morex>-1
  if (<argo.morex> == 0) || (<argo.morex> >= 1000)
   argo.more2=0
   if !(strmatch(<argo.tag.name>,)
    argo.name <argo.tag.name>
   endif
  endif
 endif
//Se a arma na HAND1 estiver encantada com Flamestrike
elif (<argo.more2>==51)
 src.effect 2 i_fire_column 0 32 0
 LOCAL.dam_fire = <eval {5 10}>
 src.damage <LOCAL.dam_fire> dam_fire <uid>
 argo.morex=<argo.morex>-1
 if (<argo.morex>==0)
  argo.more2=0
 endif
//Se a arma na HAND1 estiver encantada com Bless
elif (<argo.more2>==17)
 src.effect 2 i_fx_heal_effect 0 32 0
 src.spelleffect s_bless <tactics> <UID>
 argo.morex=<argo.morex>-1
 if (<argo.morex>==0)
  argo.more2=0
 endif
//Se a arma na HAND1 estiver encantada com Paralyse
elif (<argo.more2>==38) && !(RAND(6))
 src.effect 2 i_fx_heal_effect 0 32 0 09c
 src.stun <eval {3 6}>
 argo.morex=<argo.morex>-1
 if (<argo.morex>==0)
  argo.more2=0
 endif
endif

///////////////////////////////////////////////
//Aplicando correção de dano por STR///////////
///////////////////////////////////////////////
if (<str> >= 95)
 LOCAL.dano=3
elif (<str> >= 90)
 LOCAL.dano=2
elif (<str> >= 85)
 LOCAL.dano=1
else
 LOCAL.dano=0
endif

///////////////////////////////////////////////
//Aplicando correção de dano por ANATOMY///////
///////////////////////////////////////////////
if (<anatomy> >= 950)
 LOCAL.dano=<LOCAL.dano>+3
elif (<anatomy> >= 750)
 LOCAL.dano=<LOCAL.dano>+2
elif (<anatomy> >= 500)
 LOCAL.dano=<LOCAL.dano>+1
endif

///////////////////////////////////////////////
//Aplicando correção de dano por POISON////////
///////////////////////////////////////////////
if (<restest 1 i_veneno>) && (RAND(3)==0)
 LOCAL.dano=<eval <LOCAL.dano>-(<eval <findid.i_veneno.more2>/15>)>
 sysmessagegreen O veneno esta te enfraquecendo. Seus golpes estao fracos!
endif

///////////////////////////////////////////////
//Aplicando correção de dano por STAM//////////
///////////////////////////////////////////////
if (<stam>==<dex>)
 LOCAL.dano=<LOCAL.dano>+1
elif (<stam> <= 5)
 LOCAL.dano=<LOCAL.dano>-2
elif (<stam> <= 15)
 LOCAL.dano=<LOCAL.dano>-1
endif

///////////////////////////////////////////////
//Aplicando correção de dano por PESO//////////
///////////////////////////////////////////////
if (<weight> >= 1500)
 LOCAL.dano=<LOCAL.dano>-3
elif (<weight> >= 1000)
 LOCAL.dano=<LOCAL.dano>-2
elif (<weight> >= 750)
 LOCAL.dano=<LOCAL.dano>-1
endif

///////////////////////////////////////////////
//Aplicando correção de dano .COMBATE//////////
///////////////////////////////////////////////
if (<eval <tag0.agr>> >= 9)
 LOCAL.dano=<LOCAL.dano>+2
elif (<eval <tag0.agr>> >= 7)
 LOCAL.dano=<LOCAL.dano>+1
elif (<eval <tag0.agr>> >= 4)
 LOCAL.dano=<LOCAL.dano>
elif (<eval <tag0.agr>> >= 2)
 LOCAL.dano=<LOCAL.dano>-1
elif (<eval <tag0.agr>> >= 1) 
 LOCAL.dano=<LOCAL.dano>-5
else 
 LOCAL.dano=<LOCAL.dano>-10
endif

///////////////////////////////////////////////
//Aplicando correção de dano por AR////////////
///////////////////////////////////////////////

LOCAL.AR_LO=<src.ac>/6
LOCAL.AR_HI=<src.ac>/3
LOCAL.dano=<LOCAL.dano>-{<eval <LOCAL.AR_LO>> <eval <LOCAL.AR_HI>>}

//////////////////////////////////////////////////////////////
//Aplicando correção de dano pela qualidade da arma///////////
//////////////////////////////////////////////////////////////
//MORE1H=qualidade total
//MOTE1L=qualidade atual

if (<findlayer.layer_hand1.type>==t_weapon_axe) || (<findlayer.layer_hand1.type>==t_weapon_fence) || (<findlayer.layer_hand1.type>==t_weapon_sword) || (<findlayer.layer_hand1.type>==t_weapon_mace_crook) || (<findlayer.layer_hand1.type>==t_weapon_mace_pick) || (<findlayer.layer_hand1.type>==t_weapon_mace_staff) || (<findlayer.layer_hand1.type>==t_weapon_mace_smith) || (<findlayer.layer_hand1.type>==t_weapon_mace_sharp)
 LOCAL.wpc=<eval <findlayer.layer_hand1.more1l>*100>
 LOCAL.wpc=<eval <LOCAL.wpc>/<findlayer.layer_hand1.more1h>+1>
// say Tenho <eval <LOCAL.wpc>>
elif (<findlayer.layer_hand2.type>==t_weapon_axe) || (<findlayer.layer_hand2.type>==t_weapon_fence) || (<findlayer.layer_hand2.type>==t_weapon_sword) || (<findlayer.layer_hand2.type>==t_weapon_mace_crook) || (<findlayer.layer_hand2.type>==t_weapon_mace_pick) || (<findlayer.layer_hand2.type>==t_weapon_mace_staff) || (<findlayer.layer_hand2.type>==t_weapon_mace_smith) || (<findlayer.layer_hand2.type>==t_weapon_mace_sharp)
 LOCAL.wpc=<eval <findlayer.layer_hand2.more1l>*100>
 LOCAL.wpc=<eval <LOCAL.wpc>/<findlayer.layer_hand2.more1h>>
// say Tenho <eval <LOCAL.wpc>>
endif

if (0<LOCAL.wpc> >= 90)
// LOCAL.dano=<LOCAL.dano>-0
elif (0<LOCAL.wpc> >= 80)
 LOCAL.dano=<LOCAL.dano>-1
elif (0<LOCAL.wpc> >= 60)
 LOCAL.dano=<LOCAL.dano>-2
elif (0<LOCAL.wpc> >= 30)
 LOCAL.dano=<LOCAL.dano>-3
else 
 LOCAL.dano=<LOCAL.dano>-4
endif

//////////////////////////////////////////////////////////////
//Aplicando correção de dano pela skill de luta //////////////
//////////////////////////////////////////////////////////////


IF (<action> == Skill_Wrestling)
  doswitch (<eval <wrestling>/100>)
   argn1=(<argn1>*70)/100)+1
   argn1=(<argn1>*70)/100)+1
   argn1=(<argn1>*70)/100)+1
   argn1=(<argn1>*80)/100)+1
   argn1=(<argn1>*90)/100)+1
   argn1=(<argn1>*100)/100)+1
   argn1=(<argn1>*110)/100)+1
   argn1=(<argn1>*120)/100)+1
   argn1=(<argn1>*120)/100)+1
   argn1=(<argn1>*120)/100)+1
   argn1=(<argn1>*120)/100)+1
  enddo
elif  (<action> == Skill_fencing)
  doswitch (<eval <fencing>/100>)
   argn1=(<argn1>*60)/100)+1
   argn1=(<argn1>*60)/100)+1
   argn1=(<argn1>*65)/100)+1
   argn1=(<argn1>*80)/100)+1
   argn1=(<argn1>*90)/100)+1
   argn1=(<argn1>*100)/100)+1
   argn1=(<argn1>*110)/100)+1
   argn1=(<argn1>*120)/100)+1
   argn1=(<argn1>*135)/100)+1
   argn1=(<argn1>*150)/100)+1
   argn1=(<argn1>*170)/100)+1
  enddo
elif (<action> == Skill_Swordsmanship)
  doswitch (<eval <swordsmanship>/100>)
   argn1=(<argn1>*30)/100)+1
   argn1=(<argn1>*45)/100)+1
   argn1=(<argn1>*60)/100)+1
   argn1=(<argn1>*75)/100)+1
   argn1=(<argn1>*90)/100)+1
   argn1=(<argn1>*100)/100)+1
   argn1=(<argn1>*110)/100)+1
   argn1=(<argn1>*120)/100)+1
   argn1=(<argn1>*130)/100)+1
   argn1=(<argn1>*140)/100)+1
   argn1=(<argn1>*155)/100)+1
  enddo
elIF (<action> == Skill_Macefighting)
  doswitch (<eval <macefighting>/100>)
   argn1=(<argn1>*60)/100)+1
   argn1=(<argn1>*60)/100)+1
   argn1=(<argn1>*70)/100)+1
   argn1=(<argn1>*80)/100)+1
   argn1=(<argn1>*90)/100)+1
   argn1=(<argn1>*100)/100)+1
   argn1=(<argn1>*110)/100)+1
   argn1=(<argn1>*120)/100)+1
   argn1=(<argn1>*120)/100)+1
   argn1=(<argn1>*130)/100)+1
   argn1=(<argn1>*130)/100)+1
  enddo
elif  (<action> == Skill_archery)
  doswitch (<eval <archery>/100>)
   argn1=(<argn1>*50)/100)+1
   argn1=(<argn1>*70)/100)+1
   argn1=(<argn1>*90)/100)+1
   argn1=(<argn1>*100)/100)+1
   argn1=(<argn1>*100)/100)+1
   argn1=(<argn1>*100)/100)+1
   argn1=(<argn1>*100)/100)+1
   argn1=(<argn1>*110)/100)+1
   argn1=(<argn1>*115)/100)+1
   argn1=(<argn1>*120)/100)+1
   argn1=(<argn1>*125)/100)+1
  enddo
endif

//////////////////////////////////////////////////////////////
// Determinando tipo de dano//////////////////////////////////
//////////////////////////////////////////////////////////////

//<ARGO> == arma usada

//Determina tipo de dano por arma
if (<argo.type>==t_weapon_fence)||(<argo.type>==t_weapon_bow)||(<argo.type>==t_weapon_mace_pick)||(<argo.type>==t_weapon_arrow)||(<argo.type>==t_weapon_xbow)||(<argo.type>==t_weapon_bolt)
 LOCAL.tipo=dam_pierce
elif (<argo.type>==t_weapon_mace_sharp)||(<argo.type>==t_weapon_sword)||(<argo.type>==t_weapon_axe)
 LOCAL.tipo=dam_slash
else
 LOCAL.tipo=dam_physical
endif

//Determinar tipo de dano especial
if (<argo.dmgfire>)
 src.damage <argo.dmgfire> dam_fire <uid>
endif
if (<argo.dmgcold>)
 src.damage <argo.dmgcold> dam_cold <uid>
endif
if (<argo.dmgenergy>)
 src.damage <argo.dmgenery> dam_energy <uid>
endif
if (<argo.dmgpoison>)
 src.damage <argo.dmgpoison> dam_poison <uid>
endif

///////////////////////////////////////////////
//Determinar região do golpe///////////////////
///////////////////////////////////////////////

//IF (<action> == Skill_Wrestling)
//  dorand (<eval <wrestling>/100>)
//   LOCAL.loc=na mao
//   LOCAL.loc=no braco
//   LOCAL.loc=no braco
//   LOCAL.loc=na perna
//   LOCAL.loc=na perna
//   LOCAL.loc=no torax
//   LOCAL.loc=no torax
//   LOCAL.loc=no torax
//   LOCAL.loc=no cabeca
//   LOCAL.loc=no pescoco
//  enddo
//ELIF (<action> == Skill_fencing)
//  dorand (<eval <fencing>/100>)
//   LOCAL.loc=na mao
//   LOCAL.loc=na mao
//   LOCAL.loc=no braco
//   LOCAL.loc=no braco
//   LOCAL.loc=no torax
//   LOCAL.loc=no torax
//   LOCAL.loc=no torax
//   LOCAL.loc=no pescoco
//   LOCAL.loc=no pescoco
//   LOCAL.loc=na cabeca
//  enddo
//elIF (<action> == Skill_Macefighting)
//  dorand (<eval <macefighting>/100>)
//   LOCAL.loc=na mao
//   LOCAL.loc=no braco
//   LOCAL.loc=no torax
//   LOCAL.loc=no braco
//   LOCAL.loc=no braco
//   LOCAL.loc=na perna
//   LOCAL.loc=no torax
//   LOCAL.loc=no torax
//   LOCAL.loc=no torax
//   LOCAL.loc=no cabeca
//  enddo
//elif (<action> == Skill_Swordsmanship)
//  dorand (<eval <swordsmanship>/100>)
//   LOCAL.loc=na mao
//   LOCAL.loc=no braco
//   LOCAL.loc=no braco
//   LOCAL.loc=na perna
//   LOCAL.loc=na perna
//   LOCAL.loc=no torax
//   LOCAL.loc=no torax
//   LOCAL.loc=no torax
//   LOCAL.loc=no cabeca
//   LOCAL.loc=no pescoco
//  enddo
//elif (<action> == Skill_Archery)
//  dorand (<eval <archery>/100>)
//   LOCAL.loc=no torax
//   LOCAL.loc=no torax
//   LOCAL.loc=na mao
//   LOCAL.loc=no braco
//   LOCAL.loc=na perna
//   LOCAL.loc=na perna
//   LOCAL.loc=no torax
//   LOCAL.loc=no torax
//   LOCAL.loc=no cabeca
//   LOCAL.loc=no pescoco
//  enddo
//endif

///////Trocando nomes engraçados.
//if (<src.body>==c_snake_giant) || (<src.body>==c_snake)
// dorand 2
//  LOCAL.loc=no corpo
//  LOCAL.loc=na cabeca
// enddo
//endif
//if (<src.body>==c_headless) && (strmatch(<LOCAL.loc>, na cabeca))
// LOCAL.loc=entre os ombros
//endif
//if (<src.body>==c_headless) && (strmatch(<LOCAL.loc>, no pescoco))
// LOCAL.loc=entre os ombros
//endif

///////////////////////////////////////////////
//Determinar força do golpe////////////////////
///////////////////////////////////////////////

//if (<eval <argn1>+<LOCAL.dano>> >= 30)
// LOCAL.cbt.for=monstruoso
//elif (<eval <argn1>+<LOCAL.dano>> >= 25)
// LOCAL.cbt.for=destruidor
//elif (<eval <argn1>+<LOCAL.dano>> >= 20)
// LOCAL.cbt.for=potente
//elif (<eval <argn1>+<LOCAL.dano>> >= 15)
// LOCAL.cbt.for=muito forte
//elif (<eval <argn1>+<LOCAL.dano>> >= 10)
// LOCAL.cbt.for=forte
//elif (<eval <argn1>+<LOCAL.dano>> >= 5)
// LOCAL.cbt.for=fraco
//else
// LOCAL.cbt.for=patetico
//endif

///////////////////////////////////////////////
//Danifica armaduras 
///////////////////////////////////////////////

dorand (<eval (<tactics>)/100>)
 LOCAL.loc=1	//torax
 LOCAL.loc=1	//torax
 LOCAL.loc=2	//mao
 LOCAL.loc=3	//braco
 LOCAL.loc=4	//perna
 LOCAL.loc=4	//perna
 LOCAL.loc=1	//torax
 LOCAL.loc=1	//torax
 LOCAL.loc=5	//pescoco
 LOCAL.loc=6	//cabeca
 LOCAL.loc=6	//cabeca
enddo

if ( <LOCAL.loc>==1 )
 src.findlayer.layer_chest.more1l=<src.findlayer.layer_chest.more1l>-{1 4} 	//torax
 if (0<src.findlayer.layer_chest.more1l> > 08000)
  if (<NPC>==0) // se não for NPC...
   sysmessageorange A protecao do peito <src.name> foi totalmente arruinada!
  endif
  if (<src.NPC>==0) // Se o defensor não for NPC...
   src.sysmessageorange Sua protecao do peito foi totalmente arruinada!
  endif
  src.findlayer.layer_chest.remove
 endif

elif ( <LOCAL.loc>==2 )
 src.findlayer.layer_gloves.more1l=<src.findlayer.layer_gloves.more1l>-{1 4} 	//mao
 if (0<src.findlayer.layer_gloves.more1l> > 08000)
  if (<NPC>==0) // se não for NPC...
   sysmessageorange A protecao das maos <src.name> foi totalmente arruinada!
  endif
  if (<src.NPC>==0) // Se o defensor não for NPC...
   src.sysmessageorange Sua protecao das maos foi totalmente arruinada!
  endif
  src.findlayer.layer_pants.remove
 endif
 
elif ( <LOCAL.loc>==3 ) 
 src.findlayer.layer_arms.more1l=<src.findlayer.layer_arms.more1l>-{1 4}	//braços
 if (0<src.findlayer.layer_arms.more1l> > 08000)
  if (<NPC>==0) // se não for NPC...
   sysmessageorange A armadura dos bracos de <src.name> foi totalmente arruinada!
  endif
  if (<src.NPC>==0) // Se o defensor não for NPC...
   src.sysmessageorange Sua armadura dos bracos foi totalmente arruinada!
  endif
  src.findlayer.layer_arms.remove
 endif

elif ( <LOCAL.loc>==4 )
 src.findlayer.layer_pants.more1l=<src.findlayer.layer_pants.more1l>-{1 4}	//perna
 if (0<src.findlayer.layer_pants.more1l> > 08000)
  if (<NPC>==0) // se não for NPC...
   sysmessageorange A protecao das pernas <src.name> foi totalmente arruinada!
  endif
  if (<src.NPC>==0) // Se o defensor não for NPC...
   src.sysmessageorange Sua protecao das pernas foi totalmente arruinada!
  endif
  src.findlayer.layer_pants.remove
 endif
 
elif ( <LOCAL.loc>==5 )
 src.findlayer.layer_collar.more1l=<src.findlayer.layer_collar.more1l>-{1 4} 	//percoço
 if (0<src.findlayer.layer_collar.more1l> > 08000)
  if (<NPC>==0) // se não for NPC...
   sysmessageorange A protecao do pescoco <src.name> foi totalmente arruinada!
  endif
  if (<src.NPC>==0) // Se o defensor não for NPC...
   src.sysmessageorange Sua protecao do pescoco foi totalmente arruinada!
  endif
  src.findlayer.layer_collar.remove
 endif

elif ( <LOCAL.loc>==6 )
 src.findlayer.layer_helm.more1l=<src.findlayer.layer_helm.more1l>-{1 4} 	//cabeça
 if (0<src.findlayer.layer_helm.more1l> > 08000)
  if (<NPC>==0) // se não for NPC...
   sysmessageorange O elmo de <src.name> foi totalmente arruinado!
  endif
  if (<src.NPC>==0) // Se o defensor não for NPC...
   src.sysmessageorange Seu elmo foi totalmente arruinado!
  endif
  src.findlayer.layer_helm.remove
 endif
endif
 
///////////////////////////////////////////////
//Danificar armas /////////////////////////////
///////////////////////////////////////////////



if (<NPC>==0) && (<action> != Skill_Wrestling)
 argo.more1l=<argo.more1l>-{0 1}
 if (<argo.more1l> > 08000 ) || (<argo.more1l> < 1 )
  sysmessageorange Sua arma foi destruida!
  if (<src.NPC>==0) // Se o defensor não for NPC...
   src.sysmessageorange A arma de <src.name> foi destruida!
  endif
  argo.remove
 endif
 if (<action> == Skill_Archery)
  argo.morey=<argo.morey>-{0 2 1 1}
  if (<argo.morey>==0)
   argo.cont=<findlayer.layer_pack>
   sysmessageorange O barbante do seu arco se partiu!
   emoteorange barbante quebrou
   argo.name=<argo.tag.name>
   argo.update
  endif
 endif
endif




///////////////////////////////////////////////
/////////////Produtos finais///////////////////
///////////////////////////////////////////////

///// Falas bonitinhas
//if (<NPC>==0) //Se for um jogador e não um NPC...
// dorand 3
//  sysmessageblue Um <LOCAL.cbt.for> golpe acerta <src.name> <LOCAL.loc>.
//  sysmessageblue Voce desfere um <LOCAL.cbt.for> golpe <LOCAL.loc> de <src.name>.
//  sysmessageblue Seu <LOCAL.cbt.for> golpe pega em cheio <LOCAL.loc> de <src.name>.
// enddo
//endif
//if (<src.NPC>==0) //Se o alvo for um jogador...
// dorand 3
//  src.sysmessagered <name> acerta um <LOCAL.cbt.for> golpe em voce <LOCAL.loc>.
//  src.sysmessagered O <LOCAL.cbt.for> golpe de <name> te acerta <LOCAL.loc>.
//  src.sysmessagered Um golpe <LOCAL.cbt.for> de <name> te pega desprevinido <LOCAL.loc>
// enddo
//endif

/////Produzir sons
IF (<action> == Skill_wrestling)
 dorand 4
  sfx 014a
  sfx 013d
  sfx 0137
  sfx 013b
 enddo
ELIF (<action> == Skill_Swordsmanship) || (<action> == Skill_Fencing)
 dorand 4
  sfx 0237
  sfx 0236
  sfx 023c
  sfx 023b
 enddo
ELIF (<action> == Skill_Macefighting)
 dorand 4
  sfx 0149
  sfx 0143
  sfx 0146
  sfx 023b
 enddo
endif

///// Modificadores de dano
argn1=<eval <argn1>+<LOCAL.dano>>
if (<argn1> <= 0)
 argn1=1
endif

/////cansar atacante
if (<stam> >= 6)
 stam=<stam>-{0 2}
endif

/////desconcentrar vítima
if (<src.mana> >= 6)
 src.mana=<src.mana>-{0 2}
endif

///// Transfere e aplica dano
src.damage <argn1> <LOCAL.tipo> <uid>

///// Marca assassino (sistema de morte)
//if (<argn1> >= <src.hits>)
// if (<NPC>==0)
//  tag.lastkillname=<src.tag.name>
//  tag.lastkillUID=<src.uid>
//  tag.fpk=(<tag0.fpk>+1)
// endif
//endif

///// Salvar nomes para .ficha
IF (<src.npc>==0) && (<npc>==0)
 IF (<src.tag.lastagr1uid> != <uid>)
  src.tag.lastagr2uid=<src.tag.lastagr1uid>
  src.tag.lastagr2name=<src.tag.lastagr1name>
  src.tag.lastagr1uid=<uid>
  src.tag.lastagr1name=<tag.name>
 endif
endif

///// Perpetuar a agreção
if !(<src.flags>&statf_war)
 src.flags <src.flags>|statf_war
endif

if (<NPC>==0)
 src.attack <UID>
endif

return 1



//*************************************
// @GETHIT
//*************************************

on=@GetHit

///// Manter apênas um tipo de dano empilhado na bitmaks se for de flecha
if (<argn2>&dam_pierce) && (!<argn>&dam_magic)
 argn2=dam_pierce
endif

///// Bloquear dano se imune
if (<argn2>&<src.tag0.immune>)
 return 1
///// Piorar dano se agravante
elif (<tag0.aggravate>&<argn2>)
 LOCAL.agg=<eval {150 325}> 		//Agrava sempre de 50 a 225% de dano
 argn1=<eval (<argn1>*<LOCAL.agg>)/100>
///// Adicionar HITS se regenera com dano
elif (<tag0.regendam>&<argn2>)
 hits=<hits>+<argn1>
 return 1
endif

///// Amenizar dano se resistente (compativel com OSI e Sphere original)
if (<argn2>&dam_fire)
 if (<RESFIRE>)
  LOCAL.res=<eval 100-<RESFIRE>>
  argn1=<eval (<argn1>*<LOCAL.res>)/100>
 endif
elif (<argn2>&dam_cold)
 if (<RESCOLD>)
  LOCAL.res=<eval 100-<RESCOLD>>
  argn1=<eval (<argn1>*<LOCAL.res>)/100>
 endif
elif (<argn2>&dam_poison)
 if (<RESPOISON>)
  LOCAL.res=<eval 100-<RESPOISON>>
  argn1=<eval (<argn1>*<LOCAL.res>)/100>
 endif
elif (<argn2>&dam_energy)
 if (<RESENERGY>)
  LOCAL.res=<eval 100-<RESENERGY>>
  argn1=<eval (<argn1>*<LOCAL.res>)/100>
 endif
elif (<TAG0.resistance>&<argn2>)	///Compatibilidade com resistencia a pierce, physical, magic de NPCs
 LOCAL.res=<eval {40 60}>		///Resistências especiais de NPCs sempre de 40 a 60%
 argn1=<eval (<argn1>*<LOCAL.res>)/100>
endif

/////aos damage (show counter above player head)
if (0<npc> == 0) && (<hits> > 0)
 SENDPACKET  0bf W11 W022 01 D<UID> B<argn1> 
endif
if (<src> > 0 ) && (0<src.npc> == 0) && (<hits> > 0) && (<src.uid> != <uid>)
 SRC.SENDPACKET  0bf W11 W022 01 D<UID> B<argn1> 
endif

//// anima
cry

////decrementa pontos
hits=<hits>-<argn1>
return 1

//*************************************
// @SPELLEFFECT
//*************************************
ON=@SPELLEFFECT
//argn1=spell
//argn2=magery skill

//s_Cure e veneno
if (<argn1>==11) && (<restest 1 i_veneno>)
 LOCAL.poisoning=<argn2>
 LOCAL.poisoning=<eval <LOCAL.poisoning>/30>
 findid.i_veneno.more2=<eval <findid.i_veneno.more2>-<LOCAL.poisoning>>
 if (<eval <findid.i_veneno.more2>> >= 08000) || (<eval <findid.i_veneno.more2>> <= 0)
  findid.i_veneno.remove
  flags=<flags>&~statf_poisoned
  update
  sysmessageblue O veneno foi removido do seu corpo!
 endif
 sysmessageblue Voce se sente um tanto melhor. 
 return 1
endif

//s_Cure e peste
if (<argn1>==11) && (<restest 1 i_mry_peste>)
 if (<argn2> < 1001)
  sysmessagered A cura foi muito fraca para amenizar a peste.
 else
  src.findid.i_mry_peste.more2=(<src.findid.i_mry_peste.more2>/3)
  sysmessageblue Voce se sente um pouco melhor.
 endif
 return 1
endif

//s_Poison
if (<argn1>==20) && (<restest 1 i_veneno>==0)
 LOCAL.poison <argn2>
 LOCAL.poison <eval <LOCAL.poison>/20>
 veneno <LOCAL.poison>
 effect 2 i_fx_curse 0 30 0
 update
 return 1
endif

//s_Fireball. Manter dano somente dam_magic|dam_fire, tirando o dam_physical.
if (<argn1>==18)
 DAMAGESPELL s_Fireball,dam_magic|dam_fire,<src.magery>,<src.uid>
 RETURN 1
endif

//*************************************
// @ITEMDCLICK
//*************************************
ON=@ITEMDCLICK
if (<act.Baseid>==i_bottle_green) && (<act.more1>==s_poison)
 LOCAL.actuid=<act.uid>
 newitem i_mry_usepoison
 new.link <LOCAL.actuid>
 new.equip
 new.use
 return 1
endif


//*****************************************************************************
//*****************************************************************************
// TYPEDEFs
//*****************************************************************************
//*****************************************************************************
[TYPEDEF t_veneno]
on=@step
if (<src.restest 1 i_veneno>==0)
 src.veneno <more2>
endif

//*****************************************************************************
//*****************************************************************************
// ITEMDEFs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// i_mry_usepoison
//*****************************************************************************
[ITEMDEF i_mry_usepoison]
id=i_rune_poison
name=usando veneno
type=t_eq_script
layer=layer_special

ON=@DCLICK
timer=30
attr=attr_decay
targetg Onde deseja utilizar o veneno?
return 1

ON=@TARGON_CHAR
if (<link.more2> <= 1000 )
 cont.sysmessage Este veneno e muito fraco para ser jogado deste forma.
 remove
 return 1
endif
if (<src.targ.distance> > 3)
 cont.sysmessage Muito longe...
 remove
 return 1
endif
if (<src.targ.restest 1 i_veneno>==0)
 sfx 03e
 cont.emotegreen joga o frasco de veneno
 cont.newitem 0122a
 cont.act.p=<src.targ.p>
 cont.act.name veneno
 cont.act.timer {10 30}
 cont.act.attr attr_decay|attr_move_never
 cont.act.color 0b0
 cont.act=0
 cont.newitem i_veneno
 cont.act.timerd 1
 cont.act.more2 <eval <link.more2>/20>
 cont.act.cont <src.targ.uid>
 cont.act.timerd 1
 src.targ.flags=<src.targ.flags>|statf_poisoned
 src.targ.update
 link.remove
 remove
endif
return 1

ON=@TARGON_ITEM
if (<src.targ.BaseID>==i_arrow)
 if (<src.targ.cont.uid> != <src.findlayer.layer_pack.uid>)
  cont.sysmessage As flechas precisam estar em suas maos.
  remove
  return 1
 endif
 if (<src.poisoning> >= {1 1300})
  src.targ.p=<src.p>
  link.remove
  src.newitem i_bottle_empty
  src.act.bounce  
  if (<src.targ.amount> >=5 )
   src.targ.cont <src.findlayer.layer_pack.uid>
   src.consume 5 <src.targ.baseid>
   src.act=0
   src.newitem i_poison_arrow
   src.act.amount 5
   src.act.bounce
  else
   src.act=0
   src.newitem i_poison_arrow
   src.act.amount <src.targ.amount>
   src.act.bounce
   src.targ.cont <src.findlayer.layer_pack.uid>
   src.consume <src.targ.amount> <src.targ.baseid>
   src.sysmessage Voce envenenou as flechas.
  endif
 else
  cont.sysmessage Voce falhou em tentar envenenar as flechas.
  link.remove
  src.newitem i_bottle_empty
  src.act.bounce
 endif
elif (<src.targ.type>==t_weapon_sword) || (<src.targ.type>==t_weapon_fence)
 if (<src.targ.cont.uid> != <src.findlayer.layer_pack.uid>) && (<src.targ.uid> != <src.findlayer.layer_hand1.uid>) && (<src.targ.uid> != <src.findlayer.layer_hand2.uid>)
  cont.sysmessage A arma precisam estar em suas maos.
  remove
  return 1
 endif
 if (<src.targ.more2> == 20)
  if (<src.targ.morex> >= 35)
   cont.sysmessage Ja tem veneno o suficiente na arma.
   remove
   return 1
  else
   src.targ.morex=<eval <src.targ.morex>+(<link.more2>/69)>
  endif
 elif (<src.targ.more2> != 0)
  cont.sysmessage Esta arma ja tem um tratamento diferenciado.
  remove
  return 1
 elif (<src.poisoning> >= {1 1300})
  src.targ.morex=<eval <link.more2>/23>
  src.targ.more2=20
  src.targ.tag.name=<src.targ.name>
  src.targ.name <src.targ.name> (envenenado)
  link.remove
  src.newitem i_bottle_empty
  src.act.bounce
  remove
  src.sysmessage Voce envenenou a arma.
  return 1
 else
  cont.sysmessage Voce falhou em tentar envenenar a arma.
  link.remove
  src.newitem i_bottle_empty
  src.act.bounce
 endif
else
 src.sysmessage Voce nao pode envenenar isto.
endif
remove
return 1


ON=@TARGON_GROUND
if (<link.more2> <=1000)
 cont.sysmessage Este veneno e muito fraco para ser jogado deste forma.
 remove
 return 1
endif
 sfx 03e
 cont.emotegreen joga o frasco de veneno
 cont.newitem 0122a
 cont.act.p=<src.targp>
 cont.act.name veneno
 cont.act.timer {30 90}
 cont.act.attr 012
 cont.act.color 0b0
 cont.act.more2=<eval <link.more2>/40>
 cont.act.type t_veneno
 link.remove
 remove
 return 1

//*****************************************************************************
// i_veneno
//*****************************************************************************
[ITEMDEF i_veneno]
id=i_rune_poison
type=t_eq_script
name=Veneno
layer=layer_flag_poison

on=@create
timer=5

on=@timer
if (<more2> == 0) || (<more2> >= 4000) || (<cont.restest 1 i_mry_desmaio>) || (<cont.flags>&statf_dead)
 cont.sysmessagegreen O efeito do veneno esta passando
 cont.flags=<cont.flags>&~statf_poisoned
 cont.update
 remove
 return 1
else
 IF (<CONT.RESTEST 2 i_veneno>
  remove
  return 1
 endif
 more2=<more2>-1
 LOCAL.dano=<eval {1 <eval <more2>/7>}>
 if (<LOCAL.dano> > 15)
  LOCAL.dano=15
 endif
 cont.damage <LOCAL.dano> dam_poison
 if (<more2> <= 20) && ({0 15}==1)
  cont.emotegreen regurgita
  cont.newitem 0122a
  cont.act.p=<cont.p>
  cont.act.name vomito
  cont.act.timer {5 10}
  cont.act.attr attr_decay|attr_move_never
  cont.act.color 04ec
 elif (<more2> >= 30) && ({0 20}==1)
  cont.stun {3 7}
 elif (<more2> > 40)
  cont.emotegreen muito doente
  more2=<more2>-{3 5}
 elif (<more2> > 15)
  cont.emotegreen doente
    more2=<more2>-{0 3}
 else
  cont.emotegreen um pouco doente
 endif
 timer {3 7}
 return 1
endif

//*****************************************************************************
// i_stun
//*****************************************************************************
[ITEMDEF i_stun]
id=02080
type=t_eq_script
name=Paralizado
layer=layer_special

on=@timer
 cont.flags=<cont.flags>&~statf_freeze
 cont.update
 cont.sysmessagered O efeito da paralisacao cessou.
 remove
 return 1
 
//*****************************************************************************
//*****************************************************************************
// FUNCTIONs
//*****************************************************************************
//*****************************************************************************

[FUNCTION combate]
ctag.roll=<eval (<tag0.agr>*24)+70>
src.dialog d_estrategia
return 1

[FUNCTION ispoisoned]
if (<restest 1 i_veneno>)	//testa se está envenenado
 return 1			//se está, retorna 1
else
 return 0			//se não está, retorna 0
endif

[FUNCTION veneno]
if (!<ispoisoned>)		//testa se está envenenado
 serv.newitem i_veneno
 new.more2 <eval <argn>>
 new.timer 5
 equip <new.uid>
 flags=<flags>|080
 update
 return 0			//avisa o caller que ele não estava envenenado
else
 return 1			//avisa o caller que ele já estava envenenado e não fez nada
endif

[FUNCTION stun]
if (<restest 1 i_stun>)
 return 1
endif
newitem i_stun
act.cont=<uid>
if (!<argn>)
 act.timer {1 5}
else
 act.timer <argn>
endif
flags=<flags>|statf_freeze
update
emotered paralizado

//*****************************************************************************
//*****************************************************************************
// GUMPs
//*****************************************************************************
//*****************************************************************************

[DIALOG d_estrategia]
200, 150
noclose
nomove
PAGE 0
gumppic 0 0 1140
gumppic 61 88 1143
gumppic 136 41 2445
text 153 41 1101 0
text 65 69 455 1
text 263 69 455 2
button <eval <ctag0.roll>> 94 2117 2118 0 0 0
button 45 95 2223 2223 1 0 1
button 328 95 2224 2224 1 0 2
button 137 118 2117 2118 1 0 3
button 137 139 2117 2118 1 0 4
button 137 161 2117 2118 1 0 5
text 159 114 1345 3
text 159 136 1345 4
text 158 156 1345 5
button 156 196 249 248 1 0 6

[DIALOG d_estrategia TEXT]
Estrategia
Defensiva
Agressiva
Agressiva
Defensiva
Neutra

[DIALOG d_estrategia BUTTON]
on=1
if (<tag0.agr><=0)
 tag.agr=0
 DIALOG d_estrategia
 return 1
endif
tag.agr=<eval (<tag0.agr>-1)>
ctag.roll=<eval (<tag0.agr>*24)+70>
DIALOG d_estrategia
return 1

on=2
if (<tag0.agr>>=10)
 tag.agr=10
 DIALOG d_estrategia
 return 1
endif
tag.agr=<eval (<tag0.agr>+1)>
ctag.roll=<eval (<tag0.agr>*24)+70>
DIALOG d_estrategia
return 1

on=3
tag.agr=10
ctag.roll=(<tag.agr>*24)+70
DIALOG d_estrategia
return 1

on=4
tag.agr=0
ctag.roll=(<tag.agr>*24)+70
DIALOG d_estrategia
return 1

on=5
tag.agr=5
ctag.roll=(<tag.agr>*24)+70
DIALOG d_estrategia
return 1

on=6
return 1

[EOF]