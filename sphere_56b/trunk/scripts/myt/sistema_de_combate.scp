//*****************************************************************************
//*****************************************************************************
//
// SISTEMA DE COMBATE por
//	Kell
//	Galthar
//	Silver
//	UsMarine (vinicius.arroyo at gmail.com)
//
//*****************************************************************************
//*****************************************************************************
//TODO:

//@HIT
//
//Verifica Parrying - (50%*skill)[%]*(1-agressividade_defensor)[%]
//  Danifica Escudo
//  Fim
//Tenta esquivar - ( 130% tactics + 200% dex + (-250 a 500)[agressividade] )*35%
//  Esquiva
//  Fim
//Tenta envenenar - (atacante é animal venenoso && poisoning > 30% && nao envenenado)
//  Envenena com 10% poisoning e duração de 20 a 30 ataques.
//Tenta envenenar - (armas envenenadas && ( 50% swords || 50% fencing || 76% poison ) )
//  Envenena com spell poison, 30% de skill.
//Tenta queimar - (armas com fogo)
//  Queima (fire collumn) - 3 a 6 de dano
//Ajusta dano
//  STR 
//    >= 95% +3
//    >= 90% +2
//    >= 85% +1
//  ANATOMY
//    >= 95% +3
//    >= 75% +2
//    >= 50% +1
//  ATACANTE ENVENENADO - 25% chance 
//    - (more2/15)
//  STAMINA
//    MAX   +1
//    <= 5  -2
//    <= 15 -1
//  PESO
//    >= 1500 -3
//    >= 1000 -2
//    >= 750  -1
//  AGRESSIVIDADE
//    9,10  +2
//    7,8   +1
//    4,5,6  0
//    2,3   -1
//    1     -5
//    0    -10
//  ARMADURA ALVO
//    - { AR/6 AR/3 }
//  QUALIDADE DA ARMA DE ATAQUE
//    >= 90%  0
//    >= 80% -1
//    >= 60% -2
//    >= 30% -3
//    else   -4 
//  SKILL DE ATAQUE (afeta dano original: argn1)
//    WRESTLING
//      < 30%  70% + 1
//      < 40%  80% + 1
//      < 50%  90% + 1
//      < 60% 100% + 1
//      < 70% 110% + 1
//      < 80% 120% + 1
//    FENCING
//      < 20%   60% + 1
//      < 30%   65% + 1
//      < 40%   80% + 1
//      < 50%   90% + 1
//      < 60%  100% + 1
//      < 70%  110% + 1
//      < 80%  120% + 1
//      < 90%  135% + 1
//      < 100% 150% + 1
//      = 100% 170% + 1
//    SWORDSMANSHIP
//      < 10%   30% + 1
//      < 20%   45% + 1
//      < 30%   60% + 1
//      < 40%   75% + 1
//      < 50%   90% + 1
//      < 60%  100% + 1
//      < 70%  110% + 1
//      < 80%  120% + 1
//      < 90%  130% + 1
//      < 100% 140% + 1
//      = 100% 155% + 1
//    MACEFIGHTING
//      < 10%   60% + 1
//      < 20%   60% + 1
//      < 30%   70% + 1
//      < 40%   80% + 1
//      < 50%   90% + 1
//      < 60%  100% + 1
//      < 70%  110% + 1
//      < 80%  120% + 1
//      < 90%  120% + 1
//      < 100% 130% + 1
//      = 100% 130% + 1
//    ARCHERY
//      < 10%   50% + 1
//      < 20%   70% + 1
//      < 30%   90% + 1
//      < 40%  100% + 1
//      < 50%  100% + 1
//      < 60%  100% + 1
//      < 70%  100% + 1
//      < 80%  110% + 1
//      < 90%  115% + 1
//      < 100% 120% + 1
//      = 100% 125% + 1
//
//Define local do ataque ( rand 0 a 10% tactics )
//  torax
//  torax
//  mao
//  braco
//  perna
//  perna
//  torax
//  torax
//  pescoco
//  cabeca
//  cabeca
//
//Danifica armadura protegendo local do ataque - 1 a 4
//Danifica armas utilizadas - 0 a 1
//Emite sons
//Cansa atacante: - 0 a 2 de stamina
//Desconcentra vitima: - 0 a 2 de mana
//Aplica dano


// POISON por magia: i_veneno.MORE2 = 50% magery repeticoes e 7% magery dano
// HEAL POISON por magia: i_veneno.MORE2 -= 33% magery
// POISON por pocao: pocao.MORE2 = (890,1000,1500,2000) (fraca,normal,concentrada,forte)
//   flecha envenenada: dano constante: i_poison_arrow
//   arma envenenada: 4,5% more2 e 1,5% na recarga (recargas de dano constante:2)
//   alvo envenenado: 5% more2 repeticoes e 0.71% more2 dano
//   chao envenenado: 2,5% more2 repeticoes e 0.36% more2 dano
// POISON NPC: i_veneno.MORE2 = ( 10% poisoning ) repeticoes e 1 a ( 10% poisoning * 14%) 

//TAGS usadas:
//tag.agr	nível de agressividade do combate (0 a 10)

//tag.fRP	pontos de rp
//tag.lastkillname nome da ultima vitima fatal
//tag.lastkillUID  uid da ultima vitima fatal
//tag.fpk	contador de vitimas fatais
//tag.lastagr2uid  uid do penultimo agressor
//tag.lastagr2name nome do penultimo agressor
//tag.lastagr1uid  uid do ultimo agressor
//tag.lastagr1name nome do ultimo agressor

//item.tag.name nome original do item antes de ser envenenado
//*****************************************************************************

//*****************************************************************************
//*****************************************************************************
// EVENTs
//*****************************************************************************
//*****************************************************************************
[EVENTS e_combat] 

//*************************************
// @HITTRY
//*************************************
ON=@HITTRY
//sai do modo batalha se alvo invul, morto, desmaiado, escondido, etc
if ( <src.flags>&(statf_invul|statf_dead|statf_invisible|statf_insubstantial|statf_stone|statf_hidden)) || (<src.restest 1 i_mry_desmaio>)
 say denyhit
 flags=<flags>&~statf_war
 update
 return 1
endif

//*************************************
// @HITMISS - somente evita mensagem de miss
//*************************************
ON=@HITMISS
return 1

//*************************************
// @HIT
//*************************************
ON=@HIT

//MORE1H = maxItemHits
//MORE1L = itemHits

///////////////////////////////////////////////
////////////Tentar aparar com escudo///////////
///////////////////////////////////////////////

if (<src.findlayer.layer_hand2.type>==t_shield)
 LOCAL.escudo=<src.parrying>/2
 if ({1 1000} <= <LOCAL.escudo>) && (RAND(<src.tag0.agr>)==0)
   if (<NPC>==0) // se não for NPC...
   sysmessageyellow <src.name> apara seu golpe com o escudo!
  endif
  if (<src.NPC>==0) // Se o defensor não for NPC...
   src.sysmessageyellow Voce apara o golpe de <name> com o escudo!
  endif

  //danifica escudo
  IF (<action> == Skill_Wrestling)
   src.findlayer.layer_hand2.more1l=<src.findlayer.layer_hand2.more1l>-{1 2}
  ELIF (<action> == Skill_Swordsmanship) || (<action> == Skill_Fencing) 
   src.findlayer.layer_hand2.more1l=<src.findlayer.layer_hand2.more1l>-{1 2}
  ELIF (<action> == Skill_Macefighting)
   src.findlayer.layer_hand2.more1l=<src.findlayer.layer_hand2.more1l>-{2 4}
  ELIF (<action> == Skill_Archery)
   src.findlayer.layer_hand2.more1l=<src.findlayer.layer_hand2.more1l>-1
  endif

  //caso escudo destruido
  if (0<src.findlayer.layer_hand2.more1l> > 0<src.findlayer.layer_hand2.more1h>) && !(<src.findlayer.layer_hand2.attr> & attr_newbie)
   if (<NPC>==0) // se não for NPC...
    sysmessageorange O escudo de <src.name> foi totalmente arruinado!
   endif
   if (<src.NPC>==0) // Se o defensor não for NPC...
    src.sysmessageorange Seu escudo foi totalmente arruinado!
   endif
   src.findlayer.layer_hand2.remove
  endif
  dorand 4
   sfx 0237
   sfx 0236
   sfx 023c
   sfx 023b
  enddo
 //IF ({0 1000} >= <parrying>) && (RAND(5)=2)
 // Parrying=<Parrying>+(1)
 //endif
  return 1
 endif
endif


///////////////////////////////////////////////
////////////Tentar esquivar o golpe////////////
///////////////////////////////////////////////
LOCAL.esquiva=<eval (((<src.tactics>/15)*2)+(<src.dex>*2))>

//&& (<src.npc>==0)
if (<src.tag0.agr>==0) 
 LOCAL.esquiva=<eval <LOCAL.esquiva>+500>
elif (<src.tag0.agr>==1)
 LOCAL.esquiva=<eval <LOCAL.esquiva>+400>
elif (<src.tag0.agr>==2)
 LOCAL.esquiva=<eval <LOCAL.esquiva>+300>
elif (<src.tag0.agr>==3)
 LOCAL.esquiva=<eval <LOCAL.esquiva>+200>
elif (<src.tag0.agr>==4)
 LOCAL.esquiva=<eval <LOCAL.esquiva>+100>
elif (<src.tag0.agr>==5)
// LOCAL.esquiva=<eval <LOCAL.esquiva>+000>
elif (<src.tag0.agr>==6)
 LOCAL.esquiva=<eval <LOCAL.esquiva>-50>
elif (<src.tag0.agr>==7)
 LOCAL.esquiva=<eval <LOCAL.esquiva>-100>
elif (<src.tag0.agr>==8)
 LOCAL.esquiva=<eval <LOCAL.esquiva>-150>
elif (<src.tag0.agr>==9)
 LOCAL.esquiva=<eval <LOCAL.esquiva>-200>
elif (<src.tag0.agr>==10)
 LOCAL.esquiva=<eval <LOCAL.esquiva>-250>
endif

//quando o atacado for um player ou npc
if (({1 2400} <= <eval <LOCAL.esquiva>>)
 if (<NPC>==0) // se não for NPC...
  sysmessageyellow <src.name> esquiva-se de seu golpe!
 endif
 if (<src.NPC>==0) // Se o defensor não for NPC...
  src.sysmessageyellow Voce consegue se esquivar do golpe!
 endif
 dorand 5
  sfx 0239
  sfx 023a
  sfx 0233
  sfx 0238
  sfx 0232
 enddo 
 return 1
endif

///////////////////////////////////////////////
//Tentar usar Poison///////////////////////////
///////////////////////////////////////////////

// Se eu for um animal venenoso e a minha presa não está envenenada...
if (<poisoning> >= 300) && (<NPC>!=0) && (<src.restest 1 i_veneno>==0)
 if ({1 1000} < <poisoning>) && (RAND(4)==0) && (<ctag0.veneno>==0)
  src.veneno <eval <poisoning>/10>
  ctag.veneno={20 30}
  emotered morde profundamente
 elif (<ctag0.veneno>!=0)
  ctag.veneno <ctag.veneno>-1
 endif
endif

//ARMAS ENVENENADAS
//MORE2=020
//MOREX=carga de veneno

//Se a arma na HAND1 estiver envenenada // && (<src.npc>!=12)
if (<findlayer.layer_hand1.more2>==20)
 if (<swordsmanship> <= {1 2000}) || (<fencing> <= {1 2000}) || (<poisoning> <= {1 1300})
  if (<src.restest 1 i_veneno>==0)
   src.spelleffect s_poison, 300
  endif
  findlayer.layer_hand1.morex=<findlayer.layer_hand1.morex>-1
  if (<findlayer.layer_hand1.morex> == 0) || (<findlayer.layer_hand1.morex> >= 1000)
   findlayer.layer_hand1.more2=0
   findlayer.layer_hand1.name <findlayer.layer_hand1.tag.name>
  endif
 endif
 
//Se a arma na HAND2 estiver envenenada // && (<src.npc>!=12)
elif (<findlayer.layer_hand2.more2>==20) 
 if (<swordsmanship> <= {1 2000}) || (<fencing> <= {1 2000}) || (<poisoning> <= {1 1300})
  if (<src.restest 1 i_veneno>==0)
   src.spelleffect s_poison, 300
  endif
  findlayer.layer_hand2.morex=<findlayer.layer_hand2.morex>-1
  if (<findlayer.layer_hand2.morex> == 0) || (<findlayer.layer_hand2.morex> >= 1000)
   findlayer.layer_hand2.more2=0
   findlayer.layer_hand2.name <findlayer.layer_hand2.tag.name>
  endif
 endif
endif

//Se a arma na HAND1 estiver??
if (<findlayer.layer_hand1.more2>==15)
 src.effect 2 i_fire_column 0 32 0
 src.do_damage {3 6},dam_fire,<uid>
 findlayer.layer_hand1.morex=<findlayer.layer_hand1.morex>-1
 if (<findlayer.layer_hand1.morex>==0)
  findlayer.layer_hand1.more2=0
 endif
endif

///////////////////////////////////////////////
//Aplicando correção de dano por STR///////////
///////////////////////////////////////////////
if (<str> >= 95)
 LOCAL.dano=3
elif (<str> >= 90)
 LOCAL.dano=2
elif (<str> >= 85)
 LOCAL.dano=1
else
 LOCAL.dano=0
endif

///////////////////////////////////////////////
//Aplicando correção de dano por ANATOMY///////
///////////////////////////////////////////////
if (<anatomy> >= 950)
 LOCAL.dano=<LOCAL.dano>+3
elif (<anatomy> >= 750)
 LOCAL.dano=<LOCAL.dano>+2
elif (<anatomy> >= 500)
 LOCAL.dano=<LOCAL.dano>+1
endif

///////////////////////////////////////////////
//Aplicando correção de dano por POISON////////
///////////////////////////////////////////////
if (<restest 1 i_veneno>) && (RAND(3)==0)
 LOCAL.dano=<eval <LOCAL.dano>-(<eval <findid.i_veneno.more2>/15>)>
 sysmessagegreen O veneno esta te enfraquecendo. Seus golpes estao fracos!
endif

///////////////////////////////////////////////
//Aplicando correção de dano por STAM//////////
///////////////////////////////////////////////
if (<stam>==<dex>)
 LOCAL.dano=<LOCAL.dano>+1
elif (<stam> <= 5)
 LOCAL.dano=<LOCAL.dano>-2
elif (<stam> <= 15)
 LOCAL.dano=<LOCAL.dano>-1
endif

///////////////////////////////////////////////
//Aplicando correção de dano por PESO//////////
///////////////////////////////////////////////
if (<weight> >= 1500)
 LOCAL.dano=<LOCAL.dano>-3
elif (<weight> >= 1000)
 LOCAL.dano=<LOCAL.dano>-2
elif (<weight> >= 750)
 LOCAL.dano=<LOCAL.dano>-1
endif

///////////////////////////////////////////////
//Aplicando correção de dano .COMBATE//////////
///////////////////////////////////////////////
if (<eval <tag0.agr>> >= 9)
 LOCAL.dano=<LOCAL.dano>+2
elif (<eval <tag0.agr>> >= 7)
 LOCAL.dano=<LOCAL.dano>+1
elif (<eval <tag0.agr>> >= 4)
 LOCAL.dano=<LOCAL.dano>
elif (<eval <tag0.agr>> >= 2)
 LOCAL.dano=<LOCAL.dano>-1
elif (<eval <tag0.agr>> >= 1) 
 LOCAL.dano=<LOCAL.dano>-5
else 
 LOCAL.dano=<LOCAL.dano>-10
endif

///////////////////////////////////////////////
//Aplicando correção de dano por AR////////////
///////////////////////////////////////////////

LOCAL.AR_LO=<src.ac>/6
LOCAL.AR_HI=<src.ac>/3
LOCAL.dano=<LOCAL.dano>-{<eval <LOCAL.AR_LO>> <eval <LOCAL.AR_HI>>}

//////////////////////////////////////////////////////////////
//Aplicando correção de dano pela qualidade da arma///////////
//////////////////////////////////////////////////////////////
//MORE1H=qualidade total
//MOTE1L=qualidade atual

if (<findlayer.layer_hand1.type>==t_weapon_axe) || (<findlayer.layer_hand1.type>==t_weapon_fence) || (<findlayer.layer_hand1.type>==t_weapon_sword) || (<findlayer.layer_hand1.type>==t_weapon_mace_crook) || (<findlayer.layer_hand1.type>==t_weapon_mace_pick) || (<findlayer.layer_hand1.type>==t_weapon_mace_staff) || (<findlayer.layer_hand1.type>==t_weapon_mace_smith) || (<findlayer.layer_hand1.type>==t_weapon_mace_sharp)
 LOCAL.wpc=<eval <findlayer.layer_hand1.more1l>*100>
 LOCAL.wpc=<eval <LOCAL.wpc>/<findlayer.layer_hand1.more1h>+1>
// say Tenho <eval <LOCAL.wpc>>
elif (<findlayer.layer_hand2.type>==t_weapon_axe) || (<findlayer.layer_hand2.type>==t_weapon_fence) || (<findlayer.layer_hand2.type>==t_weapon_sword) || (<findlayer.layer_hand2.type>==t_weapon_mace_crook) || (<findlayer.layer_hand2.type>==t_weapon_mace_pick) || (<findlayer.layer_hand2.type>==t_weapon_mace_staff) || (<findlayer.layer_hand2.type>==t_weapon_mace_smith) || (<findlayer.layer_hand2.type>==t_weapon_mace_sharp)
 LOCAL.wpc=<eval <findlayer.layer_hand2.more1l>*100>
 LOCAL.wpc=<eval <LOCAL.wpc>/<findlayer.layer_hand2.more1h>>
// say Tenho <eval <LOCAL.wpc>>
endif

if (0<LOCAL.wpc> >= 90)
// LOCAL.dano=<LOCAL.dano>-0
elif (0<LOCAL.wpc> >= 80)
 LOCAL.dano=<LOCAL.dano>-1
elif (0<LOCAL.wpc> >= 60)
 LOCAL.dano=<LOCAL.dano>-2
elif (0<LOCAL.wpc> >= 30)
 LOCAL.dano=<LOCAL.dano>-3
else 
 LOCAL.dano=<LOCAL.dano>-4
endif

//////////////////////////////////////////////////////////////
//Aplicando correção de dano pela skill de luta////////////
//////////////////////////////////////////////////////////////


IF (<action> == Skill_Wrestling)
  doswitch (<eval <wrestling>/100>)
   argn1=(<argn1>*70)/100)+1
   argn1=(<argn1>*70)/100)+1
   argn1=(<argn1>*70)/100)+1
   argn1=(<argn1>*80)/100)+1
   argn1=(<argn1>*90)/100)+1
   argn1=(<argn1>*100)/100)+1
   argn1=(<argn1>*110)/100)+1
   argn1=(<argn1>*120)/100)+1
   argn1=(<argn1>*120)/100)+1
   argn1=(<argn1>*120)/100)+1
   argn1=(<argn1>*120)/100)+1
  enddo
elif  (<action> == Skill_fencing)
  doswitch (<eval <fencing>/100>)
   argn1=(<argn1>*60)/100)+1
   argn1=(<argn1>*60)/100)+1
   argn1=(<argn1>*65)/100)+1
   argn1=(<argn1>*80)/100)+1
   argn1=(<argn1>*90)/100)+1
   argn1=(<argn1>*100)/100)+1
   argn1=(<argn1>*110)/100)+1
   argn1=(<argn1>*120)/100)+1
   argn1=(<argn1>*135)/100)+1
   argn1=(<argn1>*150)/100)+1
   argn1=(<argn1>*170)/100)+1
  enddo
elif (<action> == Skill_Swordsmanship)
  doswitch (<eval <swordsmanship>/100>)
   argn1=(<argn1>*30)/100)+1
   argn1=(<argn1>*45)/100)+1
   argn1=(<argn1>*60)/100)+1
   argn1=(<argn1>*75)/100)+1
   argn1=(<argn1>*90)/100)+1
   argn1=(<argn1>*100)/100)+1
   argn1=(<argn1>*110)/100)+1
   argn1=(<argn1>*120)/100)+1
   argn1=(<argn1>*130)/100)+1
   argn1=(<argn1>*140)/100)+1
   argn1=(<argn1>*155)/100)+1
  enddo
elIF (<action> == Skill_Macefighting)
  doswitch (<eval <macefighting>/100>)
   argn1=(<argn1>*60)/100)+1
   argn1=(<argn1>*60)/100)+1
   argn1=(<argn1>*70)/100)+1
   argn1=(<argn1>*80)/100)+1
   argn1=(<argn1>*90)/100)+1
   argn1=(<argn1>*100)/100)+1
   argn1=(<argn1>*110)/100)+1
   argn1=(<argn1>*120)/100)+1
   argn1=(<argn1>*120)/100)+1
   argn1=(<argn1>*130)/100)+1
   argn1=(<argn1>*130)/100)+1
  enddo
elif  (<action> == Skill_archery)
  doswitch (<eval <archery>/100>)
   argn1=(<argn1>*50)/100)+1
   argn1=(<argn1>*70)/100)+1
   argn1=(<argn1>*90)/100)+1
   argn1=(<argn1>*100)/100)+1
   argn1=(<argn1>*100)/100)+1
   argn1=(<argn1>*100)/100)+1
   argn1=(<argn1>*100)/100)+1
   argn1=(<argn1>*110)/100)+1
   argn1=(<argn1>*115)/100)+1
   argn1=(<argn1>*120)/100)+1
   argn1=(<argn1>*125)/100)+1
  enddo
endif


///////////////////////////////////////////////
//Determinar região do golpe///////////////////
///////////////////////////////////////////////

//IF (<action> == Skill_Wrestling)
//  dorand (<eval <wrestling>/100>)
//   LOCAL.loc=na mao
//   LOCAL.loc=no braco
//   LOCAL.loc=no braco
//   LOCAL.loc=na perna
//   LOCAL.loc=na perna
//   LOCAL.loc=no torax
//   LOCAL.loc=no torax
//   LOCAL.loc=no torax
//   LOCAL.loc=no cabeca
//   LOCAL.loc=no pescoco
//  enddo
//ELIF (<action> == Skill_fencing)
//  dorand (<eval <fencing>/100>)
//   LOCAL.loc=na mao
//   LOCAL.loc=na mao
//   LOCAL.loc=no braco
//   LOCAL.loc=no braco
//   LOCAL.loc=no torax
//   LOCAL.loc=no torax
//   LOCAL.loc=no torax
//   LOCAL.loc=no pescoco
//   LOCAL.loc=no pescoco
//   LOCAL.loc=na cabeca
//  enddo
//elIF (<action> == Skill_Macefighting)
//  dorand (<eval <macefighting>/100>)
//   LOCAL.loc=na mao
//   LOCAL.loc=no braco
//   LOCAL.loc=no torax
//   LOCAL.loc=no braco
//   LOCAL.loc=no braco
//   LOCAL.loc=na perna
//   LOCAL.loc=no torax
//   LOCAL.loc=no torax
//   LOCAL.loc=no torax
//   LOCAL.loc=no cabeca
//  enddo
//elif (<action> == Skill_Swordsmanship)
//  dorand (<eval <swordsmanship>/100>)
//   LOCAL.loc=na mao
//   LOCAL.loc=no braco
//   LOCAL.loc=no braco
//   LOCAL.loc=na perna
//   LOCAL.loc=na perna
//   LOCAL.loc=no torax
//   LOCAL.loc=no torax
//   LOCAL.loc=no torax
//   LOCAL.loc=no cabeca
//   LOCAL.loc=no pescoco
//  enddo
//elif (<action> == Skill_Archery)
//  dorand (<eval <archery>/100>)
//   LOCAL.loc=no torax
//   LOCAL.loc=no torax
//   LOCAL.loc=na mao
//   LOCAL.loc=no braco
//   LOCAL.loc=na perna
//   LOCAL.loc=na perna
//   LOCAL.loc=no torax
//   LOCAL.loc=no torax
//   LOCAL.loc=no cabeca
//   LOCAL.loc=no pescoco
//  enddo
//endif

///////Trocando nomes engraçados.
//if (<src.body>==c_snake_giant) || (<src.body>==c_snake)
// dorand 2
//  LOCAL.loc=no corpo
//  LOCAL.loc=na cabeca
// enddo
//endif
//if (<src.body>==c_headless) && (strmatch(<LOCAL.loc>, na cabeca))
// LOCAL.loc=entre os ombros
//endif
//if (<src.body>==c_headless) && (strmatch(<LOCAL.loc>, no pescoco))
// LOCAL.loc=entre os ombros
//endif

///////////////////////////////////////////////
//Determinar força do golpe////////////////////
///////////////////////////////////////////////

//if (<eval <argn1>+<LOCAL.dano>> >= 30)
// LOCAL.cbt.for=monstruoso
//elif (<eval <argn1>+<LOCAL.dano>> >= 25)
// LOCAL.cbt.for=destruidor
//elif (<eval <argn1>+<LOCAL.dano>> >= 20)
// LOCAL.cbt.for=potente
//elif (<eval <argn1>+<LOCAL.dano>> >= 15)
// LOCAL.cbt.for=muito forte
//elif (<eval <argn1>+<LOCAL.dano>> >= 10)
// LOCAL.cbt.for=forte
//elif (<eval <argn1>+<LOCAL.dano>> >= 5)
// LOCAL.cbt.for=fraco
//else
// LOCAL.cbt.for=patetico
//endif

///////////////////////////////////////////////
//Danifica armaduras 
///////////////////////////////////////////////

dorand (<eval (<tactics>)/100>)
 LOCAL.loc=1	//torax
 LOCAL.loc=1	//torax
 LOCAL.loc=2	//mao
 LOCAL.loc=3	//braco
 LOCAL.loc=4	//perna
 LOCAL.loc=4	//perna
 LOCAL.loc=1	//torax
 LOCAL.loc=1	//torax
 LOCAL.loc=5	//pescoco
 LOCAL.loc=6	//cabeca
 LOCAL.loc=6	//cabeca
enddo

if ( <LOCAL.loc>==1 )
 src.findlayer.layer_chest.more1l=<src.findlayer.layer_chest.more1l>-{1 4} 	//torax
 if (0<src.findlayer.layer_chest.more1l> > 5000)
  if (<NPC>==0) // se não for NPC...
   sysmessageorange A protecao do peito <src.name> foi totalmente arruinada!
  endif
  if (<src.NPC>==0) // Se o defensor não for NPC...
   src.sysmessageorange Sua protecao do peito foi totalmente arruinada!
  endif
  src.findlayer.layer_chest.remove
 endif

elif ( <LOCAL.loc>==2 )
 src.findlayer.layer_gloves.more1l=<src.findlayer.layer_gloves.more1l>-{1 4} 	//mao
 if (0<src.findlayer.layer_gloves.more1l> > 5000)
  if (<NPC>==0) // se não for NPC...
   sysmessageorange A protecao das maos <src.name> foi totalmente arruinada!
  endif
  if (<src.NPC>==0) // Se o defensor não for NPC...
   src.sysmessageorange Sua protecao das maos foi totalmente arruinada!
  endif
  src.findlayer.layer_pants.remove
 endif
 
elif ( <LOCAL.loc>==3 ) 
 src.findlayer.layer_arms.more1l=<src.findlayer.layer_arms.more1l>-{1 4}	//braços
 if (0<src.findlayer.layer_arms.more1l> > 5000)
  if (<NPC>==0) // se não for NPC...
   sysmessageorange A armadura dos bracos de <src.name> foi totalmente arruinada!
  endif
  if (<src.NPC>==0) // Se o defensor não for NPC...
   src.sysmessageorange Sua armadura dos bracos foi totalmente arruinada!
  endif
  src.findlayer.layer_arms.remove
 endif

elif ( <LOCAL.loc>==4 )
 src.findlayer.layer_pants.more1l=<src.findlayer.layer_pants.more1l>-{1 4}	//perna
 if (0<src.findlayer.layer_pants.more1l> > 5000)
  if (<NPC>==0) // se não for NPC...
   sysmessageorange A protecao das pernas <src.name> foi totalmente arruinada!
  endif
  if (<src.NPC>==0) // Se o defensor não for NPC...
   src.sysmessageorange Sua protecao das pernas foi totalmente arruinada!
  endif
  src.findlayer.layer_pants.remove
 endif
 
elif ( <LOCAL.loc>==5 )
 src.findlayer.layer_collar.more1l=<src.findlayer.layer_collar.more1l>-{1 4} 	//percoço
 if (0<src.findlayer.layer_collar.more1l> > 5000)
  if (<NPC>==0) // se não for NPC...
   sysmessageorange A protecao do pescoco <src.name> foi totalmente arruinada!
  endif
  if (<src.NPC>==0) // Se o defensor não for NPC...
   src.sysmessageorange Sua protecao do pescoco foi totalmente arruinada!
  endif
  src.findlayer.layer_collar.remove
 endif

elif ( <LOCAL.loc>==6 )
 src.findlayer.layer_helm.more1l=<src.findlayer.layer_helm.more1l>-{1 4} 	//cabeça
 if (0<src.findlayer.layer_helm.more1l> > 5000)
  if (<NPC>==0) // se não for NPC...
   sysmessageorange O elmo de <src.name> foi totalmente arruinado!
  endif
  if (<src.NPC>==0) // Se o defensor não for NPC...
   src.sysmessageorange Seu elmo foi totalmente arruinado!
  endif
  src.findlayer.layer_helm.remove
 endif
endif
 
///////////////////////////////////////////////
//Danificar armas /////////////////////////////
///////////////////////////////////////////////

if (<findlayer.layer_hand1.type>==t_weapon_axe) || (<findlayer.layer_hand1.type>==t_weapon_fence) || (<findlayer.layer_hand1.type>==t_weapon_sword) || (<findlayer.layer_hand1.type>==t_weapon_mace_crook) || (<findlayer.layer_hand1.type>==t_weapon_mace_pick) || (<findlayer.layer_hand1.type>==t_weapon_mace_staff) || (<findlayer.layer_hand1.type>==t_weapon_mace_smith) || (<findlayer.layer_hand1.type>==t_weapon_mace_sharp)
 if (<npc>==0)
  findlayer.layer_hand1.more1l=<findlayer.layer_hand1.more1l>-{0 1}
  if (<findlayer.layer_hand1.more1l>==0) || (<findlayer.layer_hand1.more1l> > <findlayer.layer_hand1.more1h>)
   if (<NPC>==0) // se não for NPC...
    sysmessageorange Sua arma foi destruida!
   endif
   if (<src.NPC>==0) // Se o defensor não for NPC...
    src.sysmessageorange A arma de <src.name> foi destruida!
   endif
   findlayer.layer_hand1.remove
  endif
 endif
endif

if (<findlayer.layer_hand2.type>==t_weapon_axe) || (<findlayer.layer_hand2.type>==t_weapon_fence) || (<findlayer.layer_hand2.type>==t_weapon_sword) || (<findlayer.layer_hand2.type>==t_weapon_mace_crook) || (<findlayer.layer_hand2.type>==t_weapon_mace_pick) || (<findlayer.layer_hand2.type>==t_weapon_mace_staff) || (<findlayer.layer_hand2.type>==t_weapon_mace_smith) || (<findlayer.layer_hand2.type>==t_weapon_mace_sharp)
 if (<NPC>==0)
  findlayer.layer_hand2.more1l=<findlayer.layer_hand2.more1l>-{0 1}
  if (<findlayer.layer_hand2.more1l>==0) || (<findlayer.layer_hand2.more1l> > <findlayer.layer_hand2.more1h>)
   if (<NPC>==0) // se não for NPC...
    sysmessageorange Sua arma foi destruida!
   endif
   if (<src.NPC>==0) // Se o defensor não for NPC...
    src.sysmessageorange A arma de <src.name> foi destruida!
   endif
   findlayer.layer_hand2.remove
  endif
 endif
endif

///////////////////////////////////////////////
/////////////Ganho de SKills///////////////////
///////////////////////////////////////////////

//IF (<NPC>==0) && (<eval <tag0.fRP>> >= (-2))
// IF <Archery>>1001
//  Archery=(1000)
// ELIF (<action> == Skill_Archery) && ({0 1000} >= <archery>) && (RAND(10)=2)
//  ARCHERY=<ARCHERY>+(1)
// ELIF (<action> == Skill_Archery) && ({0 1000} >= <tactics>) && (RAND(14)=1)
//  tactics=<tactics>+(1)
// ELIF <Fencing>>1001
//  Fencing=(1000)
// ELIF (<action> == Skill_Fencing) && ({0 1000} >= <fencing>) && (RAND(14)=1)
//  FENCING=<FENCING>+(1)
// ELIF (<action> == Skill_Fencing) && ({0 1000} >= <tactics>) && (RAND(20)=1)
//  tactics=<tactics>+(1)
// ELIF <Macefighting>>1001
//  Macefighting=(1000)
// ELIF (<action> == Skill_Macefighting) && ({0 1000} >= <macefighting>) && (RAND(14)=2)
//  MACEFIGHTING=<MACEFIGHTING>+(1)
// ELIF (<action> == Skill_macefighting) && ({0 1000} >= <tactics>) && (RAND(20)=1)
//  tactics=<tactics>+(1)
// ELIF <Parrying>>1001
//  Parrying=(1000)
// ELIF (<action> == Skill_Parrying) && ({0 1000} >= <parrying>) && (RAND(14)=2)
//  Parrying=<Parrying>+(1)
// ELIF <swordsmanship>>1001
//  swordsmanship=(1000)
// ELIF (<action> == Skill_Swordsmanship) && ({0 1000} >= <swordsmanship>) && (RAND(14)=2)
//  swordsmanship=<swordsmanship>+(1)
// ELIF (<action> == Skill_swordsmanship) && ({0 1000} >= <tactics>) && (RAND(20)=1)
//  tactics=<tactics>+(1)
// ELIF <wrestling>>1001
//  Wrestling=(1000)
// ELIF (<action> == Skill_Wrestling) && ({0 1000} >= <wrestling>) && (RAND(14)=2)
//  Wrestling=<Wrestling>+(1)
// ELIF (<action> == Skill_wrestling) && ({0 1000} >= <tactics>) && (RAND(14)=1)
//  tactics=<tactics>+(1)
// ENDIF
//ENDIF

//if (<eval <tag0.fRP>> < (-2)) && (<npc>==0)
//DORAND 6
// sysmessageorange Seus pontos de RP estao baixos. Ganho de skill negado.
//ENDDO
//endif


///////////////////////////////////////////////
/////////////Produtos finais///////////////////
///////////////////////////////////////////////

///// Falas bonitinhas
//if (<NPC>==0) //Se for um jogador e não um NPC...
// dorand 3
//  sysmessageblue Um <LOCAL.cbt.for> golpe acerta <src.name> <LOCAL.loc>.
//  sysmessageblue Voce desfere um <LOCAL.cbt.for> golpe <LOCAL.loc> de <src.name>.
//  sysmessageblue Seu <LOCAL.cbt.for> golpe pega em cheio <LOCAL.loc> de <src.name>.
// enddo
//endif
//if (<src.NPC>==0) //Se o alvo for um jogador...
// dorand 3
//  src.sysmessagered <name> acerta um <LOCAL.cbt.for> golpe em voce <LOCAL.loc>.
//  src.sysmessagered O <LOCAL.cbt.for> golpe de <name> te acerta <LOCAL.loc>.
//  src.sysmessagered Um golpe <LOCAL.cbt.for> de <name> te pega desprevinido <LOCAL.loc>
// enddo
//endif

/////Produzir sons
IF (<action> == Skill_wrestling)
 dorand 4
  sfx 014a
  sfx 013d
  sfx 0137
  sfx 013b
 enddo
 if (<NPC>==0)
  bark
 endif
ELIF (<action> == Skill_Swordsmanship) || (<action> == Skill_Fencing)
 dorand 4
  sfx 0237
  sfx 0236
  sfx 023c
  sfx 023b
 enddo
ELIF (<action> == Skill_Macefighting)
 dorand 4
  sfx 0149
  sfx 0143
  sfx 0146
  sfx 023b
 enddo
endif

///// Modificadores de dano
argn1=<eval <argn1>+<LOCAL.dano>>
if (<argn1> <= 0)
 argn1=1
endif
 
/////cansar atacante
if (<stam> >= 6)
 stam=<stam>-{0 2}
endif

/////desconcentrar vítima
if (<src.mana> >= 6)
 src.mana=<src.mana>-{0 2}
endif

///// Transfere e aplica dano
src.do_damage <argn1>,dam_physical,<uid>

///// Marca assassino (sistema de morte)
//if (<argn1> >= <src.hits>)
// if (<NPC>==0)
//  tag.lastkillname=<src.tag.name>
//  tag.lastkillUID=<src.uid>
//  tag.fpk=(<tag0.fpk>+1)
// endif
//endif

///// Salvar nomes para .ficha
IF (<src.npc>==0) && (<npc>==0)
 IF (<src.tag.lastagr1uid> != <uid>)
  src.tag.lastagr2uid=<src.tag.lastagr1uid>
  src.tag.lastagr2name=<src.tag.lastagr1name>
  src.tag.lastagr1uid=<uid>
  src.tag.lastagr1name=<tag.name>
 endif
endif

///// Perpetuar a agreção
if !(<src.flags>&statf_war)
 src.flags <src.flags>|statf_war
endif

if (<NPC>==0)
 src.attack <UID>
endif

return 1

//*************************************
// @SPELLEFFECT
//*************************************
ON=@SPELLEFFECT
//argn1=spell
//argn2=magery skill

//cura?
if (<argn1>==11) && (<restest 1 i_veneno>)
 LOCAL.poisoning=<argn2>
 LOCAL.poisoning=<eval <LOCAL.poisoning>/30>
 findid.i_veneno.more2=<eval <findid.i_veneno.more2>-<LOCAL.poisoning>>
 if (<eval <findid.i_veneno.more2>> >= 4000)
  findid.i_veneno.more2=00
  sysmessageblue O veneno foi removido do seu corpo!
  flags=<flags>&~statf_poisoned
 endif
 sysmessageblue Voce se sente um tanto melhor. 
 return 1
endif

//cura??
if (<argn1>==11) && (<restest 1 i_mry_peste>)
 if (<argn2> < 1001)
  sysmessagered A cura foi muito fraca para amenizar a peste.
 else
  src.findid.i_mry_peste.more2=(<src.findid.i_mry_peste.more2>/3)
  sysmessageblue Voce se sente um pouco melhor.
 endif
 return 1
endif

//poison?
if (<argn1>==20) && (<restest 1 i_veneno>==0)
 LOCAL.poison <argn2>
 LOCAL.poison <eval <LOCAL.poison>/20>
 newitem i_veneno
 act.more2 <LOCAL.poison>
 act.timer 5
 act.cont <uid>
 flags=<flags>|statf_poisoned
 effect 2 i_fx_curse 0 30 0
 return 1
endif



//*************************************
// @ITEMDCLICK
//*************************************
ON=@ITEMDCLICK
if (<act.Baseid>==i_bottle_green) && (<act.more1>==s_poison)
 LOCAL.actuid=<act.uid>
 newitem i_mry_usepoison
 new.link <LOCAL.actuid>
 new.equip
 new.use
 return 1
endif


//*****************************************************************************
//*****************************************************************************
// TYPEDEFs
//*****************************************************************************
//*****************************************************************************
[TYPEDEF t_veneno]
on=@step
if (<src.restest 1 i_veneno>==0)
 src.veneno <more2>
endif

//*****************************************************************************
//*****************************************************************************
// ITEMDEFs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// i_mry_usepoison
//*****************************************************************************
[ITEMDEF i_mry_usepoison]
id=i_rune_poison
name=usando veneno
type=t_eq_script
layer=layer_special

ON=@DCLICK
timer=30
attr=attr_decay
targetg Onde deseja utilizar o veneno?
return 1

ON=@TARGON_CHAR
if (<link.more2> <= 1000 )
 cont.sysmessage Este veneno e muito fraco para ser jogado deste forma.
 remove
 return 1
endif
if (<src.targ.distance> > 3)
 cont.sysmessage Muito longe...
 remove
 return 1
endif
if (<src.targ.restest 1 i_veneno>==0)
 sfx 03e
 cont.emotegreen joga o frasco de veneno
 cont.newitem 0122a
 cont.act.p=<src.targ.p>
 cont.act.name veneno
 cont.act.timer {10 30}
 cont.act.attr attr_decay|attr_move_never
 cont.act.color 0b0
 cont.act=0
 cont.newitem i_veneno
 cont.act.timerd 1
 cont.act.more2 <eval <link.more2>/20>
 cont.act.cont <src.targ.uid>
 cont.act.timerd 1
 src.targ.flags=<src.targ.flags>|statf_poisoned
 src.targ.update
 link.remove
 remove
endif
return 1

ON=@TARGON_ITEM
if (<src.targ.BaseID>==i_arrow)
 if (<src.targ.cont.uid> != <src.findlayer.layer_pack.uid>)
  cont.sysmessage As flechas precisam estar em suas maos.
  remove
  return 1
 endif
 if (<src.poisoning> >= {1 1300})
  src.targ.p=<src.p>
  link.remove
  src.newitem i_bottle_empty
  src.act.bounce  
  if (<src.targ.amount> >=5 )
   src.targ.cont <src.findlayer.layer_pack.uid>
   src.consume 5 <src.targ.baseid>
   src.act=0
   src.newitem i_poison_arrow
   src.act.amount 5
   src.act.bounce
  else
   src.act=0
   src.newitem i_poison_arrow
   src.act.amount <src.targ.amount>
   src.act.bounce
   src.targ.cont <src.findlayer.layer_pack.uid>
   src.consume <src.targ.amount> <src.targ.baseid>
   src.sysmessage Voce envenenou as flechas.
  endif
 else
  cont.sysmessage Voce falhou em tentar envenenar as flechas.
  link.remove
  src.newitem i_bottle_empty
  src.act.bounce
 endif
elif (<src.targ.type>==t_weapon_sword) || (<src.targ.type>==t_weapon_fence)
 if (<src.targ.cont.uid> != <src.findlayer.layer_pack.uid>) && (<src.targ.uid> != <src.findlayer.layer_hand1.uid>) && (<src.targ.uid> != <src.findlayer.layer_hand2.uid>)
  cont.sysmessage A arma precisam estar em suas maos.
  remove
  return 1
 endif
 if (<src.targ.more2> == 20)
  if (<src.targ.morex> >= 35)
   cont.sysmessage Ja tem veneno o suficiente na arma.
   remove
   return 1
  else
   src.targ.morex=<eval <src.targ.morex>+(<link.more2>/69)>
  endif
 elif (<src.targ.more2> != 0)
  cont.sysmessage Esta arma ja tem um tratamento diferenciado.
  remove
  return 1
 elif (<src.poisoning> >= {1 1300})
  src.targ.morex=<eval <link.more2>/23>
  src.targ.more2=20
  src.targ.tag.name=<src.targ.name>
  src.targ.name <src.targ.name> (envenenado)
  link.remove
  src.newitem i_bottle_empty
  src.act.bounce
  remove
  src.sysmessage Voce envenenou a arma.
  return 1
 else
  cont.sysmessage Voce falhou em tentar envenenar a arma.
  link.remove
  src.newitem i_bottle_empty
  src.act.bounce
 endif
else
 src.sysmessage Voce nao pode envenenar isto.
endif
remove
return 1


ON=@TARGON_GROUND
if (<link.more2> <=1000)
 cont.sysmessage Este veneno e muito fraco para ser jogado deste forma.
 remove
 return 1
endif
 sfx 03e
 cont.emotegreen joga o frasco de veneno
 cont.newitem 0122a
 cont.act.p=<src.targp>
 cont.act.name veneno
 cont.act.timer {30 90}
 cont.act.attr 012
 cont.act.color 0b0
 cont.act.more2=<eval <link.more2>/40>
 cont.act.type t_veneno
 link.remove
 remove
 return 1

//*****************************************************************************
// i_veneno
//*****************************************************************************
[ITEMDEF i_veneno]
id=i_rune_poison
type=t_eq_script
name=Veneno
layer=layer_flag_poison

on=@create
timer=5

on=@timer
if (<more2> == 0) || (<more2> >= 4000) || (<cont.restest 1 i_mry_desmaio>) || (<cont.flags>&statf_dead)
 cont.sysmessagegreen O efeito do veneno esta passando
 cont.flags=<cont.flags>&~statf_poisoned
 cont.update
 remove
 return 1
else
 IF (<CONT.RESTEST 2 i_veneno>
  remove
  return 1
 endif
 more2=<more2>-1
 LOCAL.dano=<eval {1 <eval <more2>/7>}>
 if (<LOCAL.dano> > 15)
  LOCAL.dano=15
 endif
 cont.do_damage <LOCAL.dano>,dam_poison,<cont>
 if (<more2> <= 20) && ({0 15}==1)
  cont.emotegreen regurgita
  cont.newitem 0122a
  cont.act.p=<cont.p>
  cont.act.name vomito
  cont.act.timer {5 10}
  cont.act.attr attr_decay|attr_move_never
  cont.act.color 02a
 elif (<more2> >= 30) && ({0 20}==1)
  cont.stun {3 7}
 elif (<more2> > 40)
  cont.emotegreen muito doente
  more2=<more2>-{3 5}
 elif (<more2> > 15)
  cont.emotegreen doente
    more2=<more2>-{0 3}
 else
  cont.emotegreen um pouco doente
 endif
 timer {3 7}
 return 1
endif

//*****************************************************************************
// i_stun
//*****************************************************************************
[ITEMDEF i_stun]
id=02080
type=t_eq_script
name=Paralizado
layer=layer_special

on=@timer
 cont.flags=<cont.flags>&~statf_freeze
 cont.sysmessagered O efeito da paralisacao cessou.
 remove
 return 1
 
//*****************************************************************************
//*****************************************************************************
// FUNCTIONs
//*****************************************************************************
//*****************************************************************************

[PLEVEL 1]
combate

[FUNCTION combate]
src.dialog d_estrategia
return 1

//*****************************************************************************
// do_damage( dano, tipo, uid_attacker )
//*****************************************************************************
//src leva o dano
[FUNCTION do_damage]
OBJ=<argv[2]>
//say d<argv[0]> t <argv[1]> de <argv[2]>

//do damage
SRC.hits=<SRC.hits>-<argv[0]>

//aos damage (show counter above player head)
if (0<src.npc> == 0)
	SRC.SENDPACKET  0bf W11 W022 01 D<SRC> B<argv[0]> 
endif
if (0<OBJ.npc> == 0)
	OBJ.SENDPACKET  0bf W11 W022 01 D<SRC> B<argv[0]> 
endif
return 1

[FUNCTION veneno]
newitem i_veneno
act.more2 <eval <argn>>
act.timer 5
act.equip
flags=<flags>|statf_poisoned


[FUNCTION stun]
newitem i_stun
act.cont=<uid>
if (0<argn1><=0)
 act.timer {1 5}
else
 act.timer <argn1>
endif
flags=<flags>|statf_freeze
emotered paralizado

//[FUNCTION action] //Thks to Kell
//RETURN (<ACTION>|0d2000000)


//*****************************************************************************
//*****************************************************************************
// GUMPs
//*****************************************************************************
//*****************************************************************************

[DIALOG d_estrategia]
200, 150
noclose
nomove
PAGE 0
gumppic 0 0 1140
gumppic 61 88 1143
gumppic 136 41 2445
text 153 41 1101 0
text 65 69 455 1
text 263 69 455 2
button <eval <ctag0.roll>> 94 2117 2118 0 0 0
button 45 95 2223 2223 1 0 1
button 328 95 2224 2224 1 0 2
button 137 118 2117 2118 1 0 3
button 137 139 2117 2118 1 0 4
button 137 161 2117 2118 1 0 5
text 159 114 1345 3
text 159 136 1345 4
text 158 156 1345 5
button 156 196 249 248 1 0 6

[DIALOG d_estrategia TEXT]
Estrategia
Defensiva
Agressiva
Agressiva
Defensiva
Neutra

[DIALOG d_estrategia BUTTON]
on=1
if (<tag0.agr><=0)
 tag.agr=0
 DIALOG d_estrategia
 return 1
endif
tag.agr=<eval (<tag0.agr>-1)>
ctag.roll=<eval (<tag0.agr>*24)+70>
DIALOG d_estrategia
return 1

on=2
if (<tag0.agr>>=10)
 tag.agr=10
 DIALOG d_estrategia
 return 1
endif
tag.agr=<eval (<tag0.agr>+1)>
ctag.roll=<eval (<tag0.agr>*24)+70>
DIALOG d_estrategia
return 1

on=3
tag.agr=10
ctag.roll=(<tag.agr>*24)+70
DIALOG d_estrategia
return 1

on=4
tag.agr=0
ctag.roll=(<tag.agr>*24)+70
DIALOG d_estrategia
return 1

on=5
tag.agr=5
ctag.roll=(<tag.agr>*24)+70
DIALOG d_estrategia
return 1

on=6
return 1

[EOF]