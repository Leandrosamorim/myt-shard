//***********************************************************
//		SPELL EARTHQUAKE
//***********************************************************

[Spell 57]
DEFNAME=s_earthquake
NAME=Earthquake
SOUND=snd_SPELL_EARTHQUAKE
RUNES=""
CAST_TIME=70
RESOURCES=i_reag_blood_moss,i_reag_ginseng,i_reag_mandrake_root,i_reag_sulfur_ash
RUNE_ITEM=i_rune_EARTHQUAKE
SCROLL_ITEM=i_scroll_EARTHQUAKE
FLAGS=SPELLFLAG_HARM| SPELLFLAG_RESIST
EFFECT_ID=0
EFFECT=30,45
DURATION=30.0
MANAUSE=35
SKILLREQ=MAGERY 57.0
// NOTE: does Earthquake use a duration?? IF so, no more then 30secs. This spell is more like an irradiating wave thus should have a set distance/time it can be effective
INTERRUPT=100.0,100.0

on=@select
return <trycast s_earthquake,spl_terra>

on=@Success
mana=<mana>-<ctag.cMana>

on=@Fail
mana=<mana>-<ctag.cMana>

on=@Effect
IF (!<src.spell_delay>)
 src.f_spl_earthquake <src.cTag.cSkill>
 src.spell_delay <f_rangeValue 600, 120, <src.cTag.cSkill>>
endif
return 1

[FUNCTION f_spl_earthquake]
SERV.NEWITEM i_spl_earthquake
IF (<IsNum <argv0>>)
 NEW.MORE1=<argv0>
ELSE
 NEW.MORE1=<magery>
ENDIF
NEW.p=<p>
NEW.LINK=<UID>
new.timerd 1
sfx 022d
TIMERF 2,SFX,022e

[FUNCTION f_spl_earthquake_rumble]
SERV.NEWITEM {{01b01 01b06} 1 {01b08} 1}
NEW.ATTR=012
NEW.TIMER=3
NEW.MOVENEAR <UID> 12
NEW.UPDATE
DORAND 12
 NEW.SAY pom
 NEW.SAY pum
 NEW.SAY pla
 NEW.SAY planc
 NEW.SAY plin
 NEW.SAY bom!
 NEW.SAY bum!
 NEW.SAY Bam!
 NEW.SAY Crash!
 NEW.SAY Cram!
 NEW.SAY TRAN!
 NEW.SAY TRATAN!
 NEW.SAY CABUM!
ENDDO

[ITEMDEF i_spl_earthquake]
id=01b01
NAME=Earthquake
TYPE=t_script

on=@timer
if (<more2> <= 8)
 f_spl_earthquake_rumble
 more2=<MORE2>+1
 timerd 2
else
 OBJ=<UID>
 FORCHARS
  sfx 011e
  damagespell s_Earthquake, DAM_PHYSICAL, <OBJ.MORE1>, <OBJ.LINK>
  f_faint
  timerf <eval {1 3}>,f_wakeup
 ENDFOR
 remove
endif
return 1

//***********************************************************
//		SPELL STALAGMITE
//***********************************************************

[Spell 200]
DEFNAME=s_stalagmite
NAME=Stalagmite
SOUND=022d
RUNES=""
CAST_TIME=15
RESOURCES=i_reag_blood_moss,i_reag_ginseng,i_reag_mandrake_root,i_reag_sulfur_ash
RUNE_ITEM=i_rune_EARTHQUAKE
SCROLL_ITEM=i_scroll_EARTHQUAKE
FLAGS=SPELLFLAG_HARM | spellflag_targ_char | spellflag_scripted
EFFECT_ID=0
EFFECT=3,8
//DURATION=30.0
MANAUSE=5
SKILLREQ=MAGERY 35.0
INTERRUPT=100.0,100.0

on=@select
return <trycast s_stalagmite,spl_terra>

on=@Success
f_consumeMana <ctag.cMana>

on=@Fail
f_consumeMana <ctag.cMana>

on=@Effect
SERV.NEWITEM i_spl_stalagmite
new.dispid={08e1 1 08e0 1 08e7 1}
new.p=<p.x>,<p.y>,<eval <p.z>-10>
new.timerd 1
sfx 022d
DAMAGESPELL s_stalagmite, dam_physical, <src.cTAG.cSkill>, <src.uid>
return 1

[ITEMDEF i_spl_stalagmite]
ID=08e1
TYPE=t_script

on=@timer
if (<more2> <=10)
 more2=<more2>+1
 nudgeup 1
 timerd 1
else
 timerf 15,remove
endif
return 1

on=@step
SRC.DAMAGE <eval {2 5}> DAM_PHYSICAL


//***********************************************************
//		SPELL CONJURE ELEM EARTH
//***********************************************************
[Spell 201]
DEFNAME=s_conjure_elem_earth
NAME=Conjurar elemental da terra
//SOUND=022d
RUNES=""
CAST_TIME=15
RESOURCES=i_reag_blood_moss,i_reag_ginseng,i_reag_mandrake_root,i_reag_sulfur_ash
RUNE_ITEM=i_rune_EARTHQUAKE
SCROLL_ITEM=i_scroll_EARTHQUAKE
FLAGS=spellflag_targ_xyz | spellflag_scripted
EFFECT_ID=0
EFFECT=3,8
//DURATION=30.0
MANAUSE=50
SKILLREQ=MAGERY 55.0
INTERRUPT=100.0,100.0

on=@start
sfx 022d

on=@select
return <trycast s_conjure_elem_earth,spl_terra>

on=@Success
LOCAL.timer=<f_rangeValue 5,120,<src.cTag.cSkill>>
obj=<f_conjure_creature c_elem_earth,<LOCAL.timer>>
obj.p=<src.targp>
obj.update
f_consumeMana <ctag.cMana>

on=@Fail
f_consumeMana <ctag.cMana>


//***********************************************************
//		SPELL HURL STONE
//***********************************************************

[Spell 202]
DEFNAME=s_hurlStone
NAME=Arremessar pedra
SOUND=0239
RUNES=""
CAST_TIME=25
RESOURCES=i_reag_blood_moss,i_reag_ginseng,i_reag_mandrake_root,i_reag_sulfur_ash
RUNE_ITEM=i_rune_EARTHQUAKE
SCROLL_ITEM=i_scroll_EARTHQUAKE
FLAGS=SPELLFLAG_HARM | spellflag_targ_char | spellflag_scripted | spellflag_dir_anim
EFFECT_ID=0
EFFECT=6,13
//DURATION=30.0
MANAUSE=5
SKILLREQ=MAGERY 35.0
INTERRUPT=100.0,100.0

on=@select
return <trycast s_hurlStone,spl_terra>

on=@Success
f_consumeMana <ctag.cMana>

on=@Fail
f_consumeMana <ctag.cMana>

on=@Effect
EFFECT 0 i_rock_7 6
DAMAGESPELL s_hurlStone, dam_physical, <src.cTAG.cSkill>, <src.uid>
return 1

//***********************************************************
//		SPELL STALAGMITE CELL
//***********************************************************

[Spell 203]
DEFNAME=s_stalagmiteCell
NAME=Cela de stalagmites
SOUND=022D
RUNES=""
CAST_TIME=15
RESOURCES=i_reag_blood_moss,i_reag_ginseng,i_reag_mandrake_root,i_reag_sulfur_ash
RUNE_ITEM=i_rune_EARTHQUAKE
SCROLL_ITEM=i_scroll_EARTHQUAKE
FLAGS=SPELLFLAG_HARM | spellflag_targ_char | spellflag_scripted
EFFECT_ID=0
EFFECT=3,8
//DURATION=30.0
MANAUSE=42
SKILLREQ=MAGERY 46.5
INTERRUPT=100.0,100.0

on=@select
return <trycast s_stalagmiteCell,spl_terra>

on=@Success
f_consumeMana <ctag.cMana>

on=@Fail
f_consumeMana <ctag.cMana>

on=@Effect
f_cela i_spl_stalagmite,i_spl_stalagmite,1
return 1

//***********************************************************
//		SPELL SUMMON EARTH ELEMENTAL
//***********************************************************
[Spell 204]
DEFNAME=s_summon_elem_earth
NAME=Conjurar elemental da terra
//SOUND=022d
RUNES=""
CAST_TIME=15
RESOURCES=i_reag_blood_moss,i_reag_ginseng,i_reag_mandrake_root,i_reag_sulfur_ash
RUNE_ITEM=i_rune_EARTHQUAKE
SCROLL_ITEM=i_scroll_EARTHQUAKE
FLAGS=spellflag_targ_xyz | spellflag_scripted
EFFECT_ID=0
EFFECT=3,8
//DURATION=30.0
MANAUSE=65
SKILLREQ=MAGERY 65.0
INTERRUPT=100.0,100.0

on=@start
sfx 022d

on=@select
return <trycast s_summon_elem_earth,spl_terra>

on=@Success
LOCAL.timer=<f_rangeValue 5,40,<src.cTag.cSkill>>
obj=<f_summon_creature c_elem_earth,<LOCAL.timer>>
obj.p=<src.targp>
obj.update
f_consumeMana <ctag.cMana>

on=@Fail
f_consumeMana <ctag.cMana>

//***********************************************************
//		SPELL SUMMON MAGIC EARTH WEAPON
//***********************************************************
[Spell 205]
DEFNAME=s_summon_earth_weapon
NAME=Conjurar arma magica da terra
//SOUND=022d
RUNES=""
CAST_TIME=33
RESOURCES=i_reag_blood_moss,i_reag_ginseng,i_reag_mandrake_root,i_reag_sulfur_ash
RUNE_ITEM=i_rune_EARTHQUAKE
SCROLL_ITEM=i_scroll_EARTHQUAKE
FLAGS=spellflag_targ_xyz | spellflag_scripted
EFFECT_ID=0
EFFECT=3,8
//DURATION=30.0
MANAUSE=15
SKILLREQ=MAGERY 35.0
INTERRUPT=100.0,100.0

on=@start
sfx 022d

on=@select
return <trycast s_summon_earth_weapon,spl_terra>

on=@Success
serv.newitem i_scimitar_dummie
new.color=0221
new.type t_weapon_sword
new.tag.override.skill=Magery
new.modar=<BETWEEN 0,20,<src.cTag.cSkill>,200.0>
new.maxhits 500
new.hits 500
new.p=<src.targp>
new.update
new.effect 2,i_fx_smoke,0,26,0
new.sfx 01f7
new.emotered surge da terra
LOCAL.timer=<BETWEEN 20,120,<src.cTag.cSkill>,200.0>
new.timerf <LOCAL.timer>,remove
timerf <LOCAL.timer>,sysmessagered,A arma magica voltou para a terra.
timerf <LOCAL.timer>,sfx,05c0
f_consumeMana <ctag.cMana>

on=@Fail
f_consumeMana <ctag.cMana>

//***********************************************************
//		SPELL SUMMON MAGIC EARTH SHIELD
//***********************************************************
[Spell 206]
DEFNAME=s_summon_earth_shield
NAME=Conjurar escudo magico da terra
//SOUND=022d
RUNES=""
CAST_TIME=33
RESOURCES=i_reag_blood_moss,i_reag_ginseng,i_reag_mandrake_root,i_reag_sulfur_ash
RUNE_ITEM=i_rune_EARTHQUAKE
SCROLL_ITEM=i_scroll_EARTHQUAKE
FLAGS=spellflag_targ_xyz | spellflag_scripted
EFFECT_ID=0
EFFECT=3,8
//DURATION=30.0
MANAUSE=30
SKILLREQ=MAGERY 35.0
INTERRUPT=100.0,100.0

on=@start
sfx 022d

on=@select
return <trycast s_summon_earth_shield,spl_terra>

on=@Success
serv.newitem i_peito_placa_dummie
new.color=0908
new.modar=<BETWEEN 0,100,<src.cTag.cSkill>,200.0>
new.maxhits 500
new.hits 500
new.name peitoral de terra
new.p=<src.targp>
new.update
new.effect 2,i_fx_smoke,0,26,0
new.sfx 01f7
new.emotered surge da terra
LOCAL.timer=<BETWEEN 20,120,<src.cTag.cSkill>,200.0>
new.timerf <LOCAL.timer>,remove
timerf <LOCAL.timer>,sysmessagered,A armadura magica voltou para a terra.
timerf <LOCAL.timer>,sfx,05c0
f_consumeMana <ctag.cMana>

on=@Fail
f_consumeMana <ctag.cMana>

[EOF]