//***********************************************************
//		SPELL EARTHQUAKE
//***********************************************************

[Spell 57]
DEFNAME=s_earthquake
NAME=Earthquake
SOUND=snd_SPELL_EARTHQUAKE
RUNES=IVP
CAST_TIME=70
RESOURCES=i_reag_blood_moss,i_reag_ginseng,i_reag_mandrake_root,i_reag_sulfur_ash
RUNE_ITEM=i_rune_EARTHQUAKE
SCROLL_ITEM=i_scroll_EARTHQUAKE
FLAGS=SPELLFLAG_HARM| SPELLFLAG_RESIST
EFFECT_ID=0
EFFECT=30,45
DURATION=30.0
MANAUSE=35
SKILLREQ=MAGERY 70.0
// NOTE: does Earthquake use a duration?? IF so, no more then 30secs. This spell is more like an irradiating wave thus should have a set distance/time it can be effective
INTERRUPT=100.0,100.0

on=@select
return <trycast s_earthquake,spl_earth>

on=@Success
mana=<mana>-<ctag.cMana>

on=@Fail
mana=<mana>-<ctag.cMana>

on=@Effect
IF (!<src.spell_delay>)
 src.f_spl_earthquake <src.cTag.cSkill>
 src.spell_delay <f_rangeValue 600, 120, <src.cTag.cSkill>>
endif
return 1

[FUNCTION f_spl_earthquake]
SERV.NEWITEM i_spl_earthquake
IF (IsNum(<argv0>))
 NEW.MORE1=<argv0>
ELSE
 NEW.MORE1=<magery>
ENDIF
NEW.p=<p>
NEW.LINK=<UID>
new.timerd 1
sfx 022d
TIMERF 2,SFX,022e

[FUNCTION f_spl_earthquake_rumble]
SERV.NEWITEM {{01b01 01b06} 1 {01b08} 1}
NEW.ATTR=012
NEW.TIMER=3
NEW.MOVENEAR <UID> 12
NEW.UPDATE
DORAND 12
 NEW.SAY pom
 NEW.SAY pum
 NEW.SAY pla
 NEW.SAY planc
 NEW.SAY plin
 NEW.SAY bom!
 NEW.SAY bum!
 NEW.SAY Bam!
 NEW.SAY Crash!
 NEW.SAY Cram!
 NEW.SAY TRAN!
 NEW.SAY TRATAN!
 NEW.SAY CABUM!
ENDDO

[ITEMDEF i_spl_earthquake]
id=01b01
NAME=Earthquake
TYPE=t_script

on=@timer
if (<more2> <= 8)
 f_spl_earthquake_rumble
 more2=<MORE2>+1
 timerd 2
else
 OBJ=<UID>
 FORCHARS
  sfx 011e
  damagespell s_Earthquake, DAM_PHYSICAL, <OBJ.MORE1>, <OBJ.LINK>
  if (<hits> > 0) && (!<NPC>)
   sleep
   morte_equip <UID>
  endif
 ENDFOR
 remove
endif
return 1

//***********************************************************
//		SPELL STALAGMITE
//***********************************************************
[FUNCTION t]
try cast 200

[Spell 200]
DEFNAME=s_stalagmite
NAME=Stalagmite
SOUND=022d
RUNES=0
CAST_TIME=15
RESOURCES=i_reag_blood_moss,i_reag_ginseng,i_reag_mandrake_root,i_reag_sulfur_ash
RUNE_ITEM=i_rune_EARTHQUAKE
SCROLL_ITEM=i_scroll_EARTHQUAKE
FLAGS=SPELLFLAG_HARM | spellflag_targ_char | spellflag_scripted
EFFECT_ID=0
EFFECT=3,8
//DURATION=30.0
MANAUSE=5
SKILLREQ=MAGERY 35.0
INTERRUPT=100.0,100.0

on=@select
return <trycast s_stalagmite,spl_earth>

on=@Success
mana=<mana>-<ctag.cMana>

on=@Fail
mana=<mana>-<ctag.cMana>

on=@Effect
SERV.NEWITEM i_spl_stalagmite
new.dispid={08e1 1 08e0 1 08e7 1}
new.p=<p.x>,<p.y>,<eval <p.z>-10>
new.timerd 1
sfx 022d
DAMAGESPELL s_stalagmite, dam_physical, <src.cTAG.cSkill>, <src.uid>

[ITEMDEF i_spl_stalagmite]
ID=08e1
TYPE=t_script

on=@timer
if (<more2> <=10)
 more2=<more2>+1
 nudgeup 1
 timerd 1
else
 timerf 15,remove
endif
return 1

on=@step
SRC.DAMAGE <eval {2 5}> DAM_PHYSICAL

//***********************************************************
//		SPELL SUMMON EARTH ELEMENTAL
//***********************************************************
[FUNCTION f_spl_summon_elem_earth]
LOCAL.timer=<eval <magery>/67>		//0 a 15 segundos
f_summon_creature c_elem_earth,<LOCAL.timer>

[EOF]