[EVENTS e_PvM]
//CAHRGE - NPCs que podem correr, correm contra o player.
on=@NPCActFight
if (<CAN>&MT_RUN)
 if (<magery> == 0) && (<distance <src.uid>> > 2) && (<NPCCanCharge>)
  f_NPC_AI_Charge 1
 elif (<magery> == 0) && (<distance <src.uid>> < 2)
  f_NPC_AI_Charge 2
 endif
endif

[FUNCTION f_NPC_AI_Charge]
if (<argn>==1)
 moddex=<moddex>+100
 tag.HasCharge=1
 LOCAL.timer=<eval {30 60}>
 timerf <LOCAL.timer>,f_NPC_AI_Charge
elif (<argn>==2)
 moddex=<moddex>-100
else
 tag.HasCharge=0
endif

[FUNCTION NPCCanCharge]
if (<tag0.HasCharge>==1)
 return 0
else
 return 1
endif

[EVENTS e_Undead]
on=@NPCLookAtChar
if (<src.restest 1 i_spl_death_mask>)
	return 1
endif


on=@NPCActFight
if (<src.restest 1 i_spl_death_mask>)
	return 1
endif

ON=@EnvironChange
//	Return 0
	// Enter "//" before Return 0, if you want to make script working
	// i like the dark
	if (<sector.isdark>) || (<flags>& (statf_nightsight|statf_indoors))
		if (!<tag0.lightstr>)
			return 0
		endif
		anim 011
		bark 4
		str=<tag.lightstr>	// restore me.
		hits=<str>
		tag.lightstr=
		karma=<tag0.lightkarma>
		tag.lightkarma=//just dispose of this.
		fame=<tag0.lightfame>
		tag.lightfame=
		flags ^= statf_conjured	// no loot if killed.
		return 0
	endif
	// i can't live in light areas. weaken me ? or destroy me ?
	anim 014
	bark 4
	if (<tag0.lightstr>)
		return 0
	endif
	if ( <flags>&statf_conjured	) 
		remove
		return 1
	endif
	flags |= statf_conjured	// no loot if killed.
	tag.lightstr=<str>
	str=1
	hits=1	// very weak.
	tag.lightkarma=<karma>	// killing it means nothing now.
	karma=-2
	tag.lightfame=<fame>
	fame=10
	return 0

[EVENTS e_mage_phaser]
on=@SpellCast
if (<mana> > 25) && (!<eval {0 4}>)
 OBJ=<MEMORYFINDTYPE.memory_war_targ.link>
 movenear <OBJ.UID> 2
 EFFECT 3 i_fx_smoke 0 16 0
endif
return 1

[EVENTS e_NPC_immune]
on=@GetHit
//ARGN2 TIPO
IF (<argn2>&<TAG0.IMMUNE>)
 DORAND 2
  act.sysmessageorange <name> parece ser imune a este tipo de ataque.
  emotered ileso
 ENDDO
endif

[EVENTS e_NPC_aggravate]
on=@GetHit
//ARGN2 TIPO
IF (<argn2>&<TAG0.AGGRAVATE>)
 DORAND 2
  act.sysmessageorange <name> parece muito assustado com este tipo de ataque.
  emotered dor
 ENDDO
endif

[EVENTS e_NPC_regendam]
on=@GetHit
//ARGN2 TIPO
IF (<argn2>&<TAG0.REGENDAM>)
 DORAND 2
  act.sysmessageorange <name> parece melhor agora do que antes do ataque.
  emotered aliviado
 ENDDO
endif

[FUNCTION NPC_IMMUNE]
tag0.immune <eval <argv0>>
EVENTS +e_NPC_immune

[FUNCTION NPC_AGGRAVATE]
tag0.aggravate <eval <argv0>>
EVENTS +e_NPC_aggravate

[FUNCTION NPC_REGENDAM]
tag0.regendam <eval <argv0>>
EVENTS +e_NPC_regendam

[DEFNAME undead_cast]
dark_gift		01
death_harm		02
death_bolt		03
death_curse		04
infestation		05
fear_paralyze		06
death_poison_field	07
in_putrefus		08
summon_undead		09
death_confusion		10

[EVENTS e_caster_warrior]

[EVENTS e_caster_undead]
on=@SpellCast
if (<NPC_CanCast>)
 OBJ=<MEMORYFINDTYPE.memory_war_targ.link>
 DORAND <eval <magery>/50>
  undead_cast 1, <obj.uid>	//Magery 5.0
  undead_cast 1, <obj.uid>
  undead_cast 1, <obj.uid>
  undead_cast 1, <obj.uid>
  undead_cast 2, <obj.uid>
  undead_cast 2, <obj.uid>	//Magery 30.0
  undead_cast 2, <obj.uid>
  undead_cast 3, <obj.uid>
  undead_cast 3, <obj.uid>
  undead_cast 3, <obj.uid>	//Magery 50.0
  undead_cast 4, <obj.uid>
  undead_cast 4, <obj.uid>
  undead_cast 5, <obj.uid>
  undead_cast 5, <obj.uid>
  undead_cast 6, <obj.uid>	//Magery 75.0
  undead_cast 6, <obj.uid>
  undead_cast 7, <obj.uid>
  undead_cast 8, <obj.uid>
  undead_cast 9, <obj.uid>
  undead_cast 10, <obj.uid>	//Magery 100.0
  undead_cast 1, <obj.uid>
  undead_cast 2, <obj.uid>
  undead_cast 3, <obj.uid>
  undead_cast 4, <obj.uid>
  undead_cast 5, <obj.uid>	//Magery 125.0
  undead_cast 6, <obj.uid>
  undead_cast 7, <obj.uid>
  undead_cast 8, <obj.uid>
  undead_cast 9, <obj.uid>
  undead_cast 10, <obj.uid>	//Magery 150.0
  undead_cast 4, <obj.uid>
  undead_cast 4, <obj.uid>
  undead_cast 4, <obj.uid>
  undead_cast 5, <obj.uid>
  undead_cast 5, <obj.uid>	//Magery 175.0
  undead_cast 6, <obj.uid>
  undead_cast 7, <obj.uid>
  undead_cast 8, <obj.uid>
  undead_cast 9, <obj.uid>
  undead_cast 10, <obj.uid>	//Magery 200.0
 ENDDO
endif
return 1

[FUNCTION undead_cast]
//argv0		magia
//argv1		alvo
local.difcast=<eval (<argv0>*100)-{10 50}>
if (!<skilltest Magery <local.difcast>>)
 effect 3 i_fx_smoke_small 0 16 0
 sfx {04f 1 0227 1 0191 1}
 spell_delay {10 35}
 return 1
endif
OBJ=<argv1>
if (<argv0>==dark_gift) && (<mana> > 5)
 say=@026 mortis funi
 sfx 0fe
 OBJ.EFFECT 0 036e4 4 24 1 08ad
 LOCAL.DAM=<eval (<magery>/300) + {0 3} +1>
 OBJ.DAMAGE <LOCAL.DAM> dam_magic|dam_cold|dam_poison <UID>
 mana=<mana>-5
 spell_delay 17
elif (<argv0>==fear_paralyze) && (<mana> > 25)
 sfx 0103
 OBJ=<UID>
 emotered gargalha
 FORPLAYERS <eval (<magery>/166)+2>
  if (<skilltest MagicResistance <obj.magery>>)
   emotered resistiu
  else
   local.timer=<eval <obj.magery>/100>
   stun <local.timer>
   emotered medo
  endif
 ENDFOR
 mana=<mana>-25
 spell_delay 32
elif (<argv0>==infestation) && (<mana> > 20)
 say=@026 infestus
 FOR <eval <magery>/200>
  OBJ=<f_summon_creature c_rato_sepultura,15>
  obj.movenear <uid> {1 2}
 ENDFOR 
 mana=<mana>-15
 spell_delay 75
elif (<argv0>==death_bolt) && (<mana> > 15)
 say=@026 mortalia ominus
 sfx 015e
 OBJ.EFFECT 3 03789 4 16 1 08ad
 LOCAL.DAM=<eval (<magery>/150) + {0 5} +1>
 OBJ.DAMAGE <LOCAL.DAM> dam_magic|dam_cold|dam_poison <UID>
 mana=<mana>-15
 spell_delay 22
elif (<argv0>==death_harm) && (<mana> > 7)
 say=@026 injuris
 sfx 01fb
 OBJ.EFFECT 3 i_fx_curse 0 16 0
 LOCAL.DAM=<eval {0 (<magery>/250)} +6>
 OBJ.DAMAGE <LOCAL.DAM> dam_magic|dam_poison <UID>
 mana=<mana>-7
 spell_delay 33
elif (<argv0>==death_curse) && (<mana> > 7)
 say=@026 malektu
 OBJ.f_spl_curse <Magery>
 mana=<mana>-7
 spell_delay 33
elif (<argv0>==death_poison_field) && (<mana> > 15)
 say=@026 venomus carceris
 OBJ.f_cela_veneno <eval <Magery>/75>
 mana=<mana>-15
 spell_delay 100
elif (<argv0>==in_putrefus) && (<mana> > 20)
 say=@026 in putrefus et
 OBJ.f_spl_polymorph {c_zombie 6 c_skeleton 1}, <Magery>
 mana=<mana>-20
 spell_delay 37
 in_putrefus
elif (<argv0>==death_confusion) && (<mana> > 20)
 say=@026 conturbatis
 OBJ.f_spl_confusion <Magery>
 mana=<mana>-20
 spell_delay 5
 in_putrefus
elif (<argv0>==death_confusion) && (<mana> > 25)
 say=@026 Kal Xen
 f_spl_summon_undead <Magery>
 mana=<mana>-25
 spell_delay 35
endif

[FUNCTION NPC_CanCast]
if (<restest i_npc_spell_delay 1>)
 RETURN 0
else
 RETURN 1
endif

[FUNCTION spell_delay]
serv.newitem i_npc_spell_delay
new.timerd <argn>
new.cont <uid>

[ITEMDEF i_npc_spell_delay]
ID=01810
NAME=spell delay
TYPE=t_eq_script
LAYER=LAYER_SPECIAL

on=@timer
remove
return 1

[FUNCTION f_summon_creature]
newnpc <argv0>
act.p <p>
obj <act.uid>
serv.newitem i_memory
new.color memory_ipet
new.more1 04
new.more2 <serv.time>
new.morep <p>
new.link <uid>
new.cont <obj>
serv.newitem i_mry_summon
new.more2 <argv1>
say <argv1>
new.link <uid>
new.cont <obj>
new.timer 1
obj.effect 3 i_fx_sparkle 0 16 0 0a1
sfx 0fb
return <obj.uid>


[ITEMDEF i_mry_summon]
ID=i_rune_summon_creature
name=summoned
type=t_eq_script
layer=layer_special

on=@create
attr=020

on=@timer
more2=<more2>-1
if (<more2> <= 0) || (!<link.uid>)
 serv.newitem i_fx_vortex_full
 new.attr=02
 new.timerd 16
 cont.sfx 020c
 new.color 0a1
 new.p=<cont.p>
 cont.remove
 remove
endif
timer=1
return 1

[FUNCTION f_spl_curse]
if (!<argn>)
 local.skill 500
 local.timer 30
else
 local.timer <eval <argn>/16>
 local.skill <argn>
endif
//modstats de -5 a -25
local.modstr=-<eval {5 (<local.skill>/40)}>
local.moddex=-<eval {5 (<local.skill>/40)}>
local.modint=-<eval {5 (<local.skill>/40)}>
serv.newitem i_spl_curse
new.timer <local.timer>
new.morex=<local.modstr>
new.morey=<local.moddex>
new.morez=<local.modint>
new.equip
new.timer=<local.timer>
effect 3 i_fx_curse 0 16 0
sfx 01fa

[FUNCTION f_spl_polymorph]
if (0<argv1>==0)
 local.skill 500
 local.timer 30
else
 local.timer <eval <argv1>/16>
 local.skill <argv1>
endif
local.body=<argv0>
if (!<serv.resources.<local.body>>)
 local.body=c_zombie
else
 local.body=<argv0>
endif
//modstats de -5 a -25
local.modstr=-<eval {5 (<local.skill>/40)}>
local.moddex=-<eval {5 (<local.skill>/40)}>
local.modint=-<eval {5 (<local.skill>/40)}>
serv.newitem i_spl_polymorph
new.morex=<local.modstr>
new.morey=<local.moddex>
new.morez=<local.modint>
new.more1=<local.body>
new.equip
new.timer=<local.timer>
effect 3 i_fx_curse 0 16 0 017d
sfx 01fa

[FUNCTION f_polymorph]
//f_polymorph <body> <tempo> <str> <dex> <int>
if (0<argv1>==0)
 local.timer 30
else
 local.timer <argv1>
endif
local.body=<argv0>
if (!<serv.resources.<local.body>>)
 local.body=c_zombie
else
 local.body=<argv0>
endif
local.modstr=<argv2>
local.moddex=<argv3>
local.modint=<argv4>
serv.newitem i_spl_polymorph
new.morex=<local.modstr>
new.morey=<local.moddex>
new.morez=<local.modint>
new.more1=<local.body>
new.equip
new.timer=<local.timer>
effect 3 i_fx_curse 0 16 0 017d
sfx 01fa

[ITEMDEF i_spl_curse]
ID=i_rune_curse
name=curse
type=t_eq_script
layer=layer_special

on=@create
attr=02

on=@equip
cont.modstr=<cont.modstr>+<morex>
cont.moddex=<cont.moddex>+<morey>
cont.modint=<cont.modint>+<morez>

on=@timer
cont.modstr=<cont.modstr>-<morex>
cont.moddex=<cont.moddex>-<morey>
cont.modint=<cont.modint>-<morez>
remove
return 1

[ITEMDEF i_spl_polymorph]
ID=i_rune_polymorph
name=polymorph
type=t_eq_script
layer=layer_special

on=@create
attr=022

on=@equip
cont.modstr=<cont.modstr>+<morex>
cont.moddex=<cont.moddex>+<morey>
cont.modint=<cont.modint>+<morez>
IF (<cont.findlayer.layer_hair.UID>)
 tag.hair=<cont.findlayer.layer_hair.ID>
 tag.haircolor=<cont.findlayer.layer_hair.color>
 tag.hairtimer=<cont.findlayer.layer_hair.timer>
endif
IF (<cont.findlayer.layer_beard.UID>)
 tag.beard=<cont.findlayer.layer_beard.ID>
 tag.beardcolor=<cont.findlayer.layer_hair.color>
 tag.beardtimer=<cont.findlayer.layer_hair.timer>
endif
color=<cont.color>
local.body <cont.body>
cont.body <more1>
more1=<local.body>
cont.update

on=@timer
cont.modstr=<eval <cont.modstr>-<morex>>
cont.moddex=<eval <cont.moddex>-<morey>>
cont.modint=<eval <cont.modint>-<morez>>
cont.color=<color>
cont.body=<more1>
IF (<tag0.hair>)
 serv.newitem <tag.hair>
 new.color=<tag.haircolor>
 new.timer=<tag.hairtimer>
ENDIF
IF (<tag0.beard>)
 serv.newitem <tag.hair>
 new.color=<tag.beardcolor>
 new.timer=<tag.beardtimer>
ENDIF
cont.update
cont.effect 3 i_fx_curse 0 16 0 017d
cont.sfx 01fa
remove
return 1

[FUNCTION f_spl_confusion]
if (!0<argn>)
 LOCAL.skill={400 600}
else
 LOCAL.skill=<argn>
endif
serv.newitem i_spl_confusion
new.more2 = <eval <LOCAL.skill>/75>
new.equip
sfx 01fb
EFFECT 3 i_fx_curse 0 16 0

[ITEMDEF i_spl_confusion]
ID=i_rune_curse
name=insanity
type=t_eq_script
layer=layer_special

on=@create
attr=022
more2=5

on=@equip
cont.flags=<cont.flags>|0400000		//Hallu
timer 3
cont.sysmessagegreen Voce se sente muito estranho.

on=@timer
IF (<more2> >=0 )
 DORAND 5
  cont.msay louco
  cont.stun 3
  cont.msay louco
  cont.msay sentidos
  cont.f_vomita
 enddo
 more2=<more2>-1
 timer={2 4}
 cont.update
else
 cont.flags=<cont.flags>&~0400000	//Tira hallu
 cont.update
 cont.sysmessagegreen Voce se sente melhor
 remove
endif
return 1

[FUNCTION f_spl_summon_undead]
if (!0<argn>)
 local.skill={400 600}
else
 local.skill=<argn>
endif
local.undead={c_zombie 16 c_skeleton 10 c_skeleton_w_axe 8 c_skeleton_w_sword 8 c_grim_reaper 4 c_zumbi_superior 4 c_spectre 1}
local.rand =<eval {0 (<local.skill>/300)}>
FOR <local.rand>
 f_summon_creature <eval <local.undead>>,30
ENDFOR
return 1

[FUNCTION f_death_mask]
//simplesmente equipa um i_spl_death_mask
if (0<argv1>==0)
 local.timer 30
else
 local.timer <argv0>
endif
serv.newitem i_spl_death_mask
new.equip
new.timer=<local.timer>
effect 3 i_fx_curse 0 16 0 017d
sfx 01fa

[ITEMDEF i_spl_death_mask]
ID=i_rune_poison
name=death mask
type=t_eq_script
layer=layer_special

on=@equip
timer 1
cont.sysmessagegreen Voce se sente todo dormente, como um defunto.

on=@timer
cont.sysmessagegreen Voce volta ao seu estado natural de ser vivo.
remove
return 1