//*******************************************************************
//                     Sistema de healing por bandagens 0.3
//                                 Por Finnegan
//                             Mystical Tales Shard
//////////////////////////////////////////////////////////////


// Bandit (AKA Banditation)
// - Healer deve estar ao lado do ferido
// - Ferido deve estar ferido...
// - Ferido nao pode estar sendo curado por outro healer
// - Healer cura (Healing/2)% + Rand(5)+1 Hitpoints do ferido
//     Ex: Healing 50, Ferido tem Str 80
//         Healing/2 = 25% dos Hitpoints; 25% de 80 = 20
//         Healer vai curar de 21 a 25 pontos do ferido
//     Obs: O maximo (Healing 100/Str do ferido 100) curavel eh 55 Hitpoints
//          O minimo curavel (Healing 0) eh 1 Hitpoint
//          Todos os pontos "curaveis" estao sujeitos a teste de skill
// - A dificuldade do teste de skill eh a % de ferimentos dividido por 4
//     Ex: Ferido tem 80 de Str e esta com 25 hitpoints
//         diff = ((1-(25/80))*100)/4 = 17 de dificuldade
// - Healer estiver curando a si proprio tem um incremento de 20% na % de ferimentos
//     Ex anterior (modificacao entre colchetes):
//         diff = ((1-(25/80))*100) [* 1,2] /4 = 20 de dificuldade
// - Chance de sucesso calculado com curva-sino para cada ponto curado.
// - Para cada ponto curado a dificuldade aumenta em 1
// - Ambos healer e ferido devem permanacer parados
// - Ambos nao podem estar em warmode
// - Ambos nao podem receber ataque algum

// Possiveis alteracoes futuras:
//   Healer nao poder estar segurando nada (escudo, arma, lanterna...)
//   Healer poder curar veneno
//   Healer poder ajudar que estiver inconsciente

// Atributos da memory
// MORE1  - hitpoints que poderao ser curados
// MORE2  - dificuldade de uso da skill healing
// MOREX  - indicador de agressao
// MOREY  - se a bandagem esta sendo aplicada na propria pessoa

[TYPEDEF t_bandagem]
ON=@Dclick
    IF ( <SRC.UID> != <TOPOBJ.UID> )
        SRC.SYSMESSAGE A bandagem precisa estar na sua mochila.
    ELSE
        TARGET Que paciente deseja tratar?
    ENDIF
RETURN 1

ON=@Targon_char
    IF ( <SRC.TARG.DISTANCE> > 1 )
        SRC.SYSMESSAGE Paciente esta muito longe.
        RETURN 1
    ENDIF
    IF ( <SRC.TARG.HITS> >= <SRC.TARG.STR> )
        SRC.SYSMESSAGE O paciente nao precisa de tratamento.
        RETURN 1
    ENDIF
    IF ( <SRC.TARG.RESTEST 1 i_mry_bandagem> )
        SRC.SYSMESSAGE Este paciente ja esta sendo tratado.
        RETURN 1
    ENDIF
    IF ( <SRC.TARG.RESTEST 1 i_mry_bandagem> )
        SRC.SYSMESSAGE Este paciente ja esta sendo tratado.
        RETURN 1
    ENDIF
    IF ( <SRC.RESTEST 1 i_bandage> )
        SRC.CONSUME 1 i_bandage
    ELSE
        SRC.SYSMESSAGE A bandagem parece nao estar mais na sua mochila.
        RETURN 1
    ENDIF
    SRC.NEWITEM i_mry_bandagem
    SRC.NEW.MORE1=<EVAL (((<SRC.HEALING> / 20)*<SRC.TARG.STR>)/100)+(RAND(5)+1)>
    SRC.NEW.MORE2=<EVAL ((<SRC.TARG.STR>+-<SRC.TARG.HITS>)*100)/<SRC.TARG.STR>>
    IF ( <SRC.UID> == <SRC.TARG.UID> )
        SRC.NEW.MORE2=<EVAL <SRC.NEW.MORE2>+(<SRC.NEW.MORE2>/5)>
        SRC.NEW.MOREY=1
    ELSE
        SRC.NEW.MOREY=0
    ENDIF
    SRC.NEW.MORE2=<EVAL <SRC.NEW.MORE2>/4>
    SRC.NEW.MOREX=0
    SRC.NEW.LINK=<SRC.UID>
    SRC.NEW.TIMER=1
    SRC.NEW.CONT=<SRC.TARG.UID>
    SRC.SYSMESSAGE Voce comeca a tratar os ferimentos.
    SRC.EVENTS +e_bandagem
    SRC.TAG.BANDMEM=<SRC.NEW.UID>
    SRC.TAG.BANDX=<SRC.P.X>
    SRC.TAG.BANDY=<SRC.P.Y>
    IF ( !<SRC.NEW.MOREY> )
        SRC.TARG.SYSMESSAGE Seus ferimentos estao sendo tratados.
        SRC.TARG.EVENTS +e_bandagem
        SRC.TARG.TAG.BANDMEM=<SRC.NEW.UID>
        SRC.TARG.TAG.BANDX=<SRC.TARG.P.X>
        SRC.TARG.TAG.BANDY=<SRC.TARG.P.Y>
    ENDIF
RETURN 1


[ITEMDEF 0e21]
DEFNAME=i_bandage
NAME=bandage%ns/m
RESOURCES=i_cloth
TYPE=t_bandagem
WEIGHT=.1
DUPELIST=0ee9
CATEGORY=MyT - Items by Professions
SUBSECTION=Healer
DESCRIPTION=Clean Bandages


[EVENTS e_bandagem]
ON=@GetHit
    IF ( 0<TAG.BANDMEM> )
        UID.<TAG.BANDMEM>.MOREX=1
    ENDIF

ON=@ItemDclick
    SYSMESSAGE Voce nao pode fazer outra coisa durante o tratamento.
RETURN 1
    

[ITEMDEF i_mry_bandagem]
NAME=Com bandagens
TYPE=t_eq_script
LAYER=30

ON=@Create
    ATTR=attr_decay

ON=@Timer
    IF ( (<EVAL <LINK.TAG.BANDX>> != <LINK.P.X>) || (<EVAL <LINK.TAG.BANDY>> != <LINK.P.Y>) )
        LINK.SYSMESSAGE Voce deve permanecer parado durante o tratamento.
    ELIF ( ((<EVAL <CONT.TAG.BANDX>> != <CONT.P.X>) || (<EVAL <CONT.TAG.BANDY>> != <CONT.P.Y>)) && (!<MOREY>) )
        CONT.SYSMESSAGE Voce deve permanecer parado durante o tratamento.
    ELIF ( (<LINK.FLAGS> & statf_war) && (!<MOREY>)  )
        LINK.SYSMESSAGE Voce precisa relaxar para tratar o paciente.
    ELIF ( <CONT.FLAGS> & statf_war )
        IF ( !<MOREY> )
            LINK.SYSMESSAGE O paciente esta muito agitado.
        ENDIF
        CONT.SYSMESSAGE Voce esta muito agitado.
    ELIF ( <MOREX> )
        IF ( !<MOREY> )
            LINK.SYSMESSAGE Um ataque interrompeu o tratamento.
        ENDIF
        CONT.SYSMESSAGE Um ataque interrompeu o tratamento.
    ELIF ( ((<LINK.FLAGS> & statf_stone) || (<LINK.FLAGS> & statf_dead)) && !<MOREY> )
        CONT.SYSMESSAGE Quem estava lhe tratando perdeu a consciencia.
    ELIF ( ((<CONT.FLAGS> & statf_stone) || (<CONT.FLAGS> & statf_dead)) && !<MOREY> )
        LINK.SYSMESSAGE O paciente perdeu a consciencia.
    ELIF ( <CONT.HITS> < <CONT.STR> )
        IF ( <LINK.SKILLCHECK HEALING <MORE2>> )
            CONT.HITS=<CONT.HITS>+1
            MORE1=<MORE1>+-1
            MORE2=<MORE2>+1
            IF ( <MORE1> )
                TIMER=1
                RETURN 1
            ENDIF
        ELSE
            //LINK.GAIN_SKILL HEALING
        ENDIF
    ENDIF
    IF ( !<MOREY> )
        LINK.SYSMESSAGE Voce termina de tratar o paciente.
        LINK.EVENTS -e_bandagem
        LINK.TAG.BANDMEM=
        LINK.TAG.BANDX=
        LINK.TAG.BANDY=
    ENDIF
    CONT.SYSMESSAGE Voce nao esta mais sendo tratado.
    CONT.EVENTS -e_bandagem
    CONT.TAG.BANDMEM=
    CONT.TAG.BANDX=
    CONT.TAG.BANDY=
    LINK.NEWITEM i_bandage_bloody
    LINK.BOUNCE <LINK.NEW.UID>
    REMOVE
RETURN 1