//*****************************************************************************
//*****************************************************************************
//
// SKILL TAILORING 
//
//	Finnegan
//	UsMarine (vinicius.arroyo at gmail.com)
//
//*****************************************************************************
//*****************************************************************************

//TODO:

//TAGS usadas:

// Bandit (AKA Banditation)
// - Healer deve estar ao lado do ferido
// - Ferido deve estar ferido...
// - Ferido nao pode estar sendo curado por outro healer
// - Healer cura (Healing/2)% + Rand(5)+1 Hitpoints do ferido
//     Ex: Healing 50, Ferido tem Str 80
//         Healing/2 = 25% dos Hitpoints; 25% de 80 = 20
//         Healer vai curar de 21 a 25 pontos do ferido
//     Obs: O maximo (Healing 100/Str do ferido 100) curavel eh 55 Hitpoints
//          O minimo curavel (Healing 0) eh 1 Hitpoint
//          Todos os pontos "curaveis" estao sujeitos a teste de skill
// - A dificuldade do teste de skill eh a % de ferimentos dividido por 4
//     Ex: Ferido tem 80 de Str e esta com 25 hitpoints
//         diff = ((1-(25/80))*100)/4 = 17 de dificuldade
// - Healer estiver curando a si proprio tem um incremento de 20% na % de ferimentos
//     Ex anterior (modificacao entre colchetes):
//         diff = ((1-(25/80))*100) [* 1,2] /4 = 20 de dificuldade
// - Chance de sucesso calculado com curva-sino para cada ponto curado.
// - Para cada ponto curado a dificuldade aumenta em 1
// - Ambos healer e ferido devem permanacer parados
// - Ambos nao podem estar em warmode
// - Ambos nao podem receber ataque algum

// Possiveis alteracoes futuras:
//   Healer nao poder estar segurando nada (escudo, arma, lanterna...)
//   Healer poder curar veneno
//   Healer poder ajudar que estiver inconsciente

// Atributos da memory
// MORE1  - hitpoints que poderao ser curados
// MORE2  - dificuldade de uso da skill healing
// MOREX  - indicador de falha (1-agressao)
// MOREY  - se a bandagem esta sendo aplicada na propria pessoa

//*****************************************************************************
//*****************************************************************************
// TYPEDEFs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// t_bandagem
//*****************************************************************************
[TYPEDEF t_bandagem]
ON=@DCLICK
    IF ( <SRC.UID> != <TOPOBJ.UID> )
        SRC.SYSMESSAGERED A bandagem precisa estar na sua mochila.
    ELSE
        TARGET Que paciente deseja tratar?
    ENDIF
RETURN 1

ON=@TARGON_CHAR
    IF ( <SRC.TARG.DISTANCE> > 1 )
        SRC.SYSMESSAGERED Paciente esta muito longe.
        RETURN 1
    ENDIF
    IF ( <SRC.TARG.HITS> >= <SRC.TARG.STR> )
        SRC.SYSMESSAGERED O paciente nao precisa de tratamento.
        RETURN 1
    ENDIF
    IF ( <SRC.TARG.RESTEST 1 i_mry_bandagem> )
        SRC.SYSMESSAGERED Este paciente ja esta sendo tratado.
        RETURN 1
    ENDIF
    IF ( <SRC.RESTEST 1 i_bandage> )
        SRC.CONSUME 1 i_bandage
    ELSE
        SRC.SYSMESSAGERED A bandagem parece nao estar mais na sua mochila.
        RETURN 1
    ENDIF
    SERV.NEWITEM i_mry_bandagem
    NEW.MORE1=<EVAL ((<SRC.HEALING>*<SRC.TARG.STR>)/2000)+RAND(5)+1>
    NEW.MORE2=<EVAL ((<SRC.TARG.STR>-<SRC.TARG.HITS>)*100)/<SRC.TARG.STR>>
    IF ( <SRC.UID> == <SRC.TARG.UID> )
        NEW.MORE2=<EVAL <NEW.MORE2>+(<NEW.MORE2>/5)>
        NEW.MOREY=1
    ELSE
        NEW.MOREY=0
    ENDIF
    NEW.MORE2=<EVAL <NEW.MORE2>/4>
    NEW.MOREX=0
    NEW.LINK=<SRC.UID>
    NEW.TIMER=1
    NEW.CONT=<SRC.TARG.UID>
    SRC.SYSMESSAGEGREEN Voce comeca a tratar os ferimentos.
    SRC.EVENTS +e_bandagem
    SRC.TAG.BANDMEM=<NEW.UID>
    SRC.TAG.BANDX=<SRC.P.X>
    SRC.TAG.BANDY=<SRC.P.Y>
    IF ( !<NEW.MOREY> )
        SRC.TARG.SYSMESSAGEGREEN Seus ferimentos estao sendo tratados.
        SRC.TARG.EVENTS +e_bandagem
        SRC.TARG.TAG.BANDMEM=<NEW.UID>
        SRC.TARG.TAG.BANDX=<SRC.TARG.P.X>
        SRC.TARG.TAG.BANDY=<SRC.TARG.P.Y>
    ENDIF
RETURN 1

//*****************************************************************************
//*****************************************************************************
// EVENTs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// e_bandagem
//*****************************************************************************
[EVENTS e_bandagem]
ON=@GETHIT
	IF ( 0<TAG.BANDMEM> )
		UID.<TAG.BANDMEM>.MOREX=1
	ELSE
		EVENTS -e_bandagem
	ENDIF

ON=@ITEMDCLICK
	SYSMESSAGERED Voce nao pode fazer outra coisa durante o tratamento.
	RETURN 1
    
//*****************************************************************************
//*****************************************************************************
// ITEMDEFs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// i_bandage
//*****************************************************************************
[ITEMDEF 0e21]
DEFNAME=i_bandage
NAME=bandage%ns/m
RESOURCES=i_cloth
TYPE=t_bandagem
WEIGHT=.1
DUPELIST=0ee9
CATEGORY=MyT - Items by Professions
SUBSECTION=Healer
DESCRIPTION=Clean Bandages

//*****************************************************************************
// i_mry_bandagem
//*****************************************************************************
[ITEMDEF i_mry_bandagem]
NAME=Com bandagens
TYPE=t_eq_script
LAYER=layer_special

ON=@CREATE
    ATTR=attr_decay

ON=@TIMER
	IF (<LINK.P.X>!=<LINK.TAG.BANDX>) || (<LINK.P.Y>!=<LINK.TAG.BANDY>) || (<CONT.P.X>!=<CONT.TAG.BANDX>) || (<CONT.P.Y>!=<CONT.TAG.BANDY>)
		IF ( !<MOREY> )
			CONT.SYSMESSAGERED Deve-se permanecer parado durante o tratamento.
		ENDIF
		LINK.SYSMESSAGERED Deve-se permanecer parado durante o tratamento.
	ELIF ( <MOREX>==1 )
		IF ( !<MOREY> )
			LINK.SYSMESSAGERED Um ataque interrompeu o tratamento.
		ENDIF
		CONT.SYSMESSAGERED Um ataque interrompeu o tratamento.
	ELIF ( (<LINK.FLAGS> & statf_war) && (!<MOREY>)  )
		LINK.SYSMESSAGERED Voce precisa relaxar para tratar o paciente.
	ELIF ( <CONT.FLAGS> & statf_war )
		IF ( !<MOREY> )
			LINK.SYSMESSAGERED O paciente esta muito agitado.
		ENDIF
		CONT.SYSMESSAGERED Voce esta muito agitado.
	ELIF ( ((<LINK.FLAGS> & statf_stone) || (<LINK.FLAGS> & statf_dead)) && !<MOREY> )
		CONT.SYSMESSAGERED Quem estava lhe tratando perdeu a consciencia.
	ELIF ( ((<CONT.FLAGS> & statf_stone) || (<CONT.FLAGS> & statf_dead)) && !<MOREY> )
		LINK.SYSMESSAGERED O paciente perdeu a consciencia.
	ELIF ( <CONT.HITS> < <CONT.STR> )
		IF ( <LINK.SKILLCHECK HEALING <MORE2>> )
			CONT.HITS=<CONT.HITS>+1
			MORE1=<MORE1>-1
			MORE2=<MORE2>+1
			IF ( <MORE1> )
				TIMER=1
				RETURN 1
			ENDIF
		ELSE
			LINK.SKILL_GAIN HEALING
		ENDIF
  ENDIF
  
	IF ( !<MOREY> )
		LINK.SYSMESSAGEGREEN Voce termina de tratar o paciente.
		LINK.EVENTS -e_bandagem
		LINK.TAG.BANDMEM=
	ENDIF
	CONT.SYSMESSAGEGREEN Voce nao esta mais sendo tratado.
	CONT.EVENTS -e_bandagem
	CONT.TAG.BANDMEM=
	//nem sempre devolve bandagem suja
	IF ( <R1,10><5 )
		SERV.NEWITEM i_bandage_bloody
		NEW.CONT=<LINK>
	ENDIF
	REMOVE
RETURN 1

[EOF]