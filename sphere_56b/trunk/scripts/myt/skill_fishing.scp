
//fishing myt

[DEFNAME min_mr2]
// fishing
min_mr2_fish1 1.0
min_mr2_fish2 1.0
min_mr2_fish3 1.0
min_mr2_fish4 1.0
min_mr2_fish5 80.0
min_mr2_fish6 80.0
min_mr2_fish7 80.0
min_mr2_fish8 80.0
min_mr2_fishing_net 30.0
min_mr_bottle_empty 40.0
min_mr_boots_calf 30.0
min_mr_sandals 45.0
min_mr_shoes_plain 40.0
min_mr_boots_thigh 50.0
min_mr_sos_bottle 98.0

// (*) ADD MINIMUM LEVELS FOR OTHER FISH HERE

default_fishing_mr mr_nothing // THIS IS THE DEFAULT RESOURCE TO OBTAIN

[FUNCTION resource_targon_ground2]
if ( <src.findid(i_resprobe2).uid> ) // we are still to be relocated
return 1
endif

if ( <SRC.ACTION> != -1 )
SRC.ACTION = -1 // cancel current activity
endif

SRC.NEWITEM i_resprobe2 // Load the probe item that will send us back
SRC.ACT.P = <SRC.TARGP> // place it at the fishing point
SRC.ACT.MOREP = <SRC.P>

if ( <SRC.ACT.DISTANCE> > 8 ) // check for distance to fishing point
src.sysmessage Fora de alcance.
SRC.ACT.REMOVE
return 1
endif

//this is disabled because if your on a boat you cant fish unless its directly NEXT to the boat
//if ( !<SRC.ACT.CANSEELOS> ) // check for line of sight to fishing point
//src.sysmessage Fora de alcance.
//SRC.ACT.REMOVE
//return 1
//endif

SRC.ACT.ATTR = <SRC.ACT.ATTR>|attr_invis
SRC.ACT.EQUIP

SRC.EVENTS +e_probe2
SRC.P = <SRC.TARGP> // move the character WITHOUT updating. Notice that
// it'll be moved back in at most 1sec, not noticeable

[EVENTS e_probe2]
// here, SRC.ACT is the item being stepped on - the spawn
ON = @itemStep
if ( 0 )
elseif ( <SRC.ACT.TYPE> == t_rock )
elseif ( <SRC.ACT.TYPE> == t_tree )
elseif ( <SRC.ACT.TYPE> == t_water )
else
return 0
endif

SRC.EVENTS -e_probe2
SRC.TAG.PROBE = <SRC.ACT.UID>
SRC.P = <SRC.FINDID(i_resprobe2).MOREP> // restore original position

SRC.FINDID(i_resprobe2).remove

//if ( <SRC.isgm> ) // just a message for GMs, remove these //3 //lines on final version
//src.sysmessage <SRC.ACTION>:: <SRC.ACT.TYPE>, //<SRC.ACT.AMOUNT> <SRC.ACT.MORE1>
//endif

// ADD tests for skill here!
if ( 0)
elseif ( <SRC.ACT.TYPE> == t_water )
if ( 0 )
elseif ( (<SRC.ACT.MORE1> == mr_fish1) &amp;&amp; (<src.fishing> < min_mr2_fish1) )
elseif ( (<SRC.ACT.MORE1> == mr_fish2) &amp;&amp; (<src.fishing> < min_mr2_fish2) )
elseif ( (<SRC.ACT.MORE1> == mr_fish3) &amp;&amp; (<src.fishing> < min_mr2_fish3) )
elseif ( (<SRC.ACT.MORE1> == mr_fish4) &amp;&amp; (<src.fishing> < min_mr2_fish4) )
elseif ( (<SRC.ACT.MORE1> == mr_fish5) &amp;&amp; (<src.fishing> < min_mr2_fish5) )
elseif ( (<SRC.ACT.MORE1> == mr_fish6) &amp;&amp; (<src.fishing> < min_mr2_fish6) )
elseif ( (<SRC.ACT.MORE1> == mr_fish7) &amp;&amp; (<src.fishing> < min_mr2_fish7) )
elseif ( (<SRC.ACT.MORE1> == mr_fish8) &amp;&amp; (<src.fishing> < min_mr2_fish8) )
elseif ( (<SRC.ACT.MORE1> == mr_fishing_net) &amp;&amp; (<src.fishing> < min_mr2_fishing_net) )
elseif ( (<SRC.ACT.MORE1> == mr_bottle_empty) &amp;&amp; (<src.fishing> < min_mr_bottle_empty) )
elseif ( (<SRC.ACT.MORE1> == mr_boots_calf) &amp;&amp; (<src.fishing> < min_mr_boots_calf) )
elseif ( (<SRC.ACT.MORE1> == mr_sandals) &amp;&amp; (<src.fishing> < min_mr_sandals) )
elseif ( (<SRC.ACT.MORE1> == mr_shoes_plain) &amp;&amp; (<src.fishing> < min_mr_shoes_plain) )
elseif ( (<SRC.ACT.MORE1> == mr_boots_thigh) &amp;&amp; (<src.fishing> < min_mr_boots_thigh) )
elseif ( (<SRC.ACT.MORE1> == mr_sos_bottle) &amp;&amp; (<src.fishing> < min_mr_sos_bottle) &amp;&amp; (<eval rand(2)>==1) )
elseif ( (<SRC.ACT.MORE1> == mr_nothing) )
else
return 0 // it's ok, we can fish
endif

SRC.ACT.MORE1 = default_fishing_mr // REPLACE the current fishing resource
return 0
elseif ( <SRC.ACT.TYPE> == t_tree )
return 0
elseif ( <SRC.ACT.TYPE> == t_rock )
return 0
endif
return 0

[ITEMDEF i_resprobe2]
ID = i_memory
TYPE = t_eq_script
LAYER = layer_special

ON = @Create
TIMER = 1
MORE2 = 0
ATTR = attr_move_never

ON = @Timer
// allow this to run twice, if we don't stop the action
if ( (<CONT.ACTION> != -1) &amp;&amp; (<MORE2> == 0) )
MORE2 = 1
TIMER = 1
return 1
endif
CONT.P = <MOREP>
CONT.UPDATE
remove
return 1

[FUNCTION sextant]
VAR.LAT = <eval (((<P.Y> - 1624) * 360 * 60) / 4096)>
VAR.LNG = <eval (((<P.X> - 1323) * 360 * 60) / 5120)>

VAR.LATd = <eval (<VAR.LAT>/60)>
VAR.LATm = <eval (<VAR.LAT> - (<VAR.LATd>*60))>

VAR.LNGd = <eval (<VAR.LNG>/60)>
VAR.LNGm = <eval (<VAR.LNG> - (<VAR.LNGd>*60))>

ABSVAL VAR.LATd
ABSVAL VAR.LATm
ABSVAL VAR.LNGd
ABSVAL VAR.LNGm

VAR.LATc = "N"
if ( <VAR.LAT> > 0 )
VAR.LATc = "S"
endif

VAR.LNGc = "W"
if ( <VAR.LNG> > 0 )
VAR.LNGc = "E"
endif

LINK.TAG.COORDS=<eval <VAR.LATd>>o<eval <VAR.LATm>>'<VAR.LATc>, <eval <VAR.LNGd>>o<eval <VAR.LNGm>>'<VAR.LNGc>

[FUNCTION ABSVAL]
if ( <<args>> < 0 )
TRY <ARGS> = <eval (0 - <<args>> )>
endif

[itemdef i_sos_bottle]
NAME=mesagem na garrafa
id=099f

on=@create
ATTR=attr_decay
TIMER=0

on=@dclick
src.newitem=i_sos_message
src.act.timer=0
src.act.bounce
src.newitem=i_garrafa_vazia
src.act.bounce
REMOVE
return 1

[itemdef i_sos_tile]
ID=i_water
type=t_script

on=@create
ATTR=attr_decay
TIMER=0

[FUNCTION f_generate_coords]
var.coords=<eval {2304 5008}>,<eval {1344 3696}>,255
//var.coords=<eval {1 5109}>,<eval {1 5109}>,255 //this will do the entire world but can pick small rivers in towns, etc
src.home=<src.p>
src.p=<var.coords>
if (!<SRC.isonwater>)
src.p=<src.home>
var.coords=
f_generate_coords
else
src.p=<src.home>
src.tag.sextantp=<var.coords>
var.coords=
endif

[itemdef i_sos_message]
NAME=mensagem de SOS
id=014ed
TYPE=t_script

on=@create
ATTR=attr_decay
TIMER=0

on=@dclick
if (!<eval <tag.sextantp>>)
src.f_generate_coords
tag.sextantp=<src.tag.sextantp>
src.tag.sextantp=
src.newitem=i_sos_tile
src.act.link=<UID>
link=<src.act.uid>
src.act.attr=attr_static|attr_move_never
src.act.p=<tag.sextantp>
src.act.sextant
src.act.fix
endif

src.tag.sextantcoords=<tag.coords>
dialog d_sos_message
return 1

[DIALOG d_sos_message]
0, 0
PAGE 0
resizepic 63 62 2520 317 319
resizepic 91 103 3500 256 189
text 122 114 0 0
text 104 130 0 1
text 104 147 0 2
text 104 165 0 3
text 104 181 0 4
text 104 197 0 5
text 104 214 0 6
text 104 231 0 7
text 104 248 0 8
button 91 317 4014 4015 1 0 0
text 132 317 0 9
text 91 293 0 10

[DIALOG d_sos_message TEXT]
Essa e uma pobre mensagem
foi escrita por  um  passangeiro 
abordo de um navio em naufragio. 
Embora seja provavel estar tarde 
parasalver  os  passageiros  e a 
tripulacao , talvez algum tesouro 
naufragou com o navio.A mensa-
gem tem as  ultimas coordenadas  
do astrolabio
OKAY
<src.tag.sextantcoords>

[DIALOG d_sos_message BUTTON]
on=0
src.tag.sextantcoords=

[ITEMDEF i_vara_pesca]
//Fishing Pole
ID=0dbf
DEFNAME=i_vara_pesca
TYPE=T_FISH_POLE
WEIGHT=2
CATEGORY=MyT
SUBSECTION=Fishing
DESCRIPTION=vara de pesca
RESOURCES=5 i_cloth
SKILLMAKE=CARPENTRY 68.4, TAILORING 40.0, t_carpentry
DUPELIST=0dc0
RESOURCES2=1 i_log,1 i_thread

ON = @DClick
Target Onde quer jogar o anzol ?
return <resource_dclick>
return 1

ON = @Targon_item
if (<src.targ.baseid>==i_sos_tile)
src.anim 12
src.sound 027
VAR.PERCENT=<EVAL RAND(100)>
if (<EVAL <VAR.PERCENT>>>80)
src.sysmessage Voce puxa da agua um tesouro!
src.newitem=loot_sos_treasure
src.act.p=<src.targp>
src.targ.link.remove
src.targ.remove
elseif (<EVAL <VAR.PERCENT>><10)
src.sysmessage Voce perturbou um monstro do mar!
src.tag.oldp=<src.p>
src.p=<src.targp>
src.newnpc=c_sea_serpent
src.p=<src.tag.oldp>
src.tag.oldp=
else
src.message Voce pesca por um tempo, mas falha em pegar qualquer coisa.
endif
VAR.PERCENT=
else
SRC.SYSMESSAGE Nao tem peixe aqui.
endif
return 1
ON = @Targon_char
SRC.SYSMESSAGE Voce nao pode pegar isso.
return 1
ON = @Targon_ground
return <resource_targon_ground2>
endif

[ITEMDEF 0dc0]
//fishing pole
DUPEITEM=0dbf

[TEMPLATE loot_sos_treasure]
CATEGORY=Item Templates
SUBSECTION=SOS LootTemplates
DESCRIPTION=
CONTAINER=0e41
ITEM=i_gold,{400 600}
ITEM=random_weapon_ruin,R1
ITEM=random_weapon_might,R5
ITEM=random_weapon_force,R10
ITEM=i_arrow,{10 25}
ITEM=random_scroll
ITEM=random_scroll
ITEM=random_scroll
ITEM={ RANDOM_SHIELD_HARDENING 1 RANDOM_PLATEMAIL_HARDENING 1 RANDOM_CHAINMAIL_HARDENING 1 RANDOM_RINGMAIL_HARDENING 1 RANDOM_SHIELD_DEFENSE 1 RANDOM_PLATEMAIL_DEFENSE 1 RANDOM_CHAINMAIL_DEFENSE 1 RANDOM_RINGMAIL_DEFENSE 1 }
ITEM=random_jewel,{1 3}
ITEM=random_jewel,{1 3}
ITEM=random_jewel,{1 3}
ITEM=i_reag_black_pearl,(10 20
ITEM=i_reag_blood_moss,(10 20)
ITEM=i_reag_garlic,(10 20)
ITEM=i_reag_ginseng,(10 20)
ITEM=i_reag_mandrake_root,(10 20)
ITEM=i_reag_nightshade,(10 20)
ITEM=i_reag_sulfur_ash,(10 20)
ITEM=i_reag_spider_silk,(10 20)

[ITEMDEF i_prize_fish_mem]
NAME=Small Fish Memory
ID=i_memory
TYPE=t_eq_script

on=@create
ATTR=attr_invis|attr_newbie
TIMER=240

on=@timer
UNEQUIP
REMOVE
RETURN 1

on=@equip
if (<eval <tag.type>>==1)
src.str=<src.str>+5
elseif (<eval <tag.type>>==2)
src.dex=<src.dex>+5
else
src.int=<src.int>+5
endif
return 1

on=@unequip
if (<eval <tag.type>>==1)
src.str=<src.str>+-5
elseif (<eval <tag.type>>==2)
src.dex=<src.dex>+-5
else
src.int=<src.int>+-5
endif
RETURN 1

[ITEMDEF i_prize_fish]
NAME=peixe%s% premiado%s%
ID=0dd6
TYPE=t_script
weight=1
CATEGORY=MyT
SUBSECTION=Fishing
DESCRIPTION=Peixe premiado(Temp +5 int)
VALUE=1000

on=@create
COLOR=033

on=@dclick
src.newitem=i_prize_fish_mem
src.act.tag.type=3
src.act.equip
REMOVE
return 1

[ITEMDEF i_wonderous_fish]
NAME=fabuloso%s% peixe%s%
ID=0dd6
TYPE=t_script
weight=1
CATEGORY=MyT
SUBSECTION=Fishing
DESCRIPTION=Fabuloso peixe (Temp +5 dex)
VALUE=1000

on=@create
COLOR=024a

on=@dclick
src.newitem=i_prize_fish_mem
src.act.tag.type=2
src.act.equip
REMOVE
return 1

[ITEMDEF i_truly_rare_fish]
NAME=Verdadeiro%s% peixe%s% raro%s%
ID=0dd6
TYPE=t_script
weight=1
CATEGORY=MyT
SUBSECTION=Fishing
DESCRIPTION=verdadeiro peixe raro (Temp +5 str)
VALUE=1000

on=@create
COLOR=017b

on=@dclick
src.newitem=i_prize_fish_mem
src.act.tag.type=1
src.act.equip
REMOVE
return 1

[ITEMDEF i_highly_peculiar_fish]
NAME=Peixe%s% altamente peculiar%es%
ID=0dd6
TYPE=t_script
weight=1
CATEGORY=MyT
SUBSECTION=Fishing
DESCRIPTION=Altamente peculiar (refreshe 10 stamina)
VALUE=1000

on=@create
COLOR=0b0

on=@dclick
if (<src.stam>==<src.dex>)
src.message You feel fully refreshed already.
else
src.message You feel refreshed.
src.stam=<src.stam>+10
if (<src.stam> > <src.dex>)
src.stam=<src.dex>
endif
remove
endif
return 1

[FUNCTION isonwater]
VAR.ISONWATER 0
SRC.NEWNPC c_dummy_dolphin
SRC.ACT.P <SRC.P>
SRC.ACT.HOME <SRC.P>
VAR.PX <SRC.ACT.P.X>
VAR.PY <SRC.ACT.P.Y>
SRC.ACT.dolphin_run nw
SRC.ACT.dolphin_run n
SRC.ACT.dolphin_run ne
SRC.ACT.dolphin_run e
SRC.ACT.dolphin_run se
SRC.ACT.dolphin_run s
SRC.ACT.dolphin_run sw
SRC.ACT.dolphin_run w
SRC.ACT.REMOVE

VAR.PX
VAR.PY

IF (<VAR.ISONWATER> == 8)
VAR.ISONWATER
RETURN 1
ENDIF

VAR.ISWATER
RETURN 0

[CHARDEF c_dummy_dolphin]
ID=c_dolphin

ON=@Click
RETURN 1

[FUNCTION dolphin_run]
RUN <ARGS>
IF (<P.X> != <VAR.PX> ) || (<P.Y> != <VAR.PY> )
VAR.ISONWATER <VAR.ISONWATER> + 1
ENDIF
P <HOME>

//since I've never actually done fishing on OSI and I no longer have
//an osi account, this is the best I could do with what little information
//I have.... if someone wants to tell me exactly what osi's nets do, I'll
//fix this up...
[ITEMDEF i_special_fishing_net]
NAME=rede de pesca especial
ID=0dca
TYPE=t_script
weight=1
CATEGORY=Items by Profession
SUBSECTION=Fisherman
DESCRIPTION=Special fishing Net
VALUE=1000

on=@dclick
TARGETG Onde voce quer jogar a rede?
RETURN 1

on=@targon_item
src.message Voce nao pode tacar a rede ai!
return 1

on=@targon_char
src.message Voce nao pode pegar uma pessoa com essa rede!
return 1

on=@targon_ground
src.home=<src.p>
src.p=<src.targp>
if (!<SRC.isonwater>)
src.p=<src.home>
src.message Use na agua.
else
remove
src.p=<src.home>
src.newitem=0dca
src.act.p=<src.targp>
src.act.attr=attr_decay
src.act.timer=3
src.anim 12
src.sound 027
src.home=<src.p>
src.p=<src.targp>
dorand 3
src.newnpc=096
src.newnpc=01024
src.newnpc=010
enddo
dorand 3
src.newnpc=096
src.newnpc=01024
src.newnpc=010
enddo
dorand 3
src.newnpc=096
src.newnpc=01024
src.newnpc=010
enddo
src.p=<src.home>
endif
return 1



[EOF]