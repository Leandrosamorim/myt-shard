//*****************************************************************************
//*****************************************************************************
//
// SISTEMA DE ANIMAIS por
//
//  Silver
//  UsMarine (vinicius.arroyo at gmail.com)
//
//*****************************************************************************
//TODO:

//USO:
//utilize (dclick) um item procriador (t_procriador) em uma femea, selecione
//entao o macho e aguarde os resultados.
//para ter sucesso os animais tem que ter uma idade minima, o procriador tem
//que ter animal lore minimo e a femea tem que estar 'disponivel'

//as caracteristicas de dex/int/str sao derivadas do pai e da mae aleatoriamente

//animais suportados para reproducao:

//c_pig
//c_pig_big
//c_chicken
//c_galo
//c_sheep_wooly
//c_sheep_shorn
//c_cow_bw / c_vaca2
//c_cow_brown / c_vaca
//c_bull_brown_dk / c_touro
//c_bull_brown_lt / c_touro2

//TAGS usadas:

[DEFNAME animais]
minReprodAge    = 216000    //6 horas de tempo minimo antes de poder reproduzir
minReprodDelay  = 72000     //2 horas de tempo minimo antes reproduzir novamente
minRetryDelay   = 600       //1 minuto antes de tentar apos falha
//*****************************************************************************
//*****************************************************************************
// FUNTCIONs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// anim_domestico
//*****************************************************************************
//configura um animal como domestico procriavel
[FUNCTION anim_domestico]
tag.born=<serv.time>

dorand 2
    tag.sexo=1      //macho
    tag.sexo=2      //femea
enddo

//*****************************************************************************
// anim_tool_dclick
//*****************************************************************************
[FUNCTION anim_tool_dclick]
src.menu anim_commands
return 1


//*****************************************************************************
// anim_tool_target
//*****************************************************************************
[FUNCTION anim_tool_target]

//É NPC?
if (0<argo>==0) || (<argo>>=040000000) || (0<argo.npc>!=brain_animal)
 src.sysmessageyellow Femea invalida.
 return 1
endif

//so aceita se for o dono do animal/npc
if (<argo.memoryfindtype.memory_ipet.link>==<src.uid>)

    sfx <serv.chardef.<argo.id>.sound>

    if (<src.ctag.anim_action>==1)
        if (<src.animallore> <= 450) 
            src.sysmessageyellow Voce nao possui conhecimento suficiente.
        elif (0<argo.tag.sexo>==1)
            src.sysmessageyellow Voce deve selecionar uma femea...
        elif (<eval <serv.time>-<argo.tag.born>> <= minReprodAge ) 
            src.sysmessageyellow Muito jovem para reproduzir.
        elif ( <serv.time> < 0<argo.tag.prenha> )
            src.sysmessageyellow Deve esperar antes de reproduzir novamente.
        //so inicia acasalamento se for femea, com idade, nao prenha e com animallore suficiente
        else
            serv.newitem i_criador
            new.link=<argo.uid>
            new.cont=<src.uid>  
            new.dclick
            return 1
        endif
        src.sysmessageyellow O animal volta a sua rotina.
    elif (<src.ctag.anim_action>==2)
        src.sysmessageyellow Para onde quer que ele(a) va?
        targetfg anim_move <argo>
    elif (<src.ctag.anim_action>==3)
        src.sysmessageyellow Quem quer que ataque?
        targetf anim_attack <argo>
    endif
else
    src.sysmessageyellow Esse animal nao lhe pertence.
endif
return 1

//*****************************************************************************
// anim_move
//*****************************************************************************
[FUNCTION anim_move]
REF1=<argv0>
if (<src.anim_skillcheck <REF1>>)
    REF1.emotered aceita o comando
    anim_move_ <argv0>,<targp>
else
    REF1.emotered ignora o comando
endif

[FUNCTION anim_move_]
LOCAL.x=0<argv3>
LOCAL.x=<eval <LOCAL.x>+1>
OBJ=<argv0>
if (<OBJ.distance <argv1>,<argv2>> > 1) && (<LOCAL.x> < 10)
    OBJ.f_npcRunStepToUid 0,1,<argv1>,<argv2>
    timerf 1,anim_move_ <argv0>,<argv1>,<argv2>,<LOCAL.x>
else
    src.sysmessageyellow Mover novamente? Para onde?:
    targetfg anim_move <OBJ>
endif

//*****************************************************************************
// anim_attack
//*****************************************************************************
[FUNCTION anim_attack]
REF1=<argv0>
if (<src.anim_skillcheck <REF1>>)
    REF1.emotered atacando
    REF1.attack <argo>
    src.sysmessageyellow Selecione outro alvo se desejar:
    targetf anim_attack <REF1>
else
    REF1.emotered ignora o comando
endif

//*****************************************************************************
// anim_skillcheck( checkAgainsUid )
//*****************************************************************************
[FUNCTION anim_skillcheck]
OBJ=<argv0>
skill_gain skill_animallore
skill_gain skill_herding

//testa herding comparando com o do animal ou random se nao tiver configurado
if (<obj.herding>) && (<R<herding>> > <obj.herding>)
    return 1
elif (<herding> > <R1000>)
    return 1
endif
return 0

//*****************************************************************************
// f_giveBirth
//*****************************************************************************
[FUNCTION f_giveBirth]

if (<link.id>==c_pig_big)
    if (!<R0,5>)
        LOCAL.c=1
        LOCAL.f=c_pig_big
    else
        LOCAL.c=4
        LOCAL.f=c_pig
    endif
elif (<link.id>==c_pig)    
    LOCAL.c=2
    LOCAL.f=c_pig
elif (<link.id>==c_chicken)
    link.emoteyellow botou ovos
    serv.newitem i_ninho
    new.p=<link.p>
    return 1
elif (<link.id>==c_sheep_woolly) || (<link.id>==c_sheep_shorn)
    LOCAL.c=1
    LOCAL.f=c_sheep_shorn
elif (<link.id>==c_cow_bw)
    LOCAL.c=1
    dorand 2
        LOCAL.f=c_cow_bw
        LOCAL.f=c_bull_brown_lt
    enddo
elif (<link.id>==c_cow_brown)
    LOCAL.c=1
    dorand 2
        LOCAL.f=c_cow_brown
        LOCAL.f=c_bull_brown_dk
    enddo
else
    LOCAL.c=1
    LOCAL.f=<link.id>
endif

link.emoteyellow da a luz
serv.newitem=i_blood_pool_large
new.p=<link.p>
new.timer=5
new.attr=02

//generate some stats
dorand 2
    LOCAL.str=<link.str>
    LOCAL.str=<cont.str>
enddo
dorand 2
    LOCAL.dex=<link.dex>
    LOCAL.dex=<cont.dex>
enddo
dorand 2
    LOCAL.int=<link.int>
    LOCAL.int=<cont.int>
enddo

while (<local.c>>0)
    LOCAL.c -= 1
    serv.newnpc <LOCAL.f>
    new.movenear <link> 1
    new.str=<LOCAL.str>
    new.dex=<LOCAL.dex>
    new.int=<LOCAL.int>
    new.anim_domestico
    serv.log criando <LOCAL.f> / <new>
endwhile

if (!<R0,15>)
    link.emotered morreu
    link.suicide
else
    link.anim 3
endif


//*****************************************************************************
//*****************************************************************************
// ITEMDEFs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// i_eggs_fresh
//*****************************************************************************
[ITEMDEF 09b5]
DEFNAME=i_eggs_fresh
VALUE=1
TYPE=T_FOOD_RAW
TDATA1=i_eggs_fried
WEIGHT=0.1
CATEGORY=MyT - Items by Professions
SUBSECTION=Cook
DESCRIPTION=Eggs

ON=@STEP
sfx 0040
src.emotered quebra os ovos
remove

//*****************************************************************************
// i_ninho
//*****************************************************************************
[ITEMDEF i_ninho]
ID=01AD4
NAME=ninho 
TYPE=t_script
CATEGORY=MyT - Items by Professions
SUBSECTION=Farmer
DESCRIPTION=ninho de galinha

ON=@DCLICK
serv.newitem i_eggs_fresh
new.amount={0 4}
new.bounce
serv.newitem i_ninho_vazio
new.p=<p>
remove
return 1

ON=@CREATE
timer=600

ON=@TIMER

if (<R0,1> == 0)
    for R 1 4
        serv.newnpc c_chicken
        new.p=<p>
    endfor
else
    for R 1 2
        serv.newnpc c_galo
        new.p=<p>
    endfor
endif

serv.newitem i_ninho_vazio
new.p=<p>
new.emotered os ovos chocaram

remove
sector.allclients update

return 1

//*****************************************************************************
// i_ninho_vazio
//*****************************************************************************
[itemdef i_ninho_vazio]
ID=01ad5
NAME=ninho (vazio)
CATEGORY=MyT - Items by Professions
SUBSECTION=Farmer
DESCRIPTION=ninho vazio

ON=@CREATE
timer=60
attr=attr_decay

//*****************************************************************************
// i_criador
//*****************************************************************************
[ITEMDEF i_criador]
ID=i_memory
NAME=Criador
TYPE=t_eq_script

ON=@DCLICK
src.sysmessageyellow Selecione o macho:
target 
return 1

ON=@TARGON_CHAR

if (<src.targ.tag.sexo>==2)
    src.sysmessageyellow Voce deve selecionar um macho...
elif !(<src.targ.memoryfindtype.memory_ipet.link>==<src.uid>)
    src.sysmessageyellow Esse animal nao lhe pertence.
elif (<eval <serv.time>-<src.targ.tag.born>> <= minReprodAge )
    src.sysmessageyellow Muito jovem para reproduzir.
else 
    if (<src.targ.id>==<link.id>) //animais com mesmo body
        LOCAL.do=1
    elif ( (<src.targ.id>==c_sheep_woolly) || (<src.targ.id>==c_sheep_shorn) ) && ( (<link.id>==c_sheep_woolly) || (<link.id>==c_sheep_shorn) )        
        LOCAL.do=1
    elif ( (<src.targ.id>==c_bull_brown_dk) || (<src.targ.id>==c_bull_brown_lt) ) && ( (<link.id>==c_cow_bw) || (<link.id>==c_cow_brown) )
        LOCAL.do=1
    elif (<src.targ.id>==c_galo) && (<link.id>==c_chicken)
        LOCAL.do=1
    else
        src.sysmessageyellow Nao pode cruzar animais de racas diferentes.
        remove
        return 1
    endif

    src.sysmessagegreen Aguarde enquanto a natureza segue seu curso.


    if ( <src.anim_skillcheck <link.herding>> )
        morey=1     //vai sair filhote no final?
        link.tag.prenha=<eval <serv.time>+minReprodDelay>
    else
        morey=0     //vai sair filhote no final?
        link.tag.prenha=<eval <serv.time>+minRetryDelay>
    endif
    
    cont=<src.targ> //move memory to father
    timer=1
    morex=15        //tempo de acasalamento (s)
    return 1
endif

remove
return 1

on=@targon_ground
remove
return 1

on=@targon_item
remove
return 1

ON=@TIMER
timer=1
if (<link.distance <cont>> > 1)
    cont.f_npcRunStepToUid <link>,1
    return 1
endif

morex -= 1
dorand 2
    sfx <serv.chardef.<link.id>.sound>
enddo

if !(<morex>)
    if (<morey>)
        f_giveBirth
    else
        link.emoteyellow nao engravidou
    endif
    remove
else
    if (<link.id>==c_pig_big) || (<link.id>==c_pig)
        LOCAL.s=roooinc
    elif (<link.id>==c_sheep_woolly) || (<link.id>==c_sheep_shorn)
        LOCAL.s=beehhhh
    elif (<link.id>==c_cow_brown) || (<link.id>==c_cow_bw) || (<link.id>==c_bull_brown_lt) || (<link.id>==c_bull_brown_dk)
        LOCAL.s=muhhhhh
    elif (<link.id>==c_chicken)
        LOCAL.s=cooohhh
    else
        LOCAL.s=geme
    endif

    dorand 3
        link.emoteyellow <LOCAL.s>
        cont.emoteyellow <LOCAL.s>
    enddo
endif
return 1

//*****************************************************************************
//*****************************************************************************
// MENUs
//*****************************************************************************
//*****************************************************************************

[MENU anim_commands]
O que deseja fazer?

on=0 Reprocriar
    src.ctag.anim_action=1
    src.sysmessageyellow Selecione a femea que deseja acasalar:
    targetf anim_tool_target

on=0 Mover
    src.ctag.anim_action=2
    src.sysmessageyellow Selecione o animal:
    targetf anim_tool_target

on=0 Atacar
    src.ctag.anim_action=3
    src.sysmessageyellow Selecione o animal:
    targetf anim_tool_target
[EOF]