//*****************************************************************************
//*****************************************************************************
//
// SISTEMA DE ANIMAIS por
//
//  Silver
//  UsMarine (vinicius.arroyo at gmail.com)
//
//*****************************************************************************
//TODO:

//USO:
//utilize (dclick) um item procriador (t_procriador) em uma femea, selecione
//entao o macho e aguarde os resultados.
//para ter sucesso os animais tem que ter uma idade minima, o procriador tem
//que ter animal lore minimo e a femea tem que estar 'disponivel'

//as caracteristicas de dex/int/str sao derivadas do pai e da mae aleatoriamente

//animais suportados para reproducao:

//c_pig
//c_pig_big
//c_chicken
//c_galo
//c_sheep_wooly
//c_sheep_shorn
//c_cow_bw / c_vaca2
//c_cow_brown / c_vaca
//c_bull_brown_dk / c_touro
//c_bull_brown_lt / c_touro2

//TAGS usadas:

[DEFNAME animais]
minReprodAge    = 216000    //6 horas de tempo minimo antes de poder reproduzir
minReprodDelay  = 72000     //2 horas de tempo minimo antes reproduzir novamente

//*****************************************************************************
//*****************************************************************************
// FUNTCIONs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// anim_domestico
//*****************************************************************************
//configura um animal como domestico procriavel
[FUNCTION anim_domestico]
tag.born=<serv.time>

dorand 2
    tag.sexo=1      //macho
    tag.sexo=2      //femea
enddo

//*****************************************************************************
//*****************************************************************************
// TYPEDEFs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// t_procriador
//*****************************************************************************
[TYPEDEF t_procriador]
ON=@DCLICK
bounce
src.sysmessageyellow Selecione a femea que deseja acasalar:
target 
return 1

ON=@TARGON_CHAR
if (<src.targ.memoryfindtype.memory_ipet.link>==<src.uid>)
    consume
//    src.targ.food += 1
    sfx <serv.chardef.<src.targ.id>.sound>

    if (<src.animallore> <= 450) 
        src.sysmessageyellow Voce nao possui conhecimento suficiente.
    elif (0<src.targ.tag.sexo>==1)
        src.sysmessageyellow Voce deve selecionar uma femea...
    elif (<eval <serv.time>-<src.targ.tag.born>> <= minReprodAge ) 
        src.sysmessageyellow Muito jovem para reproduzir.
    elif (<eval <serv.time>-<src.targ.tag.prenha>> <= minReprodDelay )
        src.sysmessageyellow Deve esperar antes de reproduzir novamente.
    //so inicia acasalamento se for femea, com idade, nao prenha e com animallore suficiente
    else
        serv.newitem i_criador
        new.link=<src.targ.uid>
        new.cont=<src.uid>  
        new.dclick
        return 1
    endif
    src.sysmessageyellow O animal come e volta a sua rotina.
else
    src.sysmessageyellow Esse animal nao lhe pertence.
endif
return 1

ON=@TARGON_GROUND
src.sysmessageyellow Alvo invalido.
return 1

ON=@TARGON_ITEM
src.sysmessageyellow Alvo invalido.
return 1 
 
//*****************************************************************************
//*****************************************************************************
// FUNCTIONs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// f_giveBirth
//*****************************************************************************
[FUNCTION f_giveBirth]

if (<link.id>==c_pig_big)
    if (!<R0,5>)
        LOCAL.c=1
        LOCAL.f=c_pig_big
    else
        LOCAL.c=4
        LOCAL.f=c_pig
    endif
elif (<link.id>==c_pig)    
    LOCAL.c=2
    LOCAL.f=c_pig
elif (<link.id>==c_chicken)
    link.emoteyellow botou ovos
    serv.newitem i_ninho
    new.p=<link.p>
    return 1
elif (<link.id>==c_sheep_woolly) || (<link.id>==c_sheep_shorn)
    LOCAL.c=1
    LOCAL.f=c_sheep_shorn
elif (<link.id>==c_cow_bw)
    LOCAL.c=1
    dorand 2
        LOCAL.f=c_cow_bw
        LOCAL.f=c_bull_brown_lt
    enddo
elif (<link.id>==c_cow_brown)
    LOCAL.c=1
    dorand 2
        LOCAL.f=c_cow_brown
        LOCAL.f=c_bull_brown_dk
    enddo
endif

link.emoteyellow da a luz
serv.newitem=i_blood_pool_large
new.p=<link.p>
new.timer=5
new.attr=02

//generate some stats
dorand 2
    LOCAL.str=<link.str>
    LOCAL.str=<cont.str>
enddo
dorand 2
    LOCAL.dex=<link.dex>
    LOCAL.dex=<cont.dex>
enddo
dorand 2
    LOCAL.int=<link.int>
    LOCAL.int=<cont.int>
enddo

while (<local.c>>0)
    LOCAL.c -= 1
    serv.newnpc <LOCAL.f>
    new.movenear <link> 3
    new.str=<LOCAL.str>
    new.dex=<LOCAL.dex>
    new.int=<LOCAL.int>
    serv.log criando <LOCAL.f> / <new>
endwhile

if (!<R0,15>)
    link.emotered morreu
    link.suicide
else
    link.anim 3
endif


//*****************************************************************************
//*****************************************************************************
// ITEMDEFs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// i_eggs_fresh
//*****************************************************************************
[ITEMDEF 09b5]
DEFNAME=i_eggs_fresh
VALUE=1
TYPE=T_FOOD_RAW
TDATA1=i_eggs_fried
WEIGHT=0.1
CATEGORY=MyT - Items by Professions
SUBSECTION=Cook
DESCRIPTION=Eggs

ON=@STEP
sfx 0040
src.emotered quebra os ovos
remove

//*****************************************************************************
// i_ninho
//*****************************************************************************
[ITEMDEF i_ninho]
ID=01AD4
NAME=ninho 
TYPE=t_script
CATEGORY=MyT - Items by Professions
SUBSECTION=Farmer
DESCRIPTION=ninho de galinha

ON=@DCLICK
serv.newitem i_eggs_fresh
new.amount={0 4}
new.bounce
serv.newitem i_ninho_vazio
new.p=<p>
remove
return 1

ON=@CREATE
timer=600

ON=@TIMER

if (<R0,1> == 0)
    for R 1 4
        serv.newnpc c_chicken
        new.p=<p>
    endfor
else
    for R 1 2
        serv.newnpc c_galo
        new.p=<p>
    endfor
endif

serv.newitem i_ninho_vazio
new.p=<p>
new.emotered os ovos chocaram

remove
sector.allclients update

return 1

//*****************************************************************************
// i_ninho_vazio
//*****************************************************************************
[itemdef i_ninho_vazio]
ID=01ad5
NAME=ninho (vazio)
CATEGORY=MyT - Items by Professions
SUBSECTION=Farmer
DESCRIPTION=ninho vazio

ON=@CREATE
timer=60
attr=attr_decay

//*****************************************************************************
// i_milho
//*****************************************************************************
[ITEMDEF 0c7f]
DEFNAME=i_milho
DEFNAME=i_fruit_corn
RESOURCES=I_CROP_CORN
TYPE=t_procriador
VALUE={2 4}
WEIGHT=0.2
CATEGORY=MyT - Vegetation
SUBSECTION=Fruits and Vegetables
DESCRIPTION=Milho
DUPELIST=0c80,0c81,0c82

//*****************************************************************************
// i_feno
//*****************************************************************************
[ITEMDEF i_feno]
ID=0100c
NAME=Feno
TYPE=t_procriador
CATEGORY=MyT - Items by Professions
SUBSECTION=Farmer
DESCRIPTION=feno

ON=@CREATE
MORE2=1
COLOR=089f

//*****************************************************************************
// i_criador
//*****************************************************************************
[ITEMDEF i_criador]
ID=i_memory
NAME=Criador
TYPE=t_eq_script

ON=@DCLICK
src.sysmessageyellow Selecione o macho para acasalar:
target 
return 1

ON=@TARGON_CHAR

if (<src.targ.tag.sexo>==2)
    src.sysmessageyellow Voce deve selecionar um macho...
elif !(<src.targ.memoryfindtype.memory_ipet.link>==<src.uid>)
    src.sysmessageyellow Esse animal nao lhe pertence.
elif (<eval <serv.time>-<src.targ.tag.born>> <= minReprodAge )
    src.sysmessageyellow Muito jovem para reproduzir.
else 
    if (<src.targ.id>==<link.id>) //animais com mesmo body
        LOCAL.do=1
    elif ( (<src.targ.id>==c_sheep_woolly) || (<src.targ.id>==c_sheep_shorn) ) && ( (<link.id>==c_sheep_woolly) || (<link.id>==c_sheep_shorn) )        
        LOCAL.do=1
    elif ( (<src.targ.id>==c_bull_brown_dk) || (<src.targ.id>==c_bull_brown_lt) ) && ( (<link.id>==c_cow_bw) || (<link.id>==c_cow_brown) )
        LOCAL.do=1
    elif (<src.targ.id>==c_galo) && (<link.id>==c_chicken)
        LOCAL.do=1
    else
        src.sysmessageyellow Nao pode cruzar animais de racas diferentes.
        remove
        return 1
    endif

    src.skill_gain skill_animallore
    src.sysmessagegreen Aguarde enquanto a natureza segue seu curso.
    
    cont=<src.targ> //move memory to father
    morex=15
    timer=1
    link.tag.prenha=<serv.time>
    return 1
endif

remove
return 1

on=@targon_ground
remove
return 1

on=@targon_item
remove
return 1

ON=@TIMER
morex -= 1
dorand 2
    sfx <serv.chardef.<link.id>.sound>
enddo

if !(<morex>)
    f_giveBirth
    remove
else
    if (<link.id>==c_pig_big) || (<link.id>==c_pig)
        LOCAL.s=roooinc
    elif (<link.id>==c_sheep_woolly) || (<link.id>==c_sheep_shorn)
        LOCAL.s=beehhhh
    elif (<link.id>==c_cow_brown) || (<link.id>==c_cow_bw) || (<link.id>==c_bull_brown_lt) || (<link.id>==c_bull_brown_dk)
        LOCAL.s=muhhhhh
    elif (<link.id>==c_chicken)
        LOCAL.s=cooohhh
    else
        LOCAL.s=geme
    endif

    dorand 3
        link.emoteyellow <LOCAL.s>
        cont.emoteyellow <LOCAL.s>
    enddo
    timer=1
endif
return 1

[EOF]