[DEFNAME bard_skills]
SND_DRUM_BATTLE		02E7
SND_DRUM_DRAMA		02E8
SND_DRUM_PEACE		02E9
SND_DRUM		02EA
SND_DRUM_BAD		02EB

SND_HARP_BATTLE		0391
SND_HARP_DRAMA		0392
SND_HARP_PEACE		0393
SND_HARP		045
SND_HARP_BAD		046

SND_LUTE_BATTLE		0403
SND_LUTE_DRAMA		040B
SND_LUTE_PEACE		0418
SND_LUTE		04C
SND_LUTE_BAD		04D

SND_TAMBOURINE_BATTLE	04B5
SND_TAMBOURINE_DRAMA	04B6
SND_TAMBOURINE_PEACE	04B7
SND_TAMBOURINE		052
SND_TAMBOURINE_BAD	053



[SKILL 22]
DEFNAME=SKILL_PROVOCATION
KEY=Provocation
TITLE=Bard
PROMPT_MSG=@036 Quem voce gostaria de incitar?
DELAY=10.0,1.0
STAT_STR=20
STAT_INT=60
STAT_DEX=70
BONUS_STR=0
BONUS_DEX=90
BONUS_INT=10
BONUS_STATS=5
//ADV_RATE=2.5,50.0,200.0

ON=@Fail
	SRC.SYSMESSAGERED Sua musica falhou em incitar raiva o suficiente.
	//Diminuir chance de ser atacado.
	IF (<SRC.PROVOCATION> < <R600>)
		return 0
	ELSE
		RETURN 1
	ENDIF

ON=@Abort
	SRC.SYSMESSAGEYELLOW Sua musica falhou em incitar raiva o suficiente.
	//Diminuir chance de ser atacado.
	IF (<SRC.PROVOCATION> < <R600>)
		return 0
	ELSE
		RETURN 1
	ENDIF

[SKILL 15]
DEFNAME=Skill_Enticement
KEY=Enticement
TITLE=Bard
PROMPT_MSG=Quem voce gostaria de incitar?
DELAY=10.0,1.0
STAT_STR=15
STAT_INT=70
STAT_DEX=50
BONUS_STR=0
BONUS_DEX=50
BONUS_INT=50
BONUS_STATS=5
//ADV_RATE=2.5,50.0,200.0

ON=@Fail
	SRC.SYSMESSAGE Your music fails to attract anyone.

ON=@Abort
	SRC.SYSMESSAGE You fail to complete the song.

[SKILL 9]
DEFNAME=SKILL_PEACEMAKING
KEY=Peacemaking
TITLE=Bard
PROMPT_MSG=
DELAY=2.0
STAT_STR=0
STAT_INT=70
STAT_DEX=70
BONUS_STR=0
BONUS_DEX=50
BONUS_INT=50
BONUS_STATS=5
//ADV_RATE=2.5,50.0,200.0

ON=@Fail
	SRC.SYSMESSAGE You attempt to calm everyone, but fail.

ON=@Abort
	SRC.SYSMESSAGE You fail to complete the song.

/////////////////////////////////////////////
///
//	FUNÇÕES DE PODERES BARDICOS


[FUNCTION BARDSONG_PLAY]
RETURN 0

[FUNCTION BARDSONG_VALOR]
emoteyellow entoa uma cancao encorajadora
LOCAL.timer=<f_rangeValue 8,2,<musicianship>>
TIMERf <LOCAL.timer>, _BARDSONG_VALOR

[FUNCTION _BARDSONG_VALOR]
IF (!<BELLTEST <Musicianship>,560>)
 sysmessagered Sua cancao nao foi muito encorajadora.
 return 1
endif
LOCAL.r=<f_rangeValue 1,5,<musicianship>>
LOCAL.moddam=<f_rangeValue -1,3,<musicianship>>
LOCAL.modar=<f_rangeValue -2,4,<musicianship>>
LOCAL.timer=<f_rangeValue 0,35,<musicianship>>
//sysmessageyellow r:<dLOCAL.r> moddam:<dLOCAL.moddam> modar:<dLOCAL.modar>
OBJ=<UID>
FORCHARS <LOCAL.r>
 IF (!<IsMyEnemy <OBJ.UID>>) && ((<IsMyMaster <OBJ.UID>>) || (<IsPlayer>)) && (!<RESTEST 4 i_mry_COMBAT_MODS>)
  serv.newitem i_mry_COMBAT_MODS
  new.more1=<LOCAL.modar>
  new.more2=<LOCAL.moddam>
  equip <new.UID>
  new.timer=<LOCAL.timer>
  message=@02, *encorajado* 
 ENDIF
ENDFOR

[ITEMDEF i_mry_COMBAT_MODS]
ID=i_lyre
NAME=VALOR
TYPE=t_eq_script
LAYER=30

on=@equip
cont.modar += <more1>
cont.tag0.moddam += <more2>
src.sysmessagegreen Voce se sente mais corajoso!

on=@timer
cont.modar -= <more1>
cont.tag.moddam -= <more2>
remove
return 1

[FUNCTION INSTRUMENT]
//Faz o som do instrumeto que o jogador possui de acordo com o argumento <ARGS>
// ARGS=[BATTLE | DRAMA | PEACE | BAD | <nada>]

//Qual é o intrumento?
IF (<src.act.type> == t_musical) && (<src.act.topobj> == <src.uid>)
 obj=<src.act.uid>
ELSE
 obj=<src.findtype.t_musical>
ENDIF

// Determinar o tipo dos intrumento
IF (<obj.baseid>==i_lute)
 LOCAL.ins=LUTE
ELIF (<obj.baseid>==i_lyre)
 LOCAL.ins=HARP
ELIF (<obj.baseid>==i_tambourine)
 LOCAL.ins=TAMBOURINE
ELIF (<obj.baseid>==i_drum)
 LOCAL.ins=DRUM
ELSE
 RETURN 0
ENDIF

//Que tipo de som?
IF (<ISEMPTY <args>> || <ISEMPTY <DEF.SND_<LOCAL.INS>_<ARGS>>>)
 SFX <DEF.SND_<LOCAL.INS>>
ELSE
 SFX <DEF.SND_<LOCAL.INS>_<ARGS>>
ENDIF
RETURN 1


[FUNCTION SETMEMORY]
//setmemory <memory_type>,<UID>
LOCAL.link=<argv[1]>
LOCAL.memtype=<argv[0]>
FORCHARLAYER 30
 if (<TYPE>==t_eq_memory_obj) && (<link>==<local.link>)
  color |= <argv[0]>
  LOCAL.set=1
  obj=<LOCAL.link>
  IF (<obj.isplayer>) && (!<IsEmpty <obj.tag.name>>)
   name=<obj.tag.name>
  ELSE
   name=<obj.name>
  ENDIF
 endif
ENDFOR
IF (!<LOCAL.set>)
 serv.newitem i_memory
 new.type=t_eq_script
 new.color = <LOCAL.memtype>
 equip <new.uid>
 new.timer=20*60
 new.link=<LOCAL.link>
 obj=<LOCAL.link>
 IF (<obj.isplayer>) && (!<IsEmpty <obj.tag.name>>)
  new.name=<obj.tag.name>
 ELSE
  new.name=<obj.name>
 ENDIF
 new.type=t_eq_memory_obj
ENDIF

[FUNCTION BARDSONG_LULABY]
emoteyellow entoa uma cancao de ninar
events +e_no_attack
events +e_no_dclick
INSTRUMENT
TIMERF 5,_BARDSONG_LULABY

[FUNCTION _BARDSONG_LULABY]
IF !(<INSTRUMENT PEACE>)
 sysmessagered Voce nao tem um instrumento
 return 1
endif
IF (<FLAGS>&statf_war|statf_dead|statf_freeze|statf_war|statf_stone) || (<cTAG0.DISTURB>) || (!<IsOnline>) || (!<BELLTEST <Musicianship>,200>) || (!<R12>)
 sysmessagered Voce foi perturbado
 emotered para de cantar
 events -e_no_attack
 events -e_no_dclick
 cTAG.DISTURB
 return 1
ENDIF
IF !(<INSTRUMENT PEACE>)
 sysmessagered Voce nao tem um instrumento
 return 1
endif
DOSWITCH <EVAL <cTAG.ESTROFE>>
 say=@06 E nessa cancao
 say=@06 cancao de ninar
 say=@06 Pode dormir
 say=@06 Nao queira acordar
 say=@06 Doces sonhos
 say=@06 Ternura no ar
 say=@06 Nao fique de pe
 say=@06 Pode deitar
ENDDO
cTAG0.ESTROFE +=1
IF (<cTAG0.ESTROFE>=8)
 cTAG.ESTROFE
ENDIF
OBJ=<UID>
FORCHARS 12
 IF ((<IsMyEnemy <OBJ.UID>>) || (!<IsMyMaster <OBJ.UID>>) || (!<IsPlayer>)) && (<CanSeeLos <OBJ.UID>>) && (<UID> != <obj.uid>)
  EFFETCT_BARDSONG_LULABY <OBJ.UID>
 ENDIF
ENDFOR
TIMERF 3,_BARDSONG_LULABY

[FUNCTION EFFETCT_BARDSONG_LULABY]
obj=<argn1>
DORAND 3
 F_SONOLENTO <argn1>
ENDDO
IF ((<R<obj.int>> > <INT>) || (<BELLTEST <obj.Musicianship>,120.0>)) && (!<R6>)
 f_faint
 timerF <BETWEEN 10,60,<obj.Musicianship>,2000>,f_WakeUp
ENDIF

[FUNCTION f_sonolento]
OBJ=<argn1>
say=@038, *sonolento*
IF (!<RESTEST 4 i_mry_COMBAT_MODS>)
 serv.newitem i_mry_COMBAT_MODS
 new.more1=-<BETWEEN 0,4,<obj.Musicianship>,2000>
 new.more2=-<BETWEEN 0,25,<obj.Musicianship>,2000>
 equip <new.UID>
 new.timer=<BETWEEN 0,120,<obj.Musicianship>,2000>
 obj.skill_gain skill_musicianship
ENDIF


[EVENTS e_no_attack]
on=@hit
disturb

on=@GetHit
disturb

on=@SpellEffect
disturb

[EVENTS e_no_dclick]
on=@dclick
disturb

[FUNCTION disturb]
cTAG.disturb 1

[EOF]