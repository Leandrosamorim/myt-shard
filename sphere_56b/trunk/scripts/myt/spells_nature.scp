//***********************************************************
//		SPELL EARTHQUAKE
//***********************************************************

[FUNCTION f_spl_eathquake]
SERV.NEWITEM i_spl_earthquake
NEW.MORE1=<magery>
NEW.p=<p>
NEW.LINK=<UID>
new.timerd 1
sfx 022d
TIMERF 2,SFX,022e

[FUNCTION f_spl_earthquake_rumble]
SERV.NEWITEM {01b01 01b08}
NEW.ATTR=012
NEW.TIMER=3
NEW.MOVENEAR <UID> 12
NEW.UPDATE
DORAND 12
 NEW.SAY pom
 NEW.SAY pum
 NEW.SAY pla
 NEW.SAY planc
 NEW.SAY plin
 NEW.SAY bom!
 NEW.SAY bum!
 NEW.SAY Bam!
 NEW.SAY Crash!
 NEW.SAY Cram!
 NEW.SAY TRAN!
 NEW.SAY TRATAN!
 NEW.SAY CABUM!
ENDDO

[ITEMDEF i_spl_earthquake]
id=01b01
NAME=Earthquake
TYPE=t_script

on=@timer
if (<more2> <= 15)
 f_spl_earthquake_rumble
 more2=<MORE2>+1
 timerd 1
else
 OBJ=<UID>
 FORCHARS 12
  sfx 011e
  damagespell s_Earthquake, DAM_PHYSICAL, <OBJ.MORE1>, <OBJ.LINK>
  if (<hits> > 0) && (!<NPC>)
   sleep
   morte_equip <UID>
  endif
 ENDFOR
 remove
endif
return 1

//***********************************************************
//		SPELL SUMMON ANIMAL
//***********************************************************
[FUNCTION f_spl_summon_animal]
LOCAL.DIF=<EVAL <magery>/100>
IF (<LOCAL.DIF> > 10)
 LOCAL.DIF=10
endif
LOCAL.DIF1=<eval {1 <LOCAL.DIF>}>
LOCAL.DIF=<eval {<LOCAL.DIF1> <LOCAL.DIF>}> //Isso aqui simula uma gausseana perneta
if (<LOCAL.DIF>==1)
 f_summon_creature c_eagle,60
elif (<LOCAL.DIF>==2)
 f_summon_creature c_snake,28
 f_summon_creature c_snake,33
elif (<LOCAL.DIF>==3)
 f_summon_creature c_goat,60
elif (<LOCAL.DIF>==4)
 f_summon_creature c_wolf,60
elif (<LOCAL.DIF>==5)
 f_summon_creature c_gorilla,60
elif (<LOCAL.DIF>==6)
 f_summon_creature c_panther,30
elif (<LOCAL.DIF>==7)
 f_summon_creature c_bear_brown,30
elif (<LOCAL.DIF>==8)
 f_summon_creature c_bear_brown,32
 f_summon_creature c_panther,37
elif (<LOCAL.DIF>==9)
 f_summon_creature c_eagle,25
 f_summon_creature c_wolf,29
 f_summon_creature c_snake,35
elif (<LOCAL.DIF>==10)
 LOCAL.animal1={c_goat 1 c_wolf 1 c_panther 1}
 LOCAL.animal2={c_panther 1 c_wolf 1 c_bear_brown 1}
 f_summon_creature <local.animal1>,45
 f_summon_creature <local.animal2>,47
endif

//***********************************************************
//		SPELL STALAGMITE
//***********************************************************
[FUNCTION f_spl_stalagmite]
SERV.NEWITEM i_spl_stalagmite
new.dispid={08e1 1 08e0 1 08e7 1}
new.p=<p>
new.z=<new.z>-10
new.timerd 1
sfx 022d
SRC.DAMAGE <eval {3 8}> DAM_PHYSICAL

[ITEMDEF i_spl_stalagmite]
ID=08e1
TYPE=t_script

on=@timer
if (<more2> <=10)
 more2=<more2>+1
 nudgeup 1
 timerd 1
else
 timerf 15,remove
endif
return 1

on=@step
SRC.DAMAGE <eval {2 5}> DAM_PHYSICAL

//***********************************************************
//		SPELL SUMMON EARTH ELEMENTAL
//***********************************************************
[FUNCTION f_spl_summon_elem_earth]
LOCAL.timer=<eval <magery>/67>		//0 a 15 segundos
f_summon_creature c_elem_earth,<LOCAL.timer>
[EOF]