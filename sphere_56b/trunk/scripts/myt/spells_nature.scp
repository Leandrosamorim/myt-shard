[EVENTS e_caster_nature]
on=@SpellCast
if (<NPC_CanCast>)
 OBJ=<MEMORYFINDTYPE.memory_war_targ.link>
 DORAND <eval <magery>/50>
  nature_cast 1, <obj.uid>	//Magery 5.0
  nature_cast 1, <obj.uid>
  nature_cast 1, <obj.uid>
  nature_cast 2, <obj.uid>
  nature_cast 1, <obj.uid>	//Magery 25.0
  nature_cast 3, <obj.uid>
  nature_cast 2, <obj.uid>
  nature_cast 1, <obj.uid>
  nature_cast 2, <obj.uid>
  nature_cast 3, <obj.uid>	//Magery 50.0
  nature_cast 2, <obj.uid>
  nature_cast 3, <obj.uid>
  nature_cast 1, <obj.uid>
  nature_cast 4, <obj.uid>
  nature_cast 4, <obj.uid>	//Magery 75.0
  nature_cast 5, <obj.uid>
  nature_cast 5, <obj.uid>
  nature_cast 6, <obj.uid>
  nature_cast 7, <obj.uid>
  nature_cast 8, <obj.uid>	//Magery 100.0
  nature_cast 2, <obj.uid>
  nature_cast 3, <obj.uid>
  nature_cast 4, <obj.uid>
  nature_cast 1, <obj.uid>
  nature_cast 2, <obj.uid>	//Magery 125.0
  nature_cast 3, <obj.uid>
  nature_cast 4, <obj.uid>
  nature_cast 5, <obj.uid>
  nature_cast 6, <obj.uid>
  nature_cast 7, <obj.uid>	//Magery 150.0
  nature_cast 8, <obj.uid>
  nature_cast 4, <obj.uid>
  nature_cast 5, <obj.uid>
  nature_cast 6, <obj.uid>
  nature_cast 7, <obj.uid>	//Magery 175.0
  nature_cast 8, <obj.uid>
  nature_cast 5, <obj.uid>
  nature_cast 6, <obj.uid>
  nature_cast 7, <obj.uid>
  nature_cast 8, <obj.uid>	//Magery 200.0
 ENDDO
endif
return 1

[FUNCTION nature_cast]
local.difcast=<eval (<argv0>*100)-{10 50}>
if (!<skilltest Magery <local.difcast>>)
 effect 3 i_fx_smoke_small 0 16 0
 sfx {04f 1 0227 1 0191 1}
 say falhou
 spell_delay {10 35}
 return 1
endif
OBJ=<argv1>
if (<argv0>==1) && (<mana> > 6)
 mana=<mana>-6
 spell_delay 12
 sfx 0232
 obj.effect 0 i_xbolt_x 9 16 1 010c
 obj.damagespell s_magic_arrow, dam_pierce, <magery>, <uid>
elif (<argv0>==2) && (<mana> > 8)
 mana=<mana>-8
 spell_delay 23
 sfx 0200
 obj.effect 0 i_fx_fireball 9 16 1 010c
 obj.damagespell s_fireball, dam_poison, <magery>, <uid>
elif (<argv0>==3) && (<mana> > 17)
 mana=<mana>-17
 spell_delay 68
 sfx 01de
 obj.effect 2 03921 0 46 0
 obj.effect 2 03915 0 46 0
 obj.damagespell s_harm, dam_poison, <magery>, <uid>
 if (!<EVAL {0 2}>)
  obj.veneno <eval <magery>/20>
 endif
elif (<argv0>==4) && (<mana> > 12)
 mana=<mana>-12
 spell_delay 65
 obj.f_spl_entangle <magery>
elif (<argv0>==5) && (<mana> > 7)
 mana=<mana>-7
 spell_delay 28
 obj.f_spl_stalagmite
elif (<argv0>==6) && (<mana> > 17)
 mana=<mana>-17
 spell_delay 92
 f_spl_summon_animal
elif (<argv0>==7) && (<mana> > 32)
 mana=<mana>-32
 spell_delay 124
 obj.f_spl_planta_carnivora
elif (<argv0>==8) && (<mana> > 42)
 mana=<mana>-42
 spell_delay 124
 obj.f_spl_earthquake
endif


//***********************************************************
//		SPELL SUMMON ANIMAL
//***********************************************************
[FUNCTION f_spl_summon_animal]
IF (<argv0>)
 LOCAL.DIF=<EVAL <argv0>/100>
else
 LOCAL.DIF=<EVAL <magery>/100>
ENDIF
IF (<LOCAL.DIF> > 10)
 LOCAL.DIF=10
endif
LOCAL.DIF1=<eval {1 <LOCAL.DIF>}>
LOCAL.DIF=<eval {<LOCAL.DIF1> <LOCAL.DIF>}> //Isso aqui simula uma gausseana perneta
if (<LOCAL.DIF>==1)
 f_summon_creature c_eagle,60
elif (<LOCAL.DIF>==2)
 f_summon_creature c_snake,28
 f_summon_creature c_snake,33
elif (<LOCAL.DIF>==3)
 f_summon_creature c_goat,60
elif (<LOCAL.DIF>==4)
 f_summon_creature c_wolf,60
elif (<LOCAL.DIF>==5)
 f_summon_creature c_gorilla,60
elif (<LOCAL.DIF>==6)
 f_summon_creature c_panther,30
elif (<LOCAL.DIF>==7)
 f_summon_creature c_bear_brown,30
elif (<LOCAL.DIF>==8)
 f_summon_creature c_bear_brown,32
 f_summon_creature c_panther,37
elif (<LOCAL.DIF>==9)
 f_summon_creature c_eagle,25
 f_summon_creature c_wolf,29
 f_summon_creature c_snake,35
elif (<LOCAL.DIF>==10)
 LOCAL.animal1={c_goat 1 c_wolf 1 c_panther 1}
 LOCAL.animal2={c_panther 1 c_wolf 1 c_bear_brown 1}
 f_summon_creature <local.animal1>,45
 f_summon_creature <local.animal2>,47
endif



//***********************************************************
//		SPELL ANIMALFORM
//***********************************************************
[FUNCTION f_spl_animal_form]
IF (<argv0>)
 LOCAL.Skill=<argv0>
ELSE
 LOCAL.Skill=<magery>
ENDIF
//Que tempo?
LOCAL.time=<eval {1 (<LOCAL.Skill>/6)}>
LOCAL.time=<eval {<LOCAL.time> (<LOCAL.Skill>/6)}>	//Gausseana acoxambrada
//Que corpo?
LOCAL.dif=<eval {4 (<LOCAL.Skill>/100)}>
LOCAL.dif=<eval {<LOCAL.dif> (<LOCAL.Skill>/100)}>	//Gausseana acoxambrada
//Pressetar para cachorro se o teste deu 50.0
LOCAL.body c_dog
LOCAL.MODSTR=5
LOCAL.MODDEX=5
LOCAL.MODINT=-15
if (<local.dif> == 5)
 LOCAL.body c_wolf
 LOCAL.MODSTR=5
 LOCAL.MODDEX=10
 LOCAL.MODINT=-15
ELIF (<local.dif> == 6)
 LOCAL.body c_wolf_white
 LOCAL.MODSTR=10
 LOCAL.MODDEX=10
 LOCAL.MODINT=-15
ELIF (<local.dif> == 7)
 LOCAL.body c_goat
 LOCAL.MODSTR=10
 LOCAL.MODDEX=15
 LOCAL.MODINT=-15
ELIF (<local.dif> == 8)
 LOCAL.body c_panther
 LOCAL.MODSTR=15
 LOCAL.MODDEX=20
 LOCAL.MODINT=-20
ELIF (<local.dif> == 9)
 LOCAL.body c_bear_brown
 LOCAL.MODSTR=20
 LOCAL.MODDEX=20
 LOCAL.MODINT=-20
ELIF (<local.dif> == 10)
 LOCAL.body c_bear_grizzly
 LOCAL.MODSTR=25
 LOCAL.MODDEX=20
 LOCAL.MODINT=-20
ELIF (<local.dif> > 10)		//Teste maior que 100.0
 LOCAL.body {c_bear_grizzly 1 c_bear_brown 1 c_panther 1}
 LOCAL.MODSTR=30
 LOCAL.MODDEX=25
 LOCAL.MODINT=-25
ENDIF
f_polymorph <LOCAL.body>,<LOCAL.time>,<LOCAL.modstr>,<LOCAL.moddex>,<LOCAL.modint>


//***********************************************************
//		SPELL PLANTA CARNIVORA
//***********************************************************
//Sumona planta carnivora no lugar do player.
//Não deixa o player andar, mas pode usar magias, lutar e usar skills.
[FUNCTION f_spl_planta_carnivora]
IF (!<argv0>
 LOCAL.skill=500
ELSE
 LOCAL.skill=<argv0>
ENDIF
LOCAL.timer=<eval (<LOCAL.skill>/22)+15>
LOCAL.str=<eval (<LOCAL.skill>/17)+20>
serv.newnpc c_corpser
new.p=<p>
new.str=<LOCAL.str>
new.hits=<LOCAL.str>
new.invis 0
new.events +e_spl_planta_carnivora
OBJ=<new.uid>
serv.newitem i_mry_summon
new.more2 <LOCAL.timer>
new.link <uid>
new.cont <obj>
new.timer 1
obj.anim 12
obj.timerf 2,attack,<uid>
f_cela_imovel <LOCAL.timer>

//***********************************************************
//		SPELL ENTANGLE
//***********************************************************

[FUNCTION f_spl_entangle]
//f_field_sparce <item>, <ammount>, <raio>, <tempo>
if (!<argv0>)
 LOCAL.skill=500
else
 LOCAL.skill=<argv0>
endif
LOCAL.amt=<eval {3 (<LOCAL.skill>/100)}>
LOCAL.timer=<eval {10 (<LOCAL.skill>/40)}>
f_field_sparce i_spl_entangle, <LOCAL.amt>, 2, <LOCAL.timer>

[ITEMDEF i_spl_entangle]
ID=i_fx_sparkle_2
NAME=galhos emaranhados

on=@create
timerd=<eval {3 12}>
color=colors_green

on=@timer
DISPID={0d3f 0d40}
timerd=<eval {3 12}>
DORAND 20
 sfx 04f
 sfx 048
 sfx 0191
ENDDO
color=colors_green
if (!<more1>) && (!RAND(4))
 movenear <link> 2
endif
update
return 1

on=@step
if (!<more1>)
 local.dam=<eval {2 5}>
 src.damage <local.dam> dam_poison
 src.stun <eval {3 7}>
 more1=1
endif

[EOF]