//*****************************************************************************
//*****************************************************************************
//
// SISTEMA DE DRESS
//
//	Metus
//	Pomp
//	UsMarine (vinicius.arroyo at gmail.com)
//
//*****************************************************************************
//*****************************************************************************

//TODO:

//TAGS usadas:
//tag.comcapuz	indica se player esta com capuz ou nao

[PLEVEL 1]
capuz

[PLEVEL 2]
dress

//*****************************************************************************
//*****************************************************************************
// EVENTs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// e_capuz
//*****************************************************************************
[EVENTS e_capuz]
on=@ItemUnequip
if (<act.layer>==layer_robe)
 capuz
endif
return 1

//*****************************************************************************
//*****************************************************************************
// FUNCTIONs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// dress
//*****************************************************************************
[FUNCTION dress]
src.menu sm_dress

//*****************************************************************************
// saveitens
//*****************************************************************************
[FUNCTION saveitens]
FOR l 1 28
	TRY findlayer.<LOCAL.l>.unequip
ENDFOR

//*****************************************************************************
// limparlayers
//*****************************************************************************
[FUNCTION limparlayers]
FOR l 1 28
	TRY findlayer.<LOCAL.l>.remove
ENDFOR

//*****************************************************************************
// capuz
//*****************************************************************************
[FUNCTION capuz]
if (<findlayer.layer_robe>)
	//poe capuz
	if (<findlayer.layer_skirt.baseid>!=i_capuz)
		tag.comcapuz=1
		//salva alguns hair e skirt no layer_vendor_buys
		obj=<findlayer.layer_hair>
		obj.layer=layer_vendor_buys
		obj.update
		findlayer.layer_skirt.layer=layer_vendor_buys
		//remove itens que atrapalham
		findlayer.layer_collar.bounce
		findlayer.layer_helm.bounce
		//coloca capuz
		serv.newitem i_capuz
		new.color=<findlayer.layer_robe.color>
		new.equip
		events +e_capuz
	else
		tag.comcapuz=0
		//remove capuz
		findlayer.layer_skirt.remove
		//recupera layers salvos
		forcharlayer layer_vendor_buys
			if (<type>==t_hair)
				layer=layer_hair
			elif (<type>==t_clothing)
				layer=layer_skirt
			endif
		endfor
		events -e_capuz
	endif
	update
else
	sysmessageyellow Voce nao esta vestido com um robe.
endif
return 1

//*****************************************************************************
//*****************************************************************************
// ITEMDEFs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// i_capuz
//*****************************************************************************
[ITEMDEF i_capuz]
NAME=robe
ID=0204E
TYPE=t_clothing
LAYER=layer_skirt

ON=@CREATE
attr=0100

//*****************************************************************************
//*****************************************************************************
// MENUs
//*****************************************************************************
//*****************************************************************************
[MENU sm_dress]
(MYT) Staff Dress System (MYT)

on=0 Acionar Status de GM
 gm 1
 invis 1
 invul 1
 privshow 0
on=0 Salvar itens do corpo
 saveitens
on=0 Admin
 limparlayers
 src.body c_man_gm
 src.color 0549
 src.nightsight 1
 src.title Staff Mystical Tales
 src.update
on=0 iGM
 limparlayers
 src.body c_man_gm
 src.color 09a4
 src.nightsight 1
 src.title iT Staff Mystical Tales
 src.update
on=0 GM
 limparlayers
 src.body c_man_gm
 src.color 0b3d
 src.nightsight 1
 src.title GMT Staff Mystical Tales
 src.update
on=0 Seer
 limparlayers
 src.body c_man_gm
 src.color 09a7
 src.nightsight 1
 src.title iT Staff Mystical Tales
 src.update
on=0 Counselor
 limparlayers
 src.body c_man_gm
 src.color 0b3f
 src.nightsight 1
 src.title GMT Staff Mystical Tales
 src.update
on=0 Humano
 limparlayers
 src.body c_man
 src.color 083ea
 src.nightsight 1
 src.title=.
 src.update
[EOF]