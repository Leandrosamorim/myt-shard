
//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\
//\\                EVENTS              //\\
//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\

[EVENTS e_house_customize]
ON=@SkillStart
    IF !(<HOUSEDESIGN>)
        RETURN 2
    ENDIF
    SYSMESSAGERED Voce esta ocupado projetando um edificio.
    RETURN 1

ON=@SkillUseQuick
    IF !(<HOUSEDESIGN>)
        RETURN 2
    ENDIF
    RETURN 1

ON=@HouseDesignCommit
    LOCAL.OLDCOST = <EVAL (<ARGN1> * <DEF.CH_COMPONENT_COST>)>
    LOCAL.NEWCOST = <EVAL (<ARGN2> * <DEF.CH_COMPONENT_COST>)>
    LOCAL.OLDCOST += <EVAL (<LOCAL.FIXTURES.OLD> * <DEF.CH_FIXTURE_COST>)>
    LOCAL.NEWCOST += <EVAL (<LOCAL.FIXTURES.NEW> * <DEF.CH_FIXTURE_COST>)>
    LOCAL.CURCOST = <EVAL (<LOCAL.NEWCOST> - <LOCAL.OLDCOST>)>
    IF (<IsEmpty <argo.tag.owner>>) || (!<UID.<argo.tag0.owner>.instances>)
        obj=<uid>
    ELSE
        obj=<argo.tag.owner>
    ENDIF
    IF (<ISGM>)
        SYSMESSAGEGREEN Seu novo projeto de edificio foi enviado.
        SYSMESSAGERED Custaria <dLOCAL.CURCOST> mc.
    ENDIF
    LOCAL.cash=<obj.bank_checkCash <LOCAL.CURCOST>,1>
    IF (<LOCAL.cash> < <LOCAL.CURCOST>)
        SYSMESSAGERED O dono da casa nao tem <dLOCAL.CURCOST> mc para pagar a construcao.
        if (<obj>==<uid>)
            SYSMESSAGERED Seu total no banco é de <dLOCAL.cash> mc.
        ENDIF
        IF (<IsGM>)
            SYSMESSAGEORANGE ATENCAO! Operacao de GM! O dono nao tem dinheiro. Nada foi cobrado.
            RETURN 2
        ELSE
            RETURN 1
        ENDIF
    ENDIF
    SYSMESSAGEGREEN Seu novo projeto de edificio foi enviado.
    IF (<LOCAL.CURCOST> == 0)
        SYSMESSAGEYELLOW Como o novo projeto tem o mesmo custo do anterior, nada foi cobrado.
    ELSEIF (<LOCAL.CURCOST> < 0)
        LOCAL.CURCOST = <EVAL (0 - <LOCAL.CURCOST>)>
        serv.newitem i_coin_copper, <eval (<LOCAL.CURCOST>*90)/100>, <OBJ>
        SYSMESSAGEYELLOW Como o novo projeto ficou mais barato que o anterior, <eval (<LOCAL.CURCOST>*90)/100> mc foram devolvidas ao dono.
    ELSE
        obj.bank_getCash <LOCAL.CURCOST>, 1
        SYSMESSAGEYELLOW <dLOCAL.CURCOST> mc foram cobrados para custear a construcao.
    ENDIF
    RETURN 2

ON=@HouseDesignExit
    SYSMESSAGEORANGE Voce saiu do modo de projeto de edificio.
    EVENTS -e_house_customize
    RETURN 2

[FUNCTION TOWN_STORE_RESOURCE]
IF (!<serv.resources.<argv[0]>>) || (!<IsNum <argv[1]>>)
 return 0
ELIF (<IsEmpty <tag.resources_<region.defname>_<argv[0]>>>)
 tag.resources_<region.defname>_<argv[0]>=<argv[1]>
ELSE
 tag0.resources_<region.defname>_<argv[0]> += <argv[1]>
ENDIF
return 1

[FUNCTION TOWN_CHECK_RESOURCE]
IF (!<serv.resources.<argv[0]>>) || (<IsEmpty <tag.resources_<region.defname>_<argv[0]>>>)
 return 0
ELSE
 return <tag0.resources_<region.defname>_<argv[0]>>
ENDIF

[FUNCTION TOWN_CONSUME_RESOURCE]
IF (!<serv.resources.<argv[0]>>) || (<IsEmpty <tag.resources_<region.defname>_<argv[0]>>>)
 return 0
ELSE
 tag0.resources_<region.defname>_<argv[0]> -= <argv[1]>
 IF !(<tag0.resources_<region.defname>_<argv[0]>>)
  tag.resources_<region.defname>_<argv[0]>=
 ENDIF
 return 1
ENDIF

[FUNCTION TOWN_SHOW_RESOURCE]


[EOF]