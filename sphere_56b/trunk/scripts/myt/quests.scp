//  ########################################################
//  ########################################################
//			Conjunto de sistemas
//		Para decoração de dungeons dinâmicas
//
//			Galthar, o Errante
//
//  ########################################################
//  ########################################################
//
//
//	TAGs utilizadas:
//	- tag.sound
//	- tag.odisp
//	- tag.trap_time
//	- tag.trap_dif
//	- tag.trap_force
//	- tag.trap_type
//
//  ########################################################
//
//	  COMO USAR
//
//
/////////////////////////////ARMADILHAS//////////////////////////
//	Ponha a armadilha aonde quizer e dê 2 clicks. Ela ficará
//   armada e invisível.
//
/////////////////////////////ALAVANCAS///////////////////////////
//	Todos os itens devem ser colcoados pelo Axis como LOCKED.
//   Ponha a alavanca no chão num lugar conveniente.
//
// - PORTAS:
//	Coloque as portas que deseja abrir e fechar pela alavanca.
//   Então deixe os types das portas t_door_locked. Use o comando
//   .link, clique na alavanca e depois na porta. Se houver mais
//   de uma porta, link a primeira na segunda. MAXIMO DE 2 PORTAS!
//
// - CONTAINERS:
//	Deixe os containers t_container_locked. Linke
//   a alavanca no container. Se houver mais de um container link
//   o primeiro no segundo. MAXIMO DE 2 CONTAINERS!
//
// - BARREIRAS:
//	Coloque as barreiras (fogo, veneno e energia) nos devidos
//   lugares e link a alavanca na primeira barreira. Depois a
//   primeira barreira na segunda, a segunda a terceira e assim
//   por diante. A ULTIMA NÃO LINK EM NADA! Pode dar freeze no
//   shard! Tente não passar de 20 barreiras para não gerar lag.
//	Não é possível linkar 2 tipos diferentes de barreiras.
//
/////////////////////////////PESTE///////////////////////////
// CUIDADO! ESSE SCRIPT É MUITO DESTRUTIVO POIS A PESTE É
// CONTAGIOSA! E DURA MUITO! NÃO HÁ CURA!
// SE ATIVADA NUM LOCAL MOVIMENTADO PODE DIZIMAR A POPULAÇÃO
// DE PLAYERS NO SHARD! USE COM CAUTELA!
// Ative a peste pelo comando ".nopeste 0" e desative por
// ".NOPESTE 1".
// Ponha algum objeto no chão, mude o type dele para t_peste
// e espere o pobre coisato pisar.
// MAS NÃO USE SEM O CONSENTIMENTO DE TODOS NA STAFF! É ABSURDO!



   //////////////////////////////////////////////
  //		ARMADILHAS PARA DUNGEONS      //
 //////////////////////////////////////////////


// ---------# TYPEDEFS #---------//

[TYPEDEF t_trap]

//more1 spell
//more2 força (%skill magery)
//morex cargas (morex>04fffffff = infinito, coisa de GM)
//morey dificuldade de desarmar/detectar (%skill detect hidden e disarm trap)
//morez typedef original
//morem tempo de rearme
//link chave
//attr_magic Se for armadilha de chão, ela pode mudar d elugar quando resetada.
//color=026 foi detectada
//tag.color A ultima cor da armadilha.


on=@step
type=<morez>
if (<morex>==0)// || (<src.isgm>)
 return 1
endif
if (<more1>==5)				//Magic Arrow
 src.effect 3 i_fx_smoke_small 0 32 0
 src.sfx 0223
elif (<more1>==18)			//Fireball
 src.effect 3 i_fx_explode 0 14 0
 src.sfx 015e
elif (<more1>==20)			//Poison
 src.effect 2 i_trap_gas_3 0 24 0
 src.sfx 022f
elif (<more1>==28)			//Fire Field
 LOCAL.time=<eval <more2>/10>
 src.f_cela_fogo <LOCAL.time>
 more2=0
elif (<more1>==30)			//Lighting
 src.effect 1 0 0 24 0
 src.sfx 028
elif (<more1>==39)			//Poison Field
 LOCAL.time=<eval <more2>/10>
 src.f_cela_veneno <LOCAL.time>
 more2=0
elif (<more1>==43)			//Explosion
 effect 3 i_fx_explode 0 14 0
 src.sfx 011b
elif (<more1>==42)			//Energy Blast
 src.effect 3 036cb 0 9 0 0b90 0
 src.sfx 0213
elif (<more1>==51)			//Flame Bolt
 src.effect 3 i_fire_column 0 16
 src.sfx 011b
endif
if (<morex> == 04fffffff)		//Carga infinita
 var.blank=
else					//decrementa carga
 morex=<morex>-1
endif
if (<attr>&080)				//Se estiver invis (testa antes de mudar attr para não dar erro no console do Sphere)
 attr==<attr>~&080			//Apareça
 update
endif
f_resetTrap
src.spelleffect <more1> <more2> <uid>	//aplica efeito hardcoded
return 1

[FUNCTION f_resetTrap]
if (!<argn>) 			//Trap cria o resetador de trap
 serv.newitem i_reset_trap
 new.link=<UID>
 new.p=<p>
 new.timer=<eval <morem>>
else				//resetador de trap reseta a trap
 link.type=t_trap
 if (<link.attr>&020)
  link.movenear <UID> <eval {0 12 1 5 2 2 3 1}>
 endif
 try link.color=0<link.tag.color>
 if (!<link.morez>)		//Se é uma armadilha de chão. Morez=t_normal=0
  link.attr=<link.attr>|080	//volta a ficar invis
 endif
 link.update
 link.REMOVEFROMVIEW		//some pros chars que estão perto
endif

[ITEMDEF i_reset_trap]
NAME=reset de armadilha
ID=i_worldgem_bit
TYPE=t_script

on=@create
attr=092
color=028

on=@timer
f_resetTrap 1

on=@dclick
f_resetTrap 1
remove



[FUNCTION dh]
FORITEMS 8
 if (<type>==t_trap)		//Para todas as traps
  if (!<morez>)			//Se for uma armadilha do chão (morez=t_normal=0)
   if (<attr>&080)		//Se estiver invis (testa antes de mudar attr para não dar erro no console do Sphere)
    attr==<attr>~&080		//Apareça
   endif
   tag.color=0<color>
   color=026
   update
   say=@028, [armadilha]
   f_resetTrap
  endif
 endif
ENDFOR

// ---------# MENUS #---------//

[menu m_set_trap]
(MyT) Que armadilha deseja instalar? (MyT)

on=0 Reduzir DEX //Clumsy
tag.trap_type=1
menu m_set_trap_force

on=0 Reduzir IND //feeblemind
tag.trap_type=3
menu m_set_trap_force

on=0 Reduzir STR //Weaken
tag.trap_type=8
menu m_set_trap_force

on=0 Envenenar
tag.trap_type=20
menu m_set_trap_force

on=0 Ferir
tag.trap_type=12
menu m_set_trap_force

on=0 Raio
tag.trap_type=30
menu m_set_trap_force

on=0 Esplosao
tag.trap_type=43
menu m_set_trap_force

[menu m_set_trap_force]
(MyT) Qual forca tera a armadilha? (MyT)
on=0 irrisoria
tag.trap_force=<eval {50 150}>
menu m_set_trap_skill

on=0 fraca
tag.trap_force=<eval {200 400}>
menu m_set_trap_skill

on=0 media
tag.trap_force=<eval {400 600}>
menu m_set_trap_skill

on=0 forte
tag.trap_force=<eval {600 850}>
menu m_set_trap_skill

on=0 mortal
tag.trap_force=<eval {850 2000}>
menu m_set_trap_skill

[menu m_set_trap_skill]
(MyT) Dificuldade para achar/desarmar? (MyT)
on=0 irrisoria
tag.trap_dif=<eval {50 150}>
menu m_set_trap_time

on=0 baixa
tag.trap_dif=<eval {200 400}>
menu m_set_trap_time

on=0 media
tag.trap_dif=<eval {400 600}>
menu m_set_trap_time

on=0 alta
tag.trap_dif=<eval {600 850}>
menu m_set_trap_time

on=0 impossivel
tag.trap_dif=<eval {1000 2000}>
menu m_set_trap_time

[menu m_set_trap_time]
(MyT) Qual sera o tempo de regeneracao? (MyT)
on=0 30 segundos
tag.trap_time 30
f_set_trap

on=0 1 minuto
tag.trap_time 60
f_set_trap

on=0 5 minutos
tag.trap_time 60*5
f_set_trap

on=0 10 minutos
tag.trap_time 60*10
f_set_trap

on=0 20 minutos
tag.trap_time 60*20
f_set_trap

on=0 30 minutos
tag.trap_time 60*30
f_set_trap

on=0 45 minutos
tag.trap_time 45*60
f_set_trap

on=0 1 hora
tag.trap_time 60*60
f_set_trap

on=0 3 horas
tag.trap_time 3*60*60
f_set_trap

on=0 5 horas
tag.trap_time 5*60*60
f_set_trap


// ---------# FUNCTIONS #---------//


[PLEVEL 2]
TRAP


[FUNCTION ganhadetecthidden]
 if (<detectinghidden> < 1000) && (<detectinghidden> >= 10)
  GAIN_SKILL detectinghidden  
 endif

[FUNCTION TRAP]
menu m_set_trap

[FUNCTION f_set_trap]
Sysmessage Selecione o item...
targetf f_set_trap_

[FUNCTION f_set_trap_]
if (0<argo>==0) || (<argo><040000000)
 Sysmessage Selecione o item...
 targetf f_set_trap_
 return 1
endif
argo.morex=<eval 0<src.tag.trap_type>>
argo.morey=<eval 0<src.tag.trap_force>>
argo.morez=<argo.type>
argo.morem=1
argo.more2=<eval 0<src.tag.trap_dif>>
argo.type=t_trap
serv.newitem i_reset_trap
new.p=<argo.p>
new.link=<argo.uid>
new.more2=<eval 0<src.tag.trap_time>>
new.attr=08090
new.timer=5
src.tag.trap_time
src.tag.trap_dif
src.tag.trap_force
src.tag.trap_type
sysmessage=@026 Pronto


// ---------# ITEMDEFS #---------//


[ITEMDEF i_armadilha_machado]
ID=01b71
type=t_trap
name=armadilha
CATEGORY=MyT - Quest Items
SUBSECTION=Armadilhas
DESCRIPTION=Machado

on=@create
more1=0114c
more2=150
attr=08090
morex=1
morey=02
tag.sound=0f1
timer=-1

[ITEMDEF i_armadilha_magica_fraca]
ID=01b71
type=t_trap
name=armadilha
CATEGORY=MyT - Quest Items
SUBSECTION=Armadilhas
DESCRIPTION=Magica fraca

on=@create
more1=i_fx_curse
more2=110
attr=08090
morex=1
morey=04
tag.sound=0fc
timer=-1

[ITEMDEF i_armadilha_magica_forte]
ID=01b71
type=t_trap
name=armadilha
CATEGORY=MyT - Quest Items
SUBSECTION=Armadilhas
DESCRIPTION=Magica forte

on=@create
more1=i_fx_vortex_full
more2=270
attr=08090
morex=1
morey=04
tag.sound=0109
timer=-1

[ITEMDEF i_armadilha_magica_media]
ID=01b71
type=t_trap
name=armadilha
CATEGORY=MyT - Quest Items
SUBSECTION=Armadilhas
DESCRIPTION=Magica media

on=@create
more1=i_fx_glow_spike
more2=200
attr=08090
morex=1
morey=04
tag.sound=0fd
timer=-1

[ITEMDEF i_armadilha_explosao_forte]
ID=01b71
type=t_trap
name=armadilha
CATEGORY=MyT - Quest Items
SUBSECTION=Armadilhas
DESCRIPTION=Explosao forte

on=@create
more1=i_fx_explode
more2=400
attr=08090
morex=1
morey=010
tag.sound=011b
timer=-1

[ITEMDEF i_armadilha_explosao_media]
ID=01b71
type=t_trap
name=armadilha
CATEGORY=MyT - Quest Items
SUBSECTION=Armadilhas
DESCRIPTION=Explosao media

on=@create
more1=i_fx_explosion_2
more2=350
attr=08090
morex=1
morey=010
tag.sound=011b
timer=-1

[ITEMDEF i_armadilha_explosao_fraca]
ID=01b71
type=t_trap
name=armadilha
CATEGORY=MyT - Quest Items
SUBSECTION=Armadilhas
DESCRIPTION=Explosao fraca

on=@create
more1=036cb
more2=200
attr=08090
morex=1
morey=010
tag.sound=011b
timer=-1

[ITEMDEF i_armadilha_gas_fraca]
ID=01b71
type=t_trap
name=armadilha
CATEGORY=MyT - Quest Items
SUBSECTION=Armadilhas
DESCRIPTION=Gas fraca

on=@create
more1=i_trap_gas_3
more2=150
attr=08090
morex=1
morey=08
tag.sound=0108
timer=-1

[ITEMDEF i_armadilha_gas_media]
ID=01b71
type=t_trap
name=armadilha
CATEGORY=MyT - Quest Items
SUBSECTION=Armadilhas
DESCRIPTION=Gas media

on=@create
more1=i_trap_gas_3
more2=200
attr=08090
morex=1
morey=08
tag.sound=0108
timer=-1

[ITEMDEF i_armadilha_gas_forte]
ID=01b71
type=t_trap
name=armadilha
CATEGORY=MyT - Quest Items
SUBSECTION=Armadilhas
DESCRIPTION=Gas forte

on=@create
more1=i_trap_gas_3
more2=300
attr=08090
morex=1
morey=08
tag.sound=0108
timer=-1

[ITEMDEF i_armadilha_gas_mortal]
ID=01b71
type=t_trap
tag.sound=0108
name=armadilha
CATEGORY=MyT - Quest Items
SUBSECTION=Armadilhas
DESCRIPTION=Gas mortal

on=@create
more1=i_trap_gas_3
more2=450
attr=08090
morey=08
morex=1
tag.sound=0108
timer=-1



   //////////////////////////////////////////////
  //		ALAVANCAS PARA DUNGEONS       //
 //////////////////////////////////////////////



// ---------# TYPEDEFS #---------//



[TYPEDEF t_alavanca]
on=@create
attr=08010
more2=30

on=@click
if (<src.isgm>)
 message <name> (<eval <more2>> segundos)
 return 1
endif


on=@dclick
sfx 0f0
f_alavancaDispchange
if (!<morex>)
 morex=1
 if (<link.type>==t_door_locked)
  f_leverDoor
 elif (<link.type>==t_container_locked)
  f_leverCont
 elif (<link.type>==t_fire_barrier)
  f_turnFireBarrier
 elif (<link.type>==t_poison_barrier)
  f_turnPoisonBarrier
 elif (<link.type>==t_energy_barrier)
  f_turnEnergyBarrier
 endif
else
 morex=0
 if (<link.type>==t_door_locked)
  f_leverDoor
 elif (<link.type>==t_container_locked)
  f_leverCont
 elif (<link.type>==t_fire_barrier)
  f_turnFireBarrier
 elif (<link.type>==t_poison_barrier)
  f_turnPoisonBarrier
 elif (<link.type>==t_energy_barrier)
  f_turnEnergyBarrier
 endif
endif
return 1

on=@timer
if (<morex>)
 f_alavancaDispchange
 sfx 0f0
 morex=0
 if (<link.type>==t_door_locked)
  f_leverDoor
 elif (<link.type>==t_container)
  f_leverCont
 elif (<link.type>==t_fire_barrier)
  f_turnFireBarrier
 elif (<link.type>==t_poison_barrier)
  f_turnPoisonBarrier
 elif (<link.type>==t_energy_barrier)
  f_turnEnergyBarrier
 endif
 timer=-1
endif
return 1



// ---------# FUNCTIONS #---------//



[FUNCTION f_alavancaDispchange]
if (<dispid>==01093)
 dispid=01095
elif (<dispid>==01095)
 dispid=01093
elif (<dispid>==i_lever)
 dispid=0108e
elif (<dispid>==0108e)
 dispid=i_lever
elif (<dispid>==i_switch)
 dispid=01090
elif (<dispid>==01090)
 dispid=i_switch
elif (<dispid>==01091)
 dispid=01092
elif (<dispid>==01092)
 dispid=01091
elif (<dispid>==i_sconce_lit)
 dispid=09fc
elif (<dispid>==09fc)
 dispid=i_sconce_lit
elif (<dispid>==i_sconce2_lit)
 dispid=i_sconce2_empty
elif (<dispid>==i_sconce2_empty)
 dispid=i_sconce2_lit
elif (<dispid>==i_torch_wall_lit)
 dispid=i_torch_wall_empty
elif (<dispid>==i_torch_wall_empty)
 dispid=i_torch_wall_lit
elif (<dispid>==i_torch_wall2_lit)
 dispid=i_torch_wall2_empty
elif (<dispid>==i_torch_wall2_empty)
 dispid=i_torch_wall2_lit
endif
update

[FUNCTION f_leverDoor]
link.timerd=1
if (<morex>)
 timer=<more2>
 link.say=@026 click...
else
 link.say=@026 TRANK!
endif
if (<link.link>!=04fffffff) && (<link.link>!=<UID>)
 link.link.timerd=1
 if (<morex>)
  link.link.say=@026 click...
 else
  link.link.say=@026 TRANK!
 endif
endif

[FUNCTION f_leverCont]
if (<morex>)
 link.type=t_container
 timer=<more2>
 link.say=@026 click...
else
 link.type=t_container_locked
 link.say=@026 TRANK!
endif
if (<link.link>!=04fffffff) && (<link.link>!=<UID>)
 if (<morex>)
  link.type=t_container
  link.link.say=@026 click...
 else
  link.type=t_container_locked
  link.link.say=@026 TRANK!
 endif
endif

// ---------# ITEMDEFS #---------//




[ITEMDEF i_alavanca_dungeon_0]
id=i_lever
name=alavanca
type=t_alavanca
CATEGORY=MyT - Quest Items
SUBSECTION=Alavancas
DESCRIPTION=Alavanca para dungeons

on=@create
ATTR=08010
MORE2=30

[ITEMDEF i_alavanca_dungeon_1]
id=01093
name=alavanca
type=t_alavanca
CATEGORY=MyT - Quest Items
SUBSECTION=Alavancas
DESCRIPTION=Alavanca para dungeons

on=@create
ATTR=08010
MORE2=30

[ITEMDEF i_alavanca_dungeon_2]
id=i_switch
name=alavanca
type=t_alavanca
CATEGORY=MyT - Quest Items
SUBSECTION=Alavancas
DESCRIPTION=Alavanca para dungeons

on=@create
ATTR=08010
MORE2=30

[ITEMDEF i_alavanca_dungeon_3]
id=01091
name=alavanca
type=t_alavanca
CATEGORY=MyT - Quest Items
SUBSECTION=Alavancas
DESCRIPTION=Alavanca para dungeons

on=@create
ATTR=08010
MORE2=30

[ITEMDEF i_alavanca_dungeon_4]
id=i_sconce_lit
name=vela
type=t_alavanca
CATEGORY=MyT - Quest Items
SUBSECTION=Alavancas
DESCRIPTION=Vela alavanca

on=@create
ATTR=08010
MORE2=30

[ITEMDEF i_alavanca_dungeon_5]
id=i_sconce2_lit
name=vela
type=t_alavanca
CATEGORY=MyT - Quest Items
SUBSECTION=Alavancas
DESCRIPTION=Vela alavanca

on=@create
ATTR=08010
MORE2=30

[ITEMDEF i_alavanca_dungeon_6]
id=i_torch_wall_lit
name=tocha
type=t_alavanca
CATEGORY=MyT - Quest Items
SUBSECTION=Alavancas
DESCRIPTION=Tocha alavanca

on=@create
ATTR=08010
MORE2=30

[ITEMDEF i_alavanca_dungeon_7]
id=i_torch_wall2_lit
name=tocha
type=t_alavanca
CATEGORY=MyT - Quest Items
SUBSECTION=Alavancas
DESCRIPTION=Tocha alavanca

on=@create
ATTR=08010
MORE2=30


   //////////////////////////////////////////////
  //		BARREIRAS PARA DUNGEONS       //
 //////////////////////////////////////////////



// ---------# TYPEDEFS #---------//



[TYPEDEF t_poison_barrier]
on=@step
if (!<morex>) && (!<src.gm>)
 src.f_retreat
 src.damage {32 42} 010 <uid>
 src.veneno 66
 src.stun
 sfx 0117
endif
return 1

[TYPEDEF t_fire_barrier]
on=@step
if (!<morex>) && (!<src.gm>)
 src.f_retreat
 src.damage {32 42} 010 <uid>
 src.stun
 sfx 015e
endif
return 1

[TYPEDEF t_energy_barrier]
on=@step
if (!<morex>) && (!<src.gm>)
 src.f_retreat
 src.damage {22 32} 010 <uid>
 src.stun <eval {20 55}>
 sfx 0235
endif
return 1




// ---------# FUNCTIONS #---------//





[FUNCTION f_turnFireBarrier]
timer = <more2>
link._f_turnFireBarrier
return 1

[FUNCTION _f_turnFireBarrier]
if (<type>!=t_fire_barrier)
 return 1
endif
if (!<morex>)
 morex=1
 tag.odisp=<dispid>
 dispid=i_fx_smoke_small
else
 morex=0
 dispid=0<tag.odisp>
endif
update
if (<link>!=04fffffff)  && (<link.morex>!=<morex>) && (<link.type>==<type>)
 link._f_turnFireBarrier
else
 sfx 054
endif

[FUNCTION f_turnPoisonBarrier]
timer = <more2>
link._f_turnPoisonBarrier
return 1

[FUNCTION _f_turnPoisonBarrier]
if (<type>!=t_poison_barrier)
 return 1
endif
if (!<morex>)
 morex=1
 tag.odisp=<dispid>
 dispid=i_fx_smoke_small
 color=colors_green
else
 morex=0
 dispid=0<tag.odisp>
 color=0
endif
update
if (<link>!=04fffffff)  && (<link.morex>!=<morex>) && (<link.type>==<type>)
 link._f_turnPoisonBarrier
else
 sfx 011a
endif




[FUNCTION f_turnEnergyBarrier]
timer = <more2>
link._f_turnEnergyBarrier
return 1

[FUNCTION _f_turnEnergyBarrier]
if (<type>!=t_energy_barrier)
 return 1
endif
if (!<morex>)
 morex=1
 tag.odisp=<dispid>
 dispid=i_fx_sparkle_2
else
 morex=0
 dispid=0<tag.odisp>
endif
update
if (<link>!=04fffffff)  && (<link.morex>!=<morex>) && (<link.type>==<type>)
 link._f_turnEnergyBarrier
else
 sfx 0231
endif





[FUNCTION f_retreat]
 if (<dir>==2)
  p=<eval <p.x>-1>,<p.y>,<p.z>
  update 
 elif (<dir>==3)
  p=<eval <p.x>-1>,<eval <p.y>-1>,<p.z>
  update
 elif (<dir>==7)
  p=<eval <p.x>+1>,<eval <p.y>+1>,<p.z>
  update
 elif (<dir>==6)
  p=<eval <p.x>+1>,<p.y>,<p.z>
  update 
 elif (<dir>==5)
  p=<eval <p.x>+1>,<eval <p.y>-1>,<p.z>
  update
 elif (<dir>==1)
  p=<eval <p.x>-1>,<eval <p.y>+1>,<p.z>
  update
 elif (<dir>==0)
  p=<p.x>,<eval <p.y>+1>,<p.z>
  update
 elif (<dir>==4)
  p=<p.x>,<eval <p.y>-1>,<p.z>
  update
 endif
 return 0
endif



// ---------# ITEMDEFS #---------//



[ITEMDEF i_fire_barrier_0]
ID=i_fx_field_fire
NAME=barreira de fogo
TYPE=t_fire_barrier

CATEGORY=MyT - Quest Items
SUBSECTION=Barreiras
DESCRIPTION=Barreira de fogo

[ITEMDEF i_fire_barrier_1]
ID=i_fx_field_fire_ns
NAME=barreira de fogo
TYPE=t_fire_barrier

CATEGORY=MyT - Quest Items
SUBSECTION=Barreiras
DESCRIPTION=Barreira de fogo


[ITEMDEF i_poison_barrier_0]
ID=03921
NAME=barreira de veneno
TYPE=t_poison_barrier

CATEGORY=MyT - Quest Items
SUBSECTION=Barreiras
DESCRIPTION=Barreira de veneno

[ITEMDEF i_poison_barrier_1]
ID=03915
NAME=barreira de veneno
TYPE=t_poison_barrier

CATEGORY=MyT - Quest Items
SUBSECTION=Barreiras
DESCRIPTION=Barreira de veneno

[ITEMDEF i_energy_barrier_0]
ID=i_fx_field_paralyze
NAME=barreira de energia
TYPE=t_energy_barrier

CATEGORY=MyT - Quest Items
SUBSECTION=Barreiras
DESCRIPTION=Barreira de energia

[ITEMDEF i_energy_barrier_1]
ID=03979
NAME=barreira de energia
TYPE=t_energy_barrier

CATEGORY=MyT - Quest Items
SUBSECTION=Barreiras
DESCRIPTION=Barreira de energia


   //////////////////////////////////////////////
  //	          TESOUROS GUARDADOS          //
 //////////////////////////////////////////////


// -------# TYPEDEFS #-------- //

[TYPEDEF t_guarded_container]
on=@dclick
if (<link.npc>
 src.sysmessagered <link.name> esta guardando este objeto.
else
 if (0<src.tag.fRP> < -1)
  src.sysmessage=@037 Seus pontos de RP estao baixos. Tesouro bloqueado.
  return 1
 endif
 type=t_container
 emoteyellow destrancou
endif
return 1

// -------# ITEMDEFS #-------- //

[ITEMDEF i_trig_guarded_cont]
ID=i_rune_glowing_1
NAME=gatilho de guarda de container
TYPE=t_script

CATEGORY=MyT - Quest Items
SUBSECTION=Gatilhos
DESCRIPTION=guarda de container

on=@create
attr=080b0

on=@dclick
if (<src.isgm>)
 TARGET Selecione o container a guardar:
 return 1
endif

on=@Targon_Item
if (<src.targ.type>==t_container)
 link=<src.targ.UID>
 link.type=t_guarded_container
 src.sysmessagered linkado em <link.name>
 src.sysmessagered Assegure-se que o guarda pise no gatilho.
else
 src.sysmessage Apenas containeres abertos.
endif
return 1

on=@step
if (<src.npc>)
 if (!<link.link.npc>)
  link.link=<src.uid>
  link.empty
  link.type t_guarded_container
  if (<link.more1>)
   serv.newitem=<link.more1>
  else
   serv.newitem=i_loot_quest_pobre
  endif
  new.cont=<link>
 endif
endif
return 1



[ITEMDEF i_tesouro_pobre]
id=0e43
name=tesouro
type=t_container
CATEGORY=MyT - Quest Items
SUBSECTION=Quest
DESCRIPTION=Tesouro pobre

on=@create
attr=08010
more1=i_loot_quest_pobre
say Use um gatilho de guarda em mim!

[ITEMDEF i_tesouro_regular]
id=i_chest_metal
name=tesouro
type=t_container
CATEGORY=MyT - Quest Items
SUBSECTION=Quest
DESCRIPTION=Tesouro regular

on=@create
COLOR=color_o_copper
attr=08010
more1=i_loot_quest_regular
say Use um gatilho de guarda em mim!

[ITEMDEF i_tesouro_rico]
id=i_chest_metal
name=tesouro
type=t_container
CATEGORY=MyT - Quest Items
SUBSECTION=Quest
DESCRIPTION=Tesouro rico

on=@create
COLOR=color_o_silver
attr=08010
more1=i_loot_quest_rico
say Use um gatilho de guarda em mim!


[ITEMDEF i_tesouro_milhonario]
id=i_chest_metal
name=tesouro
type=t_container
CATEGORY=MyT - Quest Items
SUBSECTION=Quest
DESCRIPTION=Tesouro Milhonario

on=@create
COLOR=color_o_gold
attr=08010
more1=i_loot_quest_milhonario
say Use um gatilho de guarda em mim!



   //////////////////////////////////////////////
  //	       PESTE PARA REGIÕES ERMAS       //
 //////////////////////////////////////////////





[ITEMDEF i_mry_peste]
ID=0ED2
NAME=contraiu a peste
type=T_EQ_SCRIPT
LAYER=30

on=@create
attr=08010
more2=31
timer 1

on=@timer
if (0<var.no_peste>==1)
 cont.flags=<cont.flags>&~080
 cont.update
 remove
 return 1
endif
if (<cont.restest 2 i_mry_peste>)
 remove
 return 1
endif
if (<cont.flags>&02) || (<cont.restest 1 i_mry_desmaio>)
 timer = 30
 return 1
endif
if !(<cont.flags>&080)
 cont.flags=<cont.flags>|080
 cont.update
endif
more2=<more2>-1
if (<more2> <= 0) || (<more2> >= 40)
 cont.flags=<cont.flags>&~080
 cont.update
 cont.events -e_peste
 remove
 return 1
endif
if (<more2> <=5)
 dorand 4
  cont.f_vomita
  cont.emotegreen suando
  cont.f_dores
  cont.emotegreen palido
 enddo
 cont.f_moscas
 cont.sysmessagegreen A peste esta mais fraca.
 cont.f_stam_peste
 timer {120 180}
elif (<more2> <=10)
 dorand 4
  cont.f_vomita
  cont.emotegreen fedendo
  cont.f_dores
  cont.emotegreen palido
 enddo
 cont.f_stam_peste
 timer {60 120}
elif (<more2> <=20)
 dorand 4
  cont.f_vomita
  cont.emotegreen fedendo
  cont.emotegreen passando mal
  cont.f_moscas
 enddo
 cont.f_dores
 timer {30 60}
elif (<more2> <= 29)
 dorand 4
  cont.f_vomita
  cont.emotegreen fedendo
  cont.f_dores
  cont.emotegreen muito mal
 enddo
 timer {12 30}
elif (<more2> == 30)
 cont.emotegreen muito mal
 cont.sysmessagegreen A Peste esta te assolando.
 timer {12 30}
endif
cont.f_dano_peste <more2>
cont.f_alastra_peste
return 1

[FUNCTION f_peste]
newitem i_mry_peste
new.equip
events +e_peste

[FUNCTION f_vomita]
newitem i_poca
new.p=<p>
new.color=colors_green
new.name=vomito
new.attr=012
new.timer={8 12}
new.update
new.type t_peste
emotegreen vomita
sysmessagegreen A peste esta lhe enfraquecendo
anim 32

[function f_dores]
say=@021 Ohhh!
anim 32
sysmessagegreen Voce esta sentindo fortes dores devido a peste!

[FUNCTION f_dano_peste]
if (<hits> >= (<str>/2)) && (<argn> < <hits>)
 local.dano=(<argn>)
 hits=<hits>-<local.dano>
endif

[FUNCTION f_stam_peste]
if (<stam> > (<dex>/2))
 stam = (<dex>/2)
endif

[TYPEDEF t_peste]
on=@step
if !(<src.restest 1 i_mry_peste>) && (!<src.isgm>)
 src.f_peste
endif

[FUNCTION f_alastra_peste]
newitem i_gold
new.p=<p>
new.color=01
new.name=alastrar a peste
new.attr=092
new.timer={60 120}
new.update
new.type t_peste
anim 32

[FUNCTION nopeste]
if (<argn>) && (0<argn>!=0)
 var.no_peste=0
 sysmessage=@024 Peste ativada.
 return 1
endif
if (<argn>) && (0<argn>==0)
 var.no_peste=1
 sysmessage=@024 Peste desativada.
 return 1
endif
if (0<argn>==0) && (0<var.no_peste>==0)
 var.no_peste=1
 sysmessage=@024 Peste desativada.
 return 1
else
 var.no_peste=0
 sysmessage=@024 Peste ativada.
 return 1
endif

[FUNCTION f_moscas]
sysmessagegreen Voce esta juntando moscas!
newitem i_moscas
new.p=<p>
new.timer 2

[ITEMDEF i_moscas]
id=i_bee_swarm
type=t_peste
name=moscas

on=@create
attr=08010
color=03de
more1=300
timer 2

on=@timer
if (0<var.no_peste>==1)
 remove
 return 1
endif
if (<more1>)
 movenear <uid> 3
 more1=<more1>-1
 timer={1 2}
else
 remove
endif
if ({1 280}==1)
 serv.newitem i_moscas
 new.p=<p>
 new.timerd 3
endif
return 1

on=@step
return 0

[EVENTS e_peste]
on=@login
if (0<var.no_peste>==1)
 flags=<cont.flags>&~080
 update
 events -e_peste
 return 1
endif
f_moscas
return 0

on=@logout
f_moscas
return 0


[FUNCTION f_cela_fogo]
f_cela i_fogo_armadilha_ns, i_fogo_armadilha_lo, <argn>
sfx 015f
return 1

[FUNCTION f_cela_veneno]
f_cela i_veneno_armadilha_ns, i_veneno_armadilha_lo, <argn>
sfx 0117
return 1

[FUNCTION f_cela]
if (<argv2>==0)
 local.timer=<eval {20 30}>
else
 local.timer=<argv2>
endif


serv.newitem=<argv1>
new.p=<p>
new.move 0 2 0
new.timer=<local.timer>

serv.newitem=<argv1>
new.p=<p>
new.move 0 -1 0
new.timer=<local.timer>

serv.newitem=<argv1>
new.p=<p>
new.move 1 -1 0
new.timer=<local.timer>

serv.newitem=<argv1>
new.p=<p>
new.move -1 -1 0
new.timer=<local.timer>

serv.newitem=<argv1>
new.p=<p>
new.move -1 2 0
new.timer=<local.timer>

serv.newitem=<argv1>
new.p=<p>
new.move 1 2 0
new.timer=<local.timer>

serv.newitem=<argv0>
new.p=<p>
new.move 2 1 0
new.timer=<local.timer>

serv.newitem=<argv0>
new.p=<p>
new.move 2 0 0
new.timer=<local.timer>

serv.newitem=<argv0>
new.p=<p>
new.move 2 -1 0
new.timer=<local.timer>

serv.newitem=<argv0>
new.p=<p>
new.move -1 -1 0
new.timer=<local.timer>

serv.newitem=<argv0>
new.p=<p>
new.move -1 0 0
new.timer=<local.timer>

serv.newitem=<argv0>
new.p=<p>
new.move -1 1 0
new.timer=<local.timer>

serv.newitem=i_forbiden
new.p=<p>
new.move 2 2 0
new.timer=<local.timer>

[ITEMDEF i_forbiden]
NAME=forbiden
ID=i_gold
TYPE=t_script

on=@create
color=07fff
attr=092

on=@step
if (!<src.isgm>)
 src.f_retreat
endif
return 1