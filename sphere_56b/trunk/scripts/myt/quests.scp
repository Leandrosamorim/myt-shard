//  ########################################################
//  ########################################################
//			Conjunto de sistemas
//		Para decoração de dungeons dinâmicas
//
//			Galthar, o Errante
//
//  ########################################################
//  ########################################################
//
//
//	TAGs utilizadas:
//	- tag.sound
//	- tag.odisp
//	- tag.trap_time
//	- tag.trap_dif
//	- tag.trap_force
//	- tag.trap_type
//	- tag.tesouro
//
//  ########################################################
//
//	  COMO USAR
//
//
/////////////////////////////ALAVANCAS///////////////////////////
//	Todos os itens devem ser colcoados pelo Axis como LOCKED.
//   Ponha a alavanca no chão num lugar conveniente.
//
// - PORTAS:
//	Coloque as portas que deseja abrir e fechar pela alavanca.
//   Então deixe os types das portas t_door_locked. Use o comando
//   .link, clique na alavanca e depois na porta. Se houver mais
//   de uma porta, link a primeira na segunda. MAXIMO DE 2 PORTAS!
//
// - CONTAINERS:
//	Deixe os containers t_container_locked. Linke
//   a alavanca no container. Se houver mais de um container link
//   o primeiro no segundo. MAXIMO DE 2 CONTAINERS!
//
// - BARREIRAS:
//	Coloque as barreiras (fogo, veneno e energia) nos devidos
//   lugares e link a alavanca na primeira barreira. Depois a
//   primeira barreira na segunda, a segunda a terceira e assim
//   por diante. A ULTIMA NÃO LINK EM NADA! Pode dar freeze no
//   shard! Tente não passar de 20 barreiras para não gerar lag.
//	Não é possível linkar 2 tipos diferentes de barreiras.
//
/////////////////////////////PESTE///////////////////////////
// CUIDADO! ESSE SCRIPT É MUITO DESTRUTIVO POIS A PESTE É
// CONTAGIOSA! E DURA MUITO! NÃO HÁ CURA!
// SE ATIVADA NUM LOCAL MOVIMENTADO PODE DIZIMAR A POPULAÇÃO
// DE PLAYERS NO SHARD! USE COM CAUTELA!
// Ative a peste pelo comando ".nopeste 0" e desative por
// ".NOPESTE 1".
// Ponha algum objeto no chão, mude o type dele para t_peste
// e espere o pobre coitato pisar.
// MAS NÃO USE SEM O CONSENTIMENTO DE TODOS NA STAFF! É ABSURDO!


   //////////////////////////////////////////////
  //		ALAVANCAS PARA DUNGEONS       //
 //////////////////////////////////////////////



// ---------# TYPEDEFS #---------//



[TYPEDEF t_alavanca]
on=@create
attr=08010
more2=30

on=@click
if (<src.isgm>)
 message <name> (<eval <more2>> segundos)
 return 1
endif

ON=@CLIENTTOOLTIP
if (<src.isgm>)
	f_sendTooltip <name>,Tempo: <eval <more2>> segundos
	return 1
endif

on=@dclick
sfx 0f0
f_alavancaDispchange
if (!<morex>)
 morex=1
 if (<link.type>==t_door_locked)
  f_leverDoor
 elif (<link.type>==t_container_locked)
  f_leverCont
 elif (<link.type>==t_fire_barrier)
  f_turnFireBarrier
 elif (<link.type>==t_poison_barrier)
  f_turnPoisonBarrier
 elif (<link.type>==t_energy_barrier)
  f_turnEnergyBarrier
 endif
else
 morex=0
 if (<link.type>==t_door_locked)
  f_leverDoor
 elif (<link.type>==t_container_locked)
  f_leverCont
 elif (<link.type>==t_fire_barrier)
  f_turnFireBarrier
 elif (<link.type>==t_poison_barrier)
  f_turnPoisonBarrier
 elif (<link.type>==t_energy_barrier)
  f_turnEnergyBarrier
 endif
endif
return 1

on=@timer
if (<morex>)
 f_alavancaDispchange
 sfx 0f0
 morex=0
 if (<link.type>==t_door_locked)
  f_leverDoor
 elif (<link.type>==t_container)
  f_leverCont
 elif (<link.type>==t_fire_barrier)
  f_turnFireBarrier
 elif (<link.type>==t_poison_barrier)
  f_turnPoisonBarrier
 elif (<link.type>==t_energy_barrier)
  f_turnEnergyBarrier
 endif
 timer=-1
endif
return 1



// ---------# FUNCTIONS #---------//



[FUNCTION f_alavancaDispchange]
if (<dispid>==01093)
 dispid=01095
elif (<dispid>==01095)
 dispid=01093
elif (<dispid>==i_lever)
 dispid=0108e
elif (<dispid>==0108e)
 dispid=i_lever
elif (<dispid>==i_switch)
 dispid=01090
elif (<dispid>==01090)
 dispid=i_switch
elif (<dispid>==01091)
 dispid=01092
elif (<dispid>==01092)
 dispid=01091
elif (<dispid>==i_sconce_lit)
 dispid=09fc
elif (<dispid>==09fc)
 dispid=i_sconce_lit
elif (<dispid>==i_sconce2_lit)
 dispid=i_sconce2_empty
elif (<dispid>==i_sconce2_empty)
 dispid=i_sconce2_lit
elif (<dispid>==i_torch_wall_lit)
 dispid=i_torch_wall_empty
elif (<dispid>==i_torch_wall_empty)
 dispid=i_torch_wall_lit
elif (<dispid>==i_torch_wall2_lit)
 dispid=i_torch_wall2_empty
elif (<dispid>==i_torch_wall2_empty)
 dispid=i_torch_wall2_lit
endif
update

[FUNCTION f_leverDoor]
link.timerd=1
if (<morex>)
 timer=<more2>
 link.say=@026 click...
else
 link.say=@026 TRANK!
endif
if (<link.link>!=04fffffff) && (<link.link>!=<UID>)
 link.link.timerd=1
 if (<morex>)
  link.link.say=@026 click...
 else
  link.link.say=@026 TRANK!
 endif
endif

[FUNCTION f_leverCont]
if (<morex>)
 link.type=t_container
 timer=<more2>
 link.say=@026 click...
else
 link.type=t_container_locked
 link.say=@026 TRANK!
endif
if (<link.link>!=04fffffff) && (<link.link>!=<UID>)
 if (<morex>)
  link.type=t_container
  link.link.say=@026 click...
 else
  link.type=t_container_locked
  link.link.say=@026 TRANK!
 endif
endif

// ---------# ITEMDEFS #---------//




[ITEMDEF i_alavanca_dungeon_0]
id=i_lever
name=alavanca
type=t_alavanca
CATEGORY=MyT - Quest Items
SUBSECTION=Alavancas
DESCRIPTION=Alavanca para dungeons

on=@create
ATTR=08010
MORE2=30

[ITEMDEF i_alavanca_dungeon_1]
id=01093
name=alavanca
type=t_alavanca
CATEGORY=MyT - Quest Items
SUBSECTION=Alavancas
DESCRIPTION=Alavanca para dungeons

on=@create
ATTR=08010
MORE2=30

[ITEMDEF i_alavanca_dungeon_2]
id=i_switch
name=alavanca
type=t_alavanca
CATEGORY=MyT - Quest Items
SUBSECTION=Alavancas
DESCRIPTION=Alavanca para dungeons

on=@create
ATTR=08010
MORE2=30

[ITEMDEF i_alavanca_dungeon_3]
id=01091
name=alavanca
type=t_alavanca
CATEGORY=MyT - Quest Items
SUBSECTION=Alavancas
DESCRIPTION=Alavanca para dungeons

on=@create
ATTR=08010
MORE2=30

[ITEMDEF i_alavanca_dungeon_4]
id=i_sconce_lit
name=vela
type=t_alavanca
CATEGORY=MyT - Quest Items
SUBSECTION=Alavancas
DESCRIPTION=Vela alavanca

on=@create
ATTR=08010
MORE2=30

[ITEMDEF i_alavanca_dungeon_5]
id=i_sconce2_lit
name=vela
type=t_alavanca
CATEGORY=MyT - Quest Items
SUBSECTION=Alavancas
DESCRIPTION=Vela alavanca

on=@create
ATTR=08010
MORE2=30

[ITEMDEF i_alavanca_dungeon_6]
id=i_torch_wall_lit
name=tocha
type=t_alavanca
CATEGORY=MyT - Quest Items
SUBSECTION=Alavancas
DESCRIPTION=Tocha alavanca

on=@create
ATTR=08010
MORE2=30

[ITEMDEF i_alavanca_dungeon_7]
id=i_torch_wall2_lit
name=tocha
type=t_alavanca
CATEGORY=MyT - Quest Items
SUBSECTION=Alavancas
DESCRIPTION=Tocha alavanca

on=@create
ATTR=08010
MORE2=30


   //////////////////////////////////////////////
  //		BARREIRAS PARA DUNGEONS       //
 //////////////////////////////////////////////



// ---------# TYPEDEFS #---------//



[TYPEDEF t_poison_barrier]
on=@step
if (!<morex>) && (!<src.npc>) && (!<src.gm>) && !(<src.flags>&statf_dead)
 src.f_retreat
 src.damage {32 42} 010 <uid>
 src.veneno 66
 src.stun
 sfx 0117
endif
return 1

[TYPEDEF t_fire_barrier]
on=@step
if (!<morex>) && (!<src.npc>) && (!<src.gm>) && !(<src.flags>&statf_dead)
 src.f_retreat
 src.damage {32 42} 010 <uid>
 src.stun
 sfx 015e
endif
return 1

[TYPEDEF t_energy_barrier]
on=@step
if (!<morex>) && (!<src.npc>) && (!<src.gm>) && !(<src.flags>&statf_dead)
 src.f_retreat
 src.damage {22 32} 010 <uid>
 src.stun <eval {20 55}>
 sfx 0235
endif
return 1




// ---------# FUNCTIONS #---------//





[FUNCTION f_turnFireBarrier]
timer = <more2>
link._f_turnFireBarrier
return 1

[FUNCTION _f_turnFireBarrier]
if (<type>!=t_fire_barrier)
 return 1
endif
if (!<morex>)
 morex=1
 tag.odisp=<dispid>
 dispid=i_fx_smoke_small
else
 morex=0
 dispid=0<tag.odisp>
endif
update
if (<link>!=04fffffff)  && (<link.morex>!=<morex>) && (<link.type>==<type>)
 link._f_turnFireBarrier
else
 sfx 054
endif

[FUNCTION f_turnPoisonBarrier]
timer = <more2>
link._f_turnPoisonBarrier
return 1

[FUNCTION _f_turnPoisonBarrier]
if (<type>!=t_poison_barrier)
 return 1
endif
if (!<morex>)
 morex=1
 tag.odisp=<dispid>
 dispid=i_fx_smoke_small
 color=colors_green
else
 morex=0
 dispid=0<tag.odisp>
 color=0
endif
update
if (<link>!=04fffffff)  && (<link.morex>!=<morex>) && (<link.type>==<type>)
 link._f_turnPoisonBarrier
else
 sfx 011a
endif




[FUNCTION f_turnEnergyBarrier]
timer = <more2>
link._f_turnEnergyBarrier
return 1

[FUNCTION _f_turnEnergyBarrier]
if (<type>!=t_energy_barrier)
 return 1
endif
if (!<morex>)
 morex=1
 tag.odisp=<dispid>
 dispid=i_fx_sparkle_2
else
 morex=0
 dispid=0<tag.odisp>
endif
update
if (<link>!=04fffffff)  && (<link.morex>!=<morex>) && (<link.type>==<type>)
 link._f_turnEnergyBarrier
else
 sfx 0231
endif





[FUNCTION f_retreat]
 if (<dir>==2)
  p=<eval <p.x>-1>,<p.y>,<p.z>
  update 
 elif (<dir>==3)
  p=<eval <p.x>-1>,<eval <p.y>-1>,<p.z>
  update
 elif (<dir>==7)
  p=<eval <p.x>+1>,<eval <p.y>+1>,<p.z>
  update
 elif (<dir>==6)
  p=<eval <p.x>+1>,<p.y>,<p.z>
  update 
 elif (<dir>==5)
  p=<eval <p.x>+1>,<eval <p.y>-1>,<p.z>
  update
 elif (<dir>==1)
  p=<eval <p.x>-1>,<eval <p.y>+1>,<p.z>
  update
 elif (<dir>==0)
  p=<p.x>,<eval <p.y>+1>,<p.z>
  update
 elif (<dir>==4)
  p=<p.x>,<eval <p.y>-1>,<p.z>
  update
 endif
 return 0
endif



// ---------# ITEMDEFS #---------//



[ITEMDEF i_fire_barrier_0]
ID=i_fx_field_fire
NAME=barreira de fogo
TYPE=t_fire_barrier

CATEGORY=MyT - Quest Items
SUBSECTION=Barreiras
DESCRIPTION=Barreira de fogo

[ITEMDEF i_fire_barrier_1]
ID=i_fx_field_fire_ns
NAME=barreira de fogo
TYPE=t_fire_barrier

CATEGORY=MyT - Quest Items
SUBSECTION=Barreiras
DESCRIPTION=Barreira de fogo


[ITEMDEF i_poison_barrier_0]
ID=03921
NAME=barreira de veneno
TYPE=t_poison_barrier

CATEGORY=MyT - Quest Items
SUBSECTION=Barreiras
DESCRIPTION=Barreira de veneno

[ITEMDEF i_poison_barrier_1]
ID=03915
NAME=barreira de veneno
TYPE=t_poison_barrier

CATEGORY=MyT - Quest Items
SUBSECTION=Barreiras
DESCRIPTION=Barreira de veneno

[ITEMDEF i_energy_barrier_0]
ID=i_fx_field_paralyze
NAME=barreira de energia
TYPE=t_energy_barrier

CATEGORY=MyT - Quest Items
SUBSECTION=Barreiras
DESCRIPTION=Barreira de energia

[ITEMDEF i_energy_barrier_1]
ID=03979
NAME=barreira de energia
TYPE=t_energy_barrier

CATEGORY=MyT - Quest Items
SUBSECTION=Barreiras
DESCRIPTION=Barreira de energia


   //////////////////////////////////////////////
  //	          TESOUROS GUARDADOS          //
 //////////////////////////////////////////////


// -------# TYPEDEFS #-------- //

[TYPEDEF t_guarded_container]
on=@dclick
obj=<more2>
if (<obj.npc>)
 src.sysmessagered <obj.name> esta guardando este objeto.
else
 if (0<src.tag.fRP> < -1)
  src.sysmessage=@037 Seus pontos de RP estao baixos. Tesouro bloqueado.
  return 1
 endif
 type=t_container
 emoteyellow destrancou
 src.sfx 01ff
 src.dclick <uid>
endif
return 1

// -------# ITEMDEFS #-------- //

//Não pode usar link. Se usa, os dois objetos somem durante o save por BAD LINK.
[ITEMDEF i_trig_guarded_cont]
ID=i_rune_glowing_1
NAME=gatilho de guarda de container
TYPE=t_script

CATEGORY=MyT - Quest Items
SUBSECTION=Gatilhos
DESCRIPTION=guarda de container

on=@create
attr=08080

on=@dclick
if (<src.isgm>)
 TARGET Selecione o container a guardar:
 return 1
endif

on=@Targon_Item
if (<src.targ.type>==t_container)
 link=<src.targ.UID>
 link.type=t_guarded_container
 src.sysmessagered linkado em <link.name>
 src.sysmessagered Assegure-se que o guarda pise no gatilho.
else
 src.sysmessage Apenas containeres abertos.
endif
return 1

on=@step
if (<src.npc>)
 obj=<link.more2>
 if (!<obj.npc>)
  link.more2=<src.uid>
  link.empty
  link.type t_guarded_container
  if (<link.more1>)
   serv.newitem=<link.more1>
  else
   serv.newitem=i_loot_quest_pobre
  endif
  new.cont=<link>
 endif
endif
return 1

[ITEMDEF i_trig_guarded_door]
ID=i_rune_glowing_2
NAME=gatilho de guarda de porta
TYPE=t_script

CATEGORY=MyT - Quest Items
SUBSECTION=Gatilhos
DESCRIPTION=guarda de porta

on=@create
attr=08080

on=@dclick
if (<src.isgm>)
 TARGET Selecione a porta a guardar:
 return 1
endif

on=@Targon_Item
if (<src.targ.type>==t_door) || (<src.targ.type>==t_door_locked)
 link=<src.targ.UID>
 link.type=t_door_locked
 src.sysmessagered linkado em <link.name>
 src.sysmessagered Assegure-se que o guarda pise no gatilho.
else
 src.sysmessage Apenas portas.
endif
return 1

on=@step
if (<src.npc>)
 obj=<link.TAG0.GUARD>
 if (!<obj.npc>)
  link.TAG.GUARD=<src.uid>
  link.type t_door_locked
  serv.newitem i_key_quest
  new.link=<link>
  new.cont=<src.uid>
 endif
endif
return 1

[ITEMDEF i_key_quest]
ID=01010
NAME=chave
TYPE=t_script

CATEGORY=MyT - Quest Items
SUBSECTION=Gatilhos
DESCRIPTION=chave de quest

on=@dclick
TARGET Que porta você gostaria de destrancar?
return 1

on=@targon_item
if (<link>==<src.targ.uid>) && (<src.targ.type>==t_door_locked)
 if (<src.targ.distance> < 2)
  src.targ.type t_door
  src.targ.dclick
  src.targ.type t_door_locked
  src.targ.timer 30
  remove
 else
  src.sysmessage Chegue mais perto...
 endif
else
 src.sysmessage Voce nao pode destrancar <src.targ.name>.
endif
return 1

[ITEMDEF i_respawn_loot]
//Respawn de loot de containers em geral
ID=01ea7
NAME=respawn de loot
TYPE=t_script
CATEGORY=MyT - Quest Items
SUBSECTION=Gatilhos
DESCRIPTION=respawn de loot

on=@Create
ATTR=8080
COLOR=0489
tag.tesouro=i_chest_dungeon_1
MORE2=259200

on=@timer
link.empty
serv.newitem <tag.tesouro>
new.cont=<link>
timer=<more2>
return 1

on=@dclick
if (<link>!=04fffffff)
 timerd 1
 src.sysmessage Tesouro reposto.
else
 src.cTAG.respawn_loot=<uid>
 src.menu m_spawn_loot_type
endif
return 1


// -------# MENUS #-------- //

[MENU m_spawn_loot_type]
(MyT)Que loot aparecera no container?(MyT)
on=0 Unico item para cura (bandagem, pocao)
src.cTAG.respawn_loot_type=i_cura
src.menu m_spawn_loot_time

on=0 Unico item pobre
src.cTAG.respawn_loot_type=i_chest_dungeon_1
src.menu m_spawn_loot_time

on=0 Unico item medio
src.cTAG.respawn_loot_type=i_chest_dungeon_2
src.menu m_spawn_loot_time

on=0 Loot pobre
src.cTAG.respawn_loot_type=i_loot_quest_pobre
src.menu m_spawn_loot_time

on=0 Loot regular
src.cTAG.respawn_loot_type=i_loot_quest_regular
src.menu m_spawn_loot_time

on=0 Loot rico
src.cTAG.respawn_loot_type=i_loot_quest_rico
src.menu m_spawn_loot_time

on=0 Loot milhonario
src.cTAG.respawn_loot_type=i_loot_quest_milhonario
src.menu m_spawn_loot_time

[MENU m_spawn_loot_time]
(MyT)Quanto tempo para respawn?(MyT)
on=0 1 hora
src.cTAG.respawn_loot_time=3600
sysmessage Selecione o container
targetf f_spawn_loot_target

on=0 3 horas
src.cTAG.respawn_loot_time=<eval 3600*3>
sysmessage Selecione o container
targetf f_spawn_loot_target

on=0 1 dia e 6 horas
src.cTAG.respawn_loot_time=111600
sysmessage Selecione o container
targetf f_spawn_loot_target

on=0 2 dias e 6 horas
src.cTAG.respawn_loot_time=201600
sysmessage Selecione o container
targetf f_spawn_loot_target

on=0 3 dias e 6 horas
src.cTAG.respawn_loot_time=291600
sysmessage Selecione o container
targetf f_spawn_loot_target

on=0 5 dias e 6 horas
src.cTAG.respawn_loot_time=471600
sysmessage Selecione o container
targetf f_spawn_loot_target

on=0 7 dias e 6 horas
src.cTAG.respawn_loot_time=651600
sysmessage Selecione o container
targetf f_spawn_loot_target

on=0 10 dias e 6 horas
src.cTAG.respawn_loot_time=921600
sysmessage Selecione o container
targetf f_spawn_loot_target


// -------# FUNCTIONS #-------- //

[FUNCTION f_spawn_loot_target]
if (<argo.type>!=t_container)&&(<argo.type>!=t_container_locked)&&(<argo.type>!=t_guarded_container)
 sysmessage Alvo invalido. Selecione o alvo.
 targetf f_spawn_loot_target
 return 1
endif
OBJ=<src.cTAG.respawn_loot>
OBJ.link=<argo.uid>
OBJ.tag.tesouro=<src.cTAG.respawn_loot_type>
OBJ.more2=<src.cTAG.respawn_loot_time>
OBJ.timerd=1
Sysmessage Tesouro configurado com sucesso!
return 1

   //////////////////////////////////////////////
  //	       PESTE PARA REGIÕES ERMAS       //
 //////////////////////////////////////////////





[ITEMDEF i_mry_peste]
ID=0ED2
NAME=contraiu a peste
type=T_EQ_SCRIPT
LAYER=30

on=@create
attr=08010
more2=31
timer 1

on=@timer
if (0<var.no_peste>==1)
 cont.flags=<cont.flags>&~080
 cont.update
 remove
 return 1
endif
if (<cont.restest 2 i_mry_peste>)
 remove
 return 1
endif
if (<cont.flags>&statf_dead)
 timer = 30
 return 1
endif
if !(<cont.flags>&080)
 cont.flags=<cont.flags>|080
 cont.update
endif
more2=<more2>-1
if (<more2> <= 0) || (<more2> >= 40)
 cont.flags=<cont.flags>&~080
 cont.update
 cont.events -e_peste
 remove
 return 1
endif
if (<more2> <=5)
 dorand 4
  cont.f_vomita
  cont.emotegreen suando
  cont.f_dores
  cont.emotegreen palido
 enddo
 cont.f_moscas
 cont.sysmessagegreen A peste esta mais fraca.
 cont.f_stam_peste
 timer {120 180}
elif (<more2> <=10)
 dorand 4
  cont.f_vomita
  cont.emotegreen fedendo
  cont.f_dores
  cont.emotegreen palido
 enddo
 cont.f_stam_peste
 timer {60 120}
elif (<more2> <=20)
 dorand 4
  cont.f_vomita
  cont.emotegreen fedendo
  cont.emotegreen passando mal
  cont.f_moscas
 enddo
 cont.f_dores
 timer {30 60}
elif (<more2> <= 29)
 dorand 4
  cont.f_vomita
  cont.emotegreen fedendo
  cont.f_dores
  cont.emotegreen muito mal
 enddo
 timer {12 30}
elif (<more2> == 30)
 cont.emotegreen muito mal
 cont.sysmessagegreen A Peste esta te assolando.
 timer {12 30}
endif
cont.f_dano_peste <more2>
cont.f_alastra_peste
return 1

[FUNCTION f_peste]
newitem i_mry_peste
new.equip
events +e_peste

[function f_dores]
say=@021 Ohhh!
cry
sysmessagegreen Voce esta sentindo fortes dores!

[FUNCTION f_dano_peste]
if (<hits> > (<str>/2)) && (<argn> < <hits>)
 local.dano=(<argn>)
 damage <LOCAL.dano> dam_poison <UID>
endif

[FUNCTION f_stam_peste]
if (<stam> > (<dex>/2))
 stam = (<dex>/2)
endif

[TYPEDEF t_peste]
on=@step
if !(<src.restest 1 i_mry_peste>) && (!<src.isgm>)
 src.f_peste
endif

[FUNCTION f_alastra_peste]
newitem i_gold
new.p=<p>
new.color=01
new.name=alastrar a peste
new.attr=092
new.timer={60 120}
new.update
new.type t_peste
anim 32

[FUNCTION nopeste]
if (<argn>) && (0<argn>!=0)
 var.no_peste=0
 sysmessage=@024 Peste ativada.
 return 1
endif
if (<argn>) && (0<argn>==0)
 var.no_peste=1
 sysmessage=@024 Peste desativada.
 return 1
endif
if (0<argn>==0) && (0<var.no_peste>==0)
 var.no_peste=1
 sysmessage=@024 Peste desativada.
 return 1
else
 var.no_peste=0
 sysmessage=@024 Peste ativada.
 return 1
endif

[FUNCTION f_moscas]
sysmessagegreen Voce esta juntando moscas!
newitem i_moscas
new.p=<p>
new.timer 2

[ITEMDEF i_moscas]
id=i_bee_swarm
type=t_peste
name=moscas

on=@create
attr=08010
color=03de
more1=300
timer 2

on=@timer
if (0<var.no_peste>==1)
 remove
 return 1
endif
if (<more1>)
 movenear <uid> 3
 more1=<more1>-1
 timer={1 2}
else
 remove
endif
if ({1 280}==1)
 serv.newitem i_moscas
 new.p=<p>
 new.timerd 3
endif
return 1

on=@step
return 0

[EVENTS e_peste]
on=@login
if (0<var.no_peste>==1)
 flags=<flags>&~080
 update
 events -e_peste
 return 1
endif
f_moscas
return 0

on=@logout
f_moscas
return 0








   //////////////////////////////////////////////
  //  	   CÓDIGO PARA PORTAS/CONTAINERES     //
 //////////////////////////////////////////////

// Created 16/8/2007 09:46:06, with Gump Studio.
// Exported with with SphereExporter ver 1.2.
// Script for 0.55R*/0.56/0.57
[FUNCTION l_doorcode]
LOCAL.char06=5547
LOCAL.char07=5549
LOCAL.char08=5551
LOCAL.char09=5553
LOCAL.char0a=5555
LOCAL.char0b=5557

[DIALOG d_doorcode]
150,100
page 0
call l_doorcode
obj=<src.ctag.codded>
resizepic 1 6 9250 497 346
tilepic 30 30 <obj.dispiddec>
dtext 90 50 42 <QVAL <src.isgm> ? MODO GM! (Gerar codigo) : Decifre o codigo>

//desenha botões de caracteres
dorigin -60 256
LOCAL.cont=01
FOR X 06 0b
 button *80 - <LOCAL.char<local.x>> <eval <LOCAL.char<local.x>>+1> 1 0 <LOCAL.cont>
 LOCAL.cont += 1
ENDFOR

//Desenha fundo dos caracteres escolhidos
dorigin -80 128
FOR X 4
 gumppic *112 - 1417
ENDFOR

//Desenha caracteres escolhidos
dorigin -71 137
FOR X 4
 IF (<src.ctag.code<local.x>>)
  button *112 - <eval <src.ctag.code<local.x>>> <eval <src.ctag.code<local.x>>+1> 1 0 <LOCAL.cont>
 ELSE
  gumppic *112 - 5545 52
 ENDIF
 LOCAL.cont += 1
ENDFOR

//Já pode mostrar o okay?
IF (<src.ctag.iCode>==4)
 button 210 220 247 248 1 0 11
ENDIF

[DIALOG d_doorcode text]
Decifre o codigo

[DIALOG d_doorcode button]
ON=1,6
obj=<src.ctag.codded>
call l_doorcode
src.cTAG.iCode += 1
try src.cTAG.code<src.cTAG.iCode>=<LOCAL.char<hval <argn>+5>>
if (<src.ctag.iCode>!=5)
 obj.sfx 05b9
 src.timerf 1,dialog,d_doorcode
ELIF (<src.isgm>)
 for X 4
  try obj.tag.code<local.x>=<src.ctag.code<local.x>>
 endfor
 src.sysmessageblue Codigo configurado com sucesso.
 obj.sfx 05BA
ELSE
 FOR X 4
  IF (<src.ctag.code<local.x>>==<obj.tag.code<local.x>>)
   local.codesright += 1
  ENDIF
 ENDFOR
 IF (<local.codesright>==4)
  src.sysmessageblue Codigo correto!
  obj.sfx 05BA
  obj.type=<obj.morez>
  src.dclick <obj>
  obj.timerf 61,type,t_codded
 else
  src.sysmessagered Codigo incorreto.
  obj.sfx 05BB
 endif
endif

ON=7,10
obj=<src.ctag.codded>
try src.cTAG.code<src.cTAG.iCode>=0
src.cTAG.iCode -= 1
obj.sfx 05BA
src.dialog d_doorcode

ON=11
FOR X 4
 IF (<src.ctag.code<local.x>>==<obj.tag.code<local.x>>)
  local.codesright += 1
 ENDIF
ENDFOR
IF (<local.codesright>==4)
 src.sysmessageblue Codigo correto!
 obj.sfx 05BA
 obj.type=<obj.morez>
 src.dclick <obj>
 obj.timerf 61,type,t_codded
else
 src.sysmessagered Codigo incorreto.
 obj.sfx 05BB
endif

[TYPEDEF t_codded]
on=@dclick
src.ctag.codded=<UID>
src.cTAG.iCode=0
FOR X 4
 try src.cTAG.code<LOCAL.x>=0
enddo
src.dialog d_doorcode
return 1

[FUNCTION codigo]
if (!<isgm>)
 sysmessagered Entre em modo GM.
 return 1
endif
sysmessageyellow Seleciona o item a por o codigo:
targetf _codigo

[FUNCTION _codigo]
if (<argo.type>!=t_door) && (<argo.type>!=t_container)
 sysmessagered Somente containeres ou portas abertas.
 return 1
else
 argo.morez=<argo.type>
 argo.type=t_codded
 sysmessageyellow Selecione o codigo:
 dclick <argo>
endif





   //////////////////////////////////////////////
  //	       	   CHAVES-ESTATUA	      //
 //////////////////////////////////////////////


[ITEMDEF i_gerador_estatua]
ID=i_pedestal
TYPE=t_script
NAME=pedestal

CATEGORY=MyT - Quest Items
SUBSECTION=Chaves-estatua
DESCRIPTION=Pedestal (gerador)

on=@create
more2=60*60	//1 hora

on=@dclick
if (!<more1>)
 obj=<uid>
 src.menu m_chave_estatua
 attr=08010
 return 1
endif
f_gerador_estatua_reset
return 1

on=@timer
f_gerador_estatua_reset
return 1

[ITEMDEF i_chave_estatua]
ID=i_statue_stone_sm_3
TYPE=t_script
NAME=estatua
FLIP=0

CATEGORY=MyT - Quest Items
SUBSECTION=Chaves-estatua
DESCRIPTION=Chave

ON=@DROPON_CHAR
//Se nunca foi pego
if (<link.baseid>!=i_gerador_estatua)
 //Remove efeito de sparkle
 link.remove
 link=<more2>
 //Aciona timer do pedestal
 link.timer=<link.more2>
 src.sfx 05c9
endif

ON=@DROPON_ITEM
trigger=@DROPON_CHAR

ON=@DCLICK
src.sysmessageyellow Aonde deseja usar isso?
target
return 1

ON=@TARGON_ITEM
IF (<src.targ.baseid>==i_pedestal_fechadura) && (<src.targ.color>==<color>)
 p=<src.targ.p>		//posicionar
 nudgeup 8
 attr=08010
 tag.porta=<src.targ.link>	//Salvar porta
 src.targ.link.timerd 1		//abrir porta
 src.targ.link.timerf 1 timer,-1	//permanecer aberta 4eva
 update				//efeitinho
 EFFECT 2 i_fx_sparkle_2 0 16 0
 sfx 05C0
ELSE
 src.sysmessagered Nada acontece...
ENDIF
return 1

ON=@TARGON_CHAR
src.sysmessagered Nada acontece...
return 1


[ITEMDEF i_pedestal_fechadura]
ID=i_pedestal
TYPE=t_script
NAME=pedestal

CATEGORY=MyT - Quest Items
SUBSECTION=Chaves-estatua
DESCRIPTION=Pedestal (fechadura)

on=@dclick
if (!<more1>) && (<src.IsGM>)
 target Selecione a porta:
ELSE
 src.SYSMESSAGEYELLOW Voce pensa em uma forma de acionar o dispositivo.
ENDIF
return 1

on=@targon_item
if (<src.targ.type>==t_door)
 src.targ.type=t_door_locked
endif
if (<src.targ.type>!=t_door_locked)
 src.sysmessagered Selecione uma PORTA! SUA PORTA!
 return 1
endif
obj=<uid>
src.targ.link=<UID>
link=<src.targ.UID>
attr=08010
src.menu m_chave_estatua
return 1

[MENU m_chave_estatua]
Escolha uma estatua
ON=i_statue_stone_sm_3, @014
obj.more1=014
obj.f_gerador_estatua_reset

ON=i_statue_stone_sm_3, @036
obj.more1=036
obj.f_gerador_estatua_reset

ON=i_statue_stone_sm_3, @016A
obj.more1=016A
obj.f_gerador_estatua_reset

ON=i_statue_stone_sm_3, @0C8
obj.more1=0C8
obj.f_gerador_estatua_reset

ON=i_statue_stone_sm_3, @026
obj.more1=026
obj.f_gerador_estatua_reset


[FUNCTION f_gerador_estatua_reset]
IF (<baseid>==i_gerador_estatua)
 IF (<link.instances>)		//Se há estátua
  if (<link.tag0.porta>)	//Se a estátua foi usada
   obj=<link.tag.porta>
   obj.timerd 1			//Fechar a porta
   obj.timerf 1,timer,-1	//Manter fechada
  endif
  link.remove			//Remover estátua velha
 ENDIF
 serv.newitem i_chave_estatua	//Criar estátua nova
 new.color=<more1>		//Colorir de acordo com o código
 new.p=<p>			//Posicionar
 new.nudgeup 8
 new.attr=028
 new.timer=-1
 new.more2 <UID>		//Salvar quem gerou a estátua
 link=<new.uid>
 serv.newitem i_fx_sparkle_2	//efeitinho
 link.link=<new.uid>
 new.timerf 1,p,<p>
 new.timerf 2,update
ELIF (<baseid>==i_pedestal_fechadura)
 color=<more1>
 attr=028
 timer=-1
ENDIF
[EOF]
