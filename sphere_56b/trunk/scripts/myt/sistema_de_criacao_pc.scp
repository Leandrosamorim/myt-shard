//*****************************************************************************
//*****************************************************************************
//
// SISTEMA DE CRIACAO DE PERSONAGEMS por
//	Galthar
//	Metus
//	UsMarine (vinicius.arroyo at gmail.com)
//
//*****************************************************************************
//*****************************************************************************
//TODO:
//
//TAGS usadas:
//tag.agr=5
//tag.mag=5
//src.tag.statsPoints
//src.tag.classe
//src.tag.raca
//src.tag.hab1=Archery
//src.tag.hab2=Tactics
//src.tag.hab3=Tracking
//src.tag.hab4=Camping
//src.tag.hab5=Camping
//src.tag.hab6=Camping
//src.tag.habesp=Camping
//src.tag.pontosdealma
//src.tag.name
//src.tag.mp
//src.tag.mc
//src.tag.str
//src.tag.dex
//src.tag.int
//-src.tag.fixstats
//src.tag.drowLowLightBonus
//*****************************************************************************

//*****************************************************************************
//*****************************************************************************
// DEFNAMEs
//*****************************************************************************
//*****************************************************************************

[DEFNAME charCreation]
//Tempo que uma conta deve ser usada entes de polher escolher a raça Drow.
//Deve ser posto em décimos de segundo.
timerDrow=10*60*60*25

//Tempo que uma conta deve ser usada entes de polher escolher a raça Orc.
//Deve ser posto em décimos de segundo.
timerOrc=10*60*60*25

//Coloque 1 para acionar a opção de raças avançadas ou 0 para desativar.
CanAdvRace=0

//Valor inicial de skill primaria, secundaria e terciaria.
char_primarySkill=500
char_secondarySkill=200
char_tertiarySkill=50

//Valor inicial de moedas de cobre
char_initialCopperCoins=300

//Coordenadas de inicio
//char_startPnt_Humano=1,1,0
//char_startPnt_Elfo=1,1,0
//char_startPnt_Anao=1,1,0
//char_startPnt_Orc=1,1,0
//char_startPnt_Drow=1,1,0

//*****************************************************************************
//*****************************************************************************
// FUNCTIONs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// char_assembleGump
//*****************************************************************************
//cria parte padrão para todos os gumps
[FUNCTION char_assembleGump]
noclose
nomove
PAGE 0
resizepic 50 74 3600 640 460
gumppic 237 99 1143
gumppic 121 95 9000
gumppic 550 93 9000
gumppic 0 0 10440
gumppic 658 0 10441

//*****************************************************************************
// char_setAsPrimarySkill( skillName )
//*****************************************************************************
//inicia skill desejada como primária
[FUNCTION char_setAsPrimarySkill]
tryp 0 src.<args> char_primarySkill

//*****************************************************************************
// char_setAsSecondarySkill( skillName )
//*****************************************************************************
//inicia skill desejada como secundária
[FUNCTION char_setAsSecondarySkill]
tryp 0 src.<args> char_secondarySkill

//*****************************************************************************
// char_setAsTertiarySkill( skillName )
//*****************************************************************************
//inicia skill desejada como terciária
[FUNCTION char_setAsTertiarySkill]
tryp 0 src.<args> char_tertiarySkill

//*****************************************************************************
// char_createNew
//*****************************************************************************
//chamado ON=@login pela skillclass 0
//inicia o processo de criação do personagem
[FUNCTION char_createNew]
//zera todas as skills,deixa com um minimo de stats e sem equipamento
src.allskills=0
src.str=10
src.dex=10
src.int=10
src.findlayer(layer_half_apron).remove
src.findlayer(layer_pack).remove
src.findlayer(layer_hand1).remove
src.findlayer(layer_hand2).remove
src.findlayer(layer_hair).remove

//equipa com o basico e comum para todas as racas e classes
src.newitem=i_backpack
src.act.equip

src.newitem=i_adaga
src.act.equip

src.newitem=i_candle
src.act.equip

src.newitem=i_coin_copper
src.act.amount=char_initialCopperCoins
src.act.bounce

//inicia tela de criacao de novo char
src.dialog d_char_creation
return 1

//*****************************************************************************
// char_setSkills
//*****************************************************************************
//configura skill de acordo com a classe escolhida
[FUNCTION char_setSkills]

//limpa habilidades primarias
src.tag.hab1=
src.tag.hab2=
src.tag.hab3=
src.tag.hab4=
src.tag.hab5=
src.tag.hab6=

//inicia? skills de luta
Archery 15
Fencing 15
Macefighting 15
Parrying 15
Swordsmanship 15
Tactics 15
Wrestling 15
 
if (strmatch(<src.tag.classe>,arqueiro))
 char_setAsPrimarySkill Archery
 char_setAsPrimarySkill Tactics
 char_setAsPrimarySkill Tracking
 char_setAsPrimarySkill Camping   

 char_setAsSecondarySkill Anatomy
 char_setAsSecondarySkill AnimalLore
 char_setAsSecondarySkill Healing
 char_setAsSecondarySkill Swordsmanship
 char_setAsSecondarySkill Taming
 char_setAsSecondarySkill Veterinary
 char_setAsSecondarySkill Carpentry

 char_setAsTertiarySkill Bowcraft
 char_setAsTertiarySkill Cooking
 char_setAsTertiarySkill DetectingHidden
 char_setAsTertiarySkill Fishing
 char_setAsTertiarySkill Hiding
 char_setAsTertiarySkill SpiritSpeak 
 char_setAsTertiarySkill Wrestling

 src.tag.hab1=Archery
 src.tag.hab2=Tactics
 src.tag.hab3=Tracking
 src.tag.hab4=Camping

 return 1
endif
if (strmatch(<src.tag.classe>,guerreiro))
 char_setAsPrimarySkill Fencing
 char_setAsPrimarySkill Macefighting
 char_setAsPrimarySkill Parrying
 char_setAsPrimarySkill Swordsmanship
 char_setAsPrimarySkill Tactics
 char_setAsPrimarySkill Wrestling

 char_setAsSecondarySkill Anatomy
 char_setAsSecondarySkill Armslore
 char_setAsSecondarySkill Forensics
 char_setAsSecondarySkill MagicResistance

 char_setAsTertiarySkill Blacksmithing
 char_setAsTertiarySkill Camping
 char_setAsTertiarySkill Cooking
 char_setAsTertiarySkill Fishing
 char_setAsTertiarySkill Hiding
 char_setAsTertiarySkill Lumberjacking
 char_setAsTertiarySkill Mining
 char_setAsTertiarySkill SpiritSpeak 
 char_setAsTertiarySkill Lumberjacking

 src.tag.hab1=Fencing
 src.tag.hab2=Macefighting
 src.tag.hab3=Parrying
 src.tag.hab4=Swordsmanship
 src.tag.hab5=Tactics
 src.tag.hab6=Wrestling
 
 return 1
endif 

if (strmatch(<src.tag.classe>,mago))
 char_setAsPrimarySkill Alchemy
 char_setAsPrimarySkill EvaluatingIntel
 char_setAsPrimarySkill Inscription
 char_setAsPrimarySkill Magery
 char_setAsPrimarySkill Meditation
 char_setAsPrimarySkill MagicResistance
 char_setAsPrimarySkill Healing

 char_setAsSecondarySkill Anatomy
 char_setAsSecondarySkill Cartography
 char_setAsSecondarySkill Macefighting
 char_setAsSecondarySkill SpiritSpeak

 char_setAsTertiarySkill Camping
 char_setAsTertiarySkill Cooking
 char_setAsTertiarySkill Fishing
 char_setAsTertiarySkill Hiding
 char_setAsTertiarySkill Lumberjacking
 char_setAsTertiarySkill Mining
 char_setAsTertiarySkill Tactics
 char_setAsTertiarySkill Wrestling

 src.tag.hab1=Alchemy
 src.tag.hab2=EvalutingIntel
 src.tag.hab3=Inscription
 src.tag.hab4=Magery
 src.tag.hab5=Meditation
 src.tag.hab6=MagicResistance
 
 return 1
endif 

if (strmatch(<src.tag.classe>,ladrao))
 char_setAsPrimarySkill Fencing
 char_setAsPrimarySkill Hiding
 char_setAsPrimarySkill Lockpicking
 char_setAsPrimarySkill Snooping
 char_setAsPrimarySkill Stealing
 char_setAsPrimarySkill Stealth


 char_setAsSecondarySkill DetectingHidden
 char_setAsSecondarySkill Poisoning
 char_setAsSecondarySkill RemoveTrap
 char_setAsSecondarySkill Tactics

 char_setAsTertiarySkill Camping
 char_setAsTertiarySkill Cartography
 char_setAsTertiarySkill Cooking
 char_setAsTertiarySkill Fishing
 char_setAsTertiarySkill Lumberjacking
 char_setAsTertiarySkill Mining
 char_setAsTertiarySkill SpiritSpeak 
 char_setAsTertiarySkill Wrestling

 src.tag.hab1=Fencing
 src.tag.hab2=Hiding
 src.tag.hab3=Lockpicking
 src.tag.hab4=Snooping
 src.tag.hab5=Stealing
 src.tag.hab6=Stealth
 
 return 1
endif 



if (strmatch(<src.tag.classe>,bardo))
 char_setAsPrimarySkill Enticement
 char_setAsPrimarySkill Musicianship
 char_setAsPrimarySkill Peacemaking
 char_setAsPrimarySkill Provocation

 char_setAsSecondarySkill Cartography
 char_setAsSecondarySkill Fencing
 char_setAsSecondarySkill Hiding
 char_setAsSecondarySkill Lockpicking 
 char_setAsSecondarySkill Stealth
 char_setAsSecondarySkill Tactics

 char_setAsTertiarySkill Anatomy
 char_setAsTertiarySkill Camping
 char_setAsTertiarySkill Cooking
 char_setAsTertiarySkill Fishing
 char_setAsTertiarySkill Lumberjacking
 char_setAsTertiarySkill Mining
 char_setAsTertiarySkill SpiritSpeak 
 char_setAsTertiarySkill Wrestling

 src.tag.hab1=Enticement
 src.tag.hab2=Musicianship
 src.tag.hab3=Peacemaking
 src.tag.hab4=Provocation
  
 return 1
endif 

if (strmatch(<src.tag.classe>,artesao))
 char_setAsPrimarySkill Cartography
 char_setAsPrimarySkill Cooking
 char_setAsPrimarySkill ItemId
 char_setAsPrimarySkill Tailoring
 char_setAsPrimarySkill TasteId
 char_setAsPrimarySkill Tinkering

 char_setAsSecondarySkill Armslore
 char_setAsSecondarySkill Fishing
 char_setAsSecondarySkill Lumberjacking
 char_setAsSecondarySkill Mining

 char_setAsTertiarySkill Blacksmithing
 char_setAsTertiarySkill Bowcraft
 char_setAsTertiarySkill Camping
 char_setAsTertiarySkill Carpentry
 char_setAsTertiarySkill Hiding
 char_setAsTertiarySkill Lockpicking
 char_setAsTertiarySkill SpiritSpeak 
 char_setAsTertiarySkill Wrestling

 src.tag.hab1=Cartography
 src.tag.hab2=Cooking
 src.tag.hab3=ItemId
 src.tag.hab4=Tailoring
 src.tag.hab5=TasteId
 src.tag.hab6=Tinkering
 
 return 1
endif 


if (strmatch(<src.tag.classe>,lenhador))
 char_setAsPrimarySkill Bowcraft 
 char_setAsPrimarySkill Carpentry
 char_setAsPrimarySkill Lumberjacking
 char_setAsPrimarySkill Tracking


 char_setAsSecondarySkill Macefighting
 char_setAsSecondarySkill Archery
 char_setAsSecondarySkill Fishing
 char_setAsSecondarySkill Hiding
 char_setAsSecondarySkill Tactics
 char_setAsSecondarySkill Stealth


 char_setAsTertiarySkill Animallore
 char_setAsTertiarySkill Armslore
 char_setAsTertiarySkill Camping
 char_setAsTertiarySkill Cooking
 char_setAsTertiarySkill Mining
 char_setAsTertiarySkill SpiritSpeak 
 char_setAsTertiarySkill Swordsmanship
 char_setAsTertiarySkill Wrestling

 src.tag.hab1= Bowcraft
 src.tag.hab2= Carpentry
 src.tag.hab3= Lumberjacking
 src.tag.hab4= Tracking
  
 return 1
endif 

if (strmatch(<src.tag.classe>,ferreiro))
 char_setAsPrimarySkill Blacksmithing
 char_setAsPrimarySkill Mining
 char_setAsPrimarySkill Armslore
 char_setAsPrimarySkill Anatomy

 char_setAsSecondarySkill Macefighting
 char_setAsSecondarySkill Parrying
 char_setAsSecondarySkill ItemId
 char_setAsSecondarySkill Tinkering
 char_setAsSecondarySkill Forensics
 char_setAsSecondarySkill Lockpicking

 char_setAsTertiarySkill Camping
 char_setAsTertiarySkill Cooking
 char_setAsTertiarySkill Fishing
 char_setAsTertiarySkill Hiding
 char_setAsTertiarySkill Lumberjacking
 char_setAsTertiarySkill SpiritSpeak
 char_setAsTertiarySkill Tactics
 char_setAsTertiarySkill Wrestling
 char_setAsTertiarySkill Lumberjacking

 src.tag.hab1= Blacksmithing
 src.tag.hab2= Mining
 src.tag.hab3= Armslore
 src.tag.hab4= Anatomy

 return 1
endif 

//*****************************************************************************
// char_setStats
//*****************************************************************************
//configura stats de acordo com a classe escolhida
[FUNCTION char_setStats]

//configura base com valor da classe
if (strmatch(<src.tag.classe>,Arqueiro))
 src.str=67
 src.dex=86
 src.int=37
elif (strmatch(<src.tag.classe>,Guerreiro))
 src.str=86
 src.dex=67
 src.int=37
elif (strmatch(<src.tag.classe>,Mago))
 src.str=52
 src.dex=52
 src.int=86
elif (strmatch(<src.tag.classe>,Ladrao))
 src.str=52
 src.dex=86
 src.int=52
elif (strmatch(<src.tag.classe>,Bardo))
 src.str=57
 src.dex=57
 src.int=76
elif (strmatch(<src.tag.classe>,Artesao))
 src.str=57
 src.dex=57
 src.int=76
elif (strmatch(<src.tag.classe>,Lenhador))
 src.str=76
 src.dex=57
 src.int=57
elif (strmatch(<src.tag.classe>,Ferreiro))
 src.str=76
 src.dex=57
 src.int=57
endif

//pontos para distribuir nos stats
src.tag.statsPoints=10

//aplica bonus e penalidades de acordo com raca
if (strmatch(<src.tag.raca>,Anao))
 src.str=<src.str>+5
 src.dex=<src.dex>-5
 return 1
elif (strmatch(<src.tag.raca>,Elfo))
 src.str=<src.str>-5
 src.dex=<src.dex>+5
 return 1
elif (strmatch(<src.tag.raca>,Drow))
 src.str=<src.str>-10
 src.dex=<src.dex>+5
 src.int=<src.int>+5
 return 1
elif (strmatch(<src.tag.raca>,Orc))
 src.str=<src.str>+10
 src.int=<src.int>-10
 return 1
endif

//*****************************************************************************
// char_finishCreation
//*****************************************************************************
//finaliza a criacao do personagem
[FUNCTION char_finishCreation]

//adiciona eventos gerais
src.events=+e_sistema_de_morte
src.events=+e_sistema_de_fe
src.events=+e_recon
src.events=+e_evaluatingintel_new
src.events=+e_magic_resistance
src.events=+e_mytgeral
src.events=+e_combat
src.events=+e_dex_loss
src.events=+e_clima
//src.events=+e_parrying_new  // Problemnas
//src.events=+e_armor_absorv  // Problemnas

//adiciona eventos especificos de raca e classe
tryp 0 src.events=+e_<src.tag.raca>
tryp 0 src.events=+e_<src.tag.classe>

//troca o nome pela raca
src.tag.name=<src.name>
src.name=<src.tag.raca>
src.title=

//salva stats
src.tag.str=<src.str>
src.tag.dex=<src.dex>
src.tag.int=<src.int>

//configura outros sistemas
src.tag.pontosdealma=5
//src.newitem i_mry_fazitem
//src.act.equip


//prepara loot inicial baseado na melhor habilidade ou na classe escolhida
if (strmatch(<src.tag.habesp>, Cooking))
	char_lootCooking
elif (strmatch(<src.tag.habesp>, Tinkering))
	char_lootTinkering
elif (strmatch(<src.tag.habesp>, Tailoring))
	char_lootTailoring
elif (strmatch(<src.tag.habesp>, Swordsmanship))
	char_lootSwordsmanship
elif (strmatch(<src.tag.habesp>, Macefighting))
	char_lootMacefighting
elif (strmatch(<src.tag.habesp>, Fencing))
	char_lootFencing
elif (strmatch(<src.tag.habesp>, Parrying)) && (strmatch(<src.tag.classe>, guerreiro))
	char_lootParrying
elif (strmatch(<src.tag.habesp>, Wrestling)) && (strmatch(<src.tag.classe>, guerreiro))
	char_lootWrestling
elif (strmatch(<src.tag.habesp>, Tactics)) && (strmatch(<src.tag.classe>, guerreiro))
	char_lootTactics
elif (strmatch(<src.tag.habesp>, Magery))
	char_lootMagery
elif (strmatch(<src.tag.habesp>, Alchemy))
	char_lootAlchemy
elif (strmatch(<src.tag.habesp>, Inscription))
	char_lootInscription
elif (strmatch(<src.tag.habesp>, Healing))
	char_lootHealing
elif (strmatch(<src.tag.classe>, lenhador))
	char_lootLenhador
elif (strmatch(<src.tag.classe>, ferreiro))
 	char_lootFerreiro
elif (strmatch(<src.tag.classe>, ladrao))
	char_lootLadrao
elif (strmatch(<src.tag.classe>, bardo))
	char_lootBardo
elif (strmatch(<src.tag.classe>, arqueiro))
	char_lootArqueiro
elif (strmatch(<src.tag.classe>, mago))
	char_lootMago
elif (strmatch(<src.tag.classe>, artesao))
	char_lootArtesao
endif

//determinar medida do player
src.tag.mp=<eval ((<src.str>*6)/10)> //60% de str peito
src.tag.mc=<eval ((<src.str>*5)/10)> //50% de str cintura

// aplica mod de raça anã (80)
if (strmatch(<src.tag.raca>,Anao))
 src.tag.mp=<eval ((<src.tag.mp>*8)/10)>
 src.tag.mc=<eval ((<src.tag.mc>*8)/10)>
endif
// aplica mod de raça Orc (120%)
if (strmatch(<src.tag.raca>,Orc))
 src.tag.mp=<eval ((<src.tag.mp>*12)/10)>
 src.tag.mc=<eval ((<src.tag.mc>*12)/10)>
endif

//salva medidas nos respectivos layers
src.findlayer.layer_shirt.more2=<src.tag.mp>
src.findlayer.layer_tunic.more2=<src.tag.mp>
src.findlayer.layer_robe.more2=<src.tag.mp>
src.findlayer.layer_pants.more2=<src.tag.mc>
src.findlayer.layer_skirt.more2=<src.tag.mc>

//define skillclass
//if (strmatch(<src.tag.classe>,arqueiro))
// src.skillclass=1
//elif (strmatch(<src.tag.classe>,guerreiro))
// src.skillclass=2
//elif (strmatch(<src.tag.classe>,mago))
// src.skillclass=3
//elif (strmatch(<src.tag.classe>,ladrao))
// src.skillclass=4
//elif (strmatch(<src.tag.classe>,bardo))
// src.skillclass=5
//elif (strmatch(<src.tag.classe>,artesao))
// src.skillclass=6
//elif (strmatch(<src.tag.classe>,lenhador))
// src.skillclass=7
//elif (strmatch(<src.tag.classe>,ferreiro))
// src.skillclass=8
//endif

//configura caracteristicais raciais e transporta para a 'cidade natal'
if (strmatch(<src.tag.raca>,elfo))
	char_configElfo
elif (strmatch(<src.tag.raca>,drow))
	char_configDrow
elif (strmatch(<src.tag.raca>,orc))
	char_configOrc
elif (strmatch(<src.tag.raca>,anao))
	char_configAnao
elif (strmatch(<src.tag.raca>,humano))
	char_configHumano
endif

src.warnAndDisconnect

//*****************************************************************************
// char_configElfo
//*****************************************************************************
[FUNCTION char_configElfo]
	if (<src.findlayer(layer_hair).id> != 0203c) 
		src.newitem i_hair_long
		src.act.color  037 //<src.findlayer(layer_hair).color>
		src.act.equip
	endif
	if (<src.findlayer(layer_beard)> == 1)
		src.findlayer(layer_beard).remove
	endif
	src.color 083ea
	src.go 916,1056,0,0

//*****************************************************************************
// char_configDrow
//*****************************************************************************
[FUNCTION char_configDrow]
	if (<src.findlayer(layer_hair).id> != 0203c)
		src.newitem i_hair_long
 		src.act.color 0
 		src.act.equip
 	endif
 	if (<src.findlayer(layer_beard)> == 1)
 		src.findlayer(layer_beard).remove
 	endif
	src.color 0455
	src.tag.drowLowLightBonus=3
 	src.go 1084,1426,0,0

//*****************************************************************************
// char_configOrc
//*****************************************************************************
[FUNCTION char_configOrc]
	if (<src.findlayer(layer_helm).baseid> != i_head_orc)
		src.findlayer(layer_helm).remove
		src.newitem i_head_orc
		src.act.equip
	endif
	if (<src.findlayer(layer_hair)> == 1) || (<src.findlayer(layer_beard)> == 1)
		src.findlayer(layer_hair).remove
		src.findlayer(layer_beard).remove
	endif
	src.color 0597
	src.go 1084,1426,0,0
	
//*****************************************************************************
// char_configAnao
//*****************************************************************************
[FUNCTION char_configAnao]
	if (<src.findlayer(layer_hair).id> != 0203c)
		src.newitem i_hair_long
		src.act.equip
	endif
	if (<src.findlayer(layer_beard).id> != 0203e) && (<src.body> != 0191)
		src.newitem i_beard_long_med
		src.act.color=<src.findlayer(layer_hair).color>
		src.act.equip
	endif
	src.color 03f6
	src.go 916,1056,0,0
	
//*****************************************************************************
// char_configHumano
//*****************************************************************************
[FUNCTION char_configHumano]
	if (<src.findlayer(layer_hair).id> == 0)
 		src.newitem i_cabelo_careca
		src.act.color <src.findlayer(layer_beard).color>
		src.act.equip
	endif
	if (<src.findlayer(layer_beard).id> == 0) && (<src.body> != 0191)
 		src.newitem i_barba_careca
		src.act.color <src.findlayer(layer_hair).color>
		src.act.equip
	endif
	if (<src.findlayer(layer_beard).id> == 0) && (<src.findlayer(layer_hair).id> == 0) && (<src.body> != 0191)
 		src.newitem i_barba_careca
		src.act.color 0223
		src.act.equip
 		src.newitem i_cabelo_careca
		src.act.color 0223
		src.act.equip
	endif
	src.update
	src.go 916,1056,0,0

//*****************************************************************************
// char_lootCooking
//*****************************************************************************
[FUNCTION char_lootCooking]
 src.newitem i_panela
 src.act.bounce

 src.newitem i_frigideira
 src.act.bounce

 src.newitem i_vasilha_vazia
 src.act.bounce

 src.newitem i_batata
 src.amount=3
 src.act.bounce

 src.newitem i_ervilha
 src.amount=3
 src.act.bounce

//*****************************************************************************
// char_lootTinkering
//*****************************************************************************
[FUNCTION char_lootTinkering]
 src.newitem i_livro_projetos
 src.act.bounce

 src.newitem i_tinker_tools
 src.act.bounce

 src.newitem i_soprador_vidro
 src.act.bounce

 src.newitem i_areia
 src.act.amount=10
 src.act.bounce

 src.newitem i_cxtinker_vazia 
 src.act.bounce

 src.newitem {i_ar_proj_trefila i_ar_proj_lanterna i_ar_proj_balao i_ar_proj_sextante}
 src.act.amount=1
 src.act.bounce

 src.newitem {i_ar_proj_tubo i_ar_proj_destilador i_ar_proj_espiriteira i_ar_proj_vara_pesca}
 src.act.amount=1
 src.act.bounce

 src.newitem {i_ar_proj_chain i_ar_proj_ring i_ar_proj_term i_ar_proj_relogio i_ar_proj_misturador}
 src.act.amount=1
 src.act.bounce

 src.newitem {i_ar_proj_lanterna i_ar_proj_recipientes i_ar_proj_misturador i_ar_proj_destilador}
 src.act.amount=1
 src.act.bounce

//*****************************************************************************
// char_lootTailoring
//*****************************************************************************
[FUNCTION char_lootTailoring]
 src.newitem i_kit_costura
 src.act.bounce

 src.newitem i_fita
 src.act.bounce

 src.newitem i_thread
 src.act.amount=5
 src.act.color=colors_all
 src.act.bounce

 src.newitem i_thread
 src.act.amount=5
 src.act.color=colors_all
 src.act.bounce

 src.newitem i_thread
 src.act.amount=5
 src.act.color=colors_all
 src.act.bounce

 src.newitem i_tesoura
 src.act.bounce

 src.newitem i_cloth
 src.act.amount=3
 src.act.color=colors_all
 src.act.bounce

//*****************************************************************************
// char_lootSwordsmanship
//*****************************************************************************
[FUNCTION char_lootSwordsmanship]
 src.newitem i_espada_longa
 src.act.bounce

 src.newitem i_pescoco_couro
 src.act.bounce

 src.newitem i_peito_couro
 src.act.bounce

 src.newitem i_braco_couro
 src.act.bounce

 src.newitem i_cabeca_couro
 src.act.bounce

 src.newitem i_perna_couro
 src.act.bounce

 src.newitem i_luva_couro
 src.act.bounce

//*****************************************************************************
// char_lootMacefighting
//*****************************************************************************
[FUNCTION char_lootMacefighting]
 src.newitem i_maca
 src.act.bounce

 src.newitem i_pescoco_couro
 src.act.bounce

 src.newitem i_peito_couro
 src.act.bounce

 src.newitem i_braco_couro
 src.act.bounce

 src.newitem i_cabeca_couro
 src.act.bounce

 src.newitem i_perna_couro
 src.act.bounce

 src.newitem i_luva_couro
 src.act.bounce

//*****************************************************************************
// char_lootFencing
//*****************************************************************************
[FUNCTION char_lootFencing]
 src.newitem i_kryss
 src.act.bounce

 src.newitem i_pescoco_couro
 src.act.bounce

 src.newitem i_peito_couro
 src.act.bounce

 src.newitem i_braco_couro
 src.act.bounce

 src.newitem i_cabeca_couro
 src.act.bounce

 src.newitem i_perna_couro
 src.act.bounce

 src.newitem i_luva_couro
 src.act.bounce

//*****************************************************************************
// char_lootParrying
//*****************************************************************************
[FUNCTION char_lootParrying]
 src.newitem i_espada_curta
 src.act.bounce

 src.newitem i_escudo_madeira
 src.act.bounce

 src.newitem i_pescoco_couro
 src.act.bounce

 src.newitem i_peito_couro
 src.act.bounce

 src.newitem i_braco_couro
 src.act.bounce

 src.newitem i_cabeca_couro
 src.act.bounce

 src.newitem i_perna_couro
 src.act.bounce

 src.newitem i_luva_couro
 src.act.bounce
 
//*****************************************************************************
// char_lootWrestling
//*****************************************************************************
[FUNCTION char_lootWrestling]
 src.newitem i_elmo_aberto
 src.act.bounce

 src.newitem i_pescoco_couro
 src.act.bounce

 src.newitem i_peito_courom
 src.act.bounce

 src.newitem i_braco_couro
 src.act.bounce

 src.newitem i_cabeca_couro
 src.act.bounce

 src.newitem i_perna_couro
 src.act.bounce

 src.newitem i_luva_couro
 src.act.bounce
 
//*****************************************************************************
// char_lootTactics
//*****************************************************************************
[FUNCTION char_lootTactics]
 src.newitem i_espada_longa
 src.act.bounce

 src.newitem i_escudo_batalha
 src.act.bounce

 src.newitem i_braco_courom
 src.act.bounce

 src.newitem i_luva_couro
 src.act.bounce

//*****************************************************************************
// char_lootMagery
//*****************************************************************************
[FUNCTION char_lootMagery]
 src.newitem i_spellbook
 src.act.link=<src.uid>
 src.act.name=grimorio
 src.act.bounce

 src.newitem i_almofariz
 src.act.bounce

 src.newitem i_tubo
 src.act.amount=2
 src.act.bounce

 src.newitem i_bottle_empty
 src.act.amount=2
 src.act.bounce

 src.newitem i_r_perola_negra
 src.act.amount=2
 src.act.bounce

 src.newitem i_r_musgo_sangue
 src.act.amount=2
 src.act.bounce

 src.newitem i_r_alho
 src.act.amount=2
 src.act.bounce

 src.newitem i_r_ginseng
 src.act.amount=2
 src.act.bounce

 src.newitem i_r_raiz_de_mandrake
 src.act.amount=2
 src.act.bounce

 src.newitem i_r_nightshade
 src.act.amount=2
 src.act.bounce

 src.newitem i_r_cinza_sulfurosa
 src.act.amount=2
 src.act.bounce

 src.newitem i_r_teia_de_aranha
 src.act.amount=2
 src.act.bounce

//*****************************************************************************
// char_lootAlchemy
//*****************************************************************************
[FUNCTION char_lootAlchemy]
 src.newitem i_spellbook
 src.act.link=<src.uid>
 src.act.name=grimorio
 src.act.bounce

 src.newitem i_almofariz
 src.act.bounce

 src.newitem i_espiriteira
 src.act.bounce

 src.newitem i_balao
 src.act.bounce

 src.newitem i_tubo
 src.act.amount=5
 src.act.bounce

 src.newitem i_bottle_empty
 src.act.amount=5
 src.act.bounce

 src.newitem i_r_perola_negra
 src.act.amount=5
 src.act.bounce

 src.newitem i_r_musgo_sangue
 src.act.amount=5
 src.act.bounce

 src.newitem i_r_alho
 src.act.amount=5
 src.act.bounce

 src.newitem i_r_ginseng
 src.act.amount=5
 src.act.bounce

 src.newitem i_r_raiz_de_mandrake
 src.act.amount=5
 src.act.bounce

 src.newitem i_r_nightshade
 src.act.amount=5
 src.act.bounce

 src.newitem i_r_cinza_sulfurosa
 src.act.amount=5
 src.act.bounce


 src.newitem i_r_teia_de_aranha
 src.act.amount=5
 src.act.bounce
 
//*****************************************************************************
// char_lootInscription
//*****************************************************************************
[FUNCTION char_lootInscription]
 src.newitem i_spellbook
 src.act.link=<src.uid>
 src.act.name=grimorio
 src.act.bounce

 src.newitem i_book_sm
 src.act.bounce

 src.newitem i_book_sm_pub
 src.act.bounce

 src.newitem i_book_sm_pub
 src.act.bounce

 src.newitem i_tinta
 src.act.bounce

 src.newitem i_tinta
 src.act.bounce

 src.newitem i_documento_branco
 src.act.amount=10
 src.act.bounce
 
//*****************************************************************************
// char_lootHealing
//*****************************************************************************
[FUNCTION char_lootHealing]
 src.newitem i_bandage
 src.amount={10 20}
 src.act.bounce

 src.newitem bottle_orange
 src.act.amount=2
 src.act.bounce

 src.newitem bottle_yellow
 src.act.amount=2
 src.act.bounce

 src.newitem bottle_blue
 src.act.amount=2
 src.act.bounce

 src.newitem bottle_white
 src.act.amount=2
 src.act.bounce
 
//*****************************************************************************
// char_lootLenhador
//*****************************************************************************
[FUNCTION char_lootLenhador]
 src.newitem i_talhadeira
 src.act.bounce

 src.newitem i_martelo
 src.act.bounce

 src.newitem i_serra
 src.act.bounce

 src.newitem i_serrote
 src.act.bounce

 src.newitem i_raspadeira
 src.act.bounce

 src.newitem i_raspadeira_p
 src.act.bounce

 src.newitem i_pregos
 src.act.amount=100
 src.act.bounce

 src.newitem i_tabua
 src.act.amount= {5 10}
 src.act.bounce

 src.newitem i_alisar
 src.act.bounce

 src.newitem i_alisar_pequeno
 src.act.bounce

 src.newitem i_alisar_moldador
 src.act.bounce

 src.newitem i_racha
 src.act.bounce

 src.newitem i_machadinha
 src.act.bounce

 src.newitem i_cxcarpinteiro_vazia
 src.act.bounce

//*****************************************************************************
// char_lootFerreiro
//*****************************************************************************
[FUNCTION char_lootFerreiro]
 src.newitem i_trefila
 src.act.bounce
 
 src.newitem i_martelo_ferreiro 
 src.act.bounce

 src.newitem i_alicate 
 src.act.bounce

 src.newitem i_picareta 
 src.act.bounce

 src.newitem i_ingot_iron
 src.act.amount={10 15}
 src.act.bounce
 
//*****************************************************************************
// char_lootLadrao
//*****************************************************************************
[FUNCTION char_lootLadrao]
 src.newitem i_lockpick
 src.act.amount=5
 src.act.bounce

 src.newitem i_adaga
 src.act.bounce

 src.newitem i_bottle_black
 src.act.amount=5
 src.act.bounce

 src.newitem i_bottle_yellow
 src.act.amount=2
 src.act.bounce
 
//*****************************************************************************
// char_lootBardo
//*****************************************************************************
[FUNCTION char_lootBardo]
 src.newitem {i_lute i_lyre}
 src.act.bounce

 src.newitem i_book_sm
 src.act.bounce

 src.newitem i_tinta
 src.act.bounce

//*****************************************************************************
// char_lootArqueiro
//*****************************************************************************
[FUNCTION char_lootArqueiro]
 src.newitem i_bow
 src.act.more2=1
 src.act.bounce

 src.newitem i_thread
 src.act.amount=2
 src.act.bounce

 src.newitem i_TALHADEIRA
 src.act.bounce

 src.newitem i_machadinha
 src.act.bounce

 src.newitem i_racha
 src.act.bounce

 src.newitem i_arrow
 src.act.amount={20 40}
 src.act.bounce

 src.newitem i_pescoco_couro
 src.act.bounce

 src.newitem i_peito_couro
 src.act.bounce

 src.newitem i_braco_couro
 src.act.bounce

 src.newitem i_cabeca_couro
 src.act.bounce

 src.newitem i_perna_couro
 src.act.bounce

 src.newitem i_luva_couro
 src.act.bounce
 
//*****************************************************************************
// char_lootMago
//*****************************************************************************
[FUNCTION char_lootMago]
 src.newitem i_spellbook
 src.act.bounce

 src.newitem i_almofariz
 src.act.bounce

 src.newitem {i_bottle_black i_bottle_yellow  i_bottle_white  i_bottle_purple  i_bottle_orange i_bottle_blue i_bottle_green i_bottle_red}
 src.act.bounce

 src.newitem {i_bottle_black i_bottle_yellow  i_bottle_white  i_bottle_purple  i_bottle_orange i_bottle_blue i_bottle_green i_bottle_red}
 src.act.bounce

 src.newitem {i_bottle_black i_bottle_yellow  i_bottle_white  i_bottle_purple  i_bottle_orange i_bottle_blue i_bottle_green i_bottle_red}
 src.act.bounce

 src.newitem {i_bottle_black i_bottle_yellow  i_bottle_white  i_bottle_purple  i_bottle_orange i_bottle_blue i_bottle_green i_bottle_red}
 src.act.bounce

 src.newitem {i_bottle_black i_bottle_yellow  i_bottle_white  i_bottle_purple  i_bottle_orange i_bottle_blue i_bottle_green i_bottle_red}
 src.act.bounce

 src.newitem i_tubo
 src.act.amount=2
 src.act.update
 src.act.bounce

 src.newitem i_bottle_empty
 src.act.amount=2
 src.act.update
 src.act.bounce

 src.newitem i_r_perola_negra
 src.act.amount=2
 src.act.bounce

 src.newitem i_r_musgo_sangue
 src.act.amount=2
 src.act.bounce

 src.newitem i_r_alho
 src.act.amount=2
 src.act.bounce

 src.newitem i_r_ginseng
 src.act.amount=2
 src.act.bounce

 src.newitem i_r_raiz_de_mandrake
 src.act.amount=2
 src.act.bounce

 src.newitem i_r_nightshade
 src.act.amount=2
 src.act.bounce

 src.newitem i_r_cinza_sulfurosa
 src.act.amount=2
 src.act.bounce

 src.newitem i_r_teia_de_aranha
 src.act.amount=2
 src.act.bounce

 src.newitem i_documento_branco
 src.act.amount=3
 src.act.bounce

 src.newitem i_tinta
 src.act.bounce

//*****************************************************************************
// char_lootArtesao
//*****************************************************************************
[FUNCTION char_lootArtesao]
 src.newitem=i_coin_copper //cooper coins
 src.act.amount=100
 src.act.bounce

 src.newitem i_thread
 src.act.amount=5
 src.act.bounce

 src.newitem i_kit_costura
 src.act.amount={10 40}
 src.act.bounce

 src.newitem i_tesoura
 src.act.bounce

 src.newitem i_panela
 src.act.bounce

 src.newitem i_vasilha_vazia
 src.act.bounce

 src.newitem {i_batata i_ervilha i_chicken_leg_raw i_lamb_leg_raw i_fish_cut_raw i_eggs_fresh i_eggs_fresh i_eggs_fresh i_bird_raw}
 src.act.amount=6
 src.act.bounce

 src.newitem {i_batata i_ervilha i_chicken_leg_raw i_lamb_leg_raw i_fish_cut_raw i_eggs_fresh i_eggs_fresh i_eggs_fresh i_bird_raw}
 src.act.amount=6
 src.act.bounce
 
//*****************************************************************************
//*****************************************************************************
// DIALOGs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// d_char_creation
//*****************************************************************************
//inicia o processo de criacao do personagem mostrando a tela de apresentação
[DIALOG d_char_creation]
0, 0
char_assembleGump

text 82 76 1152 0

text 290 101 32 1
text 211 125 152 2
text 283 146 152 3
text 152 220 95 4
text 151 250 95 5
text 154 280 95 6
text 154 310 95 7
text 155 340 95 8
text 280 407 162 9
button 318 451 5033 5034 1 0 1
text 306 485 32 10

[DIALOG d_char_creation TEXT]

Mystical Tales Shard
Bem vindo ao sistema de criacao de personagens
do Mystical Tales Shard.
Atravez deste sistema, seu personagem sera recriado nos moldes
de nosso shard. Antes de prosseguir, tenha em mente que voce
esta aceitando as regras de nossa comunidade. Tenha certeza de
ter lido e entendido o funcionamento dos sistemas basicos e dos
sistemas que sao pertinentes a sua raca e classe.
Tenha um bom divertimento!
http;\\www.myt.com.br


[DIALOG d_char_creation BUTTON]
on=1
src.dialog d_char_creation_races

//*****************************************************************************
// d_char_creation_races
//*****************************************************************************
//selecao de raça
[DIALOG d_char_creation_races]
0, 0
char_assembleGump

button 140 230 2151 2152 1 0 1
gumppic 183 232 2445
button 140 270 2151 2152 1 0 2
button 140 310 2151 2152 1 0 3
button 140 350 2151 2152 1 0 4
button 140 390 2151 2152 1 0 5
gumppic 183 272 2445
gumppic 183 312 2445
gumppic 183 352 2445
gumppic 183 392 2445
text 290 101 32 0
text 307 133 152 1
text 194 232 95 2
text 194 272 95 3
text 194 312 95 4
text 194 352 95 5
text 194 392 95 6
text 108 468 95 7
//dtext 129 489 95 xxx
resizepic 353 213 3500 294 223
button 536 480 5033 5034 1 0 6

if (strmatch(<src.tag.raca>,Orc))
	LOCAL.desc=Orcs sao brutos e de habitos desagradaveis. Muito individualistas, so compartilhariam algo com seus semelhantes ainda que raramente. Detestam os Elfos mais do que tudo, mas mentem algumas relacoes com Humanos e anoes por interresse. Mortes serao consideradas PKiling.
elif (strmatch(<src.tag.raca>,Anao))
	LOCAL.desc=Os anoes sao uma raca especialmente orgulhosa e de personalidade muito forte. Altamente materialistas os anoes tem um facinio especial por ouro e pedras preciosas. Anoes tem fama por sua coragem e bravura, da qual tem muito orgulho.	Vivem pacificamente com os Humanos e toleram os Elfos.
elif (strmatch(<src.tag.raca>,Elfo))
	LOCAL.desc=Elfos se consideram superiores as outras racas. Sao amigos de Humanos e vivem com anoes, mas NUNCA se esquecam do sentimento de superioridade dos Elfos. O odio pelos Drows e explicito, em caso de encontro dicussoes sao inevitaveis. Contudo, pkilling nao sera tolerado.
elif (strmatch(<src.tag.raca>,Drow))
	LOCAL.desc=Drows sao os senhores do medo e das trapassas. Por motivo nenhum se misturariam com Elfos mas toleram a companhia de Humanos uma vez que sao fonte de renda garantida para suas transacoes e trapassas. Sao sempre sombrios e misteriosos. Nao ha como ler a mente de um Drow. 
else //if (strmatch(<src.tag.raca>,Humano)) PADRAO
	src.tag.raca=Humano
	LOCAL.desc=Humanos sao altamente versateis e se adaptam facilmente ao ambiente. Se integram facilmente, e tratam a todos como como iguais, o que lhes fornece uma imagem neutra. Alguns sao preconceituosos, mas quando sao, nao transparecem  isso se nao houverem outros por perto ou se estao em minoria.	
endif

dhtmlgump 377 230 250 185 0 1 <LOCAL.desc>
dtext 375 188 162 <src.tag.raca>

[DIALOG d_char_creation_races TEXT]
Mystical Tales Shard
Selecione sua raca
Humano
Anao
Elfo
Drow *
Orc *
* Racas avancadas. Somente com um mes de jogo e autorizacao.

[DIALOG d_char_creation_races BUTTON]
on=1	//humano
src.tag.raca=Humano
src.dialog d_char_creation_races
return 1

on=2	//anao
src.tag.raca=Anao
src.dialog d_char_creation_races
return 1

on=3	//elfo
src.tag.raca=Elfo
src.dialog d_char_creation_races
return 1

on=4	//drow
if (<eval 0<src.account.TOTALCONNECTTIME>> >=<eval 0(timerDrow)>) && ((CanAdvRace)==1) || (<src.isgm>==1)
	src.tag.raca=Drow
else
	f_sendMessage <SRC>,Indisponivel,SM_YELLOW
endif
src.dialog d_char_creation_races
return 1

on=5	//orc
if (<eval 0<src.account.TOTALCONNECTTIME>> >=<eval 0(timerOrc)>) && ((CanAdvRace)==1) || (<src.isgm>==1)
	src.tag.raca=Orc
else
	f_sendMessage <SRC>,Indisponivel,SM_YELLOW
endif
src.dialog d_char_creation_races
return 1

on=6	//continue
src.dialog d_char_creation_classes
return 1


//*****************************************************************************
// d_char_creation_classes
//*****************************************************************************
//selecao de classe
[DIALOG d_char_creation_classes]
0, 0
char_assembleGump
button 140 200 2151 2152 1 0 1
gumppic 183 202 2445
button 140 240 2151 2152 1 0 2
button 140 280 2151 2152 1 0 3
button 140 320 2151 2152 1 0 4
button 140 360 2151 2152 1 0 5
gumppic 183 242 2445
gumppic 183 282 2445
gumppic 183 322 2445
gumppic 183 362 2445
resizepic 353 213 3500 294 223
gumppic 183 402 2445
gumppic 183 442 2445
gumppic 183 482 2445
button 140 400 2151 2152 1 0 6
button 140 440 2151 2152 1 0 7
button 140 480 2151 2152 1 0 8
button 322 481 5035 5036 1 0 9
button 529 481 5033 5034 1 0 10
text 290 101 32 0
text 303 133 152 1
text 194 200 95 2
text 194 242 95 3
text 194 282 95 4
text 194 322 95 5
text 194 362 95 6
text 194 402 95 7
text 194 442 95 8
text 194 482 95 9

if (strmatch(<src.tag.classe>,ferreiro))
	LOCAL.desc=Das minas para a oficina, e entao para as minas denovo. Esse eh o dia-a-dia de um ferreiro. Servindo sempre aos guerreiros com suas armas e armaduras, fazendo os pregos e as ferramentas do carpinteiro e barras de metal e arames para o artesao. Sempre muito requisitados por todos, entre panelas e laminas, o ferreiro tambem constroi fama e fortuna.
	LOCAL.title=Ferreiro
elif (strmatch(<src.tag.classe>,guerreiro))
	LOCAL.desc=A classe mais procurada entre os aventureiros se satisfaz com uma arma adequada e uma boa armadura. Guerreiros frequentemente cacam para obter mercadorias para as demais classes e sao peritos em lutas. 
	LOCAL.title=Guerreiro
elif (strmatch(<src.tag.classe>,mago))
	LOCAL.desc=Ser um arcano significa ter nos gestos e nas palavras o poder sobre a vida, a morte e os materiais inanimados. Eles aceitam ficar horas sentados lendo enigamas e purificando suas substancias alquimicas para criar novas formas de viver, matar e construir o que nenhuma engenharia ja mais conseguiria.
	LOCAL.title=Mago
elif (strmatch(<src.tag.classe>,ladrao))
	LOCAL.desc=Os ladinos nao sao sempre salteadores. A pessoa que voce mais tem como sua amiga pode tranquilamente ser um sabichao tirando proveito de voce para os seus interesses. Eles tem habilidade com as palavras e usam as sombras com seu escudo. Nunca subjulgue um ladrao, pois ele pode aparecer subitamente nas suas costas, com uma adaca em seu percoco.
	LOCAL.title=Ladrao
elif (strmatch(<src.tag.classe>,bardo))
	LOCAL.desc=Boemios e de belas cancoes, sao os homens que espalham as noticias. Muitas aventureiros pagam para os bardos compor cancoes e versos sobre seus feitos, e executa-las em publico para lhes render fama. Os bardos, alem de bons musicos, conhecem as leis, a historia a geografia e ate os sentimentos de seu povo.
	LOCAL.title=Bardo
elif (strmatch(<src.tag.classe>,lenhador))
	LOCAL.desc=O homem do machado tem o tato apurado e o olho na lamina, sempre cortando e construindo. Se voce quer um objeto e ele eh feito de madeira, eh com o lenhador que voce deve falar. Construir arcos, moveis, flechas, ferramentas de madeira e materia prima para as outras classes eh o que tras a comida para a mesa de um lenhador.
	LOCAL.title=Lenhador
elif (strmatch(<src.tag.classe>,artesao))
	LOCAL.desc=Artistas natos, o artesao tem otimas habilidades manuais e pode manufaturar objetos diversos como maquinas, ferramentas, roupas e utensilios. Tem o dom do comercio e de ganhar dinheiro, colecionando novos projetos e novas ferramentas de trabalho. Muitas outras classes dependem diretamente do artesao.
	LOCAL.title=Artesao
else //if (strmatch(<src.tag.classe>,arqueiro)) PADRAO
	src.tag.classe=arqueiro
	LOCAL.desc=Arqueiros sao comumente a linha de tiro de uma equipe de aventureiros. Suas epecialidades, elem do arco, eh a de rastreador e campista. Muito versateis, sao ideais para uso em florestas.
	LOCAL.title=Arqueiro
endif

dhtmlgump 377 230 250 185 0 1 <LOCAL.desc>
dtext 375 188 162 <SRC.tag.raca> - <LOCAL.title>

[DIALOG d_char_creation_classes TEXT]
Mystical Tales Shard
Selecione sua classe
Arqueiro
Guerreiro
Mago
Ladrao
Bardo
Artesao
Lenhador
Ferreiro


[DIALOG d_char_creation_classes BUTTON]
on=1	//arqueiro
if (strmatch(<src.tag.raca>,Anao))
	f_sendMessage <SRC>,Classe nao permitida para essa raca.,SM_YELLOW
else
	src.tag.classe=arqueiro
endif
src.dialog d_char_creation_classes

on=2	//guerreiro
src.tag.classe=guerreiro
src.dialog d_char_creation_classes

on=3	//mago
if (strmatch(<src.tag.raca>,Anao))
	f_sendMessage <SRC>,Classe nao permitida para essa raca.,SM_YELLOW
else
	src.tag.classe=mago
endif
src.dialog d_char_creation_classes

on=4	//ladrao
src.tag.classe=ladrao
src.dialog d_char_creation_classes

on=5	//bardo
if (strmatch(<src.tag.raca>,Anao)) || (strmatch(<src.tag.raca>,Orc)) || (strmatch(<src.tag.raca>,Drow))
 	f_sendMessage <SRC>,Classe nao permitida para essa raca.,SM_YELLOW
 else
 	src.tag.classe=bardo
 endif
src.dialog d_char_creation_classes

on=6	//artesao
src.tag.classe=artesao
src.dialog d_char_creation_classes

on=7	//lenhador
if (strmatch(<src.tag.raca>,Anao)) || (strmatch(<src.tag.raca>,Drow))
 	f_sendMessage <SRC>,Classe nao permitida para essa raca.,SM_YELLOW
 else
 	src.tag.classe=lenhador
 endif
src.dialog d_char_creation_classes

on=8	//ferreiro
if (strmatch(<src.tag.raca>,Elfo))
 	f_sendMessage <SRC>,Classe nao permitida para essa raca.,SM_YELLOW
 else
 	src.tag.classe=ferreiro
 endif
src.dialog d_char_creation_classes

on=9	//voltar
src.dialog d_char_creation_races
return 1

on=10	//continue
src.char_setSkills
src.dialog d_char_creation_top_skill


//*****************************************************************************
// d_char_creation_top_skill
//*****************************************************************************
//selecao da melhor habilidade
[DIALOG d_char_creation_top_skill]
0, 0
char_assembleGump
text 290 101 32 0
text 267 133 152 1

resizepic 182 219 3500 165 135
resizepic 387 219 3500 165 135

button 140 230 2151 2152 1 0 1
text 205 233 95 2
button 140 310 2151 2152 1 0 2
text 205 313 95 3
button 568 230 2151 2152 1 0 3
text 423 233 95 4
button 568 310 2151 2152 1 0 4
text 423 313 95 5
if ( strlen( <src.tag.hab5> ) > 0 ) || ( strlen( <src.tag.hab6> ) > 0 )
	button 140 270 2151 2152 1 0 5
	text 205 273 95 6
	button 568 270 2151 2152 1 0 6
	text 423 273 95 7
endif

button 322 481 5035 5036 1 0 7	//voltar

text 176 419 1152 8
text 176 449 1152 9


[DIALOG d_char_creation_top_skill TEXT]
Mystical Tales Shard
Selecione sua melhor habilidade
<src.tag.hab1>
<src.tag.hab2>
<src.tag.hab3>
<src.tag.hab4>
<src.tag.hab5>
<src.tag.hab6>
Esta sera a pericia na qual voce ganhara 10.0 pontos
adicionais e definira seus itens iniciais.


[DIALOG d_char_creation_top_skill BUTTON]
on=1,6
try src.<src.tag.hab<argn>> =<src.<src.tag.hab<argn>>> + 100
src.tag.habesp=<src.tag.hab<argn>>
src.char_setStats
src.dialog d_char_creation_bonus
return 1

on=7
//voltar
src.dialog d_char_creation_classes
return 1


//*****************************************************************************
// d_char_creation_bonus
//*****************************************************************************
[DIALOG d_char_creation_bonus]
0, 0
noclose
char_assembleGump

text 290 101 32 0
text 267 133 152 1
button 270 230 2151 2152 1 0 1
button 270 270 2151 2152 1 0 2
button 270 310 2151 2152 1 0 3
button 322 481 5035 5036 1 0 4
resizepic 312 219 3500 165 135
text 335 233 1345 2
text 284 178 162 3
text 335 273 95 4
text 335 313 95 5
text 427 233 32 6
text 427 273 32 7
text 427 313 32 8
text 432 178 10 9
text 327 153 152 10

[DIALOG d_char_creation_bonus TEXT]
Mystical Tales Shard
Distribuas os pontos conforme
Forca
Pontos para gastar;
Destreza
Inteligencia
<src.str>
<src.dex>
<src.int>
<eval <src.tag.statsPoints>>
sua vontade


[DIALOG d_char_creation_bonus BUTTON]
on=1,3

if (<argn> == 1)
	src.str=<src.str>+1
elif (<argn> == 2)
	src.dex=<src.dex>+1
else
	src.int=<src.int>+1
endif

src.tag.statsPoints=<src.tag.statsPoints> - 1
if (<src.tag.statsPoints> == 0)
 src.char_finishCreation
else
 src.dialog=d_char_creation_bonus
endif

on=4	//voltar
src.char_setSkills
src.dialog d_char_creation_top_skill

[EOF]