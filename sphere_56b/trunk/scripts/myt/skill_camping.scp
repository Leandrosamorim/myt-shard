//Camping system v1.1
//Scripted by Rudenid for use on the Final Aegis shard.
//Sphere version: 55i.
//Replace the current campfire items with these... or just load this one after the sphereitem*.scp scripts.

[FUNCTION CAMPHEAL]
IF (<FLAGS> & statf_war) 
VAR.OLDACT=<ACT.UID>
ACT=<EVAL <VAR.CAMPFIREUID>>
ACT.TAG.HEALINTERVAL=-1
ACT.REGION.ALLCLIENTS SYSMESSAGE Um <NAME> perturbou o acampamento!
ACT.LINK.REMOVE
ACT.REMOVE
ACT=<EVAL <VAR.OLDACT>>
VAR.OLDACT=
ENDIF

IF (<food> >= 10 )
 IF (<HITS> < <STR>)
  HITS=<HITS>+<ARGN>+1
 ENDIF
 IF (<MANA> < <INT>)
   MANA=<MANA>+<ARGN>+2
 ENDIF
 IF (<STAM> < <DEX>)
  STAM=<STAM>+<ARGN>+3
 ENDIF
ELSE
  IF (<HITS> < <STR>)
    HITS=<HITS>+<ARGN>/2
  ENDIF
  IF (<MANA> < <INT>)
   MANA=<MANA>+<ARGN>/2
  ENDIF
  IF (<STAM> < <DEX>)
   STAM=<STAM>+<ARGN>/2
  ENDIF
ENDIF


[ITEMDEF i_multi_campfire]
// Good campfire
ID=i_campfire
TYPE=t_multi
MULTIREGION=-5,-5,5,5

ON=@Create
MOREZ=29


[ITEMDEF i_campfire_tracker]
ID=01ea7
TYPE=t_eq_script

ON=@Create
ATTR=attr_move_never|attr_invis

ON=@Timer
IF (<MORE2> < 1)
LINK.REMOVE
REMOVE
RETURN 1
ENDIF

VAR.CAMPFIREUID=<UID>
REGION.ALLCLIENTS CAMPHEAL 1
VAR.CAMPFIREUID=

MORE2=<MORE2>+(<EVAL -1*<TAG.HEALINTERVAL>>)
TIMER=<EVAL <TAG.HEALINTERVAL>>
RETURN 1

[ITEMDEF 0de1]
//Kindling
DEFNAME=i_kindling
DEFNAME2=i_gravetos
TYPE=T_KINDLING
NAME=gravetos%s%
RESOURCES=1 I_LOG
SKILLMAKE=BOWCRAFT 0.0
REPLICATE=1
WEIGHT=1
CATEGORY=MyT
SUBSECTION=Camping
DESCRIPTION=gravetos
DUPELIST=0de2

ON=@DClick
IF (<SRC.CAMPING> < 50.0)
RETURN 0
ENDIF


IF (<SRC.CAMPING> < 65.0) 
IF (rand(12) !=1) //1
RETURN 0 
ENDIF
ELSEIF (<SRC.CAMPING> < 80.0)
IF (rand(5) != 1) //1
RETURN 0
ENDIF
ELSE
IF (rand(2) != 1) // !=1
RETURN 0
ENDIF
ENDIF


VAR.CURRENTREGIONFLAGS=<REGION.FLAGS>

SRC.NEWITEM i_multi_campfire
SRC.ACT.P=<P>
SRC.ACT.ATTR=<SRC.ACT.ATTR>|attr_move_never
VAR.OLDACT=<SRC.ACT.UID>
SRC.NEWITEM i_campfire_tracker
SRC.ACT.P=<P>
SRC.ACT.LINK=<EVAL <VAR.OLDACT>>
VAR.OLDACT=
SRC.ACT.MORE2=<EVAL <EVAL <SRC.CAMPING>*3> / 10>
SRC.ACT.TAG.HEALINTERVAL=<EVAL <SRC.CAMPING> / 100>
SRC.ACT.TAG.HEALINTERVAL=<EVAL <SRC.ACT.TAG.HEALINTERVAL> * -1>
SRC.ACT.TAG.HEALINTERVAL=<EVAL 12 + <SRC.ACT.TAG.HEALINTERVAL>>

SRC.ACT.REGION.FLAGS=<EVAL <VAR.CURRENTREGIONFLAGS>>
SRC.ACT.TIMER=<EVAL <SRC.ACT.TAG.HEALINTERVAL>>
SRC.P=<SRC.P>
UPDATE
SRC.SAYU Ahhh , isto e que e uma fogueira!
src.tentarganhar_camping


[FUNCTION tentarganhar_camping]
if ({1 120} >= <eval 0<camping>>) && (<eval 0<camping>> <=100)
 camping=<camping>+5
endif

[ITEMDEF 0de2]
//kindling
DUPEITEM=0de1

[ITEMDEF 0de3]
//campfire
DEFNAME=i_campfire
TYPE=T_CAMPFIRE
name=fogueira
RESOURCES=i_kindling
CATEGORY=MyT
SUBSECTION=Camping
DESCRIPTION=fogueira
DUPELIST=0de4,0de5,0de6,0de7,0de8

ON=@PICKUP_GROUND
MOVABLE=0
SRC.RESEND
return 1

[ITEMDEF 0de4]
//campfire
DUPEITEM=0de3

[ITEMDEF 0de5]
//campfire
DUPEITEM=0de3

[ITEMDEF 0de6]
//campfire
DUPEITEM=0de3

[ITEMDEF 0de7]
//campfire
DUPEITEM=0de3

[ITEMDEF 0de8]
//campfire
DUPEITEM=0de3

[ITEMDEF 0de9]
//embers
NAME=braza
DEFNAME=i_ember_lit
TYPE=t_CAMPFIRE
VALUE=2
CATEGORY=MyT
SUBSECTION=Camping
DESCRIPTION=brazas

ON=@PICKUP_GROUND
MOVABLE=0
return 1

[ITEMDEF 0dea]
//burned wood
DEFNAME=i_ember
TYPE=t_garbage
name=madeira queimada
RESOURCES=1 I_KINDLING
CATEGORY=MyT
SUBSECTION=Camping
DESCRIPTION=madeira queimada

ON=@PICKUP_GROUND
MOVABLE=0
return 1
