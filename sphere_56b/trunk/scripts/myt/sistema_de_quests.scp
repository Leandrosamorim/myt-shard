//*****************************************************************************
//*****************************************************************************
//
// SISTEMA DE QUESTS por UsMarine (vinicius.arroyo at gmail.com)
//
//*****************************************************************************
//*****************************************************************************
//TODO:
//fazer Reward de stats
//limitar numero de quests ativas

//Tipos de eventos e parametros:
//*****************************************************************************
//* TIPO ********** PARAM1 ******************* PARAM 2 ************************
//*****************************************************************************
//* kill mob     * mob defname              * N/A                             *
//* get mob item * mob defname              * item defname                    *
//* get item     * NPC uid                  * item defname                    *
//* craft item   * item defname             * N/A                             *
//* find item    * item defname             * N/A                             *
//* escort       * x,y,z,m                  * N/A                             *
//* delivery     * item defname (1)         * NPC uid / N/A=NPC QUEST         *
//* text         * timeout (s)              *                                 *
//* talk to      * item defname (1)         * NPC uid / N/A=NPC QUEST         *
//* reward       * item defname             * NPC uid / N/A=NPC QUEST         *
//*****************************************************************************
//(1) player must have this item so this step can be completed

//TAGS usadas:
//src.ctag.quest_numPages	//numero de paginas do dialog do vendedor
//src.ctag.quest_page		//pagina atual
//src.ctag.quest_numQuests	//numero total de itens disponiveis para compra/venda
//src.ctag.quest_names		//array com itens disponiveis para compra/venda
//src.ctag.quest_ids		//array com os donos dos itens a venda
//src.ctag.quest_selectedQuest	//item selecionada para compra/venda
//LINK.TAG0.quest_destination
//LINK.TAG0.quest_home
//LINK.TAG0.quest_npcMoving
//LINK.TAG0.quest_npcInQuest
//LINK.TAG0.quest_npcQuestMry

//TABELAS:

//CREATE TABLE `mytserver`.`quests` (
//  `idQuest` INTEGER UNSIGNED NOT NULL AUTO_INCREMENT,
//  `idNpc` INTEGER UNSIGNED NOT NULL,
//  `name` VARCHAR(45) NOT NULL,
//  `description` VARCHAR(255) NOT NULL,
//  PRIMARY KEY(`idQuest`),
//  INDEX `Index_2`(`idNpc`)
//)
//ENGINE = MyISAM;

//CREATE TABLE `mytserver`.`questSteps` (
//  `idQuest` INTEGER UNSIGNED NOT NULL,
//  `idStep` TINYINT UNSIGNED NOT NULL,
//  `description` VARCHAR(255) NOT NULL,
//  `type` ENUM('Kill Mob','Get Mob Item','Get Item','Craft Item','Find Item','Escord','Delivery','Text','Talk To','Reward') NOT NULL,
//  `quantity` SMALLINT UNSIGNED NOT NULL,
//  `param1` VARCHAR(45) NOT NULL,
//  `param2` VARCHAR(45) NOT NULL,
//  PRIMARY KEY(`idQuest`, `idStep`)
//)
//ENGINE = MyISAM;

//CREATE TABLE `mytserver`.`questState` (
//  `idPlayer` INTEGER UNSIGNED NOT NULL,
//  `idQuest` INTEGER UNSIGNED NOT NULL,
//  `idStep` TINYINT UNSIGNED NOT NULL,
//  `dateStarted` DATETIME NOT NULL,
//  `dateCompleted` DATETIME NOT NULL,
//  PRIMARY KEY(`idPlayer`, `idQuest`)
//)
//ENGINE = MyISAM;

//*****************************************************************************
//*****************************************************************************
// DEFNAMEs
//*****************************************************************************
//*****************************************************************************

[DEFNAME quests_constants]
quest_itemsPerPage=15

//quest types
quest_type_kill_mob=1
quest_type_get_mob_item=2
quest_type_get_item=3		//*
quest_type_craft_item=4		//*
quest_type_find_item=5		//*
quest_type_escort=6			
quest_type_delivery=7
quest_type_text=8
quest_type_talkto=9
quest_type_reward=10

//*****************************************************************************
//*****************************************************************************
// EVENTs
//*****************************************************************************
//*****************************************************************************

[EVENTS e_quest]

//ON=@itemDclick
ON=@KILL

//remove evento se nao tiver quest ativa
if ( !<SRC.findid.i_quest_system> )
	EVENTS -e_quest
//	SRC.SYSMESSAGE EVENTO REMOVIDO
	return 0
endif	

forcont <SRC> 0
	if ( <baseid>==i_quest_system )
//		SRC.SYSMESSAGE LOOP <morex> <SRC.ACT.baseid> <more1>
		if ( <morex> == quest_type_kill_mob ) 
			if ( <SRC.ACT.baseid>==<more1> )
				quest_updateKillMobQuest <uid>
			endif
		elif ( <morex> == quest_type_get_mob_item )
			if ( <SRC.ACT.baseid>==<more1> )
				quest_updateGetMobItemQuest <uid>
			endif
		endif
	endif
endfor
return 0


[EVENTS e_quest_npc]

//previne que NPCs de quest morram. manda de volta pra 'casa'.
ON=@DEATH
	if ( <TAG0.quest_npcInQuest> != 0 )
		TAG0.quest_npcInQuest = 0
		home = <TAG0.quest_home>
		
		OBJ=<TAG0.quest_npcQuestMry>
		if ( <DB.connected> )
			DB.EXECUTE "DELETE FROM questState WHERE idQuest=<eval <OBJ.morey>> AND idPlayer=<eval <OBJ.cont>>"
		endif
		OBJ.remove
		
		sysmessage Um objetivo da missão falhou.
	endif

	p=<home>
	hits=<str>
	
return 1

//*****************************************************************************
//*****************************************************************************
// ITEMDEFSs
//*****************************************************************************
//*****************************************************************************

[ITEMDEF i_quest_system]
ID=i_deed
NAME=Quest
TYPE=t_eq_script
LAYER=layer_special
WEIGHT=0

//MORE1 - Quest Parameter 1
//MORE2 - Quest Parameter 2
//MOREX	- Quest Type
//MOREY - Quest ID
//MOREZ - Quest Step
//LINK - NPC quest

ON=@timer 

if ( <morex> == quest_type_escort )
	quest_updateEscortQuest <uid>
else
	quest_nextStep <uid>
endif

return 1

[ITEMDEF i_quest_sendNpcBack]
ID=i_deed
NAME=QuestNpc
TYPE=t_eq_script
LAYER=layer_special
WEIGHT=0

//LINK - NPC quest

ON=@timer 
//so transporta o NPC se o player estiver longe
if ( <LINK.DISTANCE <CONT>> > 20 )
	LINK.home <LINK.TAG0.quest_home>
	LINK.go <LINK.TAG0.quest_home>
	remove
//senao tenta denovo mais tarde
else
	timer=10
endif
return 1

//*****************************************************************************
//*****************************************************************************
// FUNCTIONs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// quest_updateKillMobQuest ( questMemory )
//*****************************************************************************
[FUNCTION quest_updateKillMobQuest]
OBJ=<argv[0]>

//verifica se o step nao foi completado
if ( <OBJ.amount> > 0 )
	OBJ.amount=<OBJ.amount>-1
	if ( <OBJ.amount> == 0 )	//acabou de completar o step
		OBJ.cont.sysmessage Voce finalizou um objetivo da quest.
		quest_nextStep <OBJ>
	else						//ainda nao completou
		SRC.sysmessage Voce matou um(a) <SRC.ACT.name>. Falta(m) <OBJ.amount>!!
	endif
endif
return 1

//*****************************************************************************
// quest_updateGetMobItemQuest ( questMemory )
//*****************************************************************************
[FUNCTION quest_updateGetMobItemQuest]
OBJ=<argv[0]>

//verifica se o step nao foi completado
if ( <OBJ.amount> > 0 )
	OBJ.amount=<OBJ.amount>-1

	SRC.NEWITEM <more2>
	NEW.cont=<SRC.findlayer.layer_pack.uid>

	if ( <OBJ.amount> == 0 )	//acabou de completar o step
		OBJ.cont.sysmessage Voce finalizou um objetivo da quest.
		quest_nextStep <OBJ>
	else						//ainda nao completou
		SRC.sysmessage Voce pegou um(a) <NEW.name>. Falta(m) <OBJ.amount>!!
	endif
endif
return 1

//*****************************************************************************
// quest_updateTalktoQuest ( questMemory )
//*****************************************************************************
[FUNCTION quest_updateTalktoQuest]

OBJ=<argv[0]>

if ( <OBJ.amount> > 0 )
	if ( <OBJ.cont.restest <OBJ.amount> <OBJ.more1>>)
		OBJ.cont.consume <OBJ.amount> <OBJ.more1>
		OBJ.cont.sysmessage Voce finalizou um objetivo da quest.	
		quest_nextStep <OBJ>
	else
		TRYSRV uid.<OBJ.more2>.message Voce nao tem o que preciso. Volte quando tiver.
	endif
else
	quest_nextStep <OBJ>
endif

return 1

//*****************************************************************************
// quest_updateEscortQuest ( questMemory )
//*****************************************************************************
//DEFAULT deve ser a questMemory para melhorar eficiencia
[FUNCTION quest_updateEscortQuest]

timer=1

//so anda se puder
if ( !<LINK.TAG0.quest_npcMoving> )
	return 1
endif

//calcula distancia do player
LOCAL.dist = <LINK.DISTANCE <CONT>>

//para se a distancia for muito grande
if ( <LOCAL.dist> > 60 )
	LINK.TAG0.quest_npcMoving=0
//pede para esperar se a distancia for media
//	CONT.sysmessage longe
	return 1
endif

if ( <LOCAL.dist> > 30 )
	DORAND 8
		CONT.sysmessage Me espere!!!
	ENDDO
//	CONT.sysmessage medio
endif

//corre atras se a distancia for pequena
if ( <LOCAL.dist> > 7 )
	if (<link.f_npcRunStepToUid <cont>,5>
		LINK.home <LINK.p>
	endif
//	message seguindo... <CONT.p> <eval <LOCAL.dx>> <eval <LOCAL.dy>>
//se a distancia for pequena, para e anda por perto
else
	LINK.home <CONT.p>
endif

//verifica se esta proximo do destino
if ( <LINK.DISTANCE <LINK.TAG0.quest_destination>> <= 7 )
	SERV.NEWITEM i_quest_sendNpcBack
	NEW.cont = <CONT> //player
	NEW.link = <LINK> //npc
	NEW.timer = 5	  //check after 5 secs


	CONT.sysmessage Voce finalizou um objetivo da quest.
	LINK.TAG.quest_npcInQuest=0
	LINK.TAG.quest_npcQuestMry=0
	timer=-1
	quest_nextStep <uid>
else
		
endif
return 1


//*****************************************************************************
// quest_updateDeliveryQuest ( questMemory )
//*****************************************************************************
[FUNCTION quest_updateDeliveryQuest]

OBJ=<argv[0]>

if ( <OBJ.cont.restest <OBJ.amount> <OBJ.more1>>)
	OBJ.cont.consume <OBJ.amount> <OBJ.more1>
	OBJ.cont.sysmessage Voce finalizou um objetivo da quest.	
	quest_nextStep <OBJ>
else
	TRYSRV uid.<OBJ.more2>.message Voce nao tem o que preciso. Volte quando tiver.
endif
return 1

//*****************************************************************************
// quest_createNpcQuestList
//*****************************************************************************
//cria lista com as quests
//
//SRC é o player
//DEFAULT é o NPC
[FUNCTION quest_createNpcQuestList]
SRC.CTAG.quest_page=1
SRC.CTAG.quest_numQuests=0

//varre o banco
if ( <DB.connected> )
	DB.QUERY "SELECT idQuest, name FROM quests WHERE idNpc=<eval <uid>> AND idQuest NOT IN (SELECT idQuest FROM questState WHERE idPlayer=<eval <SRC>>) ORDER BY name"
    if (<DB.ROW.NUMROWS> > 0)
        for R 0 <eval <DB.ROW.NUMROWS>-1>
		SRC.CTAG.quest_numQuests += 1
		TRYP 0 SRC.CTAG.quest_names<eval  (<SRC.CTAG.quest_numQuests>)>=<DB.ROW.<eval <LOCAL.R>>.name>
		TRYP 0 SRC.CTAG.quest_ids<eval (<SRC.CTAG.quest_numQuests>)>=<DB.ROW.<eval <LOCAL.R>>.idQuest>
        end
    endif
endif

SRC.CTAG.quest_numPages=<eval (((<SRC.CTAG.quest_numQuests>-1) / quest_itemsPerPage) + 1)> 
return 1

//*****************************************************************************
// quest_createPlayerQuestList
//*****************************************************************************
//cria lista com as quests
//
//DEFAULT é o player
[FUNCTION quest_createPlayerQuestList]
CTAG.quest_page=1
CTAG.quest_numQuests=0

//varre o banco
if ( <DB.connected> )
	DB.QUERY "SELECT idQuest, name FROM quests LEFT JOIN questState USING (idQuest) WHERE idPlayer=<eval <uid>> AND dateCompleted='0000-00-00 00:00:00' ORDER BY name"
    if (<DB.ROW.NUMROWS> > 0)
        for R 0 <eval <DB.ROW.NUMROWS>-1>
		CTAG.quest_numQuests += 1
		TRYSRV CTAG.quest_names<eval  (<CTAG.quest_numQuests>)>=<DB.ROW.<eval <LOCAL.R>>.name>
		TRYSRV CTAG.quest_ids<eval (<CTAG.quest_numQuests>)>=<DB.ROW.<eval <LOCAL.R>>.idQuest>
        end
    endif
endif

CTAG.quest_numPages=<eval (((<CTAG.quest_numQuests>-1) / quest_itemsPerPage) + 1)> 
return 1

//*****************************************************************************
// quest_createPlayerDoneList
//*****************************************************************************
//cria lista com as quests
//
//DEFAULT é o player
[FUNCTION quest_createPlayerDoneList]
CTAG.quest_page=1
CTAG.quest_numQuests=0

//varre o banco
if ( <DB.connected> )
	DB.QUERY "SELECT idQuest, name FROM quests LEFT JOIN questState USING (idQuest) WHERE idPlayer=<eval <uid>> AND dateCompleted!='0000-00-00 00:00:00' ORDER BY name"
    if (<DB.ROW.NUMROWS> > 0)
        for R 0 <eval <DB.ROW.NUMROWS>-1>
		CTAG.quest_numQuests += 1
		TRYSRV CTAG.quest_names<eval  (<CTAG.quest_numQuests>)>=<DB.ROW.<eval <LOCAL.R>>.name>
		TRYSRV CTAG.quest_ids<eval (<CTAG.quest_numQuests>)>=<DB.ROW.<eval <LOCAL.R>>.idQuest>
        end
    endif
endif

CTAG.quest_numPages=<eval (((<CTAG.quest_numQuests>-1) / quest_itemsPerPage) + 1)> 
return 1

//*****************************************************************************
// quest_startSelectedQuest
//*****************************************************************************
//inicia a quest selecionada em SRC.CTAG.quest_selectedQuest
//
//SRC é o player
//DEFAULT é o NPC
[FUNCTION quest_startSelectedQuest]

if ( <TAG0.quest_npcInQuest> )
	say Estou ocupado no momento, me procure outra hora.
	return 1
endif

if ( <DB.connected> )
	DB.EXECUTE "INSERT INTO questState SET idQuest=<eval <SRC.CTAG.quest_selectedQuest>>, idPlayer=<eval <SRC>>, dateStarted=NOW(), idStep=0"

	SERV.NEWITEM i_quest_system
	NEW.cont = <SRC> //player
	NEW.link = <uid> //npc
	NEW.morey = <SRC.CTAG.quest_selectedQuest>	
	
	quest_nextStep <NEW>
endif
return 1


//*****************************************************************************
// quest_nextStep( idMryQuest )
//*****************************************************************************
//executa o proximo passo da quest selecionada
//
//SRC é o player
//DEFAULT é o NPC?
[FUNCTION quest_nextStep]

if !( <DB.connected> ) || !<argv[0]>
	return 1
endif

OBJ=<ARGV[0]>
LOCAL.nextStep=<OBJ.morez>+1
//OBJ.cont.sysmessage INDO STEP <LOCAL.nextStep>

DB.QUERY "SELECT idQuest, idStep, type+0 AS type, quantity, param1, param2, description FROM questSteps WHERE idQuest=<eval <OBJ.morey>> AND idStep=<eval <LOCAL.nextStep>> LIMIT  1"
if ( <DB.ROW.NUMROWS> <= 0 )
	DB.EXECUTE "UPDATE questState SET dateCompleted=NOW() WHERE idQuest=<eval <OBJ.morey>> AND idPlayer=<eval <OBJ.cont>>"
	OBJ.cont.sysmessage Missao Cumprida.
	OBJ.remove
	return 1
endif

if ( <DB.ROW.quantity> > 0 )
	OBJ.amount = <DB.ROW.quantity>
else
	OBJ.amount = 1
endif
OBJ.more1 = <DB.ROW.param1>
OBJ.more2 = <DB.ROW.param2>
OBJ.morex = <DB.ROW.type>
OBJ.morez = <DB.ROW.idStep>
OBJ.timer = -1

OBJ.cont.EVENTS +e_quest

if ( <OBJ.morex> == quest_type_text )
	OBJ.link.message <DB.ROW.description>
	if ( <OBJ.more1> > 0 )
		OBJ.timer = <OBJ.more1>
	else
		OBJ.timer = 1
	endif
elif ( <DB.ROW.type> == quest_type_talkto )
	//set default NPC to the MAIN QUEST NPC
	if ( !<OBJ.more2> )
		OBJ.more2=<OBJ.link>
	endif
	
	OBJ.cont.sysmessage Fale com <uid.<OBJ.more2>.name>
elif ( <DB.ROW.type> == quest_type_escort )
	OBJ.LINK.TAG.quest_npcInQuest=1
	OBJ.LINK.TAG.quest_destination=<DB.ROW.param1>
	OBJ.LINK.TAG.quest_home=<OBJ.LINK.home>
	OBJ.LINK.TAG.quest_npcQuestMry=<OBJ>
	OBJ.timer = 1
elif ( <DB.ROW.type> == quest_type_delivery )
	SERV.NEWITEM <OBJ.more1>
	NEW.amount=<OBJ.amount>
	NEW.cont=<OBJ.cont.findlayer.layer_pack.uid>

	//set default NPC to the MAIN QUEST NPC
	if ( !<OBJ.more2> )
		OBJ.more2=<OBJ.link>
	endif

elif ( <DB.ROW.type> == quest_type_reward )
	SERV.NEWITEM <OBJ.more1>
	NEW.amount=<OBJ.amount>
	NEW.cont=<OBJ.cont.findlayer.layer_pack.uid>

	if ( <OBJ.more2> )
		TRYSRV uid.<OBJ.more2>.message <DB.ROW.description>
	else
		OBJ.link.message <DB.ROW.description>
	endif
	OBJ.timer = 1
endif   

DB.EXECUTE "UPDATE questState SET idStep=<eval <OBJ.morez>> WHERE idQuest=<eval <OBJ.morey>> AND idPlayer=<eval <OBJ.cont>>"

//elif ( <DB.ROW.type> == quest_type_kill_mob )
//elif ( <DB.ROW.type> == quest_type_get_mob_item )
//elif ( <DB.ROW.type> == quest_type_get_item )
//elif ( <DB.ROW.type> == quest_type_craft_item )
//elif ( <DB.ROW.type> == quest_type_find_item )
//elif ( <DB.ROW.type> == quest_type_escort )
//elif ( <DB.ROW.type> == quest_type_delivery )

//*****************************************************************************
// quest_drawGumps
//*****************************************************************************
[FUNCTION quest_drawGumps]
PAGE 0
resizepic 50 20 9260 450 452
gumppictiled 50 20 450 18 10250		//T BRICK
gumppictiled 50 460 450 18 10101	//B BRICK
gumppictiled 65 40 30 420 10460		//L BRICK
gumppictiled 455 40 30 420 10460	//R BRICK
gumppic 50 460 10306 0				//LB BRICK
gumppic 482 460 10304 0				//RB BRICK
gumppic 0 0 10440 0					//L SNAKE
gumppic 470 0 10441 0				//R SNAKE
gumppictiled 95 38 359 423 5124		//DARK BACK

gumppic 110 50 9005 0				//SELO
gumppictiled 150 80 200 1 9101		//UNDERLINE
//button 240 435 12012 12013 1 0 1	//OK button
return 1

//*****************************************************************************
// quest_closeDialogs
//*****************************************************************************
[FUNCTION quest_closeDialogs]
dialogclose d_quest_list
dialogclose d_quest_info
dialogclose d_quest_active
return 1

//*****************************************************************************
// quest_userQuests
//*****************************************************************************
//esta funcao eh chamada qndo o player aperta o botao "quest" do paperdoll
[FUNCTION quest_userQuests]
menu m_quest_button
return 1

//*****************************************************************************
// quest_addNpc( npcId )
//*****************************************************************************
//use sem parametros para abrir o menu de configuracao
[FUNCTION quest_addNpc]
if (<eval <argv[0]>> == 0) 
	menu m_quest_addNpc
	return 1
else
	SRC.targetfg quest_placeNpc <argv[0]>
	SRC.sysmessage Onde deseja colocar o NPC?
endif
return 1

//*****************************************************************************
// quest_placeNpc( npcId )
//*****************************************************************************
//argo = target
//argv[0] = npcId
[FUNCTION quest_placeNpc]
SERV.NEWNPC=<argv[0]>
NEW.P=<TARGP>
NEW.HOME=<TARGP>
NEW.FIX
return 1

//*****************************************************************************
// quest_cleanup
//*****************************************************************************
[FUNCTION quest_cleanup]
forcont <SRC> 0
	if ( <baseid>==i_quest_system )
		remove
	endif
endfor

if ( <DB.connected> )
	DB.EXECUTE "DELETE FROM questState WHERE idPlayer=<eval <OBJ.cont>>"
endif

//*****************************************************************************
// quest_assign( idQuest )
//*****************************************************************************
[FUNCTION quest_assign]

if !( <argv[0]> )
	sysmessage Use quest_assign quest_id
	return 1
endif

if ( <argo>==0 )
	sysmessage Selecion o NPC para associar a quest <argv[0]>
	targetf quest_assign <argv[0]>
	return 1
endif

sysmessage Quest associada.

if ( <DB.connected> )
	DB.EXECUTE "UPDATE quests SET idNpc=<eval <argo>> WHERE idQuest=<eval <argv[0]>>"
endif


//*****************************************************************************
//*****************************************************************************
// DIALOGOS E MENUS
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// m_quest_button
//*****************************************************************************
[MENU m_quest_button]

O que deseja ver?

ON=0 Quests em andamento
	quest_closeDialogs
	quest_createPlayerQuestList
	dialog d_quest_list	

ON=0 Quests terminadas
	quest_closeDialogs
	quest_createPlayerDoneList
	dialog d_quest_list	
	
//*****************************************************************************
// m_quest_addNpc
//*****************************************************************************
//menu de criacao do NPC quest. escolha a raca e sexo
[MENU m_quest_addNpc]
                -=Criacao de NPC Quest=-
on=0 Humano
	quest_addNpc C_H_QUEST_M
 	return 1

//on=0 Mulher - Humano
//	quest_addNpc C_H_QUEST_M
// 	return 1

on=0 Orc
	quest_addNpc C_O_QUEST_M
 	return 1

//on=0 Femea - Orc
//	quest_addNpc C_H_QUEST_M
// 	return 1

//on=0 Bambi - Elfo
//	quest_addNpc C_H_QUEST_M
// 	return 1

on=0 Elfo
	quest_addNpc C_E_QUEST_F
 	return 1

on=0 Anao
	quest_addNpc C_D_QUEST_M
 	return 1

//on=0 Barbuda - Anao
//	quest_addNpc C_H_QUEST_M
// 	return 1
	

//*****************************************************************************
// d_quest_list
//*****************************************************************************

[DIALOG d_quest_list]
100, 100
quest_drawGumps
dtext 150 60 1152 Quests

LOCAL.startQuest=<eval ((<SRC.CTAG0.quest_page> - 1) * quest_itemsPerPage ) + 1>
LOCAL.endQuest=<eval (<SRC.CTAG0.quest_page> * quest_itemsPerPage)>

if ( <LOCAL.endQuest> > <SRC.CTAG0.quest_numQuests> )
	LOCAL.endQuest = <SRC.CTAG0.quest_numQuests>
endif 

dorigin 110 110
if ( <SRC.CTAG0.quest_numQuests> > 0 )
	for x  <LOCAL.startQuest> <LOCAL.endQuest> 
		button *0 *20 9702 9703 1 0 <eval 1000 + <SRC.CTAG0.quest_ids<eval <LOCAL.x>>>>
		dtext +25 -3 1152 <SRC.CTAG0.quest_names<eval <LOCAL.x>>>
	endfor

	if (<SRC.CTAG0.quest_page> > 1)
		// Back button
		button 102 435 12015 12016 1 0 2 //4014
	endif
	if (<SRC.CTAG0.quest_page> < <SRC.CTAG0.quest_numPages>)
		// Forward button
		button 372 435 12009 12010 1 0 3 //4005
	endif
else
	dtext *0 *0 1152 Nao ha quests disponiveis.
endif

button 282 435 12012 12013 1 0 1	//OK button
//button 192 435 12006 12007 1 0 1	//Close button

[DIALOG d_quest_list BUTTON]
on=0
return 1

on=1 // Ok button
return 1

on=2 // Previous Button
SRC.CTAG0.quest_page -= 1
if (<SRC.CTAG.quest_page> < 1)
	SRC.CTAG.quest_page=1
endif
dialog d_quest_list

on=3 // Continue button
src.CTAG0.quest_page += 1
if (<SRC.CTAG.quest_page> > <SRC.CTAG0.quest_numPages>)
	SRC.CTAG.quest_page=<SRC.CTAG0.quest_numPages>
endif
dialog d_quest_list

on=1001,65535 //itens buttons
SRC.CTAG.quest_selectedQuest=<argn> - 1000

dialog d_quest_info


//*****************************************************************************
// d_quest_info
//*****************************************************************************
[DIALOG d_quest_info]
100, 100
quest_drawGumps

DB.QUERY "SELECT name, description FROM quests WHERE idQuest=<eval <SRC.CTAG0.quest_selectedQuest>>"
if (<DB.ROW.NUMROWS> <= 0)
	return 1
endif

dtext 150 60 1152 Quest: <DB.ROW.name>

dorigin 110 120
dtext *0 *0 1152 Objetivo:
dhtmlgump *0 +20 340 100 0 1 <DB.ROW.description>

dorigin 110 220
DB.QUERY "SELECT description, type+0 FROM questSteps AS steps LEFT JOIN questState AS state USING (idQuest) WHERE idQuest=<eval <SRC.CTAG0.quest_selectedQuest>> AND idPlayer=<eval <SRC>> AND steps.idStep <= state.idStep AND type+0 IN (1,2,3,4,5,6,7,9) ORDER BY idStep"
if ( <DB.ROW.NUMROWS> > 0 )
	for x 0 <eval <DB.ROW.NUMROWS>-1>
		gumppic - *20 9702 0
		dtext +25 -3 1152 <DB.ROW.<eval <LOCAL.x>>.description>
	endfor
	button 192 435 12015 12016 1 0 1	// Previous button
else
	button 282 435 12000 12001 1 0 2	//Accept button
	button 192 435 12018 12019 1 0 3	//Refuse button
endif

[DIALOG d_quest_info BUTTON]
on=0
return 1

on=1 // Previous button
dialog d_quest_list
return 1

on=2 // Accept button
quest_startSelectedQuest
return 1

on=3 // Refuse button
dialog d_quest_list
return 1

//*****************************************************************************
//*****************************************************************************
// NPCs E SPEECHes
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// c_h_quest_m
//*****************************************************************************
[CHARDEF c_h_quest_m]
DEFNAME=C_H_QUEST_M
NAME=#NAMES_HUMANMALE
ID=C_MAN
DESIRES=i_gold,e_notoriety
AVERSIONS=t_TRAP,t_eerie_stuff

TSPEECH=jobQUEST_SYSTEM

ON=@Create
HOMEDIST=5
COLOR=03eb
STR={71 85}
DEX={66 80}
INT={66 80}
EVENTS=+e_quest_npc

tag.raca Humano

SWORDSMANSHIP={15.0 38.0}
ITEMID={50.0 70.0}
TACTICS={15.0 38.0}

NPC=BRAIN_BANKER
NEED=i_gold

ITEMNEWBIE=i_hair_ponytail
COLOR=0456
ITEMNEWBIE=i_beard_vandyke
COLOR=match_hair


ON=@NPCRestock
ITEM=RANDOM_LIGHT
ITEM=i_shirt_fancy
COLOR=colors_green
ITEM=i_pants_long
COLOR=colors_yellow
ITEM=random_shoes
COLOR=colors_neutral
ITEM=random_coin_purse

CATEGORY=MyT
SUBSECTION=Quests
DESCRIPTION=Quest (Humano)

//*****************************************************************************
// c_o_quest_m
//*****************************************************************************
[CHARDEF c_o_quest_m]
DEFNAME=c_o_quest_m
NAME=#NAMES_HUMANMALE
ID=C_MAN
DESIRES=i_gold,e_notoriety
AVERSIONS=t_TRAP,t_eerie_stuff

TSPEECH=jobQUEST_SYSTEM

ON=@Create
HOMEDIST=5
COLOR=0597
STR={71 85}
DEX={66 80}
INT={66 80}
EVENTS=+e_quest_npc

tag.raca Orc

SWORDSMANSHIP={15.0 38.0}
ITEMID={50.0 70.0}
TACTICS={15.0 38.0}

NPC=BRAIN_BANKER
NEED=i_gold

ITEMNEWBIE=i_head_orc

ON=@NPCRestock
ITEM=RANDOM_LIGHT
ITEM=i_shirt_fancy
COLOR=colors_green
ITEM=i_pants_long
COLOR=colors_yellow
ITEM=random_coin_purse

CATEGORY=MyT
SUBSECTION=Quests
DESCRIPTION=Quest (Orc)


//*****************************************************************************
// c_e_vendor_f
//*****************************************************************************
[CHARDEF c_e_quest_f]
ID=c_woman
DEFNAME=c_e_quest_f
NAME=#NAMES_HUMANFEMALE
DESIRES=i_gold,e_notoriety
AVERSIONS=t_TRAP,t_eerie_stuff

TSPEECH=jobQUEST_SYSTEM

ON=@Create
HOMEDIST=5
COLOR=0412
STR={71 85}
DEX={66 80}
INT={66 80}
EVENTS=+e_quest_npc

tag.raca Elfo

SWORDSMANSHIP={15.0 38.0}
ITEMID={50.0 70.0}
TACTICS={15.0 38.0}

NPC=BRAIN_BANKER
NEED=i_gold


ON=@NPCRestock
ITEM=i_hair_long
COLOR=035
ITEM=i_shirt_fancy
COLOR=colors_green
ITEM=i_skirt_long
COLOR=colors_yellow
ITEM=random_coin_purse

CATEGORY=MyT
SUBSECTION=Quests
DESCRIPTION=Quest (Elfa)

//*****************************************************************************
// c_d_quest_m
//*****************************************************************************
[CHARDEF c_d_quest_m]
ID=c_man
DEFNAME=c_d_quest_m
NAME=#NAMES_HUMANMALE
DESIRES=i_gold,e_notoriety
AVERSIONS=t_TRAP,t_eerie_stuff

TSPEECH=jobQUEST_SYSTEM

ON=@Create
HOMEDIST=5
COLOR=0412
STR={71 85}
DEX={66 80}
INT={66 80}
EVENTS=+e_quest_npc

tag.raca Anao

SWORDSMANSHIP={15.0 38.0}
ITEMID={50.0 70.0}
TACTICS={15.0 38.0}

NPC=BRAIN_BANKER
NEED=i_gold


ON=@NPCRestock
ITEM=i_hair_long
COLOR=0
ITEM=i_beard_long
COLOR=0
ITEM=i_shirt_fancy
COLOR=colors_green
ITEM=i_pants_long
COLOR=colors_yellow
ITEM=random_coin_purse
ITEM=random_shoes
COLOR=colors_neutral

CATEGORY=MyT
SUBSECTION=Quests
DESCRIPTION=Quest (Anao)

//*****************************************************************************
// jobQUEST_SYSTEM
//*****************************************************************************
[SPEECH jobQUEST_SYSTEM]
ON=*oi*
ON=*ola*
ON=*bom dia*
ON=*boa tarde*
ON=*boa noite*

	if ( <TAG0.quest_npcInQuest> > 0 )
		if ( <uid.<TAG0.quest_npcQuestMry>.cont> == <SRC> )
			message Ola!	
		else
			message Estou ocupado no momento. Me procure outra hora.
		endif
		return 1
	endif
	
	quest_closeDialogs
	
	quest_createNpcQuestList
	if ( <SRC.CTAG0.quest_numQuests> < 1 )
		message Nao tenho nenhuma missao para voce no momento.
	else
		message	O que deseja fazer?
	
		dialog d_quest_list
	endif
return 1

//SOMENTE EM QUEST "ESCORT". FAZ NPC SEGUIR
ON=*venha*
ON=*ande*

	if ( <TAG0.quest_npcInQuest> == 0 )
		return 1
	endif

	LOCAL.uid=<uid>
	forcont <SRC> 0
		if ( <baseid>==i_quest_system )
			if ( <morex> == quest_type_escort ) 
				if ( <LOCAL.uid>==<link> )
					LINK.SAY Ok, estou indo!!
					LINK.TAG.quest_npcMoving=1
				endif
			endif
		endif
	endfor
return 1

//SOMENTE EM QUEST "ESCORT". FAZ NPC PARAR
ON=*pare*
ON=*espere*

	if ( <TAG0.quest_npcInQuest> == 0 )
		return 1
	endif

	LOCAL.uid=<uid>
	forcont <SRC> 0
		if ( <baseid>==i_quest_system )
			if ( <morex> == quest_type_escort ) 
				if ( <LOCAL.uid>==<link> )
					LINK.SAY Ok, esperarei aqui!!
					LINK.TAG.quest_npcMoving=0
				endif
			endif
		endif
	endfor
return 1

//FINALIZA QUESTS TIPO "TALK TO"
ON=*

	if ( <TAG0.quest_npcInQuest> > 0 )
		return 1
	endif

	LOCAL.uid=<uid>
	forcont <SRC> 0
		if ( <baseid>==i_quest_system )
			if ( <morex> == quest_type_talkto ) 
				if ( <LOCAL.uid>==<more2> )
					quest_updateTalktoQuest <uid>
				endif
			elif ( <morex> == quest_type_delivery ) 
				if ( <LOCAL.uid>==<more2> )
					quest_updateDeliveryQuest <uid>
				endif
			endif
		endif
	endfor
return 1


[EOF]
