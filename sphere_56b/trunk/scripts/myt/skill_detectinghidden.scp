[DEFNAME DetectingHidden]
//Tempo antes de poder detectar no mesmo lugar
DetectingHidden_timer   15

//Distância mínima de um lugar já detectado para poder detectar
DetectingHidden_dist    8

[FUNCTION sk_DetectingHidden]
//Função inicial chamada pelo botão da skill

//Checar se o char pode detectar aqui
LOCAL.canDet=1
obj=<uid>
FORCHARLAYER 30
 IF (<BaseID>==i_mry_DetectingHidden)                   //Ele usou essa skill faz pouco tempo?
  IF (<obj.distance <morep>> < DetectingHidden_dist)            //Foi aqui perto?
   LOCAL.canDet=0                           //Se sim, cancele tudo.
  ENDIF
 ENDIF
ENDFOR
IF (!<LOCAL.canDet>)
 SYSMESSAGERED Voce procurou aqui faz pouco tempo.
 return 1
endif
local.timer=<f_rangeValue 15,5,<DetectingHidden>>
DORAND 3
 emoteyellow observando atentamente
 emoteyellow procurando
 emoteyellow apalpando o ambiente
ENDDO

ctag.disturb=
ctag.e_no_move_x=<p.x>
ctag.e_no_move_y=<p.y>
events +e_no_attack
events +e_no_dclick

ctag.dh_timer=<LOCAL.timer>

TIMERF 1,DetectingHidden_,<DetectingHidden>
return 1

[FUNCTION DetectingHidden_]

if (<ctag0.disturb>) || (<ctag.e_no_move_x>!=<p.x>) || (<ctag.e_no_move_y>!=<p.y>)
    sysmessageyellow Interrompido
    return 1
elif (0<ctag0.dh_timer> > 0)
    ctag.dh_timer=<eval <ctag0.dh_timer>-1>
    TIMERF 1,DetectingHidden_,<argv0>
    return 1
endif
obj=<uid>
LOCAL.vision=<eval (<argn>/166)+2>
FORITEMS <LOCAL.vision>

 //Para traps
 if (<type>==t_trap) && ((<BELLTEST <argn>,<morey>>) || (<obj.IsGM>))   //Para todas as traps no campo de visao E se eu passar no teste.
  if (<morez>==0) && (<attr>&080)                   //Se for uma armadilha do chão (morez=t_normal=0)
   attr=<attr>&~080                         //Apareça
   update
  endif
  if (!<canseelos>) && (<morez>==0)
   attr=<attr>|080
   removefromview
  elif (<canseelos>)
   if (<color>!=026)                            //se eu não estiver detectada
    tag.color=0<color>
    color=026
    update
   endif
   say=@028, [armadilha]
   f_resetTrap
  endif

 //Para itens invisiveis
 elif (<attr>&attr_invis) && (!<IsEmpty <tag.Detecting_Hidden>>) && (<obj.canseelos <uid>>)
  if (<BELLTEST <argn>,<tag0.Detecting_Hidden>>) || (<obj.IsGM>) || (!<tag0.Detecting_Hidden>)
   if !(strmatch(<tag.name>,)
    name=<tag.name>
   endif
   color=<tag0.color>
   attr ^= attr_invis
   update
   say=@04, [escondido]
  endif
 endif
ENDFOR
serv.newitem i_mry_DetectingHidden
new.morep=<p>
new.timer <DEF.DetectingHidden_timer>
new.equip
SKILL_GAIN skill_DetectHidden
sysmessageyellow Voce nao esta mais procurando.


[ITEMDEF i_mry_DetectingHidden]
ID=i_rune_reveal
NAME=DetectingHidden
TYPE=t_eq_script
LAYER=layer_special

on=@Timer
remove
return 1