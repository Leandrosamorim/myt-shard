//*****************************************************************************
//*****************************************************************************
//
// SKILL TAMING 
//
//  Galthar
//  UsMarine (vinicius.arroyo at gmail.com)
//
//*****************************************************************************
//*****************************************************************************

//TODO:

//TAGS usadas:

//NOTAS:
//usar MAXFOLLOWER para definir maximo de pets
//*****************************************************************************
//*****************************************************************************
// EVENTSs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// e_tamingcheck - Check de falha no taming
//*****************************************************************************
[EVENTS e_tamingcheck]
ON=@GETHIT
findid.i_taming.remove
events=-e_tamingcheck
sysmessagered Voce se desconcentrou.

//*****************************************************************************
// e_pet - evento de pets
//*****************************************************************************
[EVENTS e_pet]

//src   master
//default npc
ON=@PETDESERT
src.curfollower=<src.curfollower>-1

ON=@DEATH
memoryfindtype.memory_ipet.link.curfollower -= 1

ON=@CONTEXTMENUSELECT

//41 guard
//42 follow
//43 drop
//44 kill
//45 stop
//46 stay
//47 friend
//48 transfer

if (<argn>==41)     //guard
    src.ctag.anim_action=4
elif (<argn>==42)   //follow
    src.ctag.anim_action=7
elif (<argn>==44)   //kill
    src.ctag.anim_action=3
elif (<argn>==45)   //stop
    src.ctag.anim_action=8
elif (<argn>==46)   //stay
    src.ctag.anim_action=8
    anim_tool_target <uid>    
    return 0
elif (<argn>==47)   //friend
    src.ctag.anim_action=6
elif (<argn>==48)   //transfer
    src.ctag.anim_action=5
else
    src.sysmessageyellow Nao suportado.
    return 1
endif

anim_tool_target <uid>
return 1

//*****************************************************************************
//*****************************************************************************
// FUNCTIONs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// sk_taming
//*****************************************************************************
[FUNCTION sk_taming]

if (<src.targ.distance>>8)
    src.sysmessageyellow Aproxime-se mais.
    return 1
endif

// Checar se ja foi tamed
if (<src.targ.memoryfindtype.memory_ipet>)
    src.sysmessageyellow Este animal ja possui dono...
elif (<src.findid.i_taming>)
    src.sysmessageyellow Voce so pode domar um animal por vez...
elif (<src.targ.npc>!=brain_animal)
    src.sysmessageyellow Voce so pode domar animais...
elif (<src.curfollower> >= <eval (<src.taming>/10)+1>)
    src.sysmessageyellow Voce so pode domar até <eval (<src.taming>/10)+1> animais...
else
    serv.newitem i_taming
    new.cont=<src>
    new.link=<src.targ>
    src.events=+e_tamingcheck
endif
return 1

//*****************************************************************************
//*****************************************************************************
// ITEMDEFs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// i_tamed
//*****************************************************************************
[ITEMDEF i_tamed] 
ID=i_deed
NAME=PetEvents
TYPE=t_eq_script 
WEIGHT=0 
LAYER=layer_special 

ON=@CREATE 
TIMER=1

ON=@TIMER
TIMER=5

//desiste do alvo se em warmode
if (<cont.flags>&statf_war)
//    f_gmLog TAM: <name> ignore war <src.name>:
    return 1
endif

OBJ=<cont>
LOCAL.g=<cont.memoryfindtype.memory_guard.link>
LOCAL.p=<cont.memoryfindtype.memory_ipet.link>
LOCAL.f=<cont.memoryfindtype.memory_friend.link>

forchars 10
    //consigo ver?
    if (<uid>!=<obj>) && (0<obj.canseelos <uid>>)
        //se esta olhando para alguem em batalha e vivo
        if (<hits>) && (<memoryfindtype.memory_fight.link>)
            //salva alvo
            REF1=<memoryfindtype.memory_fight.link>
            //se estiver protegendo (guard), for pet ou amigo DO ATACANTE, ataca o mesmo alvo
            if ( <LOCAL.g>==<uid> ) || ( <LOCAL.p>==<uid> ) || ( <LOCAL.f>==<uid> )
//                f_gmLog TAM: (A) <OBJ.name> ajudando <name> a atacar / <ref1.name>
                OBJ.attack <REF1>
            //se estiver protegendo (guard), for pet ou amigo DO ATACADO, 'ataca o atacante'
            elif ( <LOCAL.g>==<REF1> ) || ( <LOCAL.p>==<REF1> ) || ( <LOCAL.f>==<REF1> )
//                f_gmLog TAM: (B) <OBJ.name> ajudando <REF1.name> a atacar / <name>
                OBJ.attack <uid>
            //pretende me atacar?
            elif (<OBJ>==<REF1>)
//                f_gmLog TAM: (C) <OBJ.name> atacando <name>
                OBJ.attack <uid>
//            else
//                f_gmLog TAM: (D) <obj.name> ignorando <name> (<ref1.name>)
            endif
        else
//            f_gmLog TAM: <obj.name> idle <name> 
        endif
    endif
endfor

return 1

//*****************************************************************************
// i_taming
//*****************************************************************************
[ITEMDEF i_taming] 
ID=i_deed
NAME=Taming Memory 
TYPE=t_eq_script 
WEIGHT=0 
LAYER=layer_special 

ON=@CREATE 
ATTR=attr_decay
MORE=15-(<cont.taming>/100) //5 a 15 segundos
TIMER=1

ON=@TIMER

// Checar distancia
if (<link.distance <cont>>>4) || !(<link.canseelos <cont>>)
    cont.sysmessageyellow O animal se distanciou demais.
    cont.events=-e_tamingcheck
    remove
    return 1
endif

// Se o animal estiver sendo atacado
if (<link.flags>&statf_War)
    cont.sysmessageyellow O animal nao pode ser domado enquanto estiver enfurecido.
    cont.events=-e_tamingcheck
    remove
    return 1
endif

// Se estiver em war mode
if (<cont.flags>&statf_War)
    cont.sysmessageyellow O animal se assusta com seus movimentos.
    cont.events=-e_tamingcheck
    remove
    return 1
endif

//loop de animacao
if (<more>)
    more=<more>-1
    timer=1
    dorand 8
        cont.say Nao vou machucar voce. 
        cont.say Eu sempre quis um <link.name> como voce. 
        cont.say Aqui <link.name>. 
        cont.say Bom <link.name>. 
    enddo
    return 1 
endif
    
LOCAL.t1=<R0,<link.taming>>
LOCAL.t2=<R0,<cont.taming>>
LOCAL.f=<R0,100>
//faz teste de skill taming e de fome do animal
if (<LOCAL.t1> < <LOCAL.t2>) && (<LOCAL.f> > <link.food>)
    cont.sysmessage Parece que ele aceitou voce como novo dono.
//    if (<link.memoryfindtype.memory_ispawned>)
    link.memoryfindtype.memory_ispawned.remove
    serv.newitem=i_memory
    new.color=memory_ipet
    new.more1=04
    new.more2=<serv.time>
    new.morep=<link.p>
    new.link=<cont>
    new.cont=<link>
    link.flags=<link.flags>|statf_pet
    link.events +e_pet
    serv.newitem=i_tamed
    new.cont=<link>
    link.update
    cont.curfollower=<cont.curfollower>+1
//faz outro teste de fome para ver se somente falha ou se ataca o domador
elif (<LOCAL.f> > <link.food>)
    cont.sysmessageyellow O animal esta enfurecido.
    link.flags=<link.flags>|statf_war
    dorand 3
        link.attack <cont>
    enddo
else
    cont.sysmessagegreen Voce falhou ao tentar domar o animal.
endif

cont.skill_gain skill_taming
cont.events=-e_tamingcheck
remove
return 1

//*****************************************************************************
//*****************************************************************************
// SPEECHes
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// spk_pet
//*****************************************************************************
[SPEECH spk_pet]
ON=*
if !(<src.isgm>)
    src.sysmessageyellow Use o cajado para comandar
    return 1
endif

[EOF]
