//*****************************************************************************
//*****************************************************************************
//
// SKILL HIDING e STEALTH por
//
//  UsMarine (vinicius.arroyo at gmail.com)
//
//*****************************************************************************
//TODO:

//TAGS usadas:
//tag.light_lvl     //0=escuro, 30=claro

//*****************************************************************************
//*****************************************************************************
// FUNCTIONs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// sk_hiding
//*****************************************************************************
[FUNCTION sk_hiding]

if (<src.findid.i_hiding>)
    src.sysmessageyellow Nao pode se esconder ainda.
    return 1
elif (<src.flags>&statf_onhorse)
    src.sysmessageyellow Voce nao pode se esconder com um cavalo...
    return 1
elif (<src.region.flags>&region_flag_underground)
    local.d=
elif (<src.hiding> < 500) && (<var.tHour> > 4) && (<var.tHour> < 20)
    src.sysmessageyellow Voce nao possui habilidade para se esconder com esta luminosidade...
    return 1
elif (<src.hiding> < 700) && (<var.tHour> > 5) && (<var.tHour> < 19)
    src.sysmessageyellow Voce nao possui habilidade para se esconder com esta luminosidade...
    return 1
elif (<src.hiding> < 900) && (<var.tHour> > 6) && (<var.tHour> < 18)
    src.sysmessageyellow Voce nao possui habilidade para se esconder com esta luminosidade...
    return 1
endif

src.act=0
src.ctag.disturb=0
serv.newitem i_hiding
new.cont=<src.uid>
new.timer 1


return 1

//*****************************************************************************
//*****************************************************************************
// ITEMDEFs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// i_hiding
//*****************************************************************************
[ITEMDEF i_hiding]
ID=i_deed
NAME=Hiding
TYPE=t_eq_script
LAYER=layer_special

ON=@create
//not hidden
more1=0

ON=@timer
timer=1
if (!<cont.isonline>)
    cont.hiding_leave
    remove
    return 1
elif (<cont.npc>)
    //
elif (<cont.findlayer.layer_hand1.type> == t_light_lit) || (<cont.findlayer.layer_hand2.type> == t_light_lit)
    cont.sysmessageyellow Apague tochas e lampioes...
    cont.hiding_leave
    remove
    return 1
//statf_dead|statf_freeze|statf_sleeping|statf_war|statf_poisoned|statf_polymorph|statf_stone|statf_hallucinating|statf_onhorse
elif (<cont.flags>&0804404b6) || (<cont.ctag0.disturb>)
    cont.sysmessageyellow Voce esta visivel// 2! <hval <cont.flags>&0804404b6>
    cont.hiding_leave
    remove
    return 1
endif

if !(<more1>)
    cont.skill_gain SKILL_HIDING
//    serv.log [hiding] testando <eval <cont.hiding>> contra <eval <cont.tag0.light_lvl>*30>
    if (<R<cont.hiding>> < <eval <cont.tag0.light_lvl>*30>)
        cont.sysmessagered Voce nao conseguiu se esconder
        cont.hiding_leave
        remove
        return 1
    else
        cont.sysmessagegreen Conseguiu se esconder
        cont.hiding_enter
        more1=1
    endif
else
    
    LOCAL.d=<cont.distance <tag0.p>>
    if (<LOCAL.d> > 0)
        cont.skill_gain SKILL_STEALTH
    
        if !(<cont.region.flags>&region_flag_underground)
            LOCAL.d=<eval (<cont.tag0.light_lvl>+1)*<LOCAL.d>>
        endif
//        serv.log [stealth] testando <eval <cont.stealth>> contra < <eval 20*<local.d>>
        if (<R<cont.stealth>> < <eval 20*<local.d>>)
            cont.sysmessageyellow Voce esta visivel!
            cont.hiding_leave
            remove
        endif    
    endif
endif

tag.p=<cont.p>
return 1

[FUNCTION hiding_enter]
invis 1
flags |= statf_hidden
events +e_no_attack
events +e_no_dclick
update

[FUNCTION hiding_leave]
invis 0
flags &= ~statf_hidden
events -e_no_attack
events -e_no_dclick
update