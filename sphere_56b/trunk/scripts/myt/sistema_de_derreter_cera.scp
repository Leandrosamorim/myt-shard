////////////////////////////////////////////////////
/////////  sistema de derreter cera  //////////////
/////////     Jean Gabin (MyT)       //////////////
////////         beta v. 0.2         /////////////
//////////////////////////////////////////////////


[ITEMDEF 01422]
DEFNAME=i_beeswax
name=Cera de abelha
WEIGHT=1
type=t_script
DUPELIST=01423,01424,01425,01426,01427

on=@click
message <amount> <NAME>
return 1


on=@dclick
Target Selecione o caldeirao que voce quer usar para derreter a cera
return 1

on=@targon_char
src.sysmessage se voce quer jogar a cera use o comando .jogar
return 1


ON=@Targon_ground 
 SRC.SYSMESSAGE se voce quer colocar a cera ai, arraste e solte. 
RETURN 1  


ON=@Targon_item
if (<src.targ.baseid>==i_caldeirao)
	if (<src.targ.more1>==0)
		if (<amount>>=6)
		 src.message *colocando a cera no caldeirao.*
		 src.anim=020
		 src.targ.attr=08010
		 src.targ.more1=2
		 src.consume 6 i_beeswax
		 src.newitem=i_beeswax
		 src.act.p=<src.targ.p>
		 src.act.z=<src.targ.z>+6
		 src.act.attr=08010
		 src.act.link=<src.targ.uid>
		 src.targ.link=<src.act.uid>
		return 1
		else
		  src.sysmessage Voce precisa de no minimo 6 porcoes de cera para derreter no caldeirao.
		  return 1
		endif
	return 1
	else
	src.sysmessage Ja tem algo dentro desse caldeirao. escolha um caldeirao vazio.
	return 1
	endif
return 1
else
	src.sysmessage Use um caldeirao para derreter a cera
return 1
endif
return 1




/////////////////////////////////////////////////////////////////////////

[ITEMDEF i_caldeirao_cera]
id=0142a
name=Caldeirao com cera derretida
WEIGHT=4
type=t_script

CATEGORY=Myt itens (Jean Gabin)
SUBSECTION=Recipiente
DESCRIPTION=Caldeirao com cera

on=@create
more1=0
more2=0

on=@click
if (<more2>==1)
message <NAME> quente (<more1> porcoes)
sfx 021
return 1
else
message <NAME> fria (<more1> porcoes)
return 1
endif
return 1

on=@dclick
if (<more2>==0)
	if (<src.restest 1 i_kindling>)
	 more2=1
	 more1=6
	 attr=08010
	 timer=350
	 src.consume= 1 i_kindling
	 src.newitem=i_campfire
	 src.act.p=<p>
	 src.act.timer=300
	 sfx 021
	return 1
	else
	 src.sysmessage Voce precisa ter gravetos para acender uma fogueira e derreter a cera fria.
	return 1
	endif
return 1
elif (<more2>==1)
  src.sysmessage use um molde para fazer velas com essa cera derretida.
return 1
endif
return 1


on=@timer
more2=0
attr=08
return 1

/////////////////////////////////////////////////////////////////////

[ITEMDEF i_molde_vela]
id=01428
NAME=Molde para fazer velas
WEIGHT=1
type=t_script
DUPELIST=01429

CATEGORY=Myt itens (Jean Gabin)
SUBSECTION=Ferramentas
DESCRIPTION=Molde para fazer velas

on=@create
more1=0

on=@click
if (<more1>==1)
message <NAME> (3 velas prontas)
return 1
else
message <NAME> (molde vazio)
return 1
endif
return 1

on=@dclick
if (<more1>==1)
	src.newitem=i_candle_lg
	src.act.bounce
	src.newitem=i_candle_lg
	src.act.bounce
	src.newitem=i_candle_lg
	src.act.bounce
	more1=0
return 1
else
	target selecione o caldeirao com cera derretida quente
return 1
endif
return 1


on=@targon_char
src.sysmessage se voce quer jogar o molde use o comando .jogar
return 1


ON=@Targon_ground 
 SRC.SYSMESSAGE se voce quer colocar o molde ai, arraste e solte. 
RETURN 1  


ON=@Targon_item
if (<src.targ.baseid>==i_caldeirao_cera)
	if (<src.targ.more2>==1)
		if (<src.targ.more1> > 1)
		 more1=1
		 src.targ.more1=<src.targ.more1>+(-1)
		 sfx 021
		return 1
		elif (<src.targ.more1> == 1)
		 more1=1
		 src.targ.more1=0
		 sfx 021
		 src.newitem=i_caldeirao
		 src.act.p=<src.targ.p>
 		 src.targ.remove
		return 1
		endif
	return 1
	endif
return 1
else
  src.sysmessage Use o molde apenas em cera derretida quente.
return 1
endif
return 1




[eof]