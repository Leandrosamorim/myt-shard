//*****************************************************************************
//*****************************************************************************
//
// SISTEMA DE RECONHECIMENTO por
//	Galthar		(galtar at terra.com.br)
//	UsMarine	(vinicius.arroyo at gmail.com)
//
//*****************************************************************************
//*****************************************************************************
//TODO:

//TAGS usadas:
//src.tag.name
//src.tag.uid_<uid_do_player_reconhecido>_name
//src.ctag.recon
//*****************************************************************************

[PLEVEL 1]
r

//*****************************************************************************
//*****************************************************************************
// EVENTs
//*****************************************************************************
//*****************************************************************************
[EVENTS e_recon]
 
ON=@CLICK
 //se estiver se olhando, se for gm, ou olhando gm
 if (<uid> == <src.uid>) || (<src.gm> == 1) || ( (0<isgm> == 1) && (0<canseelos> == 1) )
//	src.sysmessage [REC] NOME REAL
  message <tag.name>
  return 1
 endif

if ( !<src.rec_canSee <uid>> )
	src.sysmessage Voce nao consegue ver o rosto...
	return 1
endif
 
//src.sysmessage [REC] NOME FALSO
message <src.tag.uid_<uid>_name>
return 1
 
ON=@DCLICK
if (<canseelos> == 0)
	src.message Nao consegue ver...
	return 1
endif

//*****************************************************************************
//*****************************************************************************
// FUNCTIONs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// r
//*****************************************************************************
[FUNCTION r]
src.newitem=i_recon
src.act.equip
src.act.use
return 1

//*****************************************************************************
// rec_checkName( name )
//*****************************************************************************
[FUNCTION rec_checkName]
if (<STRLEN(<ARGS>)> >= 17)
 src.sysmessage Nome muito longo.
 return 1
endif
if strmatch(*[0-1]*, <args>)
 src.sysmessage Nao pode conter numeros.
 return 1
endif
if strmatch(* [a-Z], <args>)
 src.sysmessage Nao pode conter letras separadas.
 return 1
endif
if strmatch([a-Z] *, <args>)
 src.sysmessage Nao pode conter letras separadas.
 return 1
endif
return 0


//*****************************************************************************
// rec_canSee( UID )
//*****************************************************************************
//SRC deve ser quem esta olhando
//UID eh quem esta sendo olhado
[FUNCTION rec_canSee]

OBJ=<argn1>

// Rosto coberto (barrar)
LOCAL.h=<OBJ.findlayer(layer_helm).id>
LOCAL.r=<OBJ.findlayer(layer_robe).id>

if (<OBJ.canseelos> == 0) || (<OBJ.distance> >= 6) || (<OBJ.body>==c_man_gm) || ( <LOCAL.h> == 01408 ) || ( <LOCAL.h> == 01409 ) || ( <LOCAL.h> == 0140c ) || ( <LOCAL.h> == 0140d ) || ( <LOCAL.h> == 01412 ) || ( <LOCAL.h> == 01419 ) || ( <LOCAL.h> == 01451 )|| ( <LOCAL.h> == 01549 ) || ( <LOCAL.h> == 0154a ) || ( <LOCAL.h> == 0154b ) || ( <LOCAL.h> == 0154c )|| ( <LOCAL.h> == 01f0b ) || ( <LOCAL.h> == 01f0c ) || ( <LOCAL.r> == 0204e ) || ( <LOCAL.r> == 0204f ) || ( <LOCAL.r> == 04501 ) || ( <LOCAL.r> == 04502 ) || ( <LOCAL.r> == 04503 )
//	src.sysmessage [REC] SEM VISAO
	return 0
endif

//src.sysmessage [REC] COM VISAO
return 1
 
//*****************************************************************************
//*****************************************************************************
// ITEMDEFs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// i_recon
//*****************************************************************************
[ITEMDEF i_recon]
ID=i_deed
NAME=Reconhecimento
TYPE=t_eq_script
LAYER=layer_special

ON=@CREATE
ATTR=attr_decay

ON=@DCLICK
Target Reconhecer...
RETURN 1

ON=@TARGON_CHAR
// É player?
if (<targ.npc>!=0)
 sysmessage Jogador invalido.
 remove
 return 1
endif

// É você mesmo?
if (<targ.uid>==<src.uid>)
 sysmessage Nao pode mudar seu nome real.
 remove
 return 1
endif

if ( !<src.rec_canSee <targ.uid>> )
	src.sysmessage Nao consegue ver...
	remove
	return 1
endif

src.ctag.recon=<targ.uid>
src.dialog=d_recon
src.say * observando *
src.sysmessage Reconhecendo...
remove
return 1

//*****************************************************************************
//*****************************************************************************
// GUMPs
//*****************************************************************************
//*****************************************************************************
[DIALOG d_recon]
200, 300
PAGE 0
resizepic 0 0 3500 356 136
text 26 31 995 0
text 141 10 152 1
gumppic 39 63 1143
textentry 59 66 220 600 1152 0 2
button 55 97 239 240 1 0 1
button 234 97 243 241 1 0 2

[DIALOG d_recon TEXT]
Como voce gostaria de reconhecer esta pessoa?
-=MyT=-
<var.blank>


[DIALOG d_recon BUTTON]
ON=1
if ( src.rec_checkName <ARGTXT[0]> )
	src.dialog d_recon
	return 1
endif

TRYP 0 src.tag.uid_<src.ctag.recon>_name = <ARGTXT[0]>
src.sysmessage Agora voce o(a) conhece como <ARGTXT[0]>
return 1

ON=2
return 1

[EOF]