//*****************************************************************************
//*****************************************************************************
//
// SISTEMA DE SKILLS por UsMarine (vinicius.arroyo at gmail.com)
//
//*****************************************************************************
//*****************************************************************************
//** ATENCAO, TENHA CERTEZA QUE ESSE SCP ENTROU DEPOIS DE SPHERE_SKILLS.SCP ***
//*****************************************************************************
// Esse sistema computa o ganho de skills cada vez que a funcao skill_gain() 
//é chamada.
//
//O 'rate' ou dificuldade de subir um skill é calculado da seguinte forma:
//
//Cada ação gera um ganho mínimo (bonusIncrease) mais um valor
//randômico entre zero e um valor X predeterminado que depende da somatórioa 
//dos skills. Para somatória zero X vale 100 e para o máximo vale 0. Dessa
//forma tem-se uma dificuldade exponencial que é dividida entre todas as skills
//conforme o total da soma for aumentando.
//
//Cada incremento corresponde a 0.001%.
//
//O sistema também conta com uma proteção para que o player não atinja o máx
//de uma skill muito rapidamente. Inicialmente ele consegue chegar até 55% 
//em qualquer skill, mas para passar disso ele deve jogar 1 hora para cada % 
//adicional. Após 45h o limite é removido.

//TODO:

//TAGS usadas:
//TAG.skill_ts                  //timestamp da proxima vez q um skill pode ser usado
//TAG.skill_spa                 //contador de infracoes. qntas vezes passou o limite de skill/segundo
//TAG.skill_blocked             //ha limite de skill ativo no char
//TAG.skill_maxSkillValue       //valor maximo que uma skill pode chegar (depende do tempo online)  
//TAG.skill_maxIncrease         //maximo de skill que pode ser ganha de uma vez (depende do skillsum)
//TAG.skill_<name>              //contador secundario de cada skill. sempre que atinge o limite incrementa a skill real

//*****************************************************************************
//*****************************************************************************
// DEFNAMEs
//*****************************************************************************
//*****************************************************************************

[DEFNAME skills_custom]
//increase 0.02% every action (minimum)
bonusIncrease       = 20

SKILL_ALCHEMY       =0
SKILL_ANATOMY       =1
SKILL_ANIMALLORE    =2
SKILL_ITEMID        =3
SKILL_ARMSLORE      =4
SKILL_Parrying      =5
SKILL_Begging       =6
SKILL_Blacksmithing =7
Skill_Bowcraft      =8
SKILL_PEACEMAKING   =9
Skill_Camping       =10
Skill_Carpentry     =11
SKILL_CARTOGRAPHY   =12
Skill_Cooking       =13
Skill_DetectHidden  =14
Skill_Enticement    =15
Skill_EvaluatingIntel =16
Skill_Healing       =17
Skill_Fishing       =18
Skill_Forensics     =19
Skill_Herding       =20
Skill_Hiding        =21
SKILL_PROVOCATION   =22
Skill_Inscription   =23
Skill_LockPicking   =24
Skill_Magery        =25
Skill_MagicResistance =26
Skill_Tactics       =27
Skill_Snooping      =28
Skill_Musicianship  =29
SKILL_POISONING     =30
Skill_Archery       =31
SKILL_SPIRITSPEAK   =32
Skill_Stealing      =33
Skill_Tailoring     =34
Skill_Taming        =35
skill_tasteid       =36
Skill_Tinkering     =37
Skill_Tracking      =38
Skill_Veterinary    =39
Skill_Swordsmanship =40
Skill_Macefighting  =41
Skill_Fencing       =42
Skill_Wrestling     =43
SKILL_LUMBERJACKING =44
SKILL_MINING        =45
SKILL_MEDITATION    =46
SKILL_STEALTH       =47
SKILL_REMOVETRAP    =48
SKILL_Necromancy    =49
SKILL_Focus         =50
SKILL_CHIVALRY      =51
SKILL_BUSHIDO       =52
SKILL_NINJITSU      =53
SKILL_SPELLWEAVING  =54

//*****************************************************************************
//*****************************************************************************
// FUNCTIONs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// skill_gain(skill) compute skill gain DEFAULT
//*****************************************************************************
//default is the player
[FUNCTION skill_gain]

OBJ=<uid>
LOCAL.amnt=<OBJ.<DEF.<argv[0]>>>

//sysmessage [SKILL] Skill: <argv[0]>::<eval <LOCAL.amnt>>
if (!<npc>)
    //verifica se ha spam de skill em andamento (max 1 skill por segundo)
    if (<TAG0.skill_ts>><SERV.time>)
        serv.log <account>: <argv[0]> SPAM
        TAG.skill_ts=<eval <SERV.time>+10>
        TAG.skill_spam=<TAG0.skill_spam>+1
        if (<TAG.skill_spam>>2)
            skill_spamDetected <argv[0]>
            return 1
        endif
    endif
    TAG.skill_ts=<eval <SERV.time>+10>
    TAG.skill_spam=0

    //se a skill nao pode subir mais devido ao limite por tempo
    if ( <LOCAL.amnt> >= <TAG.skill_maxSkillValue> )
    //  sysmessage [SKILL] Habilidade bloqueada
        TAG.skill_blocked=1
        if ( <LOCAL.amnt> > <TAG.skill_maxSkillValue> )
            TRY <DEF.<argv[0]>> <TAG.skill_maxSkillValue>
            serv.log <account>/<uid>: <argv[0]> passou dos limites: <LOCAL.amnt>
        endif
        serv.log <account>: <argv[0]> bloqueada
        return 1
    endif
endif

//define valor limiar para incremento de skill
//aumente o valor para skills usadas muito rapidamente, como sword
doswitch <DEF.<argv[0]>>
LOCAL.threshold=200     // SKILL_ALCHEMY
LOCAL.threshold=100     // SKILL_ANATOMY
LOCAL.threshold=1000    // SKILL_ANIMALLORE
LOCAL.threshold=1000    // SKILL_ITEMID
LOCAL.threshold=100     // SKILL_ARMSLORE
LOCAL.threshold=100     // SKILL_Parrying
LOCAL.threshold=100     // SKILL_Begging
LOCAL.threshold=100     // SKILL_Blacksmithing
LOCAL.threshold=1000    // Skill_Bowcraft
LOCAL.threshold=1000    // SKILL_PEACEMAKING
LOCAL.threshold=100     // Skill_Camping
LOCAL.threshold=100     // Skill_Carpentry
LOCAL.threshold=100     // SKILL_CARTOGRAPHY
LOCAL.threshold=1000    // Skill_Cooking
LOCAL.threshold=100     // Skill_DetectHidden
LOCAL.threshold=100     // Skill_Enticement
LOCAL.threshold=500     // Skill_EvaluatingIntel
LOCAL.threshold=1500    // Skill_Healing
LOCAL.threshold=100     // Skill_Fishing
LOCAL.threshold=100     // Skill_Forensics
LOCAL.threshold=1000     // Skill_Herding
LOCAL.threshold=500     // Skill_Hiding
LOCAL.threshold=1000    // SKILL_PROVOCATION
LOCAL.threshold=20      // Skill_Inscription
LOCAL.threshold=100     // Skill_LockPicking
LOCAL.threshold=1000    // Skill_Magery
LOCAL.threshold=100     // Skill_MagicResistance
LOCAL.threshold=100     // Skill_Tactics
LOCAL.threshold=2000    // Skill_Snooping
LOCAL.threshold=100     // Skill_Musicianship
LOCAL.threshold=100     // SKILL_POISONING
LOCAL.threshold=1000    // Skill_Archery
LOCAL.threshold=500     // SKILL_SPIRITSPEAK
LOCAL.threshold=100     // Skill_Stealing
LOCAL.threshold=100     // Skill_Tailoring
LOCAL.threshold=100     // Skill_Taming
LOCAL.threshold=100     // skill_tasteid
LOCAL.threshold=100     // Skill_Tinkering
LOCAL.threshold=100     // Skill_Tracking
LOCAL.threshold=100     // Skill_Veterinary
LOCAL.threshold=1000    // Skill_Swordsmanship
LOCAL.threshold=1000    // Skill_Macefighting
LOCAL.threshold=1000    // Skill_Fencing
LOCAL.threshold=1000    // Skill_Wrestling
LOCAL.threshold=1000    // SKILL_LUMBERJACKING
LOCAL.threshold=1000    // SKILL_MINING
LOCAL.threshold=1000    // SKILL_MEDITATION
LOCAL.threshold=100     // SKILL_STEALTH
LOCAL.threshold=100     // SKILL_REMOVETRAP
LOCAL.threshold=100     // SKILL_Necromancy
LOCAL.threshold=100     // SKILL_Focus
LOCAL.threshold=100     // SKILL_CHIVALRY
LOCAL.threshold=100     // SKILL_BUSHIDO
LOCAL.threshold=100     // SKILL_NINJITSU
LOCAL.threshold=100     // SKILL_SPELLWEAVING
enddo

//calcula incremento aleatorio de acordo com maximo permitido 
//(depende do skillsum)
LOCAL.inc=<eval {0 <TAG.skill_maxIncrease> }>
//soma com o minimo de incremento e os incrementos anteriores
LOCAL.inc=<eval <DEF.bonusIncrease>+<LOCAL.inc>+<TAG0.<argv[0]>>>

//sysmessage [SKILL] Acumulado: <LOCAL.inc>/<LOCAL.threshold>

//se passou do limiar, sobe skill
if ( <LOCAL.inc> > <LOCAL.threshold> )
  LOCAL.inc=<LOCAL.inc>-<LOCAL.threshold>
  LOCAL.s=<skilllock.<eval <def.<argv[0]>>>>
  if ( <LOCAL.s> == 0 ) //subir
    if ( <skilltotal> > <skillclass.skillsum> )
        serv.log [SKILL] <account> SKILLCLASS LIMIT
    else
        TRY <DEF.<argv[0]>> <EVAL <LOCAL.amnt> + 0.1>
    endif
  elif ( <LOCAL.s> == 1 && <LOCAL.amnt> > 100 ) //descer
    TRY <DEF.<argv[0]>> <EVAL <LOCAL.amnt> - 0.1>
  endif
  serv.log [SKILL] <account> <DEF.<argv[0]>> <LOCAL.amnt> <LOCAL.s>
endif

//salva incrementos
TRY TAG0.<argv[0]>=<LOCAL.inc>

//*****************************************************************************
// skill_spamDetected(skill)
//*****************************************************************************
[FUNCTION skill_spamDetected]

doswitch <DEF.<argv[0]>>
detem 300,Abuso de skills.  // SKILL_ALCHEMY
detem 300,Abuso de skills.  // SKILL_ANATOMY
detem 300,Abuso de skills.  // SKILL_ANIMALLORE
detem 300,Abuso de skills.  // SKILL_ITEMID
detem 300,Abuso de skills.  // SKILL_ARMSLORE
return 1                                        // SKILL_Parrying
detem 300,Abuso de skills.  // SKILL_Begging
detem 300,Abuso de skills.  // SKILL_Blacksmithing
detem 300,Abuso de skills.  // Skill_Bowcraft
detem 300,Abuso de skills.  // SKILL_PEACEMAKING
detem 300,Abuso de skills.  // Skill_Camping
detem 300,Abuso de skills.  // Skill_Carpentry
detem 300,Abuso de skills.  // SKILL_CARTOGRAPHY
detem 300,Abuso de skills.  // Skill_Cooking
detem 300,Abuso de skills.  // Skill_DetectHidden
detem 300,Abuso de skills.  // Skill_Enticement
detem 300,Abuso de skills.  // Skill_EvaluatingIntel
detem 300,Abuso de skills.  // Skill_Healing
detem 300,Abuso de skills.  // Skill_Fishing
detem 300,Abuso de skills.  // Skill_Forensics
detem 300,Abuso de skills.  // Skill_Herding
detem 300,Abuso de skills.  // Skill_Hiding 
detem 300,Abuso de skills.  // SKILL_PROVOCATION
detem 300,Abuso de skills.  // Skill_Inscription
detem 300,Abuso de skills.  // Skill_LockPicking
return 1                                        // Skill_Magery
return 1                                        // Skill_MagicResistance
return 1                                        // Skill_Tactics
detem 300,Abuso de skills.  // Skill_Snooping
detem 300,Abuso de skills.  // Skill_Musicianship
detem 300,Abuso de skills.  // SKILL_POISONING
return 1                                        // Skill_Archery
detem 300,Abuso de skills.  // SKILL_SPIRITSPEAK
detem 300,Abuso de skills.  // Skill_Stealing
detem 300,Abuso de skills.  // Skill_Tailoring
detem 300,Abuso de skills.  // Skill_Taming
detem 300,Abuso de skills.  // skill_tasteid
detem 300,Abuso de skills.  // Skill_Tinkering
detem 300,Abuso de skills.  // Skill_Tracking
detem 300,Abuso de skills.  // Skill_Veterinary
return 1                                        // Skill_Swordsmanship
return 1                                        // Skill_Macefighting
return 1                                        // Skill_Fencing
return 1                                        // Skill_Wrestling
detem 300,Abuso de skills.  // SKILL_LUMBERJACKING
detem 300,Abuso de skills.  // SKILL_MINING
detem 300,Abuso de skills.  // SKILL_MEDITATION
detem 300,Abuso de skills.  // SKILL_STEALTH
detem 300,Abuso de skills.  // SKILL_REMOVETRAP
return 1                                        // SKILL_Necromancy
detem 300,Abuso de skills.  // SKILL_Focus
return 1                                        // SKILL_CHIVALRY
return 1                                        // SKILL_BUSHIDO
return 1                                        // SKILL_NINJITSU
return 1                                        // SKILL_SPELLWEAVING
enddo
return 1

//*****************************************************************************
// skill_spamDetected(skill)
//*****************************************************************************
[FUNCTION skill_init]
TAG.skill_maxIncrease=<eval ((<skillclass.skillsum>-<skilltotal>)*100)/<skillclass.skillsum>>
if ( <TAG.skill_maxIncrease> < 0 )
  TAG.skill_maxIncrease=0
endif

TAG0.skill_maxSkillValue=<eval (<account.TOTALCONNECTTIME>/8)+600>
if ( <TAG0.skill_maxSkillValue> > 1000 )
  TAG.skill_maxSkillValue=1000
endif

[EOF]
