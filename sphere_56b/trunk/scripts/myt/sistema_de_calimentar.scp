//*****************************************************************************
//*****************************************************************************
//
// SISTEMA DE CADEIA ALIMENTAR por
//	Silver
//	UsMarine (vinicius.arroyo at gmail.com)
//
//*****************************************************************************
//*****************************************************************************
//
// Adicionar isso as animais:
// TEVENTS=e_marker_init
//
// TAGS
// tag.animal=1  carnivoro grande porte
// tag.animal=2	 carnivoro médio porte
// tag.animal=3	 carnivoro pequeno porte
// tag.animal=4  herbivoro grande
// tag.animal=5  herbivoro médio 
// tag.animal=6  herbivoro pequeno
// tag.ca_eat		 id body que o npc ira comer

[DEFNAME calimentar]
ca_fome	10

//*****************************************************************************
//*****************************************************************************
// EVENTs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// e_calimentar
//*****************************************************************************
[EVENTS e_calimentar]

ON=@DEATHCORPSE
f_gmLog <name> died <argo.name>
argo.tag.animal=<tag.animal>

//default: npc
//src: (n)pc being looked
ON=@NPCLookAtChar

//f_gmLog <name> see <src.name>/<act.name>/<argo.name>/<src.act.name>/<argo.act.name>

//desiste do alvo se em warmode
if (<flags>&statf_war)
//	f_gmLog <name> ignorewar <src.name>:
	return 0
//ou se alvo nao for animal
elif !( 0<src.tag.animal> )
//	f_gmLog <name> tags <src.name>:
	return 0
//consigo ver?
elif !( (0<src.canseelos>) )
//	f_gmLog <name> los <src.name>:
	return 0
//se sou um pet comportado
elif ( (0<memoryfindtype(memory_ipet)>) )
//	f_gmLog <name> pet <src.name>:
	return 0
endif

//se for carnivoro, tiver maior porte que o alvo, nao estiver 'cheio' e for mais forte
if ( <tag.animal> < 4 ) && ( <tag.animal> < <src.tag.animal> ) && (<food> < <def.ca_fome>) && (<str> >= <src.str>)
	f_gmLog <name> atacando <src.name> <src.tag.animal>
	attack <src>
	if (<R0,100> < <src.int>)
		src.attack <uid>
	endif
//se mesmo porte (aliado) e aliado precisando de ajuda (e vivo)
elif  ( <tag.animal>==<src.tag.animal> ) && (<src.flags>&statf_war) && (0<src.hits>) ) 
	f_gmLog <name> ajudando <src.name> <src.tag.animal>
	attack <src.memoryfindtype.memory_fight.link>
//else
//	f_gmLog <name> ignorando <src.name> <str>/<src.str>
endif
return 0

//corpses me interessam se for carnivoro
ON=@NPCLOOKATITEM

//f_gmLog <name> vendo <argo.name>	
if (<flags>&statf_war)
	return 0 //ignora se em warmode
elif ( <argo.type> != t_corpse )
	return 0 //ignora se nao for corpse
elif ( <argo.link.dex> )
	return 0 //ignora body de players
elif ( <tag.animal> > 3 )
	return 0 //ignora se for herbivoro
elif (<food> > <def.ca_fome>)
	return 0 //ignora se nao tiver com fome
elif (0<tag.ca_eat>) && (0<tag.ca_eat>!=<argo>)
	return 0 //ignora se ja estiver indo atras de outro
elif (<argo.distance> >= 2)
	tag.ca_eat=<argo>
	f_npcRunStepToUid <argo>,1
	f_gmLog <name> seguindo corpse <argo.name>
	return 0
else
//	f_gmLog <name> comendo corpse <argo.name>	
	tag.ca_eat=0
	emotered comendo
	if ((0<tag.animal>) == 1 )		// se é carnivoro de grande porte
		food=<food>+10
	elif ((0<tag.animal>) == 2 )
		food=<food>+20
	else	
		food=<food>+30
	endif
	serv.newitem i_blood_pool_large
	new.p=<p>
	sfx 03c
	new.attr=attr_decay|attr_move_never
	new.timer=15
	update
	argo.remove 	
endif
return 0

//src:player
//default:npc
ON=@NPCSEENEWPLAYER
if (<memoryfindtype(memory_ipet)>)
	return 0
//se carnivoro de grande porte e com fome, ataca player
elif (<tag.animal>==1) && ( <food> < 20 )
	f_gmLog <name> atacando player <src.name> f:<food>
	attack <src>
endif
return 1

ON=@CLICK
if ( ((0<memoryfindtype(memory_ipet).link>)==(<src.uid>)) )
	message <name> [companheiro]
	return 1
endif
return 0

ON=@CLIENTTOOLTIP
if ( ((0<memoryfindtype(memory_ipet).link>)==(<src.uid>)) )
	f_sendtooltip <name>,companheiro
else
	f_sendtooltipData <name>
endif
return 1

//*****************************************************************************
// e_marker_init
//*****************************************************************************
//mantido por compatibilidade com sistema antigo
[EVENTS e_marker_init]
ON=@EnvironChange
SRC.EVENTS +e_calimentar
SRC.EVENTS -e_marker_init
RETURN 0

//*****************************************************************************
//*****************************************************************************
// ITEMDEFs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// 01f13
//*****************************************************************************
//mantido por compatibilidade com sistema antigo
[ITEMDEF 01f13]
DEFNAME=i_monster_marker
NAME=Monster Marker
TYPE=t_script
LAYER=layer_special
CATEGORY=MyT - Provisions - GM Items
SUBSECTION=Special Items
DESCRIPTION=Monster Marker

ON=@Create
ATTR=04082

[EOF]