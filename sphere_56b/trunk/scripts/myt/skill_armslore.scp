//*****************************************************************************
//*****************************************************************************
//
// SKILL ARMS LORE
//
//  UsMarine (vinicius.arroyo at gmail.com)
//
//*****************************************************************************
//*****************************************************************************

//TODO:

//TAGS usadas:

//*****************************************************************************
//*****************************************************************************
// DEFNAMEs
//*****************************************************************************
//*****************************************************************************
[DEFNAME armslore_msg]
arml_dam1= Nao parece nem um pouco perigoso.
arml_dam2= Pode machucar alguem um pouco.
arml_dam3= Causa pouco dano.
arml_dam4= Causa um dano razoavel.
arml_dam5= Causa um dano consideravel.
arml_dam6= E uma arma perigosa.
arml_dam7= Causa um dano grande.
arml_dam8= Causa um dano mortal.
arml_dam9= E mortal.
arml_dam10= E extremamente mortal.
arml_dam11= E sobrenatural.
 
arml_arm1= Nao oferece protecao.
arml_arm2= Pode proteger um pouco.
arml_arm3= Protege pouco.
arml_arm4= Oferece uma protecao razoavel.
arml_arm5= Oferece uma protecao consideravel.
arml_arm6= Protege bem.
arml_arm7= Protege muito bem.
arml_arm8= E uma excelente protecao.
arml_arm9= E quase a melhor protecao.
arml_arm10= Te faz quase invencivel.
arml_arm11= Tem uma protecao sobrenatural

arml_dur1= quebrando.
arml_dur2= em boas condicoes.
arml_dur3= perfeito.

//*****************************************************************************
//*****************************************************************************
// FUNCTIONs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// sk_armslore - armslore skill
//*****************************************************************************
[FUNCTION sk_armslore]

if (<src.targ.canseelos> == 0)
    src.sysmessagered Seu alvo esta fora da linha de visao.
    return 1
elif (!<src.targ.isweapon>) && (!<src.targ.isarmor>)
    src.sysmessagered Use arms lore em armas ou armaduras.
    return 1
elif (<src.findid.i_armslore_mry>)
    src.sysmessagered Voce ainda nao pode analisar outro item.
    return 1
endif

src.emoteyellow Observando <src.targ.name>
src.newitem i_armslore_mry
NEW.more=<eval 11-(<src.armslore>/100)>

if ( <NEW.more> <= 0 )
    NEW.more=1
endif

NEW.link=<src.targ>
NEW.cont=<src>
NEW.timer=1

return 1    

//*****************************************************************************
// f_weaponTargetItem
//*****************************************************************************
//evita que os corpos dos players sejam cortados sem perder ponto de alma
[FUNCTION f_weaponTargetItem]
if (<src.targ.type>==t_corpse) && (<src.targ.link>!=04fffffff)
 src.morte_decrementaPontos 2
elif (<src.targ.type>==t_corpse) && (<src.targ.link>==04fffffff) && (<baseid>!=i_faca_pelar) && (<baseid>!=i_faca_corte) && (<baseid>!=i_cutelo)
 src.corte_normal <src.targ.uid>
elif (<src.targ.type>==t_log)
 src.sysmessageyellow Utilize as ferramentas de carpintaria.
 return 1
endif

//*****************************************************************************
// f_weaponDclick
//*****************************************************************************
//Evita criação de arcos complexos com dagger e strip de resources padrão dos corpses
[FUNCTION f_weaponDclick]
if (<baseid>!=i_faca_pelar) && (<baseid>!=i_faca_corte) && (<baseid>!=i_cutelo)
 src.equip <UID>
 src.sysmessageyellow Aonde voce gostaria de usar isso?
 target
 return 1
endif


//*****************************************************************************
// f_weaponTooltip( [skipTooltipAndReturnText] )
//*****************************************************************************
//mostra tooltip de armas de acordo com armslore
[FUNCTION f_weaponTooltip]
if (!<maxhits>)
    serv.log ALOOOW: <name> <type> <uid> <src>
endif
//gm ve tudo
if (<src.gm>)
    LOCAL.e=0
    LOCAL.c=1
else
    //calcular erro de 1 a 100
    LOCAL.e=((1000-<src.armslore>)/10)
    //variavel que diz se pode mostrar dados numericos
    LOCAL.c=<src.armslore> > 500
endif

//definir nome de exibição
if (<ATTR>&attr_identified) || (<SRC.IsGM>)
 IF (<ISEMPTY <DEF.<BASEID>_IDNAME>>)
  LOCAL.name=<name>
 ELSE
  LOCAL.name=<DEF.<BASEID>_IDNAME>
 ENDIF
ELSE
 LOCAL.name=<name>
ENDIF

//mostrar atributos
IF !(<attr>&attr_identified) && (!<src.IsGM>)
 LOCAL.t = (nao identificado)<DEF.BR>
ELSE
 IF (<more2>==20)
  LOCAL.t = (envenenado)<DEF.BR>
 ELIF (<attr>&ATTR_MAGIC)
  LOCAL.t = (mágico)<DEF.BR>
  IF (<more2>) && (<more2>!=20)
   LOCAL.t .= Magia: <SERV.SPELL.<more2>.NAME><DEF.BR>
  ENDIF
  IF (<DMGFIRE>)
   LOCAL.t .= "+<eval <DMGFIRE>> (fogo)<DEF.BR>"
  ENDIF
  IF (<DMGCOLD>)
   LOCAL.t .= "+<eval <DMGCOLD>> (gelo)<DEF.BR>"
  ENDIF
  IF (<DMGPOISON>)
   LOCAL.t .= "+<eval <DMGPOISON>> (veneno)<DEF.BR>"
  ENDIF
  IF (<DMGENERGY>)
   LOCAL.t .= "+<eval <DMGENERGY>> (eletricidade)<DEF.BR>"
  ENDIF
 ELSE
  LOCAL.t = (identificado)<DEF.BR>
 ENDIF
 IF (<attr>&attr_cursed)
  LOCAL.t .= (amaldiçoado)<DEF.BR>
 ENDIF
 IF (<attr>&attr_cursed2)
  LOCAL.t .= (maldito)<DEF.BR>
 ENDIF
 IF (<attr>&attr_blessed)
  LOCAL.t .= (abençoado)<DEF.BR>
 ENDIF
 IF (<attr>&attr_blessed2)
  LOCAL.t .= (sagrado)<DEF.BR>
 ENDIF
ENDIF

//mostra dano
LOCAL.t .= "Dano: "
LOCAL.min=<f_spreadValue <dam.lo>,<LOCAL.e>>
LOCAL.max=<f_spreadValue <dam.hi>,<LOCAL.e>>
LOCAL.d=<eval (<dam.hi>+<dam.lo>)/2>
if (<LOCAL.max> < <LOCAL.min>)
    LOCAL.x=<LOCAL.max>
    LOCAL.max=<LOCAL.min>
    LOCAL.min=<LOCAL.x>
endif
if (<LOCAL.d><5)
    LOCAL.t .= Fraco
elif (<LOCAL.d><10)
    LOCAL.t .= Baixo
elif (<LOCAL.d><25)
    LOCAL.t .= Medio
elif (<LOCAL.d><40)
    LOCAL.t .= Forte
else
    LOCAL.t .= Muito forte
endif
if (<LOCAL.c>)
    LOCAL.t .= " (<eval <LOCAL.min>>/<eval <LOCAL.max>>)"
endif

//mostra durabilidade
LOCAL.t .= "<def.br>Qualidade: "
LOCAL.min=<f_spreadValue <hits>,<LOCAL.e>>
LOCAL.max=<f_spreadValue <maxhits>,<LOCAL.e>>
LOCAL.d=<eval (<hits>*100)/<maxhits>>
if (<LOCAL.max> < <LOCAL.min>)
    LOCAL.x=<LOCAL.max>
    LOCAL.max=<LOCAL.min>
    LOCAL.min=<LOCAL.x>
endif
if (<LOCAL.d><25)
    LOCAL.t .= Pessimo
elif (<LOCAL.d><50)
    LOCAL.t .= Degradado
elif (<LOCAL.d><75)
    LOCAL.t .= Bom
else
    LOCAL.t .= Perfeito
endif
if (<LOCAL.c>)
    LOCAL.t .= " (<eval <LOCAL.min>>/<eval <LOCAL.max>>)"
endif

//forca necessaria
LOCAL.t .= "<def.br>Forca para uso: "
LOCAL.min=<f_spreadValue <reqstr>,<LOCAL.e>>
if (<reqstr><25)
    LOCAL.t .= Nenhuma
elif (<reqstr><50)
    LOCAL.t .= Pouca
elif (<reqstr><75)
    LOCAL.t .= Moderada
else
    LOCAL.t .= Alta
endif
if (<LOCAL.c>)
    LOCAL.t .= " (<eval <LOCAL.min>>)"
endif

//duas maos?
if (<TWOHANDS>)
    LOCAL.t .= "<def.br>Duas maos"
endif

//definir TOOLTIP se houver
if (<ATTR>&attr_identified) || (<SRC.IsGM>)
 IF (!<ISEMPTY <DEF.<BASEID>_IDTIP>>)
  LOCAL.t .= <DEF.BR><DEF.BFONT_CYAN><DEF.<BASEID>_IDTIP>
 ENDIF
ENDIF

//Sneek Peak de GM?
IF !(<attr>&attr_identified) && (<src.isgm>)
 LOCAL.t .= <DEF.BR><DEF.BR><DEF.BFONT_LRED>[MODO GM]<DEF.BR><DEF.BFONT_white>(Nao identificado)
ENDIF

//LOCAL.t .= <def.br> Durabilidade
//f_sendTooltip <LOCAL.name>,<local.t>

if !( 0<argv0> )
    src.addcliloc 1042971,<LOCAL.name>
    src.addcliloc 1070722,<LOCAL.t>
else
    return <LOCAL.t>
endif

//*****************************************************************************
// f_armorTooltip
//*****************************************************************************
//mostra tooltip de armaduras de acordo com armslore
[FUNCTION f_armorTooltip]
//gm ve tudo
if (<src.gm>)
    LOCAL.e=0
    LOCAL.c=1
else
    //calcular erro de 1 a 100
    LOCAL.e=((1000-<src.armslore>)/10)
    //variavel que diz se pode mostrar dados numericos
    LOCAL.c=<src.armslore> > 500
endif

//definir nome de exibição
if (<ATTR>&attr_identified) || (<SRC.IsGM>)
 IF (<ISEMPTY <DEF.<BASEID>_IDNAME>>)
  LOCAL.name=<name>
 ELSE
  LOCAL.name=<DEF.<BASEID>_IDNAME>
 ENDIF
ELSE
 LOCAL.name=<name>
ENDIF

//mostrar atributos
IF !(<attr>&attr_identified) && (!<src.IsGM>)
 LOCAL.t = (nao identificado)<DEF.BR>
ELSE
 IF (<attr>&ATTR_MAGIC)
  LOCAL.t = (mágico)<DEF.BR>
  IF (<more2>) && (<more2>!=20)
   LOCAL.t .= Magia: <SERV.SPELL.<more2>.NAME><DEF.BR>
  ENDIF
  IF (<morex>)
   LOCAL.t .= "+<eval <morex>> (fogo)<DEF.BR>"
  ENDIF
  IF (<morey>)
   LOCAL.t .= "+<eval <morey>> (gelo)<DEF.BR>"
  ENDIF
  IF (<morez>)
   LOCAL.t .= "+<eval <morez>> (veneno)<DEF.BR>"
  ENDIF
  IF (<morem>)
   LOCAL.t .= "+<eval <morem>> (eletricidade)<DEF.BR>"
  ENDIF
 ELSE
  LOCAL.t = (identificado)<DEF.BR>
 ENDIF
 IF (<attr>&attr_cursed)
  LOCAL.t .= (amaldiçoado)<DEF.BR>
 ENDIF
 IF (<attr>&attr_cursed2)
  LOCAL.t .= (maldito)<DEF.BR>
 ENDIF
 IF (<attr>&attr_blessed)
  LOCAL.t .= (abençoado)<DEF.BR>
 ENDIF
 IF (<attr>&attr_blessed2)
  LOCAL.t .= (sagrado)<DEF.BR>
 ENDIF
ENDIF

//mostra dano
LOCAL.t .= "Protecao: "
LOCAL.min=<f_spreadValue <armor.lo>,<LOCAL.e>>
LOCAL.max=<f_spreadValue <armor.hi>,<LOCAL.e>>
LOCAL.d=<eval (<armor.hi>+<armor.lo>)/2>
if (<LOCAL.max> < <LOCAL.min>)
    LOCAL.x=<LOCAL.max>
    LOCAL.max=<LOCAL.min>
    LOCAL.min=<LOCAL.x>
endif
if (<LOCAL.d><5)
    LOCAL.t .= Fraca
elif (<LOCAL.d><10)
    LOCAL.t .= Pouca
elif (<LOCAL.d><25)
    LOCAL.t .= Media
elif (<LOCAL.d><40)
    LOCAL.t .= Forte
else
    LOCAL.t .= Muito forte
endif
if (<LOCAL.c>)
    LOCAL.t .= " (<eval <LOCAL.min>>/<eval <LOCAL.max>>)"
endif

//mostra durabilidade
LOCAL.t .= "<def.br>Qualidade: "
LOCAL.min=<f_spreadValue <hits>,<LOCAL.e>>
LOCAL.max=<f_spreadValue <maxhits>,<LOCAL.e>>
LOCAL.d=<eval (<hits>*100)/<maxhits>>
if (<LOCAL.max> < <LOCAL.min>)
    LOCAL.x=<LOCAL.max>
    LOCAL.max=<LOCAL.min>
    LOCAL.min=<LOCAL.x>
endif
if (<LOCAL.d><25)
    LOCAL.t .= Pessimo
elif (<LOCAL.d><50)
    LOCAL.t .= Degradado
elif (<LOCAL.d><75)
    LOCAL.t .= Bom
else
    LOCAL.t .= Perfeito
endif
if (<LOCAL.c>)
    LOCAL.t .= " (<eval <LOCAL.min>>/<eval <LOCAL.max>>)"
endif

//forca necessaria
LOCAL.t .= "<def.br>Forca para uso: "
LOCAL.min=<f_spreadValue <reqstr>,<LOCAL.e>>
if (<reqstr><25)
    LOCAL.t .= Nenhuma
elif (<reqstr><50)
    LOCAL.t .= Pouca
elif (<reqstr><75)
    LOCAL.t .= Moderada
else
    LOCAL.t .= Alta
endif
if (<LOCAL.c>)
    LOCAL.t .= " (<eval <LOCAL.min>>)"
endif

//definir TOOLTIP se houver
if (<ATTR>&attr_identified) || (<SRC.IsGM>)
 IF (!<ISEMPTY <DEF.<BASEID>_IDTIP>>)
  LOCAL.t .= <DEF.BR><DEF.BFONT_CYAN><DEF.<BASEID>_IDTIP>
 ENDIF
ENDIF

//Sneek Peak de GM?
IF !(<attr>&attr_identified) && (<src.isgm>)
 LOCAL.t .= <DEF.BR><DEF.BR><DEF.BFONT_LRED>[MODO GM]<DEF.BR><DEF.BFONT_white>(Nao identificado)
ENDIF

//f_sendTooltip <name>,<local.t>

if !( 0<argv0> )
    src.addcliloc 1042971,<LOCAL.name>
    src.addcliloc 1070722,<LOCAL.t>
else
    return <LOCAL.t>
endif

//*****************************************************************************
//*****************************************************************************
// TYPEDEFs - ARMAS
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// t_weapon_mace_smith
//*****************************************************************************
[TYPEDEF t_weapon_mace_smith]
ON=@CLIENTTOOLTIP
f_weaponTooltip
return 1

//*****************************************************************************
// t_weapon_mace_sharp
//*****************************************************************************
[TYPEDEF t_weapon_mace_sharp]
ON=@TARGON_ITEM
f_weaponTargetItem
return 0

ON=@CLIENTTOOLTIP
f_weaponTooltip
return 1

//*****************************************************************************
// t_weapon_mace_staff
//*****************************************************************************
[TYPEDEF t_weapon_mace_staff]
ON=@CLIENTTOOLTIP
f_weaponTooltip
return 1

//*****************************************************************************
// t_weapon_mace_crook
//*****************************************************************************
[TYPEDEF t_weapon_mace_crook]
ON=@CLIENTTOOLTIP
f_weaponTooltip
return 1

//*****************************************************************************
// t_weapon_mace_pick
//*****************************************************************************
[TYPEDEF t_weapon_mace_pick]
ON=@CLIENTTOOLTIP
f_weaponTooltip
return 1


//*****************************************************************************
// t_weapon_axe
//*****************************************************************************
[TYPEDEF t_weapon_axe]
ON=@TARGON_ITEM
f_weaponTargetItem
return 0

ON=@CLIENTTOOLTIP
f_weaponTooltip
return 1

ON=@DClick
f_weaponDclick
return 1

//*****************************************************************************
// t_weapon_sword
//*****************************************************************************
[TYPEDEF t_weapon_sword]
ON=@TARGON_ITEM
f_weaponTargetItem
return 0

ON=@CLIENTTOOLTIP
f_weaponTooltip
return 1

ON=@DClick
f_weaponDclick
return 1

//*****************************************************************************
// t_weapon_fence
//*****************************************************************************
[TYPEDEF t_weapon_fence]
ON=@TARGON_ITEM
f_weaponTargetItem
return 0

ON=@CLIENTTOOLTIP
f_weaponTooltip
return 1

ON=@DClick
f_weaponDclick
return 1

//*****************************************************************************
// t_weapon_arrow
//*****************************************************************************
[TYPEDEF t_weapon_arrow]

//*****************************************************************************
// t_weapon_bolt
//*****************************************************************************
[TYPEDEF t_weapon_bolt]

//*****************************************************************************
// t_weapon_bow
//*****************************************************************************
[TYPEDEF t_weapon_bow]
ON=@CLIENTTOOLTIP
f_weaponTooltip
return 1

//*****************************************************************************
// t_weapon_xbow
//*****************************************************************************
[TYPEDEF t_weapon_xbow]
ON=@CLIENTTOOLTIP
f_weaponTooltip
return 1


//*****************************************************************************
//*****************************************************************************
// TYPEDEFs - ARMADURAS
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// t_armor
//*****************************************************************************
[TYPEDEF t_armor]
ON=@CLIENTTOOLTIP
f_armorTooltip
return 1

//*****************************************************************************
// t_weapon_xbow
//*****************************************************************************
[TYPEDEF t_armor_leather]
ON=@CLIENTTOOLTIP
f_armorTooltip
return 1

//*****************************************************************************
//*****************************************************************************
// ITEMDEFs
//*****************************************************************************
//*****************************************************************************

[ITEMDEF i_armslore_mry]
id=i_deed
name=Armslore Memory
type=t_eq_script
layer=layer_special
weight=0

ON=@TIMER

// Se estiver fora de visao
if (<cont>!=<link.topobj>) && (<cont.canseelos <link.topobj>> == 0)
    cont.sysmessageyellow Seu alvo esta fora da linha de visao.
    remove
    return 1
endif 

// Se for perturbado ou estiver incapacitado:
if (<cont.f_isDisturbed>)
    cont.sysmessageyellow Voce nao esta em condicoes de analisar nada.
    remove
    return 1
endif

//loop de animacao
if (<more>)
    more=<more>-1
    timer=1
    return 1 
endif

LOCAL.d=<eval (<link.dam.lo>+<link.dam.hi>)/2>
LOCAL.a=<eval (<link.armor.lo>+<link.armor.hi>)/2>
if ( <cont.armslore> < 30.0 )
    LOCAL.dam=<eval ((<LOCAL.d>/25)*5)+2>
    LOCAL.armor=<eval ((<LOCAL.a>/25)*5)+2>
elif ( <cont.armslore> < 70.0 )
    LOCAL.dam=<eval ((<LOCAL.d>/10)*2)+1>
    LOCAL.armor=<eval ((<LOCAL.d>/10)*2)+1>
else
    LOCAL.dam=<eval (<LOCAL.d>/5)+1>
    LOCAL.armor=<eval (<LOCAL.d>/5)+1>
endif
if ( <LOCAL.dam> > 11 )
    LOCAL.dam = 11
endif
if ( <LOCAL.armor> > 11 )
    LOCAL.armor = 11
endif

if (<link.isweapon>)
    LOCAL.t=<link.name>: <DEF.arml_dam<eval <LOCAL.dam>>>
else
    LOCAL.t=<link.name>: <DEF.arml_arm<eval <LOCAL.armor>>>
endif

LOCAL.h=<eval ((<link.hits>*3)/<link.maxhits>)+1>

if ( <LOCAL.h> > 3 )
  LOCAL.h = 3
endif

cont.sysmessagegreen <LOCAL.t> Esta <DEF.arml_dur<eval <LOCAL.h>>>

cont.skill_gain skill_armslore
remove
return 1

[EOF]