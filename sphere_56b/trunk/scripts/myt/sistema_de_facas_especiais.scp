/////////////////////////////////////////////////////////
///////////// Sistema de Facas especiais ///////////////
/////////////       Jean Gabin (MYT)     ///////////////
/////////////         beta v. 04         ///////////////
/////////////////////////////////////////////////////////



///////////////////////////////////////////////////////////
//////////////  ferramentas  /////////////////////////////
/////////////////////////////////////////////////////////


[ITEMDEF i_faca_pelar]  //Skinning kinife - usada para tirar o couro dos seres
NAME=Faca para pelar
id=0ec4
TYPE=T_WEAPON_FENCE
RESOURCES=2 i_ingot_iron
DAM=1,10
//SPEED=80
SKILL=Swordsmanship
REQSTR=5
TWOHANDS=N
WEIGHT=1
CATEGORY=MyT - Provisions - Weapons
SUBSECTION=Knives & Daggers
DESCRIPTION=Faca para pelar

ON=@Create
HITPOINTS={31 40}

on=@dclick
src.equip <UID>
src.SYSMESSAGEYELLOW  Em que voce deseja usar isso?
target
return 1

ON=@Targon_ground 
 SRC.SYSMESSAGERED Use esta faca apenas para tirar o couro ou o pelo de seres mortos.
RETURN 1  

on=@targon_char
 SRC.SYSMESSAGERED Use esta faca apenas para tirar o couro ou o pelo de seres mortos.
return 1

ON=@Targon_item 
if (<src.targ.type>!=t_corpse)
 SRC.SYSMESSAGERED Use esta faca apenas para tirar o couro ou o pelo de seres mortos.
elif (<src.targ.link>!=04fffffff)
 return 0
else
 src.pelar <src.targ.uid>
endif
return 1


/////////////////////////////////////



[ITEMDEF i_cutelo]
//Cleaver - usada para cortar a cabeça de alguns animais e seres para fazer troféu e/ou mascara. corta o corpo humano em partes também
id=0ec2
Name=Cutelo de Acougueiro
TYPE=T_WEAPON_SWORD
RESOURCES=3 i_ingot_iron
DAM=2,13
//SPEED=70
SKILL=Swordsmanship
REQSTR=10
TWOHANDS=N
WEIGHT=1

CATEGORY=MyT - Provisions - Weapons
SUBSECTION=Knives & Daggers
DESCRIPTION=Cutelo de acougueiro

ON=@Create
HITPOINTS={31 50}

on=@dclick
src.equip <UID>
src.SYSMESSAGEYELLOW Em que voce deseja usar isso?
target
return 1

ON=@Targon_ground 
 SRC.SYSMESSAGERED Use o cutelo apenas para cortar a carne de seres mortos.
RETURN 1  

ON=@Targon_item
if (<src.targ.type>==t_fish)
 src.corte_peixe <src.targ.uid>
elif (<src.targ.type>!=t_corpse)
 SRC.SYSMESSAGERED Use o cutelo apenas para cortar a carne de seres mortos.
elif (<src.targ.link>!=04fffffff)
 return 0
else
 src.acogueiro_cutelo <src.targ.uid>
endif
return 1

on=@targon_char
 SRC.SYSMESSAGERED Use o cutelo apenas para cortar a carne de seres mortos.
return 1


//////////////////////////////////////



[ITEMDEF 013f6]
defname=i_faca_corte
name=Faca de acogueiro
WEIGHT=2
TYPE=T_WEAPON_SWORD
FLIP=1
DAM=2,8
//SPEED=80
SKILL=Swordsmanship
REQSTR=5
TWOHANDS=N
DUPELIST=013f7

CATEGORY=MyT - Provisions - Weapons
SUBSECTION=Knives & Daggers
DESCRIPTION=Faca de acougueiro

ON=@Create
HITPOINTS={31 40}

on=@dclick
src.equip <UID>
src.SYSMESSAGEYELLOW Em que voce deseja usar isso?
target
return 1

ON=@Targon_ground 
 SRC.SYSMESSAGERED Use esta faca apenas para extrair carne e orgaos de seres mortos.
RETURN 1  

on=@targon_char
 SRC.SYSMESSAGERED Use esta faca apenas para extrair carne e orgaos de seres mortos.
return 1

ON=@Targon_item 
if (<src.targ.type>==t_fish)
 src.corte_peixe <src.targ.uid>
elif (<src.targ.topobj.uid> == <src.uid>)
 src.sysmessageRED coloque o corpo em uma mesa ou no chão para poder extrair a carne e os orgaos.
elif (<src.targ.type>!=t_corpse)
 SRC.SYSMESSAGERED Use o cutelo apenas para cortar a carne de seres mortos. 
elif (<src.targ.link>!=04fffffff)
 return 0
else
 src.acogueiro <src.targ.uid>
endif
return 1


/////////////////////////////////////////////
//////////// funções ///////////////////////
///////////////////////////////////////////

[FUNCTION sangrar]
	sfx 059
IF (<IsEmpty <serv.chardef.<morex>.bloodcolor>>)
 return 1
endif
LOCAL.color=<serv.chardef.<morex>.bloodcolor>
dorand 5
	serv.newitem=0122a
	serv.newitem=0122b
	serv.newitem=0122d
	serv.newitem=0122e
	serv.newitem=0122f
enddo
	new.color=<LOCAL.color>
	new.attr=012
	new.p=<p>
	new.timer={60 80}
dorand 5
	serv.newitem=0122a
	serv.newitem=0122b
	serv.newitem=0122d
	serv.newitem=0122e
	serv.newitem=0122f
enddo
	new.attr=012
	new.color=<LOCAL.color>
	new.movenear <uid> 1
	new.timer={60 80}
return 1

///////////////////////////////////////////////////////////////////////////////

[FUNCTION corte_normal]
//Retirar pelo, pele, penas e carne do corpo com penalidade.
obj=<argn>
ctag.penalty=1
IF (<obj.morey>&07)
 sysmessageyellow Nao ha nada para tirar nesse corpo.
 return 1
ENDIF
obj.morey |= 07
obj.sangrar
anim 32
emoteyellow cortando corpo
f_get_corpse_resources <obj.uid>,t_feather;t_hide;t_fur;t_reagente;t_script;i_ribs_raw;I_CHICKEN_LEG_RAW

[FUNCTION corte_peixe]
//Retirar pelo, pele, penas e carne do corpo com penalidade.
obj=<argn>
anim 9
emoteyellow cortando peixe
f_get_corpse_resources <obj.uid>,;t_script;T_FOOD_RAW
uid.<argn>.decrement

[FUNCTION pelar]
//Retirar pelo, pele, penas e carne do corpo.
obj=<argn>
IF (<obj.morey>&01)
 sysmessageyellow Este corpo ja foi depenado.
 return 1
ENDIF
obj.morey |= 01
obj.sangrar
anim 32
emoteyellow depenando corpo
f_get_corpse_resources <obj.uid>,t_feather;t_hide;t_fur;t_reagente;t_script

[FUNCTION acogueiro]
//Retirar carne do corpo
obj=<argn>
IF (<obj.morey>&02)
 sysmessageyellow Este corpo ja foi retalhado.
 return 1
ENDIF
obj.morey |= 02
obj.sangrar
anim 32
emoteyellow retalhando corpo
f_get_corpse_resources <obj.uid>,i_ribs_raw;i_bird_raw;i_chicken_leg_raw

[FUNCTION acogueiro_cutelo]
//Retirar miudos do corpo
obj=<argn>
IF (<obj.morey>&04)
 sysmessageyellow Este corpo nao tem mais miudos.
 return 1
ENDIF
obj.morey |= 04
obj.sangrar
anim 32
emoteyellow retirando miudos do corpo
f_get_corpse_resources <obj.uid>,i_lamb_leg_raw;i_rabbit_raw;i_ham_raw;i_pig_raw;i_bacon_slab_raw;i_reag_worm_heart;i_flesh_heart;i_flesh_liver;i_flesh_brain

[FUNCTION f_get_corpse_resources]
//<argv[0]> = UID do corpse
//<argv[1]> = string com os TYPEs e/ou ITEMDEFs separador por ; (ex.: t_meat_raw;t_feather;t_hide;i_ribs_raw)
obj=<argv[0]>
if (<obj.type>==t_corpse)
 LOCAL.resources=<SERV.CHARDEF.<obj.morex>.resources>
elif (<obj.type>==t_fish)
 LOCAL.resources=<obj.TAG.fish>
endif
for r 1 <f_count_resources <LOCAL.resources>>
 LOCAL.res=<f_strip_resources <LOCAL.r>,<LOCAL.resources>>
 LOCAL.amt=<strarg <LOCAL.res>>
 IF (<LOCAL.amt>)
  LOCAL.res=<streat <LOCAL.res>>
 ENDIF
 IF (STRMATCH(*<SERV.itemdef.<LOCAL.res>.type>*,<argv[1]>)) || (STRMATCH(*<LOCAL.res>*,<argv[1]>))
  LOCAL.done=1
  IF (<f_try_get_corpse_resource <LOCAL.res>>)
   IF (<LOCAL.amt> > 2)
    LOCAL.amt=<BETWEEN 0,<LOCAL.amt>,<anatomy>,100.0>
   ENDIF
   skill_gain Skill_Anatomy
   serv.newitem <LOCAL.res>
   if (<LOCAL.amt>)
    //aplica penalidade se estiver usando uma ferramenta que não seja uma faca especial.
    if (<ctag0.penalty>)
     new.amount=<EVAL (<local.amt>*3)/5>
    else
     new.amount=<LOCAL.amt>
    endif
    if (!<new.amount>)
     new.amount 1
    endif
   endif
   //Salva que tipo de couro é
   if (<new.type>==t_fur) || (<new.type>==t_hide)
    new.more2=<uid.<argv0>.morex>
    new.color=<uid.<argv0>.color>
   endif
   //Se é corpo, põe dentro do corpo. Se não é corpo, bounce.
   if (<uid.<argv[0]>.type>==t_corpse)
    new.cont=<argv[0]>
   else
    bounce <new>
   endif
  ENDIF
 ENDIF
endfor
if (<local.done>)
 sysmessagegreen Voce pode cortar o corpo e retirar alguma coisa.
else
 sysmessagegreen Voce corta o corpo man nao tira nada de util.
endif
ctag.penalty

[FUNCTION f_try_get_corpse_resource]
//Checa skill para tirar aquele resource em específico
//Testa o SKILLMAKE do itemdef (ex.: SKILLMAKE=ANATOMY 60.0)
//<args> = ITEMDEF
IF (<IsEmpty <serv.itemdef.<args>.SKILLMAKE>>)
 //Se não precisa de skill, retornar 1 mesmo.
 return 1
else
try LOCAL.skill=<src.<streat <serv.itemdef.<args>.SKILLMAKE>>>
if (<ctag0.penaly>)
 //aplica penalidade se estiver usando uma ferramenta que não seja uma faca especial.
 LOCAL.skill=<eval (<LOCAL.skill>*3)/5>
endif
IF (<BELLTEST <LOCAL.skill>,<strarg <serv.itemdef.<args>.SKILLMAKE>>>)
 //Se ele consegue tirar o resource, retornar 1.
 return 1
endif
//Não consegue tirar o resource. Retornar 0
return 0

[FUNCTION f_strip_resources]
//Explode e retorna a resource ARGN
return <argv[<eval <argn>>]>

[FUNCTION f_count_resources]
//Conta quantas resources tem <args>
return <argv>