//*****************************************************************************
//*****************************************************************************
//
// SISTEMA DE SKILLS por
//
//			Galthar
//			UsMarine (vinicius.arroyo at gmail.com)
//
//*****************************************************************************
//*****************************************************************************

//TODO:
//-ajustar valores (roupas, temperaturas)
//-reativar danos devido ao clima

//TAGS usadas:
//REGION.TAG.modtemp	//modificador de temperatura por regiao
//SRC.TAG.no_clima	//habilita ou desabilita checagem de clima
//
//VARS usadas:
//VAR.tMin
//VAR.tHour
//VAR.tDay
//VAR.tMonth
//VAR.tYear
//VAR.tSeason
//VAR.temperature

//*****************************************************************************
//*****************************************************************************
// DEFNAMEs
//*****************************************************************************
//*****************************************************************************

[DEFNAME tempo]
minutos_por_tick=6
segundos_por_minuto=10
minutos_por_hora=60
horas_por_dia=24
dias_por_mes=30
meses_por_ano=12

//valores max e min de temperaturas suportadas
tBodyMax2=50
tBodyMax1=30
tBodyMin1=18
tBodyMin2=10


//corresponde ao código de luz do Sphere para cada
//hora do dia.
luz_0=28
luz_1=27
luz_2=26
luz_3=25
luz_4=24
luz_5=21
luz_6=15
luz_7=8
luz_8=6
luz_9=3
luz_10=2
luz_11=2
luz_12=1
luz_13=2
luz_14=2
luz_15=3
luz_16=6
luz_17=8
luz_18=15
luz_19=21
luz_20=24
luz_21=25
luz_22=26
luz_23=27
luz_24=28

temp_0=15
temp_1=15
temp_2=15
temp_3=16
temp_4=16
temp_5=17
temp_6=17
temp_7=18
temp_8=18
temp_9=19
temp_10=19
temp_11=19
temp_12=20
temp_13=19
temp_14=19
temp_15=18
temp_16=18
temp_17=17
temp_18=17
temp_19=16
temp_20=16
temp_21=16
temp_22=15
temp_23=15

temp_verao=7
temp_outono=-2
temp_inverno=-11
temp_primavera=0

dungeon_light=25

//*****************************************************************************
//*****************************************************************************
// EVENTs
//*****************************************************************************
//*****************************************************************************
[EVENTS e_clima]
ON=@LOGIN
src.findid.i_mry_clima.remove
src.newitem=i_mry_clima
src.act.link <src.uid>
src.act.cont <src.uid>

ON=@ENVIRONCHANGE
//sysmessage [e_clima] ENVIRONCHANGE

SRC.events -e_clima

if (<SRC.REGION.flags>&region_flag_underground)
 SECTOR.light <DEF.dungeon_light>
 SRC.events +e_clima
 return 1
endif

LOCAL.luz=<DEF.luz_<eval <var.tHour>>>
//sysmessage [LUZ]  <LOCAL.luz>
if (<SECTOR.light>!=<LOCAL.luz>)
 SECTOR.light=<LOCAL.luz>
endif

SRC.EVENTS +e_clima

ON=@ITEMDCLICK
if (<SRC.ACT.type>==t_clock)
 SRC.time_now
 return 1
endif

//*****************************************************************************
//*****************************************************************************
// ITEMDEFs
//*****************************************************************************
//*****************************************************************************

[ITEMDEF i_pedra_tempo]
ID=01810
NAME=Pedra dos Tempos
CATEGORY=MyT - Provisions - GM Items
SUBSECTION=Tempo
DESCRIPTION=Pedra dos Tempos
TYPE=t_script

on=@click
say <instances>

ON=@create
attr=attr_static|attr_move_never
if (<instances> > 1)
 remove
 return 1
endif

on=@dclick
dialog d_tempo
return 1

//*****************************************************************************
// i_clima_off
//*****************************************************************************
[ITEMDEF i_clima_off]
ID=0495
type=t_script
name=clima off (entra)

ON=@CREATE
ATTR=attr_invis|attr_move_never
TIMER=0
COLOR=084c

ON=@STEP
SRC.TAG.no_clima=1

//*****************************************************************************
// i_clima_on
//*****************************************************************************
[ITEMDEF i_clima_on]
ID=0495
type=t_script
name=clima on (sai)

ON=@CREATE
ATTR=attr_invis|attr_move_never
TIMER=0
COLOR=0b0c

ON=@STEP
SRC.TAG.no_clima=0

//*****************************************************************************
// i_termometro
//*****************************************************************************
[itemdef i_termometro]
id=0104b
name=Termometro
type=t_script
CATEGORY=MyT - Provisions - GM Items
SUBSECTION=Tempo
DESCRIPTION=Termometro
weight=0

ON=@CREATE
color=0321

ON=@DCLICK
message Temperatura: <eval <temp_get>>oC
message Estacao: <VAR.tSeason>
return 1

//*****************************************************************************
// i_mry_clima
//*****************************************************************************
[ITEMDEF i_mry_clima]
ID=i_deed
NAME=controle de clima
TYPE=t_eq_script
LAYER=30

ON=@CREATE
TIMER=1

ON=@TIMER
TIMER={4 8}
OBJ=<CONT.uid>
OBJ.temp_check
if (<CONT.flags>&~statf_indoors)
 if (<MORE1> > 0)
   OBJ.temp_addSnow
   MORE1=<MORE1>-1
 endif
 if ( (<MORE2> > 0)
   OBJ.temp_addSplash
   MORE2=<MORE2>-1
  endif
endif
return 1

//*****************************************************************************
// i_neve
//*****************************************************************************
[ITEMDEF i_neve]
ID=020E8
NAME=neve
TYPE=t_script

ON=@CREATE
COLOR=07A1
ATTR=attr_move_never|attr_decay
TIMER={6 15}

ON=@DCLICK
TARGET Voce pegou um pouco de neve e fez uma bola. Deseja atacar em quem?
return 1

ON=@targon_char
SRC.TARG.EFFECT 0,0E73,6,30,0
SRC.TARG.sysmessageyellow Jogaram uma bola de neve em voce!
remove
return 1

ON=@step
remove
return 1

//*****************************************************************************
//*****************************************************************************
// FUNCTIONs
//*****************************************************************************
//*****************************************************************************
[FUNCTION f_onserver_timer]
//serv.b tick

VAR.tMin=<eval <VAR.tMin>+minutos_por_tick>
if (<VAR.tMin> >= minutos_por_hora) //minuto
 VAR.tMin=0
 VAR.tHour=<VAR.tHour>+1

 f_connectDB

 // serv.allclients findid.i_mry_clima.timer=8
 // serv.allclients sector.light=0
 // if (0<var.fixclima>==1)
 //  say *arrumando clima*
 //  serv.allclients events +e_clima
 //  serv.allclients sysmessageorange ,*arrumando clima*
 // endif

 if (<VAR.tHour> >= horas_por_dia)  //hora
  VAR.tHour=0
  VAR.tDay=<VAR.tDay>+1
  doswitch <eval <VAR.tMonth> - 1>
   var.tSeason=inverno
   var.tSeason=inverno
   var.tSeason=primavera
   var.tSeason=primavera
   var.tSeason=primavera
   var.tSeason=verao
   var.tSeason=verao
   var.tSeason=verao
   var.tSeason=outono
   var.tSeason=outono
   var.tSeason=outono
   var.tSeason=inverno
  enddo
  
  if (<VAR.tDay> > dias_por_mes)   //dia
   VAR.tDay=1
   VAR.tMonth=<VAR.tMonth>+1
   
   if (<VAR.tMonth> > meses_por_ano)  //meses 
    VAR.tMonth=1
    VAR.tYear=<VAR.tYear>+1
   endif
  endif
 endif
 VAR.temperature=<eval <DEF.temp_<eval <VAR.tHour>>>>+<eval <DEF.temp_<VAR.tSeason>>>
endif
return 1

//*****************************************************************************
// time_now
//*****************************************************************************
//mostra a data e hora atual
[function time_now]
 sysmessage Data: <eval <var.tHour>>:<eval <var.tMin>> -- <eval <var.tDay>>/<eval <var.tMonth>>/<eval <var.tYear>>
return 1

//*****************************************************************************
// temp_get
//*****************************************************************************
//pega a temperatura da regiao atual
[function temp_get]
return <VAR.temperature>+<region.tag0.modtemp>

//*****************************************************************************
// temp_check()
//*****************************************************************************
//'calcula' quantidade de roupa e temperatura de onde UID está.
//compara com valores pre-estabelecidos para saber se UID está com muito frio,
//frio, normal ou calor
[FUNCTION temp_check]

if (<tag0.no_clima>==1) || (<flags>&statf_indoors)
 findid.i_mry_clima.more1=0
 findid.i_mry_clima.more2=0
 return 1
endif

LOCAL.temp=<eval <temp_get>>

//'calcula' quantidade de roupa
LOCAL.cloth=0
FOR l 1 24
	OBJ=<findlayer.<LOCAL.l>>
	if (<OBJ.type>==t_clothing) || (<OBJ.type>==t_armor_leather)
		LOCAL.cloth=<eval <LOCAL.cloth>+<OBJ.weight>>
	endif
ENDFOR

//assumindo nivel de roupa de 0 a 300, permite 300/15 graus de protecao
LOCAL.cloth=<eval <LOCAL.cloth>/15>

//adiciona protecao devido a calor proximo
if (<isneartype t_fire 1>) || (<isneartype t_forge 1>)
 LOCAL.cloth=<LOCAL.cloth>+3
endif

//decide efeito
if ( <LOCAL.temp> < <eval <DEF.tBodyMin2>-<LOCAL.cloth>> )
 LOCAL.f=3
elif ( <LOCAL.temp> < <eval <DEF.tBodyMin1>-<LOCAL.cloth>> )
 LOCAL.f=2
elif ( <LOCAL.temp> > <eval <DEF.tBodyMax1>-<LOCAL.cloth>> )
 LOCAL.f=1
else 
 LOCAL.f=0
endif

temp_effect <LOCAL.f>

if (0<argn1> > 0 )
 say temp: <eval <LOCAL.temp>> cloth: <eval <LOCAL.cloth>> e:<LOCAL.f>
endif

//sysmessage [CLIMA] T:<eval <LOCAL.temp>> R:<eval <LOCAL.cloth>> <eval <DEF.tBodyMin2>-<LOCAL.cloth>> <eval <DEF.tBodyMin1>-<LOCAL.cloth>> <eval <DEF.tBodyMax1>-<LOCAL.cloth>>

//*****************************************************************************
// temp_effect( effect )
//*****************************************************************************
//cria efeito devido a temperatura:
//1 = calor
//2 = frio
//3 = muito frio
[FUNCTION temp_effect] 
if (<isgm>)
 return
endif
if (0<argn>==1)
 if (<stam> >= 5)
  stam=<stam>//-{1 3}
 endif
 DORAND 5
  sysmessagegreen Voce esta se sentindo cancado devido ao calor.
  emotered sua
 ENDDO
elif (0<argn>==2)
 if (<hits> >= 10)  && (<stam> > 5)
  stam=<obj.stam>//-{2 4}
 elif (<hits> < 10)  && (<stam> > 5)
  stam=<stam>//-{1 2}
 else //(<stam>==0)
  hits=<hits>//-{2 5}
 endif
 DORAND 5
  sysmessagegreen Voce esta sentindo frio. Abrigue-se.
  emotered treme
 ENDDO
elif (0<argn>==3)
 hits=<hits>//-{2 5}
 stam=<stam>//-{1 3}
 DORAND 5
  sysmessagered Seus membros estao congelando de frio! Voce vai morrer!
  sysmessagegreen Voce esta sentindo MUITO frio. Abrigue-se.
  emotered treme muito
 ENDDO
endif
if (<stam> < 0) || (<stam> > 900)
 stam=0
endif

//*****************************************************************************
// temp_snow
//*****************************************************************************
//faz nevar
[FUNCTION temp_snow]
region.RAINCHANCE=0
region.COLDCHANCE=064
src.findid.i_mry_clima.more1={3 4}
sector.snow

//*****************************************************************************
// temp_rain
//*****************************************************************************
//faz chover
[FUNCTION temp_rain]
region.RAINCHANCE=064
region.COLDCHANCE=0
src.findid.i_mry_clima.more2={3 4}
sector.rain

//*****************************************************************************
// temp_dry
//*****************************************************************************
//faz ficar seco
[FUNCTION temp_dry]
region.RAINCHANCE=0
region.COLDCHANCE=0
src.findid.i_mry_clima.more1=0
src.findid.i_mry_clima.more2=0
sector.dry

//*****************************************************************************
// temp_addSnow
//*****************************************************************************
//cria poca de neve
[FUNCTION temp_addSnow]
newitem i_neve
act.movenear <uid> 6

//*****************************************************************************
// temp_addSplash
//*****************************************************************************
//cria poca de agua
[FUNCTION temp_addSplash]
src.newitem i_poca
src.act.timer {6 15}
src.act.movenear <src.uid> 6


//*****************************************************************************
//*****************************************************************************
// GUMPs
//*****************************************************************************
//*****************************************************************************

[DIALOG d_tempo]
250, 125	
resizepic 0 0 3600 250 210

resizepic 20 20 3000 50 22
textentry 20 20 50 25 0 1 0
text 75 20 026 1

resizepic 20 50 3000 50 22
textentry 20 50 50 25 0 2 2
text 75 50 026 3

resizepic 20 80 3000 50 22
textentry 20 80 50 25 0 3 4
text 75 80 026 5

resizepic 20 110 3000 50 22
textentry 20 110 50 25 0 4 6
text 75 110 026 7

resizepic 20 140 3000 50 22
textentry 20 140 50 25 0 5 8
text 75 140 026 9

button 100 168 248 247 1 0 1

[DIALOG d_tempo TEXT]
<EVAL <VAR.tMin>>
Minutos
<EVAL <VAR.tHour>>
Horas
<EVAL <VAR.tDay>>
Dia
<EVAL <VAR.tMonth>>
Mes
<EVAL <VAR.tYear>>
Ano

[DIALOG d_tempo BUTTON]

ON=0
  SRC.SYSMESSAGE O tempo nao foi modificado.

ON=1
LOCAL.retry=0
IF (<ARGTXT[1]>>=0) && (<ARGTXT[1]><<DEF.minutos_por_hora>)
  VAR.tMin=<ARGTXT[1]>
  
    
ELSE
  LOCAL.retry=1
  SRC.SYSMESSAGE Minutos devem ser entre 0 e <eval <DEF.minutos_por_hora>-1>.
ENDIF

IF (<ARGTXT[2]>>=0) && (<ARGTXT[2]><<DEF.horas_por_dia>)
  VAR.tHour=<ARGTXT[2]>
ELSE
  LOCAL.retry=1
  SRC.SYSMESSAGE Horas devem ser entre 0 e <eval <DEF.horas_por_dia>-1>.
ENDIF

IF (<ARGTXT[3]>>0) && (<ARGTXT[3]><=<DEF.dias_por_mes>)
  VAR.tDay=<ARGTXT[3]>
ELSE
  LOCAL.retry=1
  SRC.SYSMESSAGE Dias devem ser entre 1 e <eval <DEF.dias_por_mes>>.
ENDIF

IF (<ARGTXT[4]>>0) && (<ARGTXT[4]><=<DEF.meses_por_ano>)
  VAR.tMonth=<ARGTXT[4]>
ELSE
  LOCAL.retry=1
  SRC.SYSMESSAGE Meses devem ser entre 1 e <eval <DEF.meses_por_ano>>.
ENDIF

IF (<ARGTXT[5]>>=0)
  VAR.tYear=<ARGTXT[5]>
ELSE
  LOCAL.retry=1
  SRC.SYSMESSAGE Anos nao podem ser negativos
ENDIF

if ( <LOCAL.retry>==1 )
  dialog d_tempo
endif  

[EOF]
