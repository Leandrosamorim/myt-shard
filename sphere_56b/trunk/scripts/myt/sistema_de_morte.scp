//*****************************************************************************
//*****************************************************************************
//
// SISTEMA DE COMBATE por
//	Galthar
//	UsMarine (vinicius.arroyo at gmail.com)
//
//*****************************************************************************
//*****************************************************************************
//TODO:

//TAGS usadas:
//tag.morte_desmaios
//tag.morte_local
//tag.morte_horario
//tag.fpk
//tag.lastkillUID
//tag.lastkillname
//tag.pontosdealma
//ctag.loot_corpse
//ctag.arrastar
//ctag.morte_corpse
//*****************************************************************************

//*****************************************************************************
//*****************************************************************************
// EVENTs
//*****************************************************************************
//*****************************************************************************
[EVENTS e_sistema_de_morte]
ON=@DEATH
 
 if ( <ACT> > 0 ) && ( <ACT.NPC>==0 )
  tag.lastkillname=<act.tag.name>
  tag.lastkillUID=<act.uid>
  act.tag.fpk=(<act.tag0.fpk>+1)
 endif
 
 if (<tag0.morte_desmaios><5)
  hits=1
  timerf 1,morte_desmaia
  return 1
 else
  say morte
  morte_decrementaPontos 1
  tag.morte_horario=<time>/(10)
  tag.morte_local=<p>
  tag.morte_desmaios=0
  return 0
 endif

 return 1


ON=@ITEMCLICK
 //indica se corpo está morto ou inconsciente
 if (<act.type> == t_corpse) && (<act.link>!=04fffffff)
  if (<act.link.flags>&statf_dead)
   act.message <act.link.name> morto
  else
   act.message <act.link.name> inconsciente
  endif
  return 1
 endif


ON=@ITEMDCLICK
 //shrine clicked
 if (<act.type> == t_shrine)
  //ressurect?
  if (<flags>&statf_dead)
   //somente se tiver pontos de alma
   if (<tag0.pontosdealma><=0)
    src.message Sua alma esta fadada ao descanso eterno.
    src.message Nunca mais voltara a vida
    return 1
   endif  

   //verifica se ja passou tmepo suficiente para recuperacao
   if (<tag0.pontosdealma> == 1)
    LOCAL.ressconcluido=<eval <tag.morte_horario>+(60 * 15)>
   elif (<tag0.pontosdealma> == 2)
    LOCAL.ressconcluido=<eval <tag.morte_horario>+(60 * 10)>
   elif (<tag0.pontosdealma> == 3)
    LOCAL.ressconcluido=<eval <tag.morte_horario>+(60 * 5)>
   elif (<tag0.pontosdealma> >= 4)
    LOCAL.ressconcluido=<eval <tag.morte_horario>+(60 * 2)>
   else
   .message Sua alma esta fadada ao descanso eterno.
    message Nunca mais voltara a vida
   endif
  
   if (<LOCAL.ressconcluido> > <eval <time>/(10)>)
    message Voce precisa recuperar um pouco suas forcas...
    return 1
   else
    go <tag.morte_local>		
    morte_equip
   endif
  else
   src.sysmessage Voce possui <eval <tag0.pontosdealma>> pontos de alma...
  endif
 endif

 //so permite vivos lootear ou mexer nos corpos de players
 if (<act.type> == t_corpse) && !(<flags>&statf_dead) && (<act.link>!=04fffffff) && (<act.link>!=<uid>)
  ctag.morte_corpse=<act.uid>
  //se player escolheu lootear o corpo, deixa usar loot padrao
  if (<ctag0.loot_corpse>==1)
   ctag.loot_corpse=0
   return 0
  //abre menu para player morto ou desmaiado
  elif (<act.link.flags>&statf_dead)
   menu=m_corpse_morto
  else
   menu=m_corpse
  endif
  return 1
 endif
 
//*****************************************************************************
//*****************************************************************************
// FUNCTIONS
//*****************************************************************************
//*****************************************************************************
 
//*****************************************************************************
// morte_incrementaPontos
//*****************************************************************************
[FUNCTION morte_incrementaPontos]
 tag.pontosdealma=<eval <tag.pontosdealma>+<argn>>
 sysmessage Voce ganhou: <argn> ponto(s) de alma
 return 1
 
//*****************************************************************************
// morte_decrementaPontos
//*****************************************************************************
[FUNCTION morte_decrementaPontos]
 tag.pontosdealma=<eval <tag.pontosdealma>-<argn>>
 sysmessage Voce perdeu: <argn> ponto(s) alma
 return 1
 
//*****************************************************************************
// morte_arrastar(uid)
//*****************************************************************************
[FUNCTION morte_arrastar]
 NEWITEM=i_mry_arrastar
 NEW.equip
 NEW.link=<argn1>
 NEW.dclick
 return 1
 
//*****************************************************************************
// morte_equip
//*****************************************************************************
[FUNCTION morte_equip]
//põe a roupa
 findlayer(layer_pack).findtype(t_clothing).equip
 findlayer(layer_pack).findtype(t_clothing).equip
 findlayer(layer_pack).findtype(t_clothing).equip
 findlayer(layer_pack).findtype(t_clothing).equip
//põe armors
 findlayer(layer_pack).findtype(t_armor).equip
 findlayer(layer_pack).findtype(t_armor).equip
 findlayer(layer_pack).findtype(t_armor).equip
 findlayer(layer_pack).findtype(t_armor).equip
 findlayer(layer_pack).findtype(t_armor).equip
 findlayer(layer_pack).findtype(t_armor).equip
 return 1
 
//*****************************************************************************
// morte_desmaia
//*****************************************************************************
[FUNCTION morte_desmaia]
 
 tag.morte_desmaios=<eval <tag0.morte_desmaios>+1>
 //morte_equip
 
 NEWITEM=i_mry_desmaio
 NEW.link=<uid>
 NEW.equip
 NEW.timer=<eval (60*<tag0.morte_desmaios>)>
 
 if (<tag0.morte_desmaios> > 3)
  //indica morte apos 'timer'
  NEW.more1=1 
 endif
 
  say [MRT] estou com <tag0.morte_desmaios> desmaios <eval 60*<tag0.morte_desmaios>>
 //timerf 1,DIALOG,d_desmaio
 
 sysmessage Voce perdeu a consciencia !!!
 sleep
 flags=<flags>|(statf_stone|statf_invul)
 update
 
  
//*****************************************************************************
// morte_corta(uid)
//*****************************************************************************
[FUNCTION morte_corta]
 if (<findlayer(layer_hand1).type>==t_weapon_fence) || (<findlayer(layer_hand1).type>==t_weapon_axe) || (<findlayer(layer_hand1).type>==t_weapon_sword)	|| (<findlayer(layer_hand2).type>==t_weapon_sword) || (<findlayer(layer_hand2).type>==t_weapon_axe) || (<findlayer(layer_hand2).type>==t_weapon_fence)
  OBJ=<argn1>
  OBJ.link.findid.i_mry_desmaio.remove	//O dono do corpo não está mais desmaiado.
  OBJ.link.say *assasinado* 		// para tirar o .sleep junto com o corpo do dono do corpo.
  OBJ.link.flags=<OBJ.link.flags>&~(statf_stone|statf_invul) 	//pra tirara o flag de invul e de paralyze do dono do corpo.
  TRYP 0 OBJ.link.tag.morte_desmaios=5		//Se o dono do corpo desmaiar (.kill) denovo ele morre.
  OBJ.link.kill				//Mata o dono do corpo.
 endif
 return 1
 
//*****************************************************************************
// morte_cura(uid)
//*****************************************************************************
[FUNCTION morte_cura]
 NEWITEM i_mry_curar_corpo
 NEW.equip
 NEW.more1=0
 NEW.link=<argn1>
 if (<restest 3 i_bandage>==1)
  NEW.more1=<eval <NEW.more1>+1>
 endif
 if (<restest 1 i_bottle_yellow>==1)
  NEW.more1=<eval <NEW.more1>+1>
 endif
 NEW.timer=1
 return 1
 
//*****************************************************************************
//*****************************************************************************
// ITEMDEFs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// i_mry_arrastar
//*****************************************************************************
[ITEMDEF i_mry_arrastar]
 NAME=arrastar
 id=i_deed
 type=t_eq_script
 layer=layer_special

ON=@CREATE
 attr=attr_can_decay
 return 1

ON=@DCLICK
 link.say *arrastando corpo*
 cont.ctag.arrastar= ((<cont.str>*5)+(<cont.stamina>*3))/8
 timer=1
 return 1
 
ON=@TIMER
 if (<eval <cont.ctag.arrastar>> > <eval RAND(100)>) && !(<cont.flags>&statf_war)
  //arrasta corpo
  link.p=<cont.p>
   
  //arrasta player se desmaiado
  if !(<link.link.flags>&statf_dead)
   link.link.p <cont.p>
  endif
   
  //sava nova posicao do corpo
  link.link.tag.morte_local=<cont.p>
  sector.allclients update
  timer=1
 else
  cont.ctag.arrastar=
  cont.say *largou*
  cont.sysmessage O corpo escapou de suas maos
  remove
 endif
 return 1
 
//*****************************************************************************
// i_mry_desmaio
//*****************************************************************************
[ITEMDEF i_mry_desmaio]
 id=i_deed
 name=desmaio
 type=t_eq_script
 layer=layer_special

ON=@CREATE
 attr=attr_decay

ON=@TIMER
 
 cont.flags=<cont.flags>&~(statf_stone|statf_invul)
 say [MRT] ACORDEI
 //recobra consciencia se more1 != 1
 if (<more1>!=1)
  cont.sysmessage Voce recobrou a consciencia.
  cont.flags=<cont.FLAGS>&~statf_dead
  cont.say *hug!*
  cont.hits=(<cont.str>/5)
  cont.morte_equip
  cont.update
 else
  cont.say *morreu*
  cont.tag.morte_desmaios=5
  cont.kill
 endif
 
 remove
 return 1

//*****************************************************************************
// i_mry_curar_corpo
//*****************************************************************************
[ITEMDEF i_mry_curar_corpo]
 id=i_deed
 name=Curando corpo
 type=t_eq_script
 layer=layer_special

ON=@CREATE
 attr=attr_decay
 more2=20

ON=@TIMER
 //inicializacao
 if (<more2> == 20)
  //verifica pre-requesitos
  if (<more1>==0)
   cont.sysmessage Voce nao tem os equipamentos necessarios...
   remove
   return 1
  else
   cont.flags=<cont.flags>|statf_freeze
   more2=11 //1 vezes no loop
   timer=1
   return 1
  endif

 else
  //loop de cura
  if (<more2> > 0) //2
   more2=<more2>-1
   DORAND 9
    cont.anim 16
    sfx 04f
    cont.anim 19
    sfx 04f
    cont.say *aplicando curativos*
    cont.anim 16
    sfx 04f
    sfx 04f
    cont.anim 19
   ENDDO
   timer=1
   return 1
  //fim da cura
  else
   cont.consume 3 i_bandage
   cont.consume 1 i_bottle_yellow
   skill_gain <cont>,skill_healing
   if ( (<more1>==1) && (<cont.healing> >= {0 1150}) ) || ( (<more1>==2) && (<cont.healing> >= {0 850}) ) 
    cont.flags=(<cont.flags>&~statf_freeze)
    cont.sysmessagegreen Seu tratamento surtiu efeito...
    link.link.consume i_mry_desmaio
    link.link.flags=<link.link.flags>&~(statf_stone|statf_invul)
    link.link.say *ouf!*
    remove
    return 1
   else
    cont.sysmessageorange Seu tratamento nao surtiu efeito...
    cont.unfreeze
    remove
    return 1
   endif
  endif
 endif

//*****************************************************************************
//*****************************************************************************
// GUMPs
//*****************************************************************************
//*****************************************************************************


//*****************************************************************************
// CORPO INCONSCIENTE
//*****************************************************************************
[MENU m_corpse]
                -=Corpo Inconsciente=-
on=0 cortar garganta (perde 2 pontos de alma)
 morte_decrementaPontos 2
 morte_corta <ctag.morte_corpse>
 return 1

on=0 curar
 morte_cura <ctag.morte_corpse>
 return 1

on=0 roubar o corpo (perde 1 ponto de alma)
 src.morte_decrementaPontos 1
 ctag.loot_corpse=1
 sysmessage Abra o corpo para roubar os itens.
 return 1

on=0 arrastar o corpo
 morte_arrastar <ctag.morte_corpse>
 return 1

//*****************************************************************************
// CORPO MORTO
//*****************************************************************************
[MENU m_corpse_morto]
                   -=Corpo Morto=-
on=0 roubar o corpo (perde 1 ponto de alma)
 morte_decrementaPontos 1
 ctag.loot_corpse=1
 sysmessage Pode abrir o corpo e roubar os itens.
 return 1

on=0 arrastar o corpo
 morte_arrastar <ctag.morte_corpse>
 return 1

//*****************************************************************************
// DESMAIO
//*****************************************************************************
[DIALOG d_desmaio]
175, 125
noclose
PAGE 0

if (<tag0.morte_desmaios>==1)
 LOCAL.l1=Esta e a primeira vez que isso
 LOCAL.l2=lhe aconteceu. Logo estara acordado
 LOCAL.l3=novamente.
elif (<tag0.morte_desmaios>==2)
 LOCAL.l1=Esta e a segunda vez que isso
 LOCAL.l2=lhe aconteceu. Em algum tempo voce
 LOCAL.l3=podera voltar a andar.
elif (<tag0.morte_desmaios>==3)
 LOCAL.l1=Esta e a terceira vez que isso
 LOCAL.l2=lhe aconteceu. Voce precisa de 
 LOCAL.l3=cuidados medicos ou morrera.
else
 LOCAL.l1=Esta e a quarta vez que isso
 LOCAL.l2=lhe aconteceu. Voce precisa de 
 LOCAL.l3=cuidados medicos ou morrera.


gumppic 0 0 2080
gumppic 17 37 2081
gumppic 17 107 2082
gumppic 18 177 2083
text 73 33 32 0
text 76 52 32 1
dtext 42 80 1152 <LOCAL.l1>
dtext 28 99 1152 <LOCAL.l2>
dtext 29 118 1152 <LOCAL.l3>
button 118 145 2311 2312 1 0 0
text 123 5 152 2

[DIALOG d_desmaio TEXT]
Voce caiu no chao e
perdeu a consciencia.
(MyT)


[DIALOG d_desmaio BUTTON]

[EOF]