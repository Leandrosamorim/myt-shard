//*****************************************************************************
//*****************************************************************************
//
// SKILL ALCHEMY
//
//	Mestre Galthar
//	UsMarine (vinicius.arroyo at gmail.com)
//
//*****************************************************************************
//*****************************************************************************

//TODO:

//TAGS usadas:

//REAGENTES (máscaras - more2)
//i_r_perola_negra    =0001
//i_r_musgo_sangue    =0002
//i_r_alho            =0004
//i_r_ginseng         =0008
//i_r_raiz_de_mandrake=0010
//i_r_nightshade      =0020
//i_r_cinza_sulfurosa =0040
//i_r_teia_de_aranha  =0080
//i_r_asa_morcego     =0100
//i_r_olho_morto_vivo =0200

//*****************************************************************************
//*****************************************************************************
// TYPEDEFs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// t_reagente
//*****************************************************************************
[TYPEDEF t_reagente]
ON=@DCLICK
src.sysmessageyellow Utilize o almofariz.
return 1

//*****************************************************************************
// t_tubo
//*****************************************************************************
[TYPEDEF t_tubo]
ON=@DCLICK
src.sysmessageyellow Utilize o almofariz.
return 1

ON=@STEP
src.emotered quebrou a vidraria
sfx 040
remove
return 1

//*****************************************************************************
// t_tubo_cheio
//*****************************************************************************
[TYPEDEF t_tubo_cheio]

ON=@STEP
src.emotered quebrou a vidraria
sfx 040
remove
return 1

//*****************************************************************************
// t_almofariz
//*****************************************************************************
[TYPEDEF t_almofariz]

//*****************************************************************************
// t_espiriteira
//*****************************************************************************
[TYPEDEF t_espiriteira]
ON=@DCLICK
if (<cont>)
 return 1
endif
if (!<more1>) //se apagado
 if (!<more2>) //se NAO tem carga
  src.sysmessageyellow A vela se acabou...
  remove
  return 1
 endif
 more1=1
 dispid=i_heating_stand_lit
 sfx=054
 update
 more2=<more2>-1
 return 1
else	//se aceso
 more1=0
 dispid=i_heating_stand
 sfx=02b
 update
 return 1
return 1

ON=@PICKUP_GROUND
if (<more1>) //se aceso
 more1=0
 dispid=i_heating_stand
 sfx=02b
endif
update

ON=@DROPON_GROUND
timer=12*60*60
return 1

ON=@TIMER
timer=12*60*60
return 1

//*****************************************************************************
// t_destilador
//*****************************************************************************
[TYPEDEF t_destilador]
 
ON=@STEP
src.emotered quebrou a vidraria
sfx 040
remove
return 1

//*****************************************************************************
// t_balao
//*****************************************************************************
[TYPEDEF t_balao]
ON=@DCLICK
if (!<more2>) //vazio
 src.sysmessagered Utilize um tubo cheio.
 return 1
else //cheio ou com borra pronto
 src.newitem=i_alc_balao
 src.act.link=<uid>
 src.act.equip
 src.act.use
 return 1
endif

ON=@STEP
src.emotered quebrou a vidraria
sfx 040
remove
return 1

//*****************************************************************************
// t_misturador
//*****************************************************************************
[TYPEDEF t_misturador]
ON=@DCLICK
if (!<more2>) //vazio
 src.sysmessagered Utilize um tubo cheio.
 return 1
else //com um ou dois compostos
 src.newitem=i_alc_misturador
 src.act.link=<uid>
 src.act.equip
 src.act.use
 return 1
endif

ON=@STEP
src.emotered quebrou a vidraria
sfx 040
remove
return 1



//*****************************************************************************
//*****************************************************************************
// FUNCTIONs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// despeja_tubo( gera_tubo_vazio )
//*****************************************************************************
[FUNCTION despeja_tubo]
sfx 0240
if (<argv[0]>)
  serv.newitem i_tubo
  new.cont=<uid>
endif

//*****************************************************************************
// get_color
//*****************************************************************************
[function get_color]
if (<more1><2)
 return 0f
elif (<more1> < 4)
  return 028
elif (<more1> < 8)
 return 047e
elif (<more1> < 10)
 return 032
elif (<more1> < 20)
 return 02c
elif (<more1> < 40)
 return 016d
elif (<more1> < 80)
 return 09a
elif (<more1> < 100)
 return 03c1
elif (<more1> < 200)
 return 03be
else
 return 034
endif

//*****************************************************************************
// find_color
//*****************************************************************************
[function find_color]
if (<more1><2)
 color=0f
elif (<more1> < 4)
 color=028
elif (<more1> < 8)
 color=047e
elif (<more1> < 10)
 color=032
elif (<more1> < 20)
 color=02c
elif (<more1> < 40)
 color=016d
elif (<more1> < 80)
 color=09a
elif (<more1> < 100)
 color=03c1
elif (<more1> < 200)
 color=03be
else
 color=034
endif
update
return 1

//*****************************************************************************
// find_name
//*****************************************************************************
[function find_name]

if (<type>=t_misturador)
	if (<more1>=0001)
	 return composto de perola negra
	elif (<more1>=0002)
	 return composto de musgo sangue
	elif (<more1>=0004)
	 return composto de alho
	elif (<more1>=0008)
	 return composto de ginseng
	elif (<more1>=0010)
	 return composto de raiz de mandrake
	elif (<more1>=0020)
	 return composto de nightshade
	elif (<more1>=0040)
	 return composto de cinza sulforoza
	elif (<more1>=0080)
	 return composto de teia de aranha
	elif (<more1>=0100)
	 return composto de asa de morcego
	elif (<more1>=0200)
	 return composto de olho de morto-vivo
	endif
endif

if (<more1>=0001)
 return perola negra
elif (<more1>=0002)
 return musgo sangue
elif (<more1>=0004)
 return alho
elif (<more1>=0008)
 return ginseng
elif (<more1>=0010)
 return raiz de mandrake
elif (<more1>=0020)
 return nightshade
elif (<more1>=0040)
 return cinza sulforoza
elif (<more1>=0080)
 return teia de aranha
elif (<more1>=0100)
 return asa de morcego
elif (<more1>=0200)
 return olho de morto-vivo
 
elif (<more1>=03)
 return solucao rubra escura
elif (<more1>=05)
 return solucao negra
elif (<more1>=06)
 return solucao rubra
elif (<more1>=09)
 return composto de Deniz
elif (<more1>=0a)
 return composto benigno
elif (<more1>=0c)
 return composto de Belline
elif (<more1>=011)
 return sulucao alpha
elif (<more1>=012)
 return solucao raiz-de-fungo
elif (<more1>=014)
 return composto raiz forte
elif (<more1>=018)
 return composto de raizes
elif (<more1>=021)
 return solucao sombra de perola
elif (<more1>=022)
 return solucao sombra de musgo
elif (<more1>=024)
 return composto de Flamarion
elif (<more1>=028)
 return composto dos sonhos
elif (<more1>=030)
 return solucao de Pulliani
elif (<more1>=041)
 return solucao da chama
elif (<more1>=042)
 return composto maligno
elif (<more1>=044)
 return solucao Ohmega
elif (<more1>=048)
 return composto de Tearina
elif (<more1>=050)
 return sulucao de cinzadrake
elif (<more1>=060)
 return solucao fogo-da-noite
elif (<more1>=081)
 return composto de Simn
elif (<more1>=082)
 return solucao sangue-de-aranha
elif (<more1>=084)
 return solucao veneno de aranha
elif (<more1>=088)
 return composto 88
elif (<more1>=090)
 return solucao de aranha velha
elif (<more1>=0a0)
 return composto da aranha noturna
elif (<more1>=0c0)
 return solucao da teia de ouro
elif (<more1>=0101)
 return solucao vida voadora
elif (<more1>=0102)
 return composto de Vividus
elif (<more1>=0104)
 return solucao de Anelion
elif (<more1>=0108)
 return composto carnigolein
elif (<more1>=0110)
 return solucao de Hulier
elif (<more1>=0120)
 return composto sucarinus
elif (<more1>=0140)
 return solucao asa-morta
elif (<more1>=0180)
 return solucao asa de aranha
elif (<more1>=0201)
 return liquido olho-marinho
elif (<more1>=0202)
 return solucao de Anelion II
elif (<more1>=0204)
 return liquido ardente
elif (<more1>=0208)
 return ministrado de Sofia
elif (<more1>=0210)
 return composto firiudiplutal
elif (<more1>=0220)
 return solucao venen'Olhos
elif (<more1>=0240)
 return ministrado metoroidal
elif (<more1>=0280)
 return mistura de teia de aranha com olho de morto-vivo
elif (<more1>=0300)
 return coisa viscosa	
else
 return mistura
endif
update
return 1

//*****************************************************************************
// alc_falhar
//*****************************************************************************
[function alc_falhar]
//balao
if (<argn>==3)
 if ({0 <eval (<cont.alchemy>) /(8)>} == 1) || (<cont.flags>&statf_war) || (<cont.flags>&statf_onhorse)
  cont.alc_subir 1
  cont.clspk
  cont.emotegreen falhou 
  cont.newitem <link.baseid>
  cont.act.cont <cont.findlayer(21).uid>
  if ({1 5} == 1)
   cont.emotered quebrou a vidraria
   sfx 040
   cont.consume 1 <link.baseid>
  endif
  link.remove
  cont.unfreeze
  remove
  return 1
 endif
//mist
elif (<argn>==4)
 if ({1 <eval (<cont.alchemy>) /(13)>} <= 1) || (<cont.flags>&statf_war) || (<cont.flags>&statf_onhorse)
  cont.alc_subir 1
  cont.clspk
  cont.emotegreen falhou
  if ({1 5} == 1)
   explode
   link.remove
   cont.unfreeze
   remove
   return 1
  endif
  cont.newitem <link.baseid>
  cont.act.cont <cont.findlayer(21).uid>
  link.remove
  cont.unfreeze
  remove
  return 1
 endif
endif

//*****************************************************************************
// alc_subir
//*****************************************************************************
[function alc_subir]
skill_gain skill_alchemy
return 1

//*****************************************************************************
// FUNCTIONs
//*****************************************************************************
[function freeze]
flags=(<flags>|04)
update
return 1

//*****************************************************************************
// unfreeze
//*****************************************************************************
[function unfreeze]
flags=(<flags>&~04)
update
return 1

//*****************************************************************************
// clspk
//*****************************************************************************
[FUNCTION clspk]
say
say
say

//*****************************************************************************
// checkalc
//*****************************************************************************
[function checkalc]
src.consume 10 i_alc_balao
src.consume 10 i_alc_misturador
return 1

//*****************************************************************************
// faz_pocao
//*****************************************************************************
//default eh o item com a pocao (more1-mascara ingredientes more2-tipo)
[function faz_pocao]
 if (<more1>==01)
  src.newitem=i_bottle_red
  src.act.name=pocao rubra
  
 elif (<more1>==02)
  src.newitem=i_bottle_blue
  src.act.name=pocao azul
 elif (<more1>==03) 
  src.newitem=i_potion_magic_reflection
  LOCAL.m=1
 elif (<more1>==04)
  src.newitem=i_bottle_orange
  src.act.name=pocao alaranjada
 elif (<more1>==05)
  src.newitem=i_potion_death_mask
  src.act.name=pocao mascara da morte
 elif (<more1>==08)
  src.newitem=i_bottle_yellow
  src.act.name=pocao amarela
 elif (<more1>==09)
  src.newitem=i_bottle_yellow
  src.act.name=pocao amarela
 elif (<more1>==0c)
  src.newitem=i_bottle_orange
  src.act.name=pocao alaranjada
 elif (<more1>==010)
  src.newitem=i_bottle_white
  src.act.name=pocao alva
 elif (<more1>==011) && (strmatch(<src.tag.raca>,Elfo))
  src.newitem=i_potion_morvor
  LOCAL.m=1
 elif (<more1>==012)
  src.newitem=i_bottle_red
  src.act.name=pocao rubra
 elif (<more1>==014) && (<src.alchemy> >= 780)
  src.newitem=i_potion_encolher
  LOCAL.m=1
 elif (<more1>==018) && (strmatch(<src.tag.raca>,Orc))
  src.newitem=i_potion_furor
  LOCAL.m=1
 elif (<more1>==020)
  src.newitem=i_bottle_green
  src.act.name=pocao verde
 elif (<more1>==021)
  src.newitem=i_potion_veneno_roxo
  src.act.name=pocao roxa escura
 elif (<more1>==022)
  src.newitem=i_bottle_white
  src.act.name=pocao alva
 elif (<more1>==028) && (strmatch(<src.tag.raca>,Humano))
  src.newitem=i_potion_formula_21
  LOCAL.m=1
 elif (<more1>==040)
  src.newitem=i_bottle_purple
  src.act.name=pocao rozea
 elif (<more1>==041)
  src.newitem=i_bottle_purple
  src.act.name=pocao rozea
 elif (<more1>==042)
  src.newitem=i_bottle_blue
  src.act.name=pocao azul
 elif (<more1>==048) && (<src.alchemy> >= 925)
  src.newitem=i_potion_sopro_de_fogo
  LOCAL.m=1
 elif (<more1>==060)
  src.newitem=i_potion_explosao_terrestre
  LOCAL.m=1
 elif (<more1>==080)
  src.newitem=i_bottle_black
  src.act.name=pocao negra
 elif (<more1>==084)
, src.newitem=i_potion_invis
  src.act.name=pocao negra
  LOCAL.m=1
 elif (<more1>==090)
  src.newitem=i_potion_poly_urso
  src.act.name=pocao do urso cinzento
  LOCAL.m=1
 elif (<more1>==0c0)
  src.newitem=i_bottle_black
  src.act.name=pocao negra
 elif (<more1>==0100)
  src.newitem=i_potion_mana
  src.act.name=pocao prateada
 elif (<more1>==0101)
  src.newitem=i_potion_poly_galinha
  src.act.name=pocao da coragem
 elif (<more1>==0200)
  src.newitem=i_potion_Protection
  src.act.name=pocao violeta
 elif (<more1>==0202)
  src.newitem=i_potion_Protection_max
  src.act.name=pocao azulada
 else
  src.sysmessage Essa pocao nao parece ter utilidade alguma.
  src.newitem i_tubo
  src.act.bounce
  src.newitem i_bottle_empty
  src.act.bounce
  return 1
endif
src.act.bounce
//src.act.more2=(<eval (<src.tag.alc_more2>*50) +50>)
doswitch <LOCAL.m>
 src.act.name=<src.act.name> fraca
 src.act.name=<src.act.name>
 src.act.name=<src.act.name> concentrada
 src.act.name=<src.act.name> forte
enddo
doswitch <LOCAL.m>
 src.act.more2=890
 src.act.more2=1000
 src.act.more2=1500
 src.act.more2=2000
enddo

src.newitem=i_tubo
src.act.bounce
return 1

//*****************************************************************************
// explode
//*****************************************************************************
[function explode]
sfx 011d
cont.newitem=i_fx_explosion_2
cont.act.p=<link.p>
cont.act.timer=2
cont.act.attr=02
cont.damage {10 15} 0010 <cont.uid>

//*****************************************************************************
// dest_setaCor
//*****************************************************************************
//seta dispid do destilador de acordo com conteudo
[FUNCTION dest_setaCor]
if (<more1>==01)			//bp
	dispid=i_beaker2_blue
elif (<more1>==02)		//bm
	dispid=i_beaker2_red
elif (<more1>==04)		//g
	dispid=i_beaker2_teal
elif (<more1>==08)		//g
	dispid=i_beaker2_red
elif (<more1>==010)		//mr
	dispid=i_beaker2_red
elif (<more1>==020)		//ns
	dispid=01837
elif (<more1>==040)		//sa
	dispid=01837
elif (<more1>==080)		//ss
	dispid=i_beaker2_blue
elif (<more1>==0100)	//am
	dispid=i_beaker2_blue
elif (<more1>==0200)	//om
	dispid=i_beaker2_blue
else
	dispid=i_beaker2_empty
endif
update

//*****************************************************************************
// dest_step( destilador )
//*****************************************************************************
//anima destilador em uso
[FUNCTION dest_step]
obj=<argv[0]>
DORAND (4)
	obj.emotered aquecendo
	obj.sfx 024
ENDDO

//*****************************************************************************
// dest_end( destilador, sucesso, nada )
//*****************************************************************************
//termina destilacao
[FUNCTION dest_end]
obj=<argv[0]>
if (<argv[1]>)
	obj.more2=2 //pronto
	obj.name=destilador (destilado de <obj.find_name>)
	obj.link.emotegreen destilou
else
  if (<R1,4>==1)
		obj.link.emotered quebrou a vidraria
		obj.sfx 040
		obj.remove
  else
		obj.more1=0 //vazio
		obj.more2=0 //vazio
		obj.name=destilador
		obj.dest_setaCor
		obj.link.emotegreen falhou
  endif
endif

//impede uso do .fazitem
obj=<findid.i_faz_item.uid>
obj.tag.abort=1

//*****************************************************************************
// balao_setaCor
//*****************************************************************************
//seta dispid do balao de acordo com conteudo
[FUNCTION balao_setaCor]
if (<more1>==01)			//bp
	dispid=0182a
elif (<more1>==02)		//bm
	dispid=0182c
elif (<more1>==04)		//g
	dispid=0182b
elif (<more1>==08)		//gi
	dispid=0182b
elif (<more1>==010)		//mr
	dispid=0182c
elif (<more1>==020)		//ns
	dispid=0182a
elif (<more1>==040)		//sa
	dispid=0182b
elif (<more1>==080)		//ss
	dispid=0182a
elif (<more1>==0100)	//bw
	dispid=0182c
elif (<more1>==0200)	//om
	dispid=0182a
endif
update

//*****************************************************************************
// mist_setaCor
//*****************************************************************************
//seta dispid do misturador de acordo com conteudo
[FUNCTION mist_setaCor]
if (<more1>==01)			//bp
	dispid=01839
elif (<more1>==02)		//bm
	dispid=01838
elif (<more1>==04)		//g
	dispid=01839
elif (<more1>==08)		//g
	dispid=01838
elif (<more1>==010)		//mr
	dispid=01839
elif (<more1>==020)		//ns
	dispid=01838
elif (<more1>==040)		//sa
	dispid=01839
elif (<more1>==080)		//ss
	dispid=01838
elif (<more1>==0100)	//bw
	dispid=01839
elif (<more1>==0200)	//om
	dispid=01839
else
	dispid={01839 1 01838 1}
endif
update

//*****************************************************************************
// ALQUIMIANOW
//*****************************************************************************
[FUNCTION ALQUIMIANOW]
src.newitem=i_tubo
src.act.amount=5
src.act.bounce

src.newitem=i_destilador
src.act.amount=3
src.act.bounce

src.newitem=i_balao
src.act.amount=3
src.act.bounce

src.newitem=i_espiriteira
src.act.bounce

src.newitem=i_almofariz
src.act.bounce

src.newitem=i_bottle_empty
src.act.amount=5
src.act.bounce

src.newitem=i_misturador
src.act.amount=3
src.act.bounce

src.newitem=i_r_alho
src.act.amount=30
src.act.bounce

src.newitem=i_r_perola_negra
src.act.amount=30
src.act.bounce

src.newitem=i_r_ginseng
src.act.amount=30
src.act.bounce

src.newitem=i_r_musgo_sangue
src.act.amount=30
src.act.bounce

src.newitem=i_r_raiz_de_mandrake
src.act.amount=30
src.act.bounce

src.newitem=i_r_nightshade
src.act.amount=30
src.act.bounce

src.newitem=i_r_cinza_sulfurosa
src.act.amount=30
src.act.bounce

src.newitem=i_r_teia_de_aranha
src.act.amount=30
src.act.bounce


//*****************************************************************************
//*****************************************************************************
// ITEMDEFs - Reagentes
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// i_r_perola_negra
//*****************************************************************************
[ITEMDEF i_r_perola_negra]
NAME=perola negra
DEFNAME=i_r_perola_negra
ID=i_reag_black_pearl
TYPE=t_reagente
CATEGORY=MyT - Items by Professions
SUBSECTION=Alchemist
DESCRIPTION=Pérola negra

ON=@CREATE
more1=001

//*****************************************************************************
// i_r_musgo_sangue
//*****************************************************************************
[ITEMDEF i_r_musgo_sangue]
NAME=musgo sangue
DEFNAME=i_r_musgo_sangue
ID=i_reag_blood_moss
TYPE=t_reagente
CATEGORY=MyT - Items by Professions
SUBSECTION=Alchemist
DESCRIPTION=Musgo sangue

ON=@CREATE
more1=002

//*****************************************************************************
// i_r_alho
//*****************************************************************************
[ITEMDEF i_r_alho]
NAME=alho
DEFNAME=i_r_alho
ID=i_reag_garlic
TYPE=t_reagente
CATEGORY=MyT - Items by Professions
SUBSECTION=Alchemist
DESCRIPTION=Alho

ON=@CREATE
more1=004

//*****************************************************************************
// i_r_ginseng
//*****************************************************************************
[ITEMDEF i_r_ginseng]
NAME=ginseng
DEFNAME=i_r_ginseng
ID=i_reag_ginseng
TYPE=t_reagente
CATEGORY=MyT - Items by Professions
SUBSECTION=Alchemist
DESCRIPTION=ginseng

ON=@CREATE
more1=008

//*****************************************************************************
// i_r_raiz_de_mandrake
//*****************************************************************************
[ITEMDEF i_r_raiz_de_mandrake]
NAME=raiz de mandrake
DEFNAME=i_r_raiz_de_mandrake
ID=i_reag_mandrake_root
TYPE=t_reagente
CATEGORY=MyT - Items by Professions
SUBSECTION=Alchemist
DESCRIPTION=Mandrake root

ON=@CREATE
more1=010

//*****************************************************************************
// i_r_nightshade
//*****************************************************************************
[ITEMDEF i_r_nightshade]
NAME=nightshade
DEFNAME=i_r_nightshade
ID=i_reag_nightshade
TYPE=t_reagente
CATEGORY=MyT - Items by Professions
SUBSECTION=Alchemist
DESCRIPTION=Nightshade

ON=@CREATE
more1=020

//*****************************************************************************
// i_r_cinza_sulfurosa
//*****************************************************************************
[ITEMDEF i_r_cinza_sulfurosa]
NAME=cinza sulfurosa
DEFNAME=i_r_cinza_sulfurosa
ID=i_reag_sulfur_ash
TYPE=t_reagente
SKILLMAKE=MINING 20.0
CATEGORY=MyT - Items by Professions
SUBSECTION=Alchemist
DESCRIPTION=Cinza sulfurosa

ON=@CREATE
more1=040

//*****************************************************************************
// i_r_teia_de_aranha
//*****************************************************************************
[ITEMDEF 0f8d]
NAME=teia de aranha
DEFNAME=i_r_teia_de_aranha
//ID=i_reag_spider_silk
TYPE=t_reagente
CATEGORY=MyT - Items by Professions
SUBSECTION=Alchemist
DESCRIPTION=Teia de aranha

ON=@CREATE
more1=080

//*****************************************************************************
// i_r_asa_morcego
//*****************************************************************************
[ITEMDEF 0f78]
//i_reag_BATWING
DEFNAME=i_r_asa_morcego
name=asa de morcego
TYPE=t_reagente
VALUE=3
PILE=1
WEIGHT=.1


CATEGORY=MyT - Items by Professions
SUBSECTION=Alchemist
DESCRIPTION=Asa de morcego

ON=@CREATE
more1=0100

//*****************************************************************************
// i_r_olho_morto_vivo
//*****************************************************************************
[ITEMDEF 0f87]
// Eye of Newt
NAME=olho de morto-vivo
DEFNAME=i_r_olho_morto_vivo
TYPE=t_reagente
VALUE=3
PILE=1
WEIGHT=.1

CATEGORY=MyT - Items by Professions
SUBSECTION=Alchemist
DESCRIPTION=Olho de morto-vivo

ON=@CREATE
more1=0200

//*****************************************************************************
//*****************************************************************************
// ITEMDEFs - Acessorios/Ferramentas
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// i_tubo
//*****************************************************************************
[ITEMDEF i_tubo]
NAME=tubo de ensaio
ID=i_vial_empty
TYPE=t_tubo
CATEGORY=MyT - Items by Professions
SUBSECTION=Alchemist
DESCRIPTION=tubo de ensaio

//*****************************************************************************
// i_tubo_cheio
//*****************************************************************************
//more1	reagente
//more2	tipo (extrato,)
[ITEMDEF i_tubo_cheio]
NAME=BUG!
ID=i_vial_empty
TYPE=t_tubo_cheio

ON=@DCLICK
src.sysmessageyellow Selecione o outro recipiente
TARGET
return 1

ON=@TARGON_ITEM
if (<src.targ.type>!=t_destilador) && (<src.targ.type>!=t_balao) && (<src.targ.type>!=t_misturador) && (<src.targ.type>!=t_potion_empty)
	src.sysmessageyellow Use em destiladores, baloes, misturadores ou frascos para pocao.
	return 1
endif

if (<src.targ.type>==t_destilador) || (<src.targ.type>==t_balao)
	if (<more2>)//se reagente do tubo nao for extrato
		src.sysmessageyellow Voce nao pode preparar isso.
		return 1
	endif
	if (<src.targ.more2>)
		src.sysmessageyellow Ja esta sendo usado.
		return 1
	endif

	decrement	
	src.despeja_tubo 1
 
	//passa dados do extrato para o destilador e diz que esta em uso
	src.targ.more1=<more1>
	src.targ.more2=1

	if (<src.targ.type>==t_destilador)
		src.targ.dest_setaCor
		src.targ.name=destilador (<name>)
	else
		src.targ.balao_setaCor
		src.targ.name=balao (<name>)
	endif
	
	src.targ.update
	return 1

elif (<src.targ.type>==t_misturador)
	if (<src.alchemy> < 65.0)
		src.sysmessageyellow Voce nao tem experiencia para fazer isso.
		return 1
	endif
	if (!<more2>)
		src.sysmessageyellow Extratos sao muito impuros para serem misturados.
		return 1
	endif
	if (<more2>==3)
		src.sysmessageyellow Voce nao pode misturar isso.
		return 1
	endif
	if (<src.targ.more2>==2)
		src.sysmessage Nao pode adicionar um terceiro produto
		return 1
	endif

	decrement	
	src.despeja_tubo 1

	src.targ.more1=<src.targ.more1>|<more1>
	src.targ.more2=<src.targ.more2>+1
	src.targ.name=misturador
	src.targ.mist_setaCor
	src.targ.update
	return 1
	
elif (<src.targ.type>==t_potion_empty)
	decrement	
	src.despeja_tubo 1
	src.targ.decrement
 	faz_pocao
endif
return 1

//*****************************************************************************
// i_almofariz
//*****************************************************************************
[ITEMDEF i_almofariz]
NAME=almofariz
ID=i_mortar_pestle
TYPE=t_almofariz
CATEGORY=MyT - Items by Professions
SUBSECTION=Alchemist
DESCRIPTION=Almofariz

ON=@DCLICK
if (<topobj>!=<src.uid>)
	src.sysmessagered O almofariz precisa estar na sua mala
	return 1
endif
src.sysmessageyellow Selecione o reagente
TARGET 
return 1


ON=@TARGON_ITEM
if (<src.targ.type> != t_reagente)
	src.sysmessagered Use o almofariz em reagentes.
	return 1
endif

obj=<src.findid.i_faz_item.uid>
obj.cleartags
obj.tag.uid1=<src.targ.uid>
obj.tag.amt1=1
obj.tag.nam1=<src.targ.name>
obj.tag.res2=i_tubo
obj.tag.amt2=1
obj.tag.nam2=tubo de ensaio vazio
obj.tag.skill=alchemy
obj.tag.dif=40
obj.tag.emote=misturando <src.targ.name>
obj.tag.sfx=578
obj.tag.delay=<eval 13-(<src.alchemy>/100)> //3 a 13
obj.tag.item=i_tubo_cheio
obj.tag.color=<src.targ.get_color>
obj.tag.more1=<src.targ.more1>
obj.tag.more2=0
obj.tag.name=extrato de <src.targ.name>
obj.tag.f_end=despeja_tubo
src.fazitem

return 1
 
//*****************************************************************************
// i_destilador
//*****************************************************************************
//more1	mascara reagentes
//more2 status (vazio,a destilar,destilado)
[ITEMDEF i_destilador]
NAME=destilador
ID=i_beaker2_empty
TYPE=t_destilador
CATEGORY=MyT - Items by Professions
SUBSECTION=Alchemist
DESCRIPTION=Destilador

ON=@DCLICK
if (!<more2>) //vazio
	src.sysmessageyellow Utilize um tubo cheio.
	return 1
elif (<more2>==01)//com algo para destilar
	src.sysmessageyellow Selecione a espiriteira
	TARGET
	return 1
elif (<more2>==02)//com destilado
	src.sysmessageyellow Selecione outro recipiente
	TARGET
	return 1
endif

ON=@TARGON_ITEM
if (<more2>==1)//com algo para destilar
	if (<src.targ.type>==t_espiriteira)
		if (<src.targ.cont>)
			src.sysmessageyellow Nao pode usar a espiriteira ai.
			return 1
		elif (!<src.targ.more1>)//espiriteira apagada?
			src.sysmessageyellow Acenda a espiriteira.
			return 1
		elif (<src.targ.distance>>1)//muito longe?
			src.sysmessageyellow Chegue mais perto.
			return 1
		endif
		p=<src.targ.p>
		link=<src>
		obj=<src.findid.i_faz_item.uid>
		obj.cleartags
		obj.tag.skill=alchemy
		obj.tag.dif=55
		obj.tag.emote=destilando
		obj.tag.delay=<eval 20-(<src.alchemy>/100)> //10 a 20
		obj.tag.item=1 
		obj.tag.f_step=dest_step <uid>,
		obj.tag.f_done=dest_end <uid>,
		src.fazitem		
		return 1
	else
		src.sysmessageyellow Selecione uma espiriteira acesa.
		return 1
	endif
elif (<more2>==2)//com destilado
	if (<src.targ.type>==t_tubo)
		src.targ.decrement
//		src.despeja_tubo 1
		sfx 0240
  	serv.newitem i_tubo
  	new.more1=<more1>
		new.more2=2 //destilado
		new.name=destilado de <NEW.find_name>
		new.color=<new.get_color>
		new.cont=<src>
		new.update

		more1=0//vazio
		more2=0//vazio
		name=destilador
		dest_setaCor
		return 1
endif

//*****************************************************************************
// i_balao
//*****************************************************************************
[ITEMDEF i_balao]
NAME=balao
ID=0182d
TYPE=t_balao
CATEGORY=MyT - Items by Professions
SUBSECTION=Alchemist
DESCRIPTION=Balão

//*****************************************************************************
// i_espiriteira
//*****************************************************************************
[ITEMDEF i_espiriteira]
NAME=espiriteira
ID=i_heating_stand
TYPE=t_espiriteira
CATEGORY=MyT - Items by Professions
SUBSECTION=Alchemist
DESCRIPTION=Espiriteira

ON=@CREATE
more1=0
more2={10 15}
attr=attr_can_decay

//*****************************************************************************
// i_misturador
//*****************************************************************************
[ITEMDEF i_misturador]
NAME=misturador
ID=0183a
TYPE=t_misturador
CATEGORY=MyT - Items by Professions
SUBSECTION=Alchemist
DESCRIPTION=Misturador

//*****************************************************************************
//*****************************************************************************
// ITEMDEFs - Memories
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// i_alc_balao
//*****************************************************************************
[ITEMDEF i_alc_balao]
NAME=Usando balao
ID=i_memory
TYPE=t_eq_script
LAYER=layer_special

ON=@CREATE
attr=attr_can_decay

ON=@DCLICK
if (<link.more2>==01)
 src.sysmessageyellow Selecione a espiriteira
 TARGET
 return 1
elif (<link.more2>==02)
 src.sysmessageyellow Selecione outro recipiente
 TARGET
 return 1
endif

ON=@TARGON_CANCEL
remove
return 1

ON=@TARGON_ITEM
if (<link.more2>==1)
 if (<cont.targ.type>==t_espiriteira)
  if (<cont.targ.cont>)
   cont.sysmessage Nao pode usar a espiriteira ai.
   remove
   return 1
  endif
  if (<cont.targ.more1>==0)
   cont.sysmessage Acenda a espiriteira.
   remove
   return 1
  endif
  link.p=<CONT.targ.p>
  link.emoteyellow aquecendo
  tag.reag=<link.tag.reag>
  more1=<link.more1>
  more2=<R10,20>
  cont.freeze
  timer=1
  return 1
 else
  cont.sysmessage Selecione uma espiriteira acesa.
  remove
  return 1
 endif
elif (<link.more2>==2)
 if (<cont.targ.type>==t_tubo)
  sfx 0240
  link.more2=0
  link.dispid=0182d
  link.update
  link.name=balao
  cont.targ.amount=<cont.targ.amount>-1
  if (<cont.targ.amount><1)
   cont.targ.remove
  endif
  cont.newitem=i_tubo_cheio
  cont.act.more1=<link.more1>
  cont.act.more2=1
  cont.act.name=borra de <cont.act.find_name>
  cont.act.cont=<cont>
  cont.act.find_color
  cont.act.update
  return 1
endif

ON=@TIMER
 if (<more2>)
  more2=<more2>-1
  alc_falhar 3
  cont.clspk
  link.clspk
  DORAND (5)
   cont.emotegreen preparando
   link.emoteyellow aquecendo
   sfx 020
  ENDDO
  timer=1  
  return 1
 else
  cont.alc_subir 3
  link.more2=2
  cont.clspk
  cont.emotegreen terminou
  link.name=balao (borra de <link.find_name>)
  cont.unfreeze
  remove
  return 1
 endif


//*****************************************************************************
// i_alc_misturador
//*****************************************************************************
[ITEMDEF i_alc_misturador]
NAME=Usando balao
ID=i_memory
TYPE=t_eq_script
LAYER=layer_special

ON=@CREATE
attr=attr_can_decay

ON=@DCLICK
if (<cont.alchemy> < 65.0)
 cont.sysmessagered Voce nao tem experiencia para fazer isso.
 remove
 return 1
endif
if (<link.more2><02)
 cont.sysmessagegreen Coloque dois produtos para usar.
 remove
 return 1
elif (<link.more2>==02)
 src.sysmessageyellow Selecione a espiriteira
 TARGET
 return 1
elif (<link.more2>==03)
 src.sysmessageyellow Selecione outro recipiente
 TARGET
 return 1
endif

ON=@TARGON_CANCEL
remove
return 1

ON=@TARGON_ITEM
if (<link.more2>==2)
 if (<cont.targ.type>==t_espiriteira)
  if (<cont.targ.cont>)
   cont.sysmessage Nao pode usar a espiriteira ai.
   remove
   return 1
  endif
  if (<cont.targ.more1>==0)
   cont.sysmessage Acenda a espiriteira.
   remove
   return 1
  endif
  link.p=<CONT.targ.p>
  link.emoteyellow aquecendo
  more2=<R10,20>
  cont.freeze
  timer=1
  return 1
 else
  cont.sysmessagegreen Selecione uma espiriteira acesa.
  remove
  return 1
 endif
elif (<link.more2>==3)
 if (<cont.targ.type>==t_tubo)
  sfx 0240
  link.more2=0
  link.dispid=0183a
  link.update
  link.name=misturador
  cont.targ.amount=<cont.targ.amount>-1
  if (<cont.targ.amount><1)
   cont.targ.remove
  endif
  cont.targ.update
  cont.newitem=i_tubo_cheio
  cont.act.more1=<link.more1>
  cont.act.more2=3
  cont.act.name=<link.find_name>
  cont.act.cont=<cont>
  cont.act.find_color
  cont.act.update
  link.more1=0
  return 1
endif

ON=@TIMER
 if (<more2>)
  more2=<more2>-1
  alc_falhar 4
  DORAND (20)
   cont.emotegreen tenso
   cont.emotegreen preparando
   cont.emotegreen preparando
   link.emoteyellow aquecendo
   link.emoteyellow aquecendo
   link.say @028 click!
   sfx 050
   sfx 020
   sfx 050
   sfx 024
  ENDDO
  timer=1  
  return 1
 else
  cont.alc_subir 3
  link.more2=3
  cont.clspk
  cont.emotegreen terminou
  link.name=misturador (<link.find_name>)
  cont.unfreeze
  remove
  return 1
 endif

[EOF]